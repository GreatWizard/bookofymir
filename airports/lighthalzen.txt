//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena, corrigé par Fern
//===== eAthena Script ======================================= 
//= Lighthalzen Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena Revision 3000+
//===== Description: ========================================= 
//= Lighthalzen Airport Staff
//===== Additional Comments: ================================= 
//= 1.0 Cloned from einbroch.txt [Justin84]
//= 1.1 Removed Duplicates [Silent]
//= 1.2 Fixed syntax errors for duplicate [KarLaeda]
//= 1.3 Removed use of goto. [L0ne_W0lf]
//============================================================


lhz_airport,126,43,4	script	Hôtesse#1::AirportL	90,{
	mes "[Personnel de l'Aéroport]";
	mes "Bienvenue à l'Aéroport.";
	mes "En quoi puis-je vous aider?";
	next;
	if (select("Embarquer:Annuler") == 1) {
		mes "Les frais d'embarquement sont de 1200 Zenys, mais si vous avez obtenu un billet gratuit vous n'aurez pas à payer ces frais. Voulez-vous prendre le zeppelin?";
		next;
		if (select("Oui:Non") == 1) {
			if(countitem(7311) > 0) {
				delitem 7311,1;
				warp "lhz_airport",148,51;
				close;
			}
			if(zeny >= 1200) {
				set Zeny,zeny-1200;
				warp "lhz_airport",148,51;
				close;
			}
			mes "[Personnel de l'Aéroport]";
			mes "Vous n'avez pas assez d'argent.";
			close;
		}
	}
	mes "[Personnel de l'Aéroport]";
	mes "Merci. Passez une agréable journée!";
	close;
}

lhz_airport,143,43,4	duplicate(AirportL)	Hôtesse#2	90
lhz_airport,156,43,4	duplicate(AirportL)	Hôtesse#3	90

lhz_airport,126,51,4	script	Hôtesse#4::AirportL2	90,{
	mes "[Personnel de l'Aéroport]";
	mes "Bienvenue~";
	mes "Veuillez suivre ce chemin afin d'embarquer sur le Zeppelin.";
	next;
	if (select("Retourner au Terminal Principal:Annuler") == 1) {
		mes "[Personnel de l'Aéroport]";
		mes "Si vous quittez le Terminal Principal, vous devrez à nouveau payer les frais d'embarquement.";
		mes "Voulez-vous vraiment sortir?";
		next;
		if (select("Oui:Non") == 1) {
			warp "lhz_airport",142,40;
			close;
		}
	}
	mes "[Personnel de l'Aéroport]";
	mes "D'accord. Merci de votre confiance. Nous esperons que vous passerez un agréable voyage~";
	close;
}

lhz_airport,143,51,4	duplicate(AirportL2)	Hôtesse#5	90
lhz_airport,156,51,4	duplicate(AirportL2)	Hôtesse#6	90
