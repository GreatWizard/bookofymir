//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Advance Class jobchanger after kRO Normals.
//===== By: ==================================================
//= Nana
//= finished, optimized and tested by Lupus
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 1.0 +
//===== Description: ========================================= 
//= Advance Class jobchanger after kRO Normals.
//===== Additional Comments: ================================= 
//= v1.1 Made all into functions, additional checks, etc. [Lupus]
//= v1.2 Made numbers into constants. [Vicious]
//= 1.3 Now saves/restores all the quest skills [Lupus]
//============================================================ 

valkyrie,44,47,5	script	Haut Sorcier#Valkyrie	735,{
	if (ADVJOB == 0 ||  Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Haut Sorcier]";
			mes "Félicitations.";
			mes "Honneur aux Guerriers!";
			close;
		}
		mes "[Haut Sorcier]";
		mes "Nous autres, les Hauts Sorciers, avons la responsabilité d'utiliser la magie pour la noble cause.";
		next;
		mes "[Haut Sorcier]";
		mes "Une vie d'entraînement est requise avant de pouvoir devenir Haut Sorcier. Pouvez-vous imaginer ce qui se passerait si notre pouvoir tombait entre de mauvaises mains?!";
		close;
	}
	else if (ADVJOB == Job_High_Wizard && Class == Job_Mage_High && JobLevel > 39) {
		mes "[Haut Sorcier]";
		mes "Il est temps.";
		mes "Rune-Midgard a besoin de ceux qui savent manier la puissance magique...";
		next;
		mes "[Haut Sorcier]";
		mes "Souhaitez-vous devenir Haut Sorcier?";
		next;
		if (select("Non.:Oui.") == 1) {
			mes "[Haut Sorcier]";
			mes "Revenez quand vous vous sentirez prêt.";
			next;
			mes "[Haut Sorcier]";
			mes "Honneur aux Guerriers!";
			close;
		}
		if (SkillPoint) {
			mes "[Haut Sorcier]";
			mes "Vous n'avez pas terminé votre apprentissage.";
			close;
		}
		jobchange Job_high_Wizard;
		set ADVJOB,0;
		mes "[Haut Sorcier]";
		mes "Félicitations.";
		mes "En tant que Haut Sorcier, j'espère que vous utiliserez vos pouvoirs afin d'apporter la paix aux oppressés.";
		close;
	}
	mes "[Haut Sorcier]";
	mes "Bienvenue au Valhalla, le Hall des Honneurs.";
	next;
	mes "[Haut Sorcier]";
	mes "Faîtes comme chez vous.";
	mes "Honneur aux Guerriers!";
	close;
}
