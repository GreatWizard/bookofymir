//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Acolyte Job Quest
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 2.2
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Job quest for Acolyte classes
//===== Additional Comments: ================================= 
//= v1.0 Fully working.
//= v1.1 Added function calls for Priest Quest.
//= v1.2 Fixed Marthilda, Yosuke bugs. [Lupus]
//= v1.2a Added instant job change for High Novice [Lupus]
//= v1.3 Added Baby Class support [Lupus]
//= v1.4 Optimised and moved first checker to Jfunc1-1 [massdriller]
//= 1.5 Now saves/restores all quest skills [Lupus]
//= 1.6 Merged back JFunc [Lupus]
//= 1.7 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//=	No longer uses function "F_ToHigh"
//=	Proper dialog for Priest job quest is unused (for now.)
//= 1.7a Functions now make use of proper priest quest dialog [L0ne_W0lf]
//= 1.8 Removed an Unnecessary "set job_acolyte_q,0;" [Samuray22]
//= 1.8a More bug fixes care of Crashy. [L0ne_W0lf]
//= 1.9 Changed Marthilda to Mathilda. [L0ne_W0llf]
//= 2.0 Fixed High Acolytesnot being given holy light. [L0ne_W0lf]
//= 2.1 Added Quest Log commands. [L0ne_W0lf]
//= 2.2 Fixed an issue with completequest by adding checkquest. [Kisuka]
//============================================================ 

prt_church,184,41,4	script	Père Mareusis#aco	60,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) {
			if (Class == Job_Novice_High) {
				mes "[Père Mareusis]";
				mes "Ah, je peux sentir que vous avez enduré beaucoup de souffrances dans votre vie antérieure...";
				mes "Vous deviez être quelqu'un d'exceptionnel pour avoir été convié au Walhalla.";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[Père Mareusis]";
					mes "Malheureusement, je ne pense pas que vous soyez prêt à devenir Acolyte. Vous devez avant tout apprendre vos compétences de base.";
					next;
					mes "[Père Mareusis]";
					mes "J'attendrai jusqu'à ce que vous soyez prêt.";
					mes "Que Dieu soit avec vous.";
					close;
				}
				mes "[Père Mareusis]";
				mes "Bon retour parmi nous. Je vous souhaite bonne chance pour votre nouveau voyage.";
				next;
				skill 143,0,0;
				jobchange Job_Acolyte_High;
				skill 156,1,0;
				mes "[Père Mareusis]";
				mes "À partir de maintenant, n'oubliez pas de venir en aide à ceux qui sont dans le besoin. Que la lumière de Dieu éclaire votre chemin.";
				close;
			}
			else {
				mes "[Père Mareusis]";
				mes "A partir de maintenant, n'oubliez pas de venir en aide à ceux qui sont dans le besoin. Que la lumière de Dieu puisse éclairer votre chemin.";
				close;
			}
		}
		else {
			mes "[Père Mareusis]";
				mes "Ah, je peux sentir que vous avez enduré beaucoup de souffrances dans votre vie antérieure...";
				mes "Vous deviez être quelqu'un d'exceptionnel pour avoir été convié au Walhalla.";
			next;
			mes "[Père Mareusis]";
			mes "Malheureusement, je ne pense pas que vous soyez prêt pour devenir Acolyte. Rappelez-vous qui vous étiez dans votre ancienne vie et trouvez votre chemin.";
			close;
		}
	}
	mes "[Père Mareusis]";
	mes "Bienvenue au Sanctuaire.";
	mes "Que cherchez-vous?";
	next;
	switch(select("Mon Père, je souhaiterais devenir Acolyte.:Quels sont les prérequis?:Je venais simplement me recueillir.")) {
	case 1:
		mes "[Père Mareusis]";
		if (BaseJob == Job_Acolyte) {
			mes "Hum....";
			emotion e_hmm;
			next;
			mes "[Père Mareusis]";
			mes "...votre toge...";
			next;
			mes "[Père Mareusis]";
			mes "Vous êtes déjà un Acolyte.....";
			next;
			mes "[Père Mareusis]";
			mes "Vous êtes en train de plaisanter, haha!!... Les jeunes, de nos jours...";
			emotion e_heh;
			close;
		}
		else if (BaseJob != Job_Novice) {
			mes "Je suis désolé, mon enfant, mais vous avez déjà dévoué votre vie à un autre métier.";
			close;
		}
		if (job_acolyte_q == 0) {
			mes "Souhaitez-vous sincèrement devenir un serviteur de Dieu?";
			next;
			if (select("Oui, mon Père.:Non, j'ai menti...") == 1) {
				mes "[Père Mareusis]";
				mes "Bien. Je comprends votre volonté de devenir Acolyte, " + strcharinfo(0) + ". Mais vous devez d'abord faire pénitence avant de pouvoir devenir un serviteur de Dieu, vous comprenez?";
				next;
				mes "[Père Mareusis]";
				mes "Bien... Voici quelle sera votre quête divine.....";
				switch(rand(3)) {
				default:
					set job_acolyte_q,2;
					mes "Rendez visite au ^000077Père Rubalkabara^000000, membre de la paroisse de Prontera, puis revenez me voir. Vous le trouverez en train de pratiquer l'ascétisme non loin de l'^000077Abbaye de Saint Capitolina^000000, à 1 jour de marche au Nord puis 2 jours de marche à l'Est, en partant de la ville de Prontera...";
					setquest 1001;
					break;
				case 1:
					set job_acolyte_q,3;
					mes "Rendez visite à ^000077Mère Mathilda^000000 puis revenez me voir. Vous la trouverez en train de pratiquer l'ascétisme dans le ^000077Désert de Sograt^000000, à 1 jour de marche au Nord de la ville de Morroc. Morroc se situe au Sud-Ouest de Prontera.";
					setquest 1002;
					break;
				case 2:
					set job_acolyte_q,4;
					mes "Rendez visite à ^000077Père Yosuke^000000 puis revenez ici. Vous le trouverez en train de pratiquer l'ascétisme sur ^000077une toute petite île^000000, à 1 jour de marche au Nord puis 2 jours de marche à l'Ouest, en partant de la ville de Prontera...";
					setquest 1003;
					break;
				}
				next;
				mes "[Père Mareusis]";
				mes "Que la grâce de Dieu éclaire votre route.";
				close;
			}
			mes "[Père Mareusis]";
			mes "Vous avez menti?";
			mes "Mais il est bon d'être venu avouer votre pécher.";
			mes "Partez en paix mon enfant.";
			close;
		}
		mes "Oh, vous êtes revenu... Laissez moi voir si vous êtes prêt à servir Dieu. Voyons...";
		next;
		mes "[Père Mareusis]";
			if (getskilllv("NV_BASIC") < 9) {
			mes "Malheureusement, je ne pense pas que vous soyez prêt pour devenir Acolyte. Vous devez avant tout apprendre vos compétences de base.";
			next;
			mes "[Père Mareusis]";
			mes "Vous devez vous entraîner encore! Repartez et soyez sûr d'atteindre le niveau 9 de votre métier et d'avoir appris toutes les compétences de base!";
			close;
		}
		if (job_acolyte_q < 5) {
			mes "Oh? Je ne trouve pas votre nom dans ma liste.";
			next;
			switch(job_acolyte_q) {
			case 2:
				mes "[Père Mareusis]";
				mes "Rendez visite à ^000077Père Rubalkabara^000000, membre de la paroisse de Prontera, puis revenez me voir.";
				next;
				mes "[Père Mareusis]";
				mes "Vous le trouverez en train de pratiquer l'ascétisme non loin de l'^000077Abbaye de Saint Capitolina^000000, à 1 jour de marche au Nord puis 2 jours de marche à l'Est, en partant de la ville de Prontera...";
				break;
			case 3:
				mes "[Père Mareusis]";
				mes "Rendez visite à ^000077Mère Mathilda^000000 puis revenez me voir.";
				next;
				mes "[Père Mareusis]";
				mes "Vous la trouverez en train de pratiquer l'ascétisme dans le ^000077Désert de Sograt^000000, à 1 jour de marche au Nord de la ville de Morroc. Morroc se situe au Sud-Ouest de Prontera.";
				break;
			case 4:
				mes "[Père Mareusis]";
				mes "Rendez visite à ^000077Père Yosuke^000000 puis revenez ici.";
				next;
				mes "[Père Mareusis]";
				mes "Vous le trouverez en train de pratiquer l'ascétisme sur ^000077une toute petite île^000000, à 1 jour de marche au Nord puis 2 jours de marche à l'Ouest, en partant de la ville de Prontera...";
				break;
			}
			next;
			mes "[Père Mareusis]";
			mes "Que la grâce de Dieu éclaire votre route.";
			close;
		}
		mes "Hmm...";
		mes "Vos prières ont été entendues, mon enfant. Que votre voeu soit exaucé!!";
		next;
		mes "[Père Mareusis]";
		mes "Par les pouvoirs qui me sont conférés, ^000077"+strcharinfo(0)+"^000000, je vous fais Acolyte!!!";
		next;
		skill 143,0,0;
		callfunc "Job_Change",Job_Acolyte;
		callfunc "F_ClearJobVar";
		if(checkquest(1001) != -1) {
			completequest 1001;
		}
		else if(checkquest(1002) != -1) {
			completequest 1002;
		}
		else {
			completequest 1003;
		}
		mes "[Père Mareusis]";
		mes "N'oubliez jamais que vous êtes redevable à Dieu, qui prend soin de nous tous, en tout temps.";
		next;
		mes "[Père Mareusis]";
		mes "Vous avez une chance unique de pouvoir le servir... Vous devez vous sentir très honoré. Maintenant, allez mon enfant, vaincre le mal qui ronge ce monde.";
		next;
		mes "[Père Mareusis]";
		mes "Je tenais à vous dire que je suis très fier de vous. Que la bénédiction de l'église vous accompagne.";
		close;
	case 2:
		mes "[Père Mareusis]";
		mes "Vous souhaitez devenir un Acolyte, n'est-ce pas?";
		mes "Je vais vous expliquer quels sont les prérequis.";
		next;
		mes "[Père Mareusis]";
		mes "Tout d'abord, vous devez avoir un Niveau de Métier égal à 9, et avoir appris toutes vos compétences de Novice. Deuxièmement, vous serez invité à participer à une épreuve divine.";
		next;
		mes "[Père Mareusis]";
		mes "Si vous réussissez, vous deviendrez un Acolyte. Si, au plus profond de votre coeur, votre désir est réellement de servir Dieu, vous n'aurez aucun mal à y parvenir.";
		next;
		mes "[Père Mareusis]";
		if (job_acolyte_q != 0) {
			switch(job_acolyte_q) {
			case 2:
				mes "[Père Mareusis]";
				mes "Rendez visite à ^000077Père Rubalkabara^000000, membre de la paroisse de Prontera, puis revenez me voir.";
				next;
				mes "[Père Mareusis]";
				mes "Vous le trouverez en train de pratiquer l'ascétisme non loin de l'^000077Abbaye de Saint Capitolina^000000, à 1 jour de marche au Nord puis 2 jours de marche à l'Est, en partant de la ville de Prontera...";
				break;
			case 3:
				mes "[Père Mareusis]";
				mes "Rendez visite à ^000077Mère Mathilda^000000 puis revenez me voir.";
				next;
				mes "[Père Mareusis]";
				mes "Vous la trouverez en train de pratiquer l'ascétisme dans le ^000077Désert de Sograt^000000, à 1 jour de marche au Nord de la ville de Morroc. Morroc se situe au Sud-Ouest de Prontera.";
				break;
			case 4:
				mes "[Père Mareusis]";
				mes "Rendez visite à ^000077Père Yosuke^000000 puis revenez ici.";
				next;
				mes "[Père Mareusis]";
				mes "Vous le trouverez en train de pratiquer l'ascétisme sur ^000077une toute petite île^000000, à 1 jour de marche au Nord puis 2 jours de marche à l'Ouest, en partant de la ville de Prontera...";
				break;
			}
			next;
			mes "[Père Mareusis]";
			mes "Que la grâce de Dieu éclaire votre route.";
		}
		else {
			mes "La destination de votre quête sera décidée une fois que vous aurez complété le formulaire de candidature.";
		}
		next;
		mes "[Père Mareusis]";
		mes "Revenez lorsque vous aurez satisfait les deux prérequis nécessaires s'il vous plaît. Si, au plus profond de votre coeur, votre désir est réellement de servir Dieu, vous n'aurez aucun mal à y parvenir.";
		close;
	case 3:
		close;
	}
}

prt_fild03,365,255,2	script	Père Rubalkabara#aco	89,{
	mes "[Père Rubalkabara]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 6) {
			mes "Faites attention. Ils devraient savoir que vous m'avez rencontré quand vous arriverez au Sanctuaire de Prontera.";
			next;
			mes "[Père Rubalkabara]";
			mes "J'ai envoyé un pigeon voyageur avec un message. J'espère qu'il arrivera sain et sauf...";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 2) {
				mes "Oh... Êtes-vous un prétendant au métier d'Acolyte...? Le Père Mareusis m'a prévenu de votre arrivée prochaine.";
				next;
				mes "[Père Rubalkabara]";
				mes "Donnez-moi un instant, que je vérifie votre nom sur la liste des candidats......";
				next;
				mes "[Père Rubalkabara]";
				mes "Vous êtes ^000077"+strcharinfo(0)+"^000000, n'est-ce pas? Merci de m'avoir rendu visite.";
				next;
				mes "[Père Rubalkabara]";
				mes "Je crois que le Père Mareusis vous a déjà informé sur le métier d'Acolyte. Je ne vais donc pas vous retenir plus longtemps avec mes sermons.";
				next;
				mes "[Père Rubalkabara]";
				mes "Je sais que votre génération n'aime pas beaucoup être sermonnée par un vieil homme comme moi. Hahaha...";
				next;
				mes "[Père Rubalkabara]";
				mes "Mais n'oubliez pas d'écouter vos aînés avec respect, car Dieu aime enseigner à ses enfants.";
				next;
				mes "[Père Rubalkabara]";
				mes "Je vais envoyer un message à Père Mareusis pour lui dire que vous m'avez rendu visite comme convenu, et que vous avez donc rempli votre quête divine.";
				next;
				mes "[Père Rubalkabara]";
				mes "Vous pouvez regagner Prontera maintenant. Adieu, et que Dieu vous bénisse.";
				close2;
				savepoint "prt_fild03",361,255;
				set job_acolyte_q,6;
				end;
			}
			else {
				mes "Oh...";
				mes "Êtes-vous un prétendant au métier d'Acolyte...?";
				mes "Voyons voir...";
				next;
				mes "[Père Rubalkabara]";
				mes "Votre nom est " + strcharinfo(0) + "?";
				mes "Je ne vois pas votre nom sur la liste des candidats. Hmmm...";
				next;
				mes "[Père Rubalkabara]";
				mes "Vous devriez retourner au Sanctuaire afin de demander au Père Mareusis le détail de votre quête divine.";
				close;
			}
		}
		else {
			mes "Hum...? Qu'est ce qui vous amène ici? C'est un endroit très dangereux pour un Novice tel que vous. Faites bien attention à vous!";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherRub";
	else {
		if (BaseJob == Job_Priest) {
			mes "Salutations.";
			next;
			mes "[Père Rubalkabara]";
			mes "Bienvenue dans cet endroit. N'hésitez pas à vous asseoir et contempler le paysage que Dieu nous offre avec moi. Ce lieu est beau, même si le danger accompagne son sens de la sérénité...";
			close;
		}
		else {
			mes "Oh ho...";
			mes "Êtes-vous venu pour vous entraîner? Ou vous êtes simplement un voyageur?";
			next;
			mes "[Père Rubalkabara]";
			mes "Qui que vous soyez, faites attention à vous. Les monstres ici sont incroyablement forts, malgré leur apparence innocente.";
			close;
		}
	}
}

moc_fild07,41,355,4	script	Mère Mathilda#2aco	95,{
	mes "[Mère Mathilda]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 7) {
			mes "J'ai envoyé un pigeon voyageur avec un message au Sanctuaire. Quand vous y retournerez, les Prêtres l'auront certainement déjà reçu.";
			next;
			mes "[Mère Mathilda]";
			mes "Je prierai Dieu pour vous, en espérant que vous deveniez bientôt un Acolyte.";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 3) {
				mes "Ah, vous êtes l'un des candidats au métier d'Acolyte. Soyez le bienvenue.";
				next;
				mes "[Mère Mathilda]";
				mes "Donnez-moi un instant, que je vérifie votre nom sur la liste des candidats......";
				next;
				mes "[Mère Mathilda]";
				mes "Votre nom est? " + strcharinfo(0) + "? Voyons voir... Ah, oui, vous êtes sur la liste!";
				next;
				mes "[Mère Mathilda]";
				mes "Je vais envoyer un message à Père Mareusis pour lui dire que vous m'avez rendue visite comme convenu, et que vous avez donc rempli votre quête divine.";
				next;
				mes "[Mère Mathilda]";
				mes "Merci de m'avoir rendue visite. Je suis heureuse que vous ayez choisi de suivre votre coeur et de vouer votre vie à notre divinité.";
				next;
				mes "[Mère Mathilda]";
				mes "Vous pouvez regagner Prontera maintenant. Adieu, et que Dieu vous bénisse.";
				close2;
				savepoint "moc_fild07",35,355;
				set job_acolyte_q,7;
				end;
			}
			else {
				mes "Ah, vous devez être l'un des candidats au métier d'Acolyte. Soyez le bienvenue.";
				next;
				mes "[Mère Mathilda]";
				mes "Quel est votre nom déjà?";
				mes "" + strcharinfo(0) + "? Hmm....Voyons voir....";
				next;
				mes "[Mère Mathilda]";
				mes "Hum... Je ne vois pas votre nom sur la liste des candidats.";
				next;
				mes "[Mère Mathilda]";
				mes "...... Vous devriez retourner au Sanctuaire afin de demander au Père Mareusis le détail de votre quête divine.";
				close;
			}
		}
		else {
			mes "...";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_MotherMart";
	else {
		if (BaseJob == Job_Priest) {
			mes "Bonjour~";
			next;
			mes "[Mère Mathilda]";
			mes "Comment se passe votre entraînement? J'espère que vous aimez vivre dans la grâce de Dieu.";
			close;
		}
		else {
			mes "Puisse Dieu être toujours à vos côtés...";
			close;
		}
	}
}

prt_fild00,208,218,6	script	Père Yosuke#3aco	98,{
	mes "[Père Yosuke]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 8) {
			mes "Pardon?";
			next;
			mes "[Père Yosuke]";
			mes "Qu'avez vous à faire ici?! Rien du tout! Retournez au Sanctuaire immédiatement!";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 4) {
				mes "Hé!! Vous, là.";
				mes "Vous êtes un candidat au métier d'Acolyte, n'est-ce pas?";
				next;
				mes "[Père Yosuke]";
				mes "Pas mal du tout, vous avez du faire un long chemin pour arriver jusqu'ici.";
				mes "Quel est votre nom?";
				next;
				mes "[Père Yosuke]";
				mes "" + strcharinfo(0) + ", huh?";
				next;
				mes "[Père Yosuke]";
				mes "Okay. Je vais envoyer un message à Père Mareusis pour lui dire que vous m'avez rendu visite comme convenu, et que vous avez donc rempli votre quête divine.";
				next;
				mes "[Père Yosuke]";
				mes "Vous pouvez regagner Prontera maintenant. Adieu, et que Dieu vous bénisse.";
				close2;
				savepoint "prt_fild00",206,230;
				set job_acolyte_q,8;
				end;
			}
			else {
				mes "Hey.";
				mes "Vous êtes un candidat au métier d'Acolyte, n'est-ce pas?";
				next;
				mes "[Père Yosuke]";
				mes "Pas mal du tout, vous avez du faire un long chemin pour arriver jusqu'ici. Quel est votre nom?";
				next;
				mes "[Père Yosuke]";
				mes "" + strcharinfo(0) + ", huh? Je ne vois pas votre nom sur la liste des candidats.";
				next;
				mes "[Père Yosuke]";
				mes "Il doit y avoir une erreur.";
				mes "Retournez au Sanctuaire afin de demander au Père Mareusis le détail de votre quête divine.";
				close;
			}
		}
		else {
			mes "Hé, vous là...";
			mes "Le Novice.";
			mes "Vous voulez me parler de quelque chose?";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherYos";
	else {
		if (BaseJob == Job_Priest) {
			mes "Hey...";
			next;
			mes "[Père Yosuke]";
			mes "Si vous le souhaitez, venez vous asseoir ici avec moi pour méditer sur les grandes vérités. Le monde de Dieu est véritablement une source d'inspiration...";
			close;
		}
		else {
			mes "Quelque chose à dire? Parce que malheureusement pour vous, je n'ai pas de réponses.";
			close;
		}
	}
}
