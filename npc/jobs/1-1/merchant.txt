//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Merchant Job Quest
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 2.2
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Job quest for Merchant classes
//===== Additional Comments: ================================= 
//= Fully working
//= v1.1 Added instant job change for High Novice [Lupus]
//= v1.3 Added Baby Class support [Lupus]
//= v1.4 Optimised and moved first checker to Jfunc1-1 [massdriller]
//= 1.5 Changed Prontera->Izlude teleport price to 600 [Lupus]
//= 1.5.1 Removed Duplicates [Silent]
//= 1.5.2 Now saves/restores all quest skills [Lupus]
//= 1.6 Merged back JFunc, CHANGED NUMBERS TO CONSTANTS [Lupus]
//= 1.7 Updated to use Free Ticket for Kafra [Lupus]
//= 1.8 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//=	No longer uses function "F_ToHigh"
//= 1.9 Bugfixes, thanks to Barron-Monster. [L0ne_W0lf]
//= 1.9a Added a missing next; (Barron-Monster) [L0ne_W0lf]
//= 1.9b Typo. "nextl" -> "next;" (Barron-Monster) [L0ne_W0lf]
//= 2.0 Should "fix" the problem with the Biliban Kafra. [L0ne_W0lf]
//= 2.1 Fixed mistake in condition check. [L0ne_W0lf]
//= 2.2 Added Quest Log commands. [L0ne_W0lf]
//============================================================ 

alberta_in,53,43,6	script	Chef Mahnsoo#mer	86,{
	if (Upper == 1) {
		if (Class == Job_Novice_High && (ADVJOB == Job_Whitesmith || ADVJOB == Job_Creator)) {
			mes "[Chef Mahnsoo]";
			mes "Hé, ça fait un bail!";
			mes "Nous pensions que vous aviez laissé tomber les affaires.";
			mes "Que s'est-il passé?";
			next;
			mes "[Chef Mahnsoo]";
			mes "Whaou...";
			mes "Alors comme ça vous étiez au Valhalla?! Incroyable...";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Chef Mahnsoo]";
				mes "Humm...";
				mes "On dirait cependant que vous n'êtes pas encore prêt à reprendre vos activités de Marchand. Pourquoi n'iriez-vous pas apprendre les compétences de base avant de revenir ici?";
				next;
				mes "[Chef Mahnsoo]";
				mes "Ne vous inquiétez pas, il y aura toujours une place dans la Guilde Marchande pour vous. Revenez quand vous serez prêt, d'accord?";
				close;
			}
			mes "[Chef Mahnsoo]";
			mes "Je suppose que c'est le destin qui nous réunit ici à nouveau. Très bien, une fois encore, je suis heureux de vous accueillir dans la Guilde Marchande!";
			next;
			skill 143,0,0;
			jobchange Job_Merchant_High;
			skill 153,1,0;
			skill 154,1,0;
			skill 155,1,0;
			mes "[Chef Mahnsoo]";
			mes "Ah~ Cela me rend nostalgique. Faîtes de votre mieux! Comme au bon vieux temps!";
			close;
		}
		else {
			mes "[Chef Mahnsoo]";
			mes "^333333*soupir*^000000";
			mes "Je m'ennuie...";
			mes "Quand aurais-je des nouvelles de ma chère et tendre?";
			close;
		}
	}
	if (BaseJob == Job_Merchant) {
		mes "[Chef Mahnsoo]";
		mes "Bonjour!";
		mes "Alors, comment trouvez vous la vie de marchand?";
		next;
		mes "[Chef Mahnsoo]";
		mes "Une vie si proche de l'argent a ses avantages, n'est ce pas?";
		close;
	}
	else if (BaseJob != Job_Merchant && BaseJob != Job_Novice) {
		mes "[Chef Mahnsoo]";
		mes "Nous, marchands, détestons les gens qui ont une vie secrète. C'est mauvais pour les affaires.";
		next;
		mes "[Chef Mahnsoo]";
		mes "Les gens qui essayent de tirer avantages des autres personnes en leur vendant des articles à des prix ridicules juste dans le but de se faire quelques pièces sont vraiment les pires.";
		next;
		mes "[Chef Mahnsoo]";
		mes "Quoi qu'il en soit, nous n'acceptons que des Novices. Mais j'apprécie l'intérêt que vous nous portez.";
		close;
	}
	else if (job_merchant_q == 9) {
		mes "[Chef Mahnsoo]";
		mes "Bonjour,";
		mes ""+ strcharinfo(0) +".";
		set job_merchant_q,0;
		set job_merchant_q2,0;
		set quest_alb_01,0;
		mes "Je viens de lire le compte-rendu de Kay, le Chef de l'Entrepôt, à votre sujet...";
		next;
		mes "[Chef Mahnsoo]";
		mes "Quelle honte, quelle honte! Comment avez-vous pu être si MALADROIT et PEU SOIGNEUX??!! Je suis sûr que Kay vous a déjà sermonné, donc je ne vais pas vous le répéter... Mais tout de même!";
		next;
		mes "[Chef Mahnsoo]";
		mes ""+ strcharinfo(0) + ", vous avez échoué à l'épreuve. Cependant...... si vous voulez VRAIMENT devenir un Marchand, je peux peut-être vous laisser une seconde chance... à condition de travailler TRES DUREMENT cette fois ci....";
		close;
	}
	else if (job_merchant_q == 8 || job_merchant_q == 7) {
		mes "[Chef Mahnsoo]";
		mes "Bonjour,";
		mes ""+ strcharinfo(0) +".";
		mes "BRAVO! Je viens de lire le compte-rendu de Kay, le Chef de l'Entrepôt, à votre sujet... Je suis impressionné! J'ai une grande nouvelle à vous annoncer!";
		next;
		mes "[Chef Mahnsoo]";
		mes "Grâce à votre DUR labeur, la Guilde Marchande vous ACCEPTE en tant que nouveau membre!";
		if (job_merchant_q == 7) {
			next;
			mes "[Chef Mahnsoo]";
			mes "Hum... avant de poursuivre, il y a une petite chose que vous devez encore régler. J'aurais besoin des 500 Zenys restants pour payer les frais d'inscription à la Guilde Marchande.";
			next;
			switch(select("Payer les 500 Zenys restants:...(courir!)...")) {
			case 1:
				mes "[Chef Mahnsoo]";
				if (Zeny < 500) {
					mes "Hmmm...";
					mes "Hem, on dirait qu'il vous manque un peu d'argent.";
					next;
					mes "[Chef Mahnsoo]";
					mes "Revenez quand vous les aurez, ils sont nécessaire pour clore votre formation.";
					close;
				}
				set zeny,zeny-500;
				mes "Très bien! Nous pouvons continuer. Où en étais-je? .... Ah oui....";
				break;
			case 2:
				mes "[Chef Mahnsoo]";
				mes "HEIN??? Revenez ici! Vous nous devez encore 500 Zenys!!!";
				close;
			}
		}
		next;
		mes "[Chef Mahnsoo]";
		mes "Félicitations!";
		callfunc "Job_Change",Job_Merchant;
		callfunc "F_ClearJobVar";
		completequest 1009;
		completequest 1010;
		completequest 1011;
		completequest 1012;
		mes "Je suis ravi de vous avoir parmi nous, et j'espère que vous jouerez un rôle actif dans l'économie du Royaume de Rune-Midgard! Bonne chance!!";
		next;
		if (quest_alb_01 == 1) {
			mes "[Chef Mahnsoo]";
			mes "*Ahem* Oh, et voici un petit quelque chose, en remerciement d'avoir délivré mon message. Merci beaucoup!)";
			mes "J'espère que vous pourrez m'aider encore la prochaine fois~";
			set zeny,zeny+200;
			set quest_alb_01,2;
		}
		else {
			mes "[Chef Mahnsoo]";
			mes "Mais qu'en est-il du message que je vous avais demandé de livrer? Vous avez oublié? Oh :( . Bon travail quand même!";
		}
		next;
		mes "[Chef Mahnsoo]";
		mes "Notre but est de contrôler 20 % du commerce mondial! Et nous avons besoin de gens jeunes, et ambitieux tels que vous!";
		next;
		mes "[Chef Mahnsoo]";
		mes "Mais avant tout, nous sommes heureux de faire des transactions.";
		mes "Et nous le savons tous~";
		close;
	}
	else if (job_merchant_q <= 6 && job_merchant_q != 0) {
		mes "[Chef Mahnsoo]";
		if (job_merchant_q2 == 1 || job_merchant_q2 == 2) {
			mes "Premièrement, retirez un Paquet à l'Entrepôt et livrez-le à l'Employée Kafra dans l'Ancienne Association des Swordman de Prontera.";
			next;
			mes "[Chef Mahnsoo]";
			mes "Quand vous y serez, allez voir l'Employée Kafra qui s'y trouve. Son nom est Blossom. Vous avez bien compris?";
			next;
			if (job_merchant_q2 == 1) {
				mes "[Chef Mahnsoo]";
				mes "Rappelez vous, le Numéro de Série du paquet est le ^3355FF2485741^000000.";
				setquest 1009;
			}
			else {
				mes "[Chef Mahnsoo]";
				mes "Rappelez vous, le Numéro de Série du paquet est le ^3355FF2328137^000000.";
				setquest 1009;
			}
		}
		else if (job_merchant_q2 == 3 || job_merchant_q2 == 4) {
			mes "Premièrement, retirez un Paquet à l'Entrepôt et livrez-le à un Membre de la Guilde des Mages à Geffen. Quand vous y serez, allez voir le Membre de la Guilde des Mages en charge.";
			next;
			if (job_merchant_q2 == 3) {
				mes "[Chef Mahnsoo]";
				mes "Rappelez vous, le Numéro de Série du paquet est le ^3355FF2989396^000000.";
				setquest 1010;
			}
			else {
				mes "[Chef Mahnsoo]";
				mes "Rappelez vous, le Numéro de Série du paquet est le ^3355FF2191737^000000.";
				setquest 1010;
			}
		}
		else if (job_merchant_q2 == 5 || job_merchant_q2 == 6) {
			mes "Premièrement, retirez un Paquet à l'Entrepôt et livrez-le à Java Dullihan, le Teinturier de Morroc..";
			next;
			mes "[Chef Mahnsoo]";
			mes "Il est un peu tête en l'air, donc vous devrez probablement donner le colis à l'un de ses étudiants.";
			next;
			if (job_merchant_q2 == 5) {
				mes "[Chef Mahnsoo]";
				mes "Rappelez vous, le Numéro de Série du paquet est le ^3355FF3012685^000000.";
				setquest 1011;
			}
			else {
				mes "[Chef Mahnsoo]";
				mes "Rappelez vous, le Numéro de Série du paquet est le ^3355FF3487372^000000.";
				setquest 1011;
			}
		}
		else if (job_merchant_q2 == 7 || job_merchant_q2 == 8) {
			mes "Premièrement, retirez un Paquet à l'Entrepôt et livrez-le à l'Employée Kafra située sur l'Île de Byalan. Son nom est Blossom.";
			next;
			if (job_merchant_q2 == 7) {
				mes "[Chef Mahnsoo]";
				mes "Rappelez vous, le Numéro de Série du paquet est le ^3355FF3318702^000000.";
				setquest 1012;
			}
			else {
				mes "[Chef Mahnsoo]";
				mes "Rappelez vous, le Numéro de Série du paquet est le ^3355FF3543625^000000.";
				setquest 1012;
			}
		}
		if (job_merchant_q2 == 7 || job_merchant_q2 == 8) {
			next;
			mes "[Chef Mahnsoo]";
			mes "*Ahem*...";
			mes "N'oubliez pas de livrer ceci de ma part s'il vous plait~";
		}
		next;
		mes "[Chef Mahnsoo]";
		mes "Conservez précieusement le Numéro de Série du Paquet ainsi que son Lieu de Livraison!";
		mes "Le Chef de l'Entrepôt en aura besoin afin de vous remettre le bon colis.";
		next;
		mes "[Chef Mahnsoo]";
		mes "L'Entrepôt est situé dans ce bâtiment, à ma droite.";
		next;
		mes "[Chef Mahnsoo]";
		mes "Après avoir effectué votre livraison, revenez ici et donnez le Reçu au Chef de l'Entrepôt. Ensuite, revenez me voir. Est-ce que tout est clair?";
		next;
		mes "[Chef Mahnsoo]";
		mes "Parfait, bonne chance!";
		close;
	}
	else if (job_merchant_q == 0) {
		mes "[Chef Mahnsoo]";
		mes "Bonjour, qu'est ce qui vous amène à la Guilde des Marchands?";
		mes "Puis je faire quelque chose pour vous aider?";
		next;
		switch(select("J'aimerais devenir Marchand.:En quoi consiste le métier de Marchand?:Quels sont les pré-requis?:Non, rien merci.")) {
		case 1:
			mes "[Chef Mahnsoo]";
			mes "Un marchand, vous dîtes? Bien...";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Chef Mahnsoo]";
				mes "Je suis désolé, mais vous devez avoir atteint le Niveau de Job 9 et avoir appris toutes vos compétences de base avant de pouvoir postuler à la Guilde Marchande.";
				next;
				mes "[Chef Mahnsoo]";
				mes "Nous ne sommes pas de simples marchands!";
				mes "Il y a des normes à respecter, et malheureusement pour vous, vous ne remplissez pas encore les conditions!";
				close;
			}
			mes "[Chef Mahnsoo]";
			mes "D'accord. Tout d'abord remplissez ce formulaire, et payez les 1000 Zenys de frais d'inscription.";
			next;
			mes "[Chef Mahnsoo]";
			mes "Oh...!";
			mes "Si vous n'avez pas assez d'argent pour l'instant, il est possible de payer en deux fois: 500 Zenys tout de suite, et 500 Zenys à la fin de votre formation.";
			next;
			mes "[Chef Mahnsoo]";
			mes "Alors? Toujours partant?";
			next;
			if (select("Bien sûr.:Plus tard, peut-être...") ==1 ) {
				mes "[Chef Mahnsoo]";
				mes "Laissez moi vérifier que vous avez bien tout complété...";
				next;
				mes "[Chef Mahnsoo]";
				mes "Hmm... ";
				mes "" + strcharinfo(0) + "...";
				mes "C'est un chouette nom.";
				next;
				mes "[Chef Mahnsoo]";
				mes "Cette candidature ne sera officielle que lorsque vous aurez payé. Alors, comment souhaitez-vous régler le montant des frais d'inscription?";
				next;
				switch(select("Payer 1000 Zenys maintenant.:Deux paiements de 500 Zenys.:Euh...")) {
				case 1:
					mes "[Chef Mahnsoo]";
					if (Zeny >= 1000) {
						set job_merchant_q,2;
						set zeny,zeny-1000;
						mes "Voyons voir... 1000 Zenys.... parfait.";
					}
					else {
						mes "On dirait que vous êtes un peu à court d'argent. Pourquoi ne pas payer en deux fois?";
						close;
					}
					break;
				case 2:
					mes "[Chef Mahnsoo]";
					if (Zeny >= 500) {
						set job_merchant_q,1;
						set zeny,zeny-500;
						mes "Voyons voir... 500 Zenys... ça suffira pour commencer... Bien que je ne crois pas qu'il soit bon de s'habituer à payer en plusieurs fois.";
					}
					else {
						mes "Hmm...";
						mes "Apparemment, vous ne pouvez même pas payer la moitié de votre formation. Revenez quand vous aurez ce qu'il faut s'il vous plait.";
						close;
					}
					break;
				case 3:
					mes "[Chef Mahnsoo]";
					mes "On dirait que vous êtes un peu à court d'argent.";
					close;
				}
			}
			else {
				mes "[Chef Mahnsoo]";
				mes "Vous n'avez pas encore assez d'argent? Pas de problème, revenez quand vous en aurez, okay?";
				close;
			}
			next;
			mes "[Chef Mahnsoo]";
			mes "Maintenant que ces formalités sont réglées, parlons un peu de l'épreuve pour l'obtention d'une Licence Marchande. Vous allez devoir mener à bien une cetaine tâche.";
			mes "La qualité de votre prestation à cette épreuve déterminera votre obtention de la Licence Marchande.";
			next;
			mes "[Chef Mahnsoo]";
			mes "Avant de commencer, je dois vous dire une chose...";
			next;
			mes "[Chef Mahnsoo]";
			mes "Certaines personnes un peu idiotes et égoïstes ne savent pas ce qu'est réellement le métier de Marchand...";
			mes "J'espère sincèrement que vous ne deviendrez jamais ainsi. (*regard intense*)";
			next;
			mes "[Chef Mahnsoo]";
			mes "Bref, voici en quoi concerne votre épreuve. Vous devrez....";
			next;
			mes "[Chef Mahnsoo]";
			switch(rand(1,4)) {
			case 1:
				mes "Retirer un Paquet à l'Entrepôt et le livrer à l'Employée Kafra dans l'Ancienne Association des Swordman de Prontera.";
				next;
				mes "[Chef Mahnsoo]";
				mes "Quand vous y serez, allez voir l'Employée Kafra qui s'y trouve. Son nom est Blossom. Vous avez bien compris?";
				callsub S_GiveSerial,2485741,1,2328137,2;
				break;
			case 2:
				mes "Retirer un Paquet à l'Entrepôt et le livrer à un Membre de la Guilde des Mages à Geffen. Quand vous y serez, allez voir le Membre de la Guilde des Mages en charge.";
				callsub S_GiveSerial,2989396,3,2191737,4;
				break;
			case 3:
				mes "Retirer un Paquet à l'Entrepôt et le livrer à Java Dullihan, le Teinturier de Morroc.";
				next;
				mes "[Chef Mahnsoo]";
				mes "Il est un peu tête en l'air, donc vous devrez probablement donner le colis à l'un de ses étudiants.";
				callsub S_GiveSerial,3012685,5,3487372,6;
				break;
			case 4:
				mes "Retirer un Paquet à l'Entrepôt et le livrer à l'Employée Kafra située sur l'Île de Byalan.";
				callsub S_GiveSerial,3318702,7,3543625,8;
				next;
				mes "[Chef Mahnsoo]";
				mes "Hum et euh... une petite requête personnelle... Ajoutez ce petit mot dans le paquet, d'accord?";
				next;
				mes "[Chef Mahnsoo]";
				mes "Pourriez vous lui donner ce message lorsque vous lui livrerez le paquet? S'il vous plait~";
				getitem 1072,1; //Delivery_Message
				break;
			}
			next;
			mes "[Chef Mahnsoo]";
			mes "Conservez précieusement le Numéro de Série du Paquet ainsi que son Lieu de Livraison!";
			mes "Le Chef de l'Entrepôt en aura besoin afin de vous remettre le bon colis.";
			next;
			mes "[Chef Mahnsoo]";
			mes "L'Entrepôt est situé dans ce bâtiment, à ma droite.";
			next;
			mes "[Chef Mahnsoo]";
			mes "Après avoir effectué votre livraison, revenez ici et donnez le Reçu au Chef de l'Entrepôt. Ensuite, revenez me voir. Est-ce que tout est clair?";
			next;
			mes "[Chef Mahnsoo]";
			mes "Parfait, bonne chance!";
			close;
		case 2:
			mes "[Chef Mahnsoo]";
			mes "Les Marchands?";
			mes "Les marchands sont des personnes dont le métier est d'acheter et vendre toutes sortes d'objets. Nous nous concentrons principalement sur les objets rares, bien entendu. Pour le reste, nous nous basons sur le principe de l'offre et de la demande.";
			next;
			mes "[Chef Mahnsoo]";
			mes "Nous ne sommes pas particulièrement doués au combat, mais nous pouvons acheter les objets de la vie courante moins cher que les autres personnes, et de la même manière, revendre bien plus cher certaines choses trouvées au gré de nos aventures.";
			next;
			mes "[Chef Mahnsoo]";
			mes "Nous pouvons également utiliser des Zenys afin de lancer une puissante attaque!";
			next;
			mes "[Chef Mahnsoo]";
			mes "Jeter l'argent de la sorte font entrer les Marchands dans une rage effrayante!";
			mes "Rien que d'y penser, mon sang ne fait qu'un tour!";
			next;
			mes "[Chef Mahnsoo]";
			mes "Concernant les armes, nous pouvons presque toutes les manier. Seuls les Arcs, Bâtons de Sorcier, et Epées à Deux Mains nous sont interdits.";
			next;
			mes "[Chef Mahnsoo]";
			mes "Oui...";
			mes "Peu importe ce que les gens disent sur nous, gagner de l'argent est l'une des devises des marchands!";
			close;
		case 3:
			mes "[Chef Mahnsoo]";
			mes "Il y a trois conditions afin de pouvoir postuler dans la Guilde Marchande.";
			next;
			mes "[Chef Mahnsoo]";
			mes "Tout d'abord, vous devez avoir atteint le Niveau de Job 9 en tant que Novice, et avoir appris toutes les compétences de base.";
			next;
			mes "[Chef Mahnsoo]";
			mes "Deuxièmement, vous devrez payer 1000 Zenys de frais d'inscription. En tant que candidat au métier de Marchand, réunir 1000 Zenys ne devrait pas poser problème~";
			next;
			mes "[Chef Mahnsoo]";
			mes "Enfin, vous devrez passer une épreuve qui permettra d'examiner votre force physique et votre sens de l'orientation. Il s'agira de livrer un colis à une personne spécifique dans un lieu donné.";
			close;
		case 4:
			close;
		}
	}

S_GiveSerial:
	next;
	mes "[Chef Mahnsoo]";
	mes "Rappelez vous,";
	if (rand(1)) {
		mes "le Numéro de Série du paquet est le";
		mes "^3355FF"+getarg(0)+"^000000.";
		set job_merchant_q2,getarg(1);
	}
	else {
		mes "le Numéro de Série du paquet est le";
		mes "^3355FF"+getarg(2)+"^000000.";
		set job_merchant_q2,getarg(3);
	}
	return;
}

alberta_in,28,29,2	script	Chef de l'Entrepôt#mer	83,{
	if (BaseJob == Job_Merchant) {
		mes "[Kay, Chef de l'Entrepôt]";
		mes "Bonjour~ Qu'est-ce qui vous amène ici?";
		close;
	}
	else if (BaseJob != Job_Merchant && BaseJob != Job_Novice) {
		mes "[Kay, Chef de l'Entrepôt]";
		mes "Du travail à mi-temps? Mais nous n'avons rien de disponible! Nous traversons actuellement quelques difficultés financières, nous ne pouvons pas embaucher de nouveaux employés.";
		close;
	}
	else if (job_merchant_q == 9) {
		mes "[Kay, Chef de l'Entrepôt]";
		mes "Hey vous là. Oui, vous.";
		mes "Si vous voulez recommencer l'épreuve, allez parler à Mahnsoo dans l'autre salle. Ensuite nous pourrons discuter.";
		close;
	}
	else if (job_merchant_q == 8 || job_merchant_q == 7) {
		mes "[Kay, Chef de l'Entrepôt]";
		mes "Parfait! Tout semble parfait! Je ferai mon rapport au chef de la Guilde Marchande. Allez voir Chef Mahnsoo pour l'instant, okay?";
		close;
	}
	else if (job_merchant_q == 6 || job_merchant_q == 5) {
		mes " [Kay, Chef de l'Entrepôt] ";
		mes "Oh, yeah? Okay, faites moi voir. Votre nom est " + strcharinfo(0) + "? Parfait, et votre destination était...";
		next;
		mes "[Kay, Chef de l'Entrepôt]";
		if (job_merchant_q2 == 1 || job_merchant_q2 == 2)
			mes "Wow! Vous avez vu l'employée Kafra de Prontera?! Chanceux va~ ...Le bon de réception?";
		else if (job_merchant_q2 == 3 || job_merchant_q2 == 4)
			mes "L'Académie de Magie de Geffen. Okay, le bon de réception?";
		else if (job_merchant_q2 == 5 || job_merchant_q2 == 6)
			mes "Le teinturier de Morroc. Pas mal. Le bon de réception?";
		else if (job_merchant_q2 == 7 || job_merchant_q2 == 8)
			mes "Oh hohohoho~! L'employée Kafra de l'Île de Byalan?! Impressionnant! Quoi qu'il en soit, avez-vous le bon de réception?";

		if (job_merchant_q2 == 1 && countitem(1073) != 0)
			delitem 1073,1; //Merchant_Voucher_1
		else if (job_merchant_q2 == 2 && countitem(1074) != 0)
			delitem 1074,1; //Merchant_Voucher_2
		else if (job_merchant_q2 == 3 && countitem(1075) != 0)
			delitem 1075,1; //Merchant_Voucher_3
		else if (job_merchant_q2 == 4 && countitem(1076) != 0)
			delitem 1076,1; //Merchant_Voucher_4
		else if (job_merchant_q2 == 5 && countitem(1077) != 0)
			delitem 1077,1; //Merchant_Voucher_5
		else if (job_merchant_q2 == 6 && countitem(1078) != 0)
			delitem 1078,1; //Merchant_Voucher_6
		else if (job_merchant_q2 == 7 && countitem(1079) != 0)
			delitem 1079,1; //Merchant_Voucher_7
		else if (job_merchant_q2 == 8 && countitem(1080) != 0)
			delitem 1080,1; //Merchant_Voucher_8
		else {
			next;
			set job_merchant_q,9;
			mes " [Kay, Chef de l'Entrepôt] ";
			mes "Attendez une seconde.";
			mes "Où est ce bon?";
			mes "Qu'en avez vous fait?";
			next;
			mes " [Kay, Chef de l'Entrepôt] ";
			mes "Si vous n'avez pas le bon, vous avez échoué à l'épreuve! Vous feriez mieux de parler à Mahnsoo si vous espérez avoir une seconde chance. Faites attention la prochaine fois!";
			close;
		}
		next;
		mes " [Kay, Chef de l'Entrepôt] ";
		mes "...Génial! Tout est parfait! Je vais faire mon rapport au chef de la Guilde. Vous devriez aller retrouver Chef Mahnsoo je crois, non?";
		close2;
		if (job_merchant_q == 6)
			set job_merchant_q,8;
		else if (job_merchant_q == 5)
			set job_merchant_q,7;
		end;
	}
	else if (job_merchant_q == 4 || job_merchant_q == 3 && countitem(1081) == 0 && countitem(1082) == 0 && countitem(1091) == 0) {
		mes " [Kay, Chef de l'Entrepôt] ";
		mes "Huh?";
		mes "Vous êtes de retour?";
		mes "Comment s'est passée la livraison?";
		next;
		if (select("*Triste* J'ai perdu le paquet.:Bien, bien.") == 1) {
			set job_merchant_q,9;
			mes "[Kay, Chef de l'Entrepôt]";
			mes "Vous vous moquez de moi? Vous allez rater votre épreuve si vous avez perdu le paquet!";
			next;
			mes "[Kay, Chef de l'Entrepôt]";
			mes "Awwww naannn mec. Bon, si vous voulez repasser l'épreuve, allez voir Mahnsoo, okay? Vous avez de la chance de pouvoir repasser le test!";
			close;
		}
		mes "[Kay, Chef de l'Entrepôt]";
		mes "Huh...";
		mes "Okay...";
		close;
	}
	else if (job_merchant_q == 4 || job_merchant_q == 3 && countitem(1081) != 0 || countitem(1082) != 0 || countitem(1091) != 0) {
		mes "[Kay, Chef de l'Entrepôt]";
		mes "Hey, que faites vous encore là? Vous devriez être parti en livraison non?";
		next;
		if (select("J'aurais besoin d'un nouveau paquet.:Oh, oui mer...credi. Vous avez raison!") == 1) {
			if (countitem(1081) == 0 && countitem(1082) == 0 && countitem(1083) == 0 && countitem(1091) == 0) {
				mes "[Kay, Chef de l'Entrepôt]";
				mes "Qu--qu--qu--QUOI?";
				mes "Mais il est où votre paquet?";
				mes "Hein?! Il est où??";
				next;
				if (select("*Triste* Je l'ai perdu!:Juste ici.") == 1) {
					set job_merchant_q,9;
					mes "[Kay, Chef de l'Entrepôt]";
					mes "Vous...";
					mes "Vous avez PERDU le colis??!! Mais c'est pas vrai quel crétin!!";
					mes "Vous avez PERDU à votre test également!";
					next;
					mes "[Kay, Chef de l'Entrepôt]";
					mes "*Soupir* Je vous suggère de trouver rapidement une bonne excuse pour quand vous vous présenterez devant le Chef Mahnsoo.";
					close;
				}
				mes "[Kay, Chef de l'Entrepôt]";
				mes "Huh.";
				mes "Je croyais que vous l'aviez perdu.";
				mes "Vous n'avez donc pas besoin d'un nouveau paquet.";
				close;
			}
			mes "[Kay, Chef de l'Entrepôt]";
			mes "*Soupir* Hey, si tu galères à ce point pour si peu. C'est pas la peine d'aller plus loin, on annule tout...";
			if (countitem(1081) != 0)
				delitem 1081,1; //Merchant_Box_1
			else if (countitem(1082) != 0)
				delitem 1082,1; //Merchant_Box_2
			else if (countitem(1091) != 0)
				delitem 1091,1; //Merchant_Box_Etc
			if (job_merchant_q == 4)
				set job_merchant_q,2;
			else if (job_merchant_q == 3)
				set job_merchant_q,1;
			next;
			mes "[Kay, Chef de l'Entrepôt]";
			mes "J'ai besoin d'un peu de temps pour tout remettre en ordre, revenez plus tard.";
			close;
		}
		mes "[Kay, Chef de l'Entrepôt]";
		mes "Mais quel crétin...";
		close;
	}
	else if ((job_merchant_q == 0 || job_merchant_q == 1 || job_merchant_q == 2)) {
		set where_village,0;
		mes "[Kay, Chef de l'Entrepôt]";
		mes "Hey là.";
		mes "Qu'est ce que vous faites ici?";
		next;
		switch(select("Je viens pour le test de la Licence Marchande.:Je cherche du travail à mi-temps.:Rien.")) {
		case 1:
			mes "[Kay, Chef de l'Entrepôt]";
			mes "Je vois.";
			mes "Parfait.";
			mes "Quel est votre nom?";
			mes "" + strcharinfo(0) + "...?";
			next;
			if (job_merchant_q == 0) {
				mes "[Kay, Chef de l'Entrepôt]";
				mes "Huh. Votre nom n'est pas sur ma liste. Avez vous postulé pour être candidat à l'épreuve?";
				next;
				mes "[Kay, Chef de l'Entrepôt]";
				mes "Vous devez d'abords parler avec Chef Mahnsoo dans la pièce d'à côté, okay?";
				close;
			}
			mes "[Kay, Chef de l'Entrepôt]";
			mes "Parfait, voyons ça. Laissez moi vous donnez votre paquet. Maintenant, choisissez le lieu de livraison.";
			next;
			switch(select("Prontera.:Geffen.:Morocc.:Île de Byalan.")) {
			case 1: set .@where_village,1; break;
			case 2: set .@where_village,2; break;
			case 3: set .@where_village,3; break;
			case 4: set .@where_village,4; break;
			}
			mes "[Kay, Chef de l'Entrepôt]";
			mes "Okay, donnez moi le Numéro de Série du paquet s'il vous plait. Si vous voulez annuler, saisissez '0', okay?";
			next;
			while(1) {
				while(1) {
					input .@input;
					if (.@input == 0) {
						mes "[Kay, Chef de l'Entrepôt]";
						mes "Vous êtes sûr de vouloir annuler?";
						if (select("Oui.:Laissez moi ressaisir s'il vous plait.") == 1) {
							mes "Okay, on annule tout pour l'instant.";
							close;
						}
						next;
					}
					else if (.@input < 1000000 || .@input > 5000000) {
						mes "[Kay, Chef de l'Entrepôt]";
						mes "Hey hey. Ce numéro n'est pas valide! Nos numéros de série sont compris entre 1000000 et 5000000.";
						next;
					}
					else 
						break;
				}
				mes "[Kay, Chef de l'Entrepôt]";
				if (.@where_village == 1)
					mes "Votre destination est donc Prontera. Le Numéro de série est le " + .@input + ". Vous confirmez?";
				else if (.@where_village == 2)
					mes "Votre destination est donc Geffen. Phew! C'est pas la porte à côté! Le Numéro de série est le " + .@input + ". Vous confirmez?";
				else if (.@where_village == 3)
					mes "Votre destination est donc Morocc. C'est pas à coté! Le Numéro de série est le " + .@input + ". Vous confirmez?";
				else
					mes "Chanceux! Votre destination est l'Île de Byalan. Le Numéro de série est le " + .@input + ". Vous confirmez?";
				next;
				if (select("Confirmer.:Whoops! je me suis trompé de numéro!") == 1) {
					break;
				}
			}
			if (.@where_village == 1) {
				if (job_merchant_q2 == 1 && .@input == 2485741)
					getitem 1081,1; //Merchant_Box_1
				else if (job_merchant_q2 == 2 && .@input == 2328137)
					getitem 1082,1; //Merchant_Box_2
				else
					getitem 1091,1; //Merchant_Box_Etc
			}
			else if (.@where_village == 2) {
				if (job_merchant_q2 == 3 && .@input == 2989396)
					getitem 1081,1; //Merchant_Box_1
				else if (job_merchant_q2 == 4 && .@input == 2191737)
					getitem 1082,1; //Merchant_Box_2
				else
					getitem 1091,1; //Merchant_Box_Etc
			}
			else if (.@where_village == 3) {
				if (job_merchant_q2 == 5 && .@input == 3012685)
					getitem 1081,1; //Merchant_Box_1
				else if (job_merchant_q2 == 6 && .@input == 3487372)
					getitem 1082,1; //Merchant_Box_2
				else
					getitem 1091,1; //Merchant_Box_Etc
			}
			else {
				if (job_merchant_q2 == 7 && .@input == 3318702)
					getitem 1081,1; //Merchant_Box_1
				else if (job_merchant_q2 == 8 && .@input == 3543625)
					getitem 1082,1; //Merchant_Box_2
				else
					getitem 1091,1; //Merchant_Box_Etc
			}
			if (job_merchant_q == 2)
				set job_merchant_q,4;
			else if (job_merchant_q == 1)
				set job_merchant_q,3;
			mes "[Kay, Chef de l'Entrepôt]";
			mes "Parfait. Prenez soin de ce paquet comme si votre vie en dépendait jusqu'à ce que vous l'ayez donné en main propre à son destinataire. N'oubliez jamais ça!";
			next;
			mes "[Kay, Chef de l'Entrepôt]";
			mes "Bon et bien, je vous souhaite bonne chance.";
			mes "Rappelez vous, vous devez me rapporter le bon de livraison quand vous reviendrez, okay?";
			close;
		case 2:
			mes "[Kay, Chef de l'Entrepôt]";
			mes "Travail à mi-temps? Mais nous n'avons rien de disponible! Nous traversons actuellement quelques difficultés financières, nous ne pouvons pas embaucher de nouveaux employés.";
			close;
		case 3:
			mes "[Kay, Chef de l'Entrepôt]";
			mes "...? Hum..? Hein..? Quoi..?";
			close;
		}
	}
}

morocc_in,140,102,4	script	Apprenti Teinturier#mer	86,{
	if ((job_merchant_q == 4) || (job_merchant_q == 3)) {
		mes "[Apprenti Teinturier]";
		mes "Vous faîtes partie de la Guilde Marchande...? Ah oui! Je vous attendais.";
		next;
		mes "[Apprenti Teinturier]";
		if (countitem(1081) == 1 || countitem(1082) == 1 || countitem(1091) == 1) {
			mes "Okay~";
			mes "Posez le paquet ici ça ira.";
		}
		else {
			mes "Mais...";
			mes "Où est le paquet que j'ai demandé?";
			mes "C'est étrange...";
			close;
		}
		next;
		mes "[Apprenti Teinturier]";
		mes "Voyons-voir le Numéro de Série du colis, juste au cas où.......";
		next;
		mes "[Apprenti Teinturier]";
		if (job_merchant_q2 == 5 && countitem(1081) != 0) {
			mes "3012685...";
			mes "Parfait! C'est exactement ce qu'on avait commandé. Tenez, voici un Reçu.";
			delitem 1081,1; //Merchant_Box_1
			getitem 1077,1; //Merchant_Voucher_5
		}
		else if (job_merchant_q2 == 6 && countitem(1082) != 0) {
			mes "3487372...";
			mes "Parfait! C'est exactement ce qu'on avait commandé. Tenez, voici un Reçu.";
			delitem 1082,1; //Merchant_Box_2
			getitem 1078,1; //Merchant_Voucher_6
		}
		else {
			mes "Excusez moi, mais...";
			if (job_merchant_q2 == 5)
				mes "Hum..... je ne pense pas que ce soit ce que nous avions commandé. Le Numéro de Série était 3012685 je crois...";
			else if (job_merchant_q2 == 6)
				mes "Hum..... je ne pense pas que ce soit ce que nous avions commandé. Le Numéro de Série était 3487372 je crois...";
			else
				mes "Hum..... je ne pense pas que ce soit ce que nous avions commandé. Le Numéro de Série était 3012685 je crois... ou bien 3487372.";
			close;
		}
		if (job_merchant_q == 4)
			set job_merchant_q,6;
		else if (job_merchant_q == 3)
			set job_merchant_q,5;
		next;
		mes "[Apprenti Teinturier]";
		mes "Merci, à une prochaine fois!~";
		close;
	}
	else if (job_merchant_q == 6 || job_merchant_q == 5 && job_merchant_q2 == 6 || job_merchant_q2 == 5) {
		mes "[Apprenti Teinturier]";
		mes "Oh...";
		mes "Vous allez être obligé de revenir avec le bon paquet je pense? Okay alors, faites attention la prochaine fois!";
		close;
	}
	else {
		mes "[Apprenti Teinturier]";
		mes "Monsieur Java Dullihan est le meilleur teinturier de tout le continent!";
		next;
		mes "[Apprenti Teinturier]";
		mes "EEEEeeeeet je suis son étudiant!! Comme je suis fier!!!! Un jour, je serai capable de faire des teintures aussi belles que les siennes!";
		next;
		mes "[Apprenti Teinturier]";
		mes "Bien sûr, j'en suis encore à apprendre les bases, mais un jour, vous verrez...";
		close;
	}
}

geffen_in,155,122,4	script	Membre de la Guilde#mer	47,{
	if (job_merchant_q == 4 || job_merchant_q == 3) {
		mes "[Membre de la Guilde]";
		mes "Oh, bonjour. Vous êtes membre de la Guilde Marchande? C'est vrai?! Ca veut dire que.........";
		next;
		mes "[Membre de la Guilde]";
		if (countitem(1081) == 1 || countitem(1082) == 1 || countitem(1091) == 1) {
			mes "Oh excusez-moi. Vous devez être très fatigué d'avoir voyagé par ce temps estival. Merci beaucoup de vos efforts.";
		}
		else {
			mes "Hum? Où est mon colis?";
			close;
		}
		next;
		mes "[Membre de la Guilde]";
		mes "Voyons-voir le Numéro de Série du colis, juste au cas où.......";
		if (job_merchant_q2 == 3 && countitem(1081) != 0) {
			mes "2989396. OUI! C'est parfait, c'est celui-là. Tenez, voici un Reçu.";
			delitem 1081,1; //Merchant_Box_1
			getitem 1075,1; //Merchant_Voucher_3
		}
		else if (job_merchant_q2 == 4 && countitem(1082) != 0) {
			mes "2191737. OUI! C'est parfait, c'est celui-là. Tenez, voici un Reçu.";
			delitem 1082,1; //Merchant_Box_2
			getitem 1076,1; //Merchant_Voucher_4
		}
		else {
			mes "Hé attendez une minute! Ce n'est pas ce que j'avais commandé!";
			mes "On dirait qu'il est adressé à quelqu'un d'autre. Je pense que vous vous êtes trompé de destination.";
			next;
			mes "[Membre de la Guilde]";
			if (job_merchant_q2 == 3) {
				mes "Le Numéro de Série devrait être 2989396.";
			}
			else if (job_merchant_q2 == 4) {
				mes "Le Numéro de Série devrait être 2191737.";
			}
			else {
				mes "Le Numéro de Série devrait être 2989396 ou bien 2191737.";
			}
			mes "Je ne sais pas comment vous avez pu vous tromper, mais sachez que je suis très mécontent! Où est mon colis?!";
			close;
		}
		if (job_merchant_q == 4)
			set job_merchant_q,6;
		else if (job_merchant_q == 3)
			set job_merchant_q,5;
		next;
		mes "[Membre de la Guilde]";
		mes "Héhéh~ Merci, au revoir.";
		close;
	}
	else if (job_merchant_q == 6 || job_merchant_q == 5 && job_merchant_q2 == 4 || job_merchant_q2 == 3) {
		mes "[Membre de la Guilde]";
		mes "Bonjour, Membre de la Guilde Marchande~";
		mes "Je vous remercie.";
		close;
	}
	else {
		mes "[Membre de la Guilde]";
		mes "Mon paquet devrait arriver d'un instant à l'autre. Huh. Je pense que la Guilde Marchande a déjà fait les démarches pour m'envoyer mon colis...";
		close;
	}
}

prontera,248,42,0	script	Kafra#mer	116,{
	cutin "kafra_02",2;
	if (job_merchant_q == 6 || job_merchant_q == 5) && (job_merchant_q2 == 2 || job_merchant_q2 == 1) {
		mes "[Employée Kafra]";
		mes "Oh! Merci d'avoir fait tout ce chemin..........~";
		close2;
		cutin "",255;
		end;
	}
	else if (job_merchant_q == 4 || job_merchant_q == 3) {
		mes "[Employée Kafra]";
		mes "Oh, une livraison provenant de la Guilde Marchande?";
		mes "Oh, oui, posez le ici ça ira...";
		if (countitem(1081) == 1 || countitem(1082) == 1 || countitem(1091) == 1) {
			mes "Vous devez être fatigué après un si long voyage!";
		}
		else {
			mes "Hum......... Excusez-moi mais... où est le colis que vous deviez nous livrer....?";
			close2;
			cutin "",255;
			end;
		}
		next;
		mes "[Employée Kafra]";
		mes "Voyons voir, le Numéro de Série du colis devrait être...";
		if (job_merchant_q2 == 1 && countitem(1081) != 0) {
			mes "2485741. Parfait! C'est le Paquet que nous avions commandé. Merci beaucoup. Voici votre Reçu.";
			next;
			delitem 1081,1; //Merchant_Box_1
			getitem 1073,1; //Merchant_Voucher_1
		}
		else if (job_merchant_q2 == 2 && countitem(1082) != 0) {
			mes "2328137. Parfait! C'est le Paquet que nous avions commandé. Merci beaucoup. Voici votre Reçu.";
			next;
			delitem 1082,1; //Merchant_Box_2
			getitem 1074,1; //Merchant_Voucher_2
		}
		else {
			mes "Mmmm? Oh, très cher, ce n'est pas ce que nous avions commandé.";
			if (job_merchant_q2 == 1)
				mes "Le numéro de série du colis que nous attendons est 2485741. Je suis certaine que ce n'est pas celui là.";
			else if (job_merchant_q2 == 2)
				mes "Le numéro de série du colis que nous attendons est 2328137. Je suis certaine que ce n'est pas celui là.";
			else
				mes "Le numéro de série du colis que nous attendons est 2485741 ou bien 2328137.";
			next;
			mes "[Employée Kafra]";
			mes "On dirait qu'il y a eu une confusion à l'Entrepôt et ils vous ont remis le mauvais colis. Ce n'est pas très professionnel tout ça... Revenez quand vous aurez le bon colis.";
			close2;
			cutin "",255;
			end;
		}
		if (job_merchant_q == 4)
			set job_merchant_q,6;
		else if (job_merchant_q == 3)
			set job_merchant_q,5;
		mes "[Employée Kafra]";
		mes "Encore merci, et désolée pour tous les dérangements~";
		close2;
		cutin "",255;
		end;
	}
	else {
		mes "[Employée Kafra]";
		mes "Bonjour. Je suis une Employée Kafra.";
		next;
		mes "[Employée Kafra]";
		mes "Comme vous pouvez le voir, l'^3355FFAssociation des Swordsman^000000 a déménagé à Izlude, une ville satellite de Prontera.";
		mes "Je peux vous y conduire en échange de 600 Zenys si vous voulez.";
		next;
		if (select("D'accord.:Annuler.") == 1) {
			if (Zeny < 600) {
				mes "[Employée Kafra]";
				mes "Je suis désolée, mais vous n'avez pas assez d'argent.";
				close2;
				cutin "",255;
				end;
			}
			set zeny,zeny-600;
			set RESRVPTS, RESRVPTS + 37;
			cutin "",255;
			warp "izlude",94,103;
			end;
		}
		close2;
		cutin "",255;
		end;
	}
}

//izlu2dun,106,58,4	script	Kafra Employee#mer	116,{
function	script	F_MercKafra	{
	if ((job_merchant_q == 6 || job_merchant_q == 5) && (job_merchant_q2 == 8 || job_merchant_q2 == 7)) {
		mes "[Employée Kafra]";
		mes "Oh bonjour~";
		mes "Hum, quelle raison peut bien vous obliger à venir dans cet endroit?";
		close2;
		cutin "",255;
		end;
	}
	else if (job_merchant_q == 4 || job_merchant_q == 3) {
		mes "[Employée Kafra]";
		mes "Une livraison provenant de la Guilde Marchande?? Ah oui! J'avais presque oublié.";
		if (countitem(1081) == 1 || countitem(1082) == 1 || countitem(1091) == 1) {
			mes ".... Je pensais avoir commandé un paquet très lourd... mais... on dirait que vous le portez sans difficulté... C'est le moins que l'on puisse dire!";
		}
		else {
			mes "At-Attendez. Pourquoi vous ne portez rien?";
			mes "Où est le colis?";
			close2;
			cutin "",255;
			end;
		}
		next;
		mes "[Employée Kafra]";
		mes "Voyons voir, le Numéro de Série du colis devrait être...";
		if (job_merchant_q2 == 7 && countitem(1081) != 0) {
			mes "3318702. Ah, voilà, c'est ça. C'est ce que nous avions commandé. Merci beaucoup! Voici votre Reçu.";
			delitem 1081,1; //Merchant_Box_1
			getitem 1079,1; //Merchant_Voucher_7
		}
		else if (job_merchant_q2 == 8 && countitem(1082) != 0) {
			mes "3543625. Ah, voilà, c'est ça. C'est ce que nous avions commandé. Merci beaucoup! Voici votre Reçu.";
			delitem 1082,1; //Merchant_Box_2
			getitem 1080,1; //Merchant_Voucher_8
		}
		else {
			mes "......? Excusez-moi? Je pense que vous m'avez remis le mauvais colis.";
			if (job_merchant_q2 == 7)
				mes "Le Numéro de Série était 3318702 je crois... Ce n'est pas celui ci.";
			else if (job_merchant_q2 == 8)
				mes "Le Numéro de Série était 3543625 je crois... Ce n'est pas celui ci.";
			else
				mes "Le Numéro de Série était 3318702 je crois... ou bien 3543625.";
			close2;
			cutin "",255;
			end;
		}
		if (job_merchant_q == 4)
			set job_merchant_q,6;
		else if (job_merchant_q == 3)
			set job_merchant_q,5;
		next;
		if (countitem(1072) != 0 && quest_alb_01 == 0) {
			select("Il y a cette lettre de Mahnsoo, le chef de la Guilde Marchande...");
			delitem 1072,1; //Delivery_Message
			set quest_alb_01,1;
			mes "[Employée Kafra]";
			mes "Oh, mon dieu! Monsieur Mahnsoo m'a écrit une lettre? Merci beaucoup de me l'avoir apportée~";
			mes "Comment va-t-il? Il va bien?";
			mes "Je ne peux pas attendre plus avant de la lire...";
			next;
			mes "[Employée Kafra]";
			mes "J'apprécie vraiment ce que vous avez fait pour moi. Voici un petit cadeau pour vous remercier.";
			getitem 513,3; //Banana
		}
		else {
			mes "[Employée Kafra]";
			mes "Merci beaucoup d'avoir fait tout ce chemin pour la livraison. Prenez soin de vous. Au revoir.";
		}
		close2;
		cutin "",255;
		end;
	}
	return;
}
