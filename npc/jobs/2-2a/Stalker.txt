//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Advance Class jobchanger after kRO Normals.
//===== By: ==================================================
//= Nana
//= finished, optimized and tested by Lupus
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 1.0 +
//===== Description: ========================================= 
//= Advance Class jobchanger after kRO Normals.
//===== Additional Comments: ================================= 
//= v1.1 Made all into functions, additional checks, etc. [Lupus]
//= v1.2 Made numbers into constants. [Vicious]
//= 1.3 Now saves/restores all the quest skills [Lupus]
//============================================================ 

valkyrie,53,58,3	script	Stalker#Valkyrie	747,{
	if (ADVJOB == 0 || Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Stalker]";
			mes "Félicitations.";
			mes "Honneur aux Guerriers!";
			close;
		}
		mes "[Stalker]";
		mes "Hé...";
		mes "Ce n'est pas facile d'être à la fois un héros, tout en devant resté caché dans l'ombre, incompris...";
		next;
		mes "[Stalker]";
		mes "Mais quand les temps s'assombrissent, vos amis savent qu'ils peuvent compter sur vous. Ils ont besoin de vous, et vous avez besoin d'eux.";
		close;
	}
	else if (ADVJOB == Job_Stalker && Class == Job_Thief_High && JobLevel > 39) {
		mes "[Stalker]";
		mes "Le monde a besoin de héros qui marchent sur la ligne entre l'ordre et le chaos.";
		next;
		mes "[Stalker]";
		mes "Souhaitez-vous devenir Stalker?";
		next;
		if (select("Non.:Oui.") == 1) {
			mes "[Stalker]";
			mes "Revenez quand vous vous sentirez prêt.";
			next;
			mes "[Stalker]";
			mes "Honneur aux Guerriers!";
			close;
		}
		if (SkillPoint) {
			mes "[Stalker]";
			mes "Vous n'avez pas terminé votre apprentissage.";
			close;
		}
		jobchange Job_Stalker;
		set ADVJOB,0;
		mes "[Stalker]";
		mes "Félicitations!";
		mes "En tant que Stalker, j'espère que vous poignarderez les bonnes personnes dans le dos.";
		close;
	}
	mes "[Stalker]";
	mes "Bienvenue au Valhalla, le Hall des Honneurs.";
	next;
	mes "[Stalker]";
	mes "Faîtes comme chez vous.";
	mes "Honneur aux Guerriers!";
	close;
}
