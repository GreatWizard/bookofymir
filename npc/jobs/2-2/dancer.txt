//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//===== eAthena Script ======================================= 
//= Dancer Job Quest
//===== By: ==================================================
//= Kalen - Original jAthena
//= Fredzilla - Converted, Lupus
//===== Current Version: ===================================== 
//= 3.7
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Job quest for Dancer classes
//===== Additional Comments: ================================= 
//= 3.2 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//=	No longer uses function "F_BlockHigh"
//= 3.2a Deleted unused variables. [Samuray22]
//= 3.3 Updated waiting room. (Removes global var) [L0ne_W0lf]
//= 3.4 Fixed bad variables, bugreport:1685 [Brainstorm]
//= 3.5 Minor corrections to script. (bugreport:1766) [L0ne_W0lf]
//= 3.6 Applied Correction bug with for loop. [L0ne_W0lf]
//= 3.6a Fixed a little Exploit to bug the quest with a timeout. (bugreport:2252) [Samuray22]
//= 3.7 Added Quest Log commands. [Kisuka]
//============================================================

comodo,180,153,4	script	Sonotora#1	90,{
	mes "[Athena Sonotora]";
	mes "J'ai vu que la fameuse école de danse de Comodo va bientôt rouvrir ses portes.";
	next;
	mes "[Athena Sonotora]";
	mes "Aaaah...";
	mes "Devenir une danseuse étoile sur la scène...";
	next;
	mes "[Athena Sonotora]";
	mes "Je voudrais y entrer mais je ne suis pas sûre que j'ai toutes les qualités pour ça. Je me demande si je devrais essayer quand même...";
	close;
}

comodo,193,151,4	script	Bor Robin#1	86,{
	mes "[Bor Robin]";
	mes "Aah....";
	mes "Une danseuse étoile sur la scène...";
	mes "J'adore les voir devenir des Danseuses sous mes yeux...";
	next;
	mes "[Bor Robin]";
	mes "C'est bon d'être un homme de mon âge en ces temps... Vive le théâtre de Comodo!";
	next;
	mes "[Bor Robin]";
	mes "Hum?";
	mes "Voulez-vous vous y rendre vous aussi? C'est une occasion unique d'observer le changement de métier des Danseuses.";
	next;
	if (select("Y assister:Annuler") == 1) {
		mes "[Bor Robin]";
		mes "Ouiii~";
		mes "Allons-y!";
		close2;
		warp "job_duncer",70,49;
		end;
	}
	mes "[Bor Robin]";
	mes "Eh bien... Si vous ne voulez pas m'accompagner, je ne peux rien y faire.";
	close;
}

job_duncer,43,93,4	script	Aile#da	724,{
	if (Upper == 1) {
		mes "[Aile]";
		mes "Un deux trois quatre,";
		mes "Deux deux trois quatre,";
		mes "Trois quatre trois quatre,";
		mes "Un deux trois quatre.";
		mes "Hum?";
		next;
		mes "[Aile]";
		mes "Je suis désolée mais vous me dérangez à me regarder avec ce drôle d'air.";
		next;
		mes "[Aile]";
		mes ".......";
		mes ".....Eh, mais... Est-ce que je ne vous ai pas déjà rencontré?";
		next;
		mes "[Aile]";
		mes "Euh...";
		mes "C'est bizarre, je ne me souviens plus où je vous ai déjà vu.";
		close2;
		cutin "",255;
		end;
	}
	if (BaseJob != Job_Archer) {
		if (BaseJob == Job_Bard) {
			cutin "job_dancer_eir01",2;
			mes "[Aile]";
			mes "Bienvenue~!";
			mes "Tenez-moi au courant si vous avez de nouvelles chansons. Nous sommes toujours prêtes à créer des revues sur de nouvelles musiques.";
			close2;
			cutin "",255;
			end;
		}
		else if (BaseJob == Job_Dancer) {
			cutin "",2;
			mes "[Aile]";
			mes "Bienvenue~!";
			mes "Comment allez-vous? Est-ce que les gens apprécient vos performances artistiques?";
			close2;
			cutin "",255;
			end;
		}
		cutin "job_dancer_eir03",2;
		mes "[Aile]";
		mes "Bienve... Hein?";
		mes "Hé! Seul le personnel autorisé eut entrer! C'est une école de danse, pas un cirque!";
		next;
		mes "[Aile]";
		mes "Si vous voulez regarder, pourquoi n'iriez-vous pas voir la scène en ville?";
		close2;
		cutin "",255;
		end;
	}
	if (DANC_Q == 0 && sex == 0 && BaseJob == Job_Archer) {
		cutin "job_dancer_eir01",2;
		mes "[Aile]";
		mes "Bienvenue dans l'École de Danse de Comodo~!";
		mes "Nous enseignons de nombreuses danses originaires de pays complètement différents. Nous apportons un petit peu de joie et de bonheur au voyageurs du monde entier.";
		next;
		mes "[Aile]";
		mes "Nous offrons aussi aux apprenties danseuses l'opportunité unique de devenir célèbre dans tout Rune-Midgard! Danser sous les feux de la rampe ne vous ferait pas envie?";
		next;
		mes "[Aile]";
		mes "Mais je dois vous prévenir: notre école est très sélective! Nous n'acceptons que ceux qui ont du talent pour devenir Danseuse.";
		next;
		cutin "job_dancer_eir02",2;
		mes "[Aile]";
		mes "Qu'en dites-vous? Voudriez-vous vous inscrire? Il n'y a que ce formulaire à remplir pour l'inscription – après, vous pourrez suivre une leçon ou deux et voir comment ça se passe.";
		next;
		cutin "job_dancer_eir01",2;
		mes "[Aile]";
		mes "Alors, qu'en dites-vous~?";
		next;
		if (select("Remplir le formulaire.:Ce n'est pas pour moi.") == 1) {
			if (JobLevel > 39) {
				cutin "job_dancer_eir02",2;
				mes "[Aile]";
				mes "Très bon choix!";
				mes "Remplissez juste ce formulaire.";
				next;
				mes "...";
				next;
				mes "...";
				mes "......";
				next;
				mes "^3355FF*Gratte Gratte*^000000";
				next;
				cutin "job_dancer_eir01",2;
				mes "[Aile]";
				mes "Alors vous vous appelez " + strcharinfo(0) + "? C'est un nom tout mimi! Un petit moment, je montre tout ça à la directrice – revenez dans un petit moment, d'accord?";
				close2;
				cutin "",255;
				set DANC_Q,1;
				setquest 7000;
				end;
			}
			else {
				cutin "job_dancer_eir01",2;
				mes "[Aile]";
				mes "Hum...";
				mes "On dirait que vous n'êtes pas encore prête à enter dans notre école. Vous devez avoir au minimum un Niveau de Métier de 40.";
				next;
				mes "[Aile]";
				mes "J'espère que vous vous inscrirez lorsque vous remplirez cette condition.";
				close2;
				cutin "",255;
				end;
			}
		}
		cutin "job_dancer_eir01",2;
		mes "[Aile]";
		mes "Aww~";
		mes "Pensez-y... Et n'hésitez pas à revenir si vous changez d'avis.";
		close2;
		cutin "",255;
		end;
	}
	else if (sex == 1) {
		cutin "job_dancer_eir03",2;
		mes "[Aile]";
		mes "Bienve... Hein?";
		mes "Hé, mais c'est réservé au personnel autorisé! Si vous voulez chanter, vous feriez mieux d'aller trouver un Barde.";
		next;
		mes "[Aile]";
		mes "Les Archers ne peuvent pas tous devenir Danseuses. Tout du moins sans opération chirurgicale~";
		close2;
		cutin "",255;
		end;
	}
// NB: toute la suite est au féminin puisque les persos masculins n'ont pas accès à cette quête
	else if (DANC_Q == 1) {
		cutin "job_dancer_eir01",2;
		mes "[Aile]";
		mes "Bien. Puisque vous vous êtes engagée plus tôt, je vais vous parler de ce dont nous avons besoin pur les leçons.";
		next;
		mes "[Aile]";
		mes "Nous n'avons pas beaucoup de matériel, mais tout ce que vous apporterez sera pour votre usage personnel de toutes façons, alors ne vous en faites pas.";
		next;
		set .@item_nd,rand(1,10);
		if (.@item_nd > 0 && .@item_nd < 3) {
			set DANC_Q,2;
			changequest 7000,7001;
			mes "[Aile]";
			mes "Tout d'abord, le prix des cours est de ^CD688910,000 Zenys^000000. Ensuite, il vous faudra environ ^CD688920 Morves^000000 pour cirer vos chaussures.";
			next;
			mes "[Aile]";
			mes "Il vous faudra aussi^CD68893 Jellopy^000000 et ^CD68895 Potions Rouges^000000 pour la pommade. Et bien sûr, il vous faudra aussi une paire de ^CD6889Chaussures^000000.";
			next;
			mes "[Aile]";
			mes "Je vous redonne la liste:";
			mes "^CD688910,000 Zenys^000000,";
			mes "^CD688920 Morves^000000,";
			mes "^CD68893 Jellopys^000000,";
			mes "^CD68895 Potions Rouges^000000 et";
			mes "^CD68891 Chaussures^000000.";
		}
		else if (.@item_nd == 4) {
			set DANC_Q,3;
			changequest 7000,7002;
			mes "[Aile]";
			mes " Tout d'abord, le prix des cours est de ^CD688910,000 Zenys^000000. Ensuite, il vous faudra ^CD68895 Peaux de Ver-de-Terre^000000 pour polir le sol, et bien sûr une paire de ^CD6889Bottes^000000.";
			next;
			mes "[Aile]";
			mes "Je vous redonne la liste:";
			mes "^CD688910,000 Zenys^000000,";
			mes "^CD68895 Peaux de Ver-de-Terre^000000 et";
			mes "^CD68891 Bottes^000000.";
		}
		else {
			set DANC_Q,4;
			changequest 7000,7003;
			mes "[Aile]";
			mes " Tout d'abord, le prix des cours est de ^CD688910,000 Zenys^000000. Ensuite, il vous faudra ^CD68892 Coquilles de Palourde^000000 pour votre tenue, ^CD68895 Potions Jaunes^000000 et ^CD688920 Jellopys^000000 pour des blessures éventuelles.";
			next;
			mes "[Aile]";
			mes "Il vous faudra aussi ^CD688910 mèches de Cheveux Noirs^000000 pour fabriquer une perruque pour le show et bien sûr une paire de ^CD6889Sandales^000000.";
			next;
			mes "[Aile]";
			mes "Je vous redonne la liste:";
			mes "^CD688910,000 Zenys^000000,";
			mes "^CD68892 Coquilles de Palourde^000000,";
			mes "^CD68895 Potions Jaunes^000000,";
			mes "^CD688920 Jellopys^000000,";
			mes "^CD688910 mèches de Cheveux Noirs^000000 et des";
			mes "^CD6889Sandales^000000.";
		}
		next;
		mes "[Aile]";
		mes "Revenez me voir quand vous aurez tout ça pour que nous puissions commencer les leçons,d'accord?";
		close2;
		cutin "",255;
		end;
	}
	else if (DANC_Q >= 2 && DANC_Q <= 4) {
		switch(DANC_Q) {
		case 2: 
			setarray .@item[0], 938,909,501,2403;
			setarray .@count[0], 20,3,5,1;
			break;
		case 3: 
			setarray .@item[0], 1055,2405;
			setarray .@count[0], 5,1;
			break;
		case 4: 
			setarray .@item[0], 965,503,909,1020,2401;
			setarray .@count[0], 2,5,20,10,1;
			break;
		}
		set .@size, getarraysize(.@item);
		for( set .@i, 0; .@i < .@size; set .@i,.@i+1 ) {
			if( countitem(.@item[.@i]) < .@count[.@i] ) {
				break;
			}
		}
		if( .@i == .@size && Zeny > 9999 ) {
			cutin "job_dancer_eir02",2;
			mes "[Aile]";
			mes "Oh...!";
			mes "Vous avez tout! Bien, dans ce cas laissez-moi encaisser votre participation financière.";
			next;
			cutin "job_dancer_eir01",2;
			set zeny,zeny-10000;
			mes "[Aile]";
			mes "Bien, maintenant, allez voir ^CD6889Bijou^000000, qui est la responsable du casting. Elle vous posera certainement quelques questions";
			set DANC_Q,5;
			if(checkquest(7001) != -1) {
				changequest 7001,7004;
			}
			else if(checkquest(7002) != -1) {
				changequest 7002,7004;
			}
			else {
				changequest 7003,7004;
			}
			close2;
			cutin "",255;
			end;
		}
		else {
			cutin "job_dancer_eir01",2;
			mes "[Aile]";
			mes "Hum...?";
			mes "Vous n'avez pas encore tout? Laissez-moi vous redonner la liste.";
			next;
			mes "[Aile]";
			mes "Il vous faudra...";
			mes "^CD688910,000 Zenys^000000,";
			if (DANC_Q == 2) {
				mes "^CD6889"+.@count[0]+" "+getitemname(.@item[0])+"^000000,";
				mes "^CD6889"+.@count[1]+" "+getitemname(.@item[1])+"^000000,";
				mes "^CD6889"+.@count[2]+" "+getitemname(.@item[2])+"^000000 et";
				mes "^CD6889"+.@count[3]+" "+getitemname(.@item[3])+"^000000.";
			}
			else if (DANC_Q == 3) {
				mes "^CD6889"+.@count[0]+" "+getitemname(.@item[0])+"^000000 et";
				mes "^CD6889"+.@count[1]+" "+getitemname(.@item[1])+"^000000.";
			}
			else {
				mes "^CD6889"+.@count[0]+" "+getitemname(.@item[0])+"^000000,";
				mes "^CD6889"+.@count[1]+" "+getitemname(.@item[1])+"^000000,";
				mes "^CD6889"+.@count[2]+" "+getitemname(.@item[2])+"^000000,";
				mes "^CD6889"+.@count[3]+" "+getitemname(.@item[3])+"^000000 et";
				mes "^CD6889"+.@count[4]+" "+getitemname(.@item[4])+"^000000.";
			}
			close2;
			cutin "",255;
			end;
		}
	}
	else if (DANC_Q == 5) {
		cutin "job_dancer_eir01",2;
		mes "[Aile]";
		mes "Hum...? Vous n'arrivez pas à trouver ^CD6889Bijou^000000?";
		next;
		mes "[Aile]";
		mes "Il faut que vous lui parliez: c'est elle qui est responsable du casting. Ne vous en faites pas, elle devrait être dans l'école.";
		close2;
		cutin "",255;
		end;
	}
	else if (DANC_Q > 5) {
		cutin "job_dancer_eir01",2;
		mes "[Aile]";
		mes "J'ai hâte de vous voir sur une scène~";
		close2;
		cutin "",255;
		end;
	}
	else {
		cutin "job_dancer_eir03",2;
		mes "[Aile]";
		mes "Bienve...Hein?";
		mes "Hé, seuls les membres autorisés peuvent rester ici.";
		next;
		mes "[Aile]";
		mes "Bon, si vous voulez rester, faites-le, mais ne dérangez pas nos danseuses. Tout le monde ici est très occupé à améliorer son style pour pouvoir devenir une grande danseuse.";
		close2;
		cutin "",255;
		end;
	}
}

job_duncer,95,93,4	script	Bijou#da	101,{
	if (SkillPoint) {
		mes "[Bijou]";
		mes "Vous ne pouvez pas changer de profession si vous avez des points de Compétences non dépensés. Utilisez-les et revenez ensuite.";
		close;
	}
	if (BaseJob != Job_Archer) {
		if (BaseJob == Job_Bard) {
			mes "[Bijou]";
			mes "Bienvenue~";
			mes "Oh, un Barde! Avez-vous de nouvelles chansons? Nous sommes toujours prêtes à créer des revues sur de nouvelles musiques.";
			close;
		}
		else if (BaseJob == Job_Dancer) {
			mes "[Bijou]";
			mes "Oh mon...!";
			mes "Bienvenue à la maison~";
			next;
			mes "[Bijou]";
			mes "Alors, que devenez-vous? J'imagine que de nombreuses personnes ont pu profiter de vos danses maintenant. Alors, vous aimez être sous les feux de la rampe?";
			close;
		}
		mes "[Bijou]";
		mes "Oh mon cher~";
		mes "On dirait que vous en avez fait, du chemin, pour venir me voir danser!";
		next;
		mes "[Bijou]";
		mes "Je suis désolée mais je suis à la retraite. J'entraîne maintenant de nouvelles danseuses. Vous pourrez regarder mes étudiantes si vous allez près de la scène centrale~";
		close;
	}
	else if (DANC_Q < 5) {
		mes "[Bijou]";
		mes "Oh mon~";
		mes "Vous voulez devenir une danseuse, n'est-ce pas?";
		next;
		mes "[Bijou]";
		mes "Je sais que vous êtes toute excitée et impatiente, mais vous devez d'abord vous inscrire. Allez trouver Aile, de l'autre côté de la scène. Elle s'occupera de vous.";
		close;
	}
	else if (DANC_Q > 4 && DANC_Q < 7) {
		if (DANC_Q == 5) {
			mes "[Bijou]";
			mes "Oh mon~";
			mes "Vous voulez devenir une danseuse, n'est-ce pas?";
			next;
			mes "[Bijou]";
			mes "Oh! Regardez-moi ce petit ventre rebondi!";
			mes "Bon, ça pourrait être pire. Vous devriez perdre ça en un rien de temps, surtout si c'est moi qui m'occupe de vous~";
			next;
			mes "[Bijou]";
			mes "Mais quand même... On dirait que les critères de beauté ont changé depuis que j'étais jeune. Enfin...";
			next;
			mes "[Bijou]";
			mes "Bon, commençons par un petit entretien. Je vais vous poser que quelques questions de base, ne vous inquiétez pas~";
			next;
			mes "[Bijou]";
			mes "Bien...";
			mes "Commençons.";
			next;
		}
		else {
			mes "[Bijou]";
			mes "Oh, vous revoilà~";
			mes "Avez-vous bien appris votre leçon? Tâchez de faire mieux cette fois, d'accord?";
			next;
		}
		switch(rand(1,3)) {
		case 1:
			mes "[Bijou]";
			mes "1. La Danse ^CD6889Baiser de la Chance^000000 augmente quelle statistique?";
			next;
			if (select("Intelligence:Dextérité:Vitalité:Taux d'Attaques Critiques") == 4)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "2. Laquelle de ces propositions n'est pas une danse?";
			next;
			switch(select("Tango:Claquettes:Hip-Hop:Salsa:Bel Canto")) {
			default:
				set .@da_score,.@da_score-10;
				break;
			case 5:
				set .@da_score,.@da_score+10;
				break;
			}
			mes "[Bijou]";
			mes "3. Laquelle de ces descriptions convient le mieux à une danseuse?";
			next;
			if (select("Une personne qui crie.:Une personne qui parle fort.:Une personne qui danse.:Une personne qui chante.") == 3)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "4. Laquelle de ces propositions ne peut pas êtes associée avec Comodo?";
			next;
			if (select("Ville sur la plage.:École de danse.:Toujours sombre, comme en pleine nuit.:Donjons dans 3 directions.:Beaucoup de Voleurs.") == 5)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "5. Avant Comodo, quel est le nom de la région située au nord-est de l'Île du Phare?";
			next;
			if (select("Plateau d'Elmeth:Plage de Comuko:Plage de Comodo:Marais Ginai") == 3)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "6. Qui est la plus belle des danseuses?";
			next;
			switch(select("" + strcharinfo(0) + ":Bijou:Aile:Bonjour")) {
			case 1:
				mes "[Bijou]";
				mes "...";
				mes "Non mais vous vous êtes regardée?! C'est ^660000completement faux^000000! Vous n'avez pas entendu les autres choix ou quoi? Un point en moins!";
				set .@da_score,.@da_score-10;
				next;
				break;
			case 2:
				set .@da_score,.@da_score+10;
				break;
			default:
				break;
			}
			mes "[Bijou]";
			mes "7. Qui parmi les propositions suivantes peut accompagner une danseuse?";
			next;
			if (select("Assassin:Barde:Alchimiste:Sage") == 2)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "8. Qu'est-ce qui n'est pas une spécialité de Comodo?";
			next;
			if (select("Potion de Furie:Coquille de Palourde:Carapace de Crabe:Pierre Luminescente") == 4)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "9. Qui est le gérant du Casino de Comodo?";
			next;
			if (select("Yoo:Moo:Hoon:Roul") == 2)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "10. Qui prend les inscriptions pour devenir Danseuse?";
			next;
			if (select("Bijou:Aile:Athena:Sonotora") == 2)
				set .@da_score,.@da_score+10;
			break;
		case 2:
			mes "[Bijou]";
			mes "1. Quel est l'effet de l'ensemble ^CD6889Monsieur Kim Le Plein Aux As^000000?";
			next;
			if (select("Mort instantanée du monstre.:Double dommages.:Augmentation de l'expérience.:Augmentation de la vitesse d'attaque.") == 3)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "2. Qu'est-ce qu'il est malvenu de faire après une danse?";
			next;
			if (select("Remercier son partenaire.:Louer son partenaire.:Demander une nouvelle danse.:Critiquer son partenaire.") == 4)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "3. Quelle n'est pas l'attitude à avoir quand son partenaire fait un faux pas pendant une danse?";
			next;
			if (select("Prendre ça avec le sourire et recommencer.:Insister sur le faux pas.:Faire comme si de rien n'était.:Lui sourire.") == 2)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "4. Dans quelle ville peut-on devenir Danseuse?";
			next;
			if (select("Cocomo:Sandarman:Comudo:Comodo") == 4)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "5. Combien de donjons sont reliés à Comodo?";
			next;
			if (select("1:2:3:4") == 3)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "6. Lequel de ces monstres ne peut pas être apprivoisé?";
			next;
			if (select("Isis:Argiope:Dokebi:Deviruchi") == 2)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "7. Laquelle de ces danseuses a le plus de grâce?";
			next;
			switch(select("" + strcharinfo(0) + ":Bijou:Isis:Mercy Bokou")) {
			case 1:
				mes "[Bijou]";
				mes "...";
				mes "Non mais vous vous êtes regardée?! C'est ^660000completement faux^000000! Vous n'avez pas entendu les autres choix ou quoi? Un point en moins!";
				set .@da_score,.@da_score-10;
				next;
				break;
			case 2:
				set .@da_score,.@da_score+10;
				break;
			default:
				break;
			}
			mes "[Bijou]";
			mes "8. Quel est le nom exact de la Kafra de Comodo?";
			next;
			if (select("Quartier Général de la Kafra:Branche Ouest de la Kafra:Services Kafra:Quartiers Généraux Kafra:Branche Ouest de la Kafra") == 4)
				set .@da_score,.@da_score+10;
			mes "[......]";
			mes "9. Quel est mon nom?";
			next;
			if (select("Borjuis:Bourgeois:Bijou:Beruberu") == 3)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "10. Quel est l'effet des ^CD6889Berceuses^000000?"; 
			next;
			if (select("Provoque l'effet Aveuglement.:Provoque l'effet Sommeil.:Provoque l'effet Saignement.:Provoque l'effet Gel.") == 2)
				set .@da_score,.@da_score+10;
			break;
		case 3:
			mes "[Bijou]";
			mes "1. Quel est l'effet de la compétence ^CD6889Leçons de Danse^000000?";
			next;
			switch(select("Augmente l'INT:Augmente l'effet des danses:Augmente les dégâts avec les fouets.:Cause l'effet Etourdissement.")) {
			default:
				break;
			case 2:
			case 3:
				set .@da_score,.@da_score+10;
				break;
			}
			mes "[Bijou]";
			mes "2. Dans quel type de de danse utilise-t-on ses chaussures pour faire un bruit de percussion et taper en rythme?";
			next;
			if (select("Claquettes:Carpette:Croquette:Crapette") == 1)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "3. Laquelle de ces propositions ne caractérise pas les Danseuses?";
			next;
			if (select("Pouvoir danser.:Attaquer à distance.:Manier le fouet.:Manier des épées à deux mains.") == 4)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "4. Dans quelle ville y a-t-il le plus de danseuses?";
			next;
			if (select("Al De Baran:Yuno:Morroc:Comodo") == 4)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "5. Qui danse le mieux parmi les personnes suivantes?";
			next;
			switch(select("" + strcharinfo(0) + ":Bijou:Isis:Guton Tak")) {
			case 1:
				mes "[Bijou]";
				mes "...";
				mes "Non mais vous vous êtes regardée?! C'est ^660000completement faux^000000! Vous n'avez pas entendu les autres choix ou quoi? Un point en moins!";
				set .@da_score,.@da_score-10;
				next;
				break;
			case 2:
				set .@da_score,.@da_score+10;
				break;
			default:
				break;
			}
			mes "[Bijou]";
			mes "6. Dans quel domaine les danseuses sont-elles meilleures que les autres professions?";
			next;
			if (select("Endurance:Comédie:Danse:Magie") == 3)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "7. Qui est le patron du Casino de Comodo?";
			next;
			if (select("Ryu:Moo:Roul:Hoon") == 2)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "8. Quel objet les danseuses ne peuvent-elles pas équiper?";
			next;
			if (select("Oreilles de Chat:Épée à deux Mains:Sandales:Boucle d'Oreille") == 2)
				set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "9. Trouvez-vous ce quiz frustrant et ennuyeux?";
			next;
			select("Oui:Non");
			set .@da_score,.@da_score+10;
			mes "[Bijou]";
			mes "10. Lequel de ces musiciens ne joue pas de Jazz?";
			next;
			if (select("Art Blakey:Billie Holiday :Louis Armstrong :Bud Powell :Neil Amstrong ") == 5)
				set .@da_score,.@da_score+10;
		}
		mes "[Bijou]";
		mes "Bravo~";
		mes "C'était la dernière question~";
		next;
		mes "[Bijou]";
		mes "Alors, voyons...";
		mes "Vous avez fait un score de " + .@da_score + " points...";
		if (.@da_score == 100) {
			set DANC_Q,7;
			mes "Vraiment bien joué!";
			mes "Un score parfait!";
			next;
			mes "[Bijou]";
			mes "Il n'y a pas beaucoup de danseuses qui ont autant de connaissances! Je vous avais mal jugée je crois~";
			next;
			mes "[Bijou]";
			mes "Bien~";
			mes "Il ne vous reste plus qu'à passer l'audition. Vous devriez vous reposer un peu pendant qu'on prépare la scène.";
			close;
		}
		else if (.@da_score > 70) {
			set DANC_Q,7;
			mes "Bon, ce n'est pas parfait mais je vais quand même vous laisser passer.";
			close;
		}
		else {
			set DANC_Q,6;
			mes "Vous.. Vous avez échoué!";
			next;
			mes "[Bijou]";
			mes "C'était trop dur? Ah, quand j'étais jeune tout le monde en savait assez pour passer ce test au moins... Allez replonger le nez dans vos bouquins avant de revenir me voir, d'accord?";
			close;
		}
	}
	else if (DANC_Q == 7) {
		mes "[Bijou]";
		mes "Bien... Êtes-vous prête à passer l'audition? Je peux vous expliquer comment ça va se passer si vous voulez.";
		next;
		if (select("Écouter les instructions.:Se rendre sur la piste.") == 1) {
			mes "[Bijou]";
			mes "Tout d'abord, tout le monde a ^CD68891 minute^000000 pour passer l'audition, et une seule personne passe à la fois^000000. Ne vous inquiétez pas si vous n'avez jamais dansé auparavant~";
			next;
			mes "[Bijou]";
			mes "Une fois dans les coulisses, vous pourrez voir la scène. Tout d'abord, ^CD6889changez l'angle de votre caméra et dirigez-la vers l'avant^000000. Vous pouvez le faire en ^CD6889double-cliquant le bouton droit de la souris^000000.";
			next;
			mes "[Bijou]";
			mes "Sans cela, vous pourriez vous emmêler les pinceaux quand je vous dirai d'aller dans une certaine direction.";
			next;
			mes "[Bijou]";
			mes "Vous devrez attendre votre tour dans la ^CD6889Salle d'Attente^000000. Vous passerez dès que la personne avant vous aura terminé.";
			next;
			mes "[Bijou]";
			mes "Si il y a trop de monde, certains ne pourront pas entrer dans la salle d'attente. Vous n'aurez alors qu'à vous mettre en ligne et faire la queue~";
			next;
			mes "[Bijou]";
			mes "La musique commencera quand vous passerez l'audition. Nous vous dirons aussi dans quelle direction aller. Vous n'aurez qu'à suivre les instructions et vous bouger un peu!";
			next;
			mes "[Bijou]";
			mes "Gardez bien à l'esprit que ^CD6889vous serez disqualifié si vous ne faites pas le bon pas à temps^000000. Attention, l'audition est très stricte~";
			close;
		}
		mes "[Bijou]";
		mes "Eh bien~";
		mes "Bonne chance...!!";
		changequest 7004,7005;
		set DANC_Q,8;
		close2;
		warp "job_duncer",105,109;
		end;
	}
	else if (DANC_Q == 8) {
		mes "[Bijou]";
		mes "Oh mon...";
		mes "Vous avez échoué la dernière fois? Bon ne stressez pas, et suivez le rythme~";
		close2;
		warp "job_duncer",105,109;
		end;
	}
	else if (DANC_Q == 9) {
		if (SkillPoint) {
			mes "[Bijou]";
			mes "Vous ne pouvez pas changer de profession si il vous reste des Points de Compétence. Utilisez-les et revenez me voir.";
			close;
		}
		mes "[Bijou]";
		mes "Oh mon...";
		mes "Je vous ai vu danser plus tôt, vous étiez géniale!";
		next;
		mes "[Bijou]";
		mes "Eh bien vous m'avez prouvée que vous êtes faite pour le métier de Danseuse! Vous allez maintenant changer de profession.";
		next;
		mes "[Bijou]";
		mes "Avec la bénédiction des dieux, vous allez devenir Danseuse! A partir de ce jour, plus personne ne vous quittera sans regret~";
		next;
		set .@jlevel,JobLevel;
		mes "[Bijou]";
		completequest 7006;
		callfunc "Job_Change",Job_Dancer;
		callfunc "F_ClearJobVar";
		mes "Oh...!";
		mes "Vous avez l'air superbe en Danseuse! Félicitations!";
		next;
		mes "[Bijou]";
		mes "Et voici un petit cadeau de ma part. Prenez-le s'il vous plaît.";
		mes "Puissent vos danses apporter un peu de joie et de bonheur dans le coeur de ceux qui y assistent~";
		if (.@jlevel == 50)
			getitem 1953,1; //Line_
		else
			getitem 1950,1; //Rope
		close;
	}
} 

// Waiting Room
//============================================================ 
job_duncer,32,154,1	script	Waiting Room#dance	66,{
OnInit:
	waitingroom "Salle d'Attente",20,"Waiting Room#dance::OnStartArena",1;
	enablewaitingroomevent;
	end;

OnStartArena:
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	donpcevent "dance#return::OnDisable";
	warpwaitingpc "job_duncer",69,110,1;
	donpcevent "Bijou#dance_timer::OnEnable";
	disablewaitingroomevent;
	end;

OnEnable:
	enablewaitingroomevent;
	end;
}

job_duncer,32,154,1	script	Waiting Room#click	66,{
	mes "[Pyorgin]";
	mes "Veuillez patienter dans la salle d'attente. Cliquez sur la bulle de chat pour y entrer.";
	next;
	mes "[Pyorgin]";
	mes "Ceux qui se posent des questions à propos de l'épreuve peuvent observer depuis les coulisses.";
	close;
}

// Dance Timer
//============================================================ 
job_duncer,69,105,0	script	Bijou#dance_timer	-1,{
OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	end;

OnTimer2000:
	mapannounce "job_duncer","Bien, commençons! Détendez-vous, l'épreuve commence dans une minute~",bc_map;
	end;

OnTimer5000:
	mapannounce "job_duncer","En avant!",bc_map;
	end;

OnTimer7000:
	disablenpc "dance#up";
	enablenpc "dance#down";
	enablenpc "dance#left";
	enablenpc "dance#right";
	enablenpc "dance#cen";
	end;

OnTimer8000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","En Arrière!",bc_map;
	end;

OnTimer11000:
	enablenpc "dance#up";
	disablenpc "dance#down";
	enablenpc "dance#left";
	enablenpc "dance#right";
	enablenpc "dance#cen";
	end;

OnTimer12000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","Sur la gauche~!",bc_map;
	end;

OnTimer15000:
	enablenpc "dance#up";
	enablenpc "dance#down";
	disablenpc "dance#left";
	enablenpc "dance#right";
	enablenpc "dance#cen";
	end;

OnTimer16000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","A gauche, puis à droite~!",bc_map;
	end;

OnTimer19000:
	enablenpc "dance#up";
	enablenpc "dance#down";
	enablenpc "dance#left";
	disablenpc "dance#right";
	enablenpc "dance#cen";
	end;

OnTimer20000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","Au milieu de nouveau~ !",bc_map;
	end;

OnTimer23000:
	enablenpc "dance#up";
	enablenpc "dance#down";
	enablenpc "dance#left";
	enablenpc "dance#right";
	disablenpc "dance#cen";
	end;

OnTimer23500:
	donpcevent "Backdancer#1::OnSmile";
	mapannounce "job_duncer","Restez au centre... ",bc_map;
	end;

OnTimer27000:
	donpcevent "Backdancer#1::OnSmile";
	mapannounce "job_duncer","Attention... 'Concentration'!",bc_map;
	end;

OnTimer28500:
	mapannounce "job_duncer","Faites bien attention! ",bc_map;
	end;

OnTimer30000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","Sur la gauche!",bc_map;
	end;

OnTimer34000:
	enablenpc "dance#up";
	enablenpc "dance#down";
	disablenpc "dance#left";
	enablenpc "dance#right";
	enablenpc "dance#cen";
	end;

OnTimer35000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","En arrière!",bc_map;
	end;

OnTimer38500:
	mapannounce "job_duncer","En arrière, puis à droite~ ",bc_map;
	end;

OnTimer40000:
	enablenpc "dance#up";
	enablenpc "dance#down";
	enablenpc "dance#left";
	disablenpc "dance#right";
	enablenpc "dance#cen";
	mapannounce "job_duncer","Restez en place~",bc_map;
	end;

OnTimer43000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","Sur la gauche, au centre, sur la droite, puis en avant!",bc_map;
	end;

OnTimer49000:
	disablenpc "dance#up";
	enablenpc "dance#down";
	enablenpc "dance#left";
	enablenpc "dance#right";
	enablenpc "dance#cen";
	end;

OnTimer50000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","Sur la droite!",bc_map;
	end;

OnTimer53000:
	enablenpc "dance#up";
	enablenpc "dance#down";
	enablenpc "dance#left";
	disablenpc "dance#right";
	enablenpc "dance#cen";
	end;

OnTimer54000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","Sur la gauche, au centre, en arrière, puis en avant~! ",bc_map;
	end;

OnTimer60000:
	disablenpc "dance#up";
	enablenpc "dance#down";
	enablenpc "dance#left";
	enablenpc "dance#right";
	enablenpc "dance#cen";
	end;

OnTimer61000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","Une fois de plus: sur la gauche, au centre, en arrière, puis en avant~ ! ",bc_map;
	end;

OnTimer66000:
	disablenpc "dance#up";
	enablenpc "dance#down";
	enablenpc "dance#left";
	enablenpc "dance#right";
	enablenpc "dance#cen";
	end;

OnTimer67000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","En arrière~!",bc_map;
	end;

OnTimer69000:
	enablenpc "dance#up";
	disablenpc "dance#down";
	enablenpc "dance#left";
	enablenpc "dance#right";
	enablenpc "dance#cen";
	end;

OnTimer70000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","Sur la gauche!",bc_map;
	end;

OnTimer74000:
	enablenpc "dance#up";
	enablenpc "dance#down";
	disablenpc "dance#left";
	enablenpc "dance#right";
	enablenpc "dance#cen";
	end;

OnTimer75000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","Au centre!",bc_map;
	end;

OnTimer80000:
	enablenpc "dance#up";
	enablenpc "dance#down";
	enablenpc "dance#left";
	enablenpc "dance#right";
	disablenpc "dance#cen";
	end;

OnTimer81000:
	donpcevent "Backdancer#1::OnSmile";
	disablenpc "dance#up";
	disablenpc "dance#down";
	disablenpc "dance#left";
	disablenpc "dance#right";
	disablenpc "dance#cen";
	mapannounce "job_duncer","Et maintenant, le final: 'Volée de Flèches!'",bc_map;
	end;

OnTimer82000:
	donpcevent "dance#poring::OnEnable";
	end;

OnTimer89000:
	donpcevent "dance#poring::OnDisable";
	donpcevent "dance#return::OnEnable";
	end;
}

job_duncer,69,110,0	script	dance#return	-1,1,4,{
OnTouch:
	mapannounce "job_duncer","Magnifique, bravo! Retournez voir Bijou!",bc_map;
	set DANC_Q,9;
	changequest 7005,7006;
	warp "comodo",188,162;
	end;

OnDisable:
	disablenpc "dance#return";
	donpcevent "dance#return#2::OnDisable";
	donpcevent "dance#return#3::OnDisable";
	end;

OnEnable:
	enablenpc "dance#return";
	donpcevent "dance#return#2::OnEnable";
	end;
}

job_duncer,66,110,0	script	dance#return#2	-1,1,1,{
OnTouch:
	set DANC_Q,9;
	warp "comodo",188,162;
	end;

OnDisable:
	disablenpc "dance#return#2";
	end;

OnEnable:
	enablenpc "dance#return#2";
	donpcevent "dance#return#3::OnEnable";
	end;
}

job_duncer,72,110,0	script	dance#return#3	-1,1,1,{
OnTouch:
	set DANC_Q,9;
	warp "comodo",188,162;
	end;

OnDisable:
	disablenpc "dance#return#3";
	end;

OnEnable:
	enablenpc "dance#return#3";
	donpcevent "Bijou#dance_timer::OnDisable";
	donpcevent "Waiting Room#dance::OnEnable";
	end;
}

// Dance Move Triggers
//============================================================ 
-	script	dancestep::StepTrigger	-1,1,1,{
OnTouch:
	donpcevent "Backdancer#1::OnOmg";
	mapannounce "job_duncer"," " + strcharinfo(0) + ", vous manquez de rythme... Vous étiez en retard sur le tempo!",bc_map;
	set DANC_Q,8;
	donpcevent "Bijou#dance_timer::OnDisable";
	donpcevent "Waiting Room#dance::OnEnable";
	warp "comodo",188,162;
	end;
}

job_duncer,69,113,0	duplicate(StepTrigger)	dance#up	-1,1,1
job_duncer,69,107,0	duplicate(StepTrigger)	dance#down	-1,1,1
job_duncer,66,110,0	duplicate(StepTrigger)	dance#left	-1,1,1
job_duncer,72,110,0	duplicate(StepTrigger)	dance#right	-1,1,1
job_duncer,69,110,0	duplicate(StepTrigger)	dance#cen	-1,1,1

job_duncer,10,10,0	script	dance#poring	-1,{
OnEnable:
	monster "job_duncer",69,106,--ja--,1002,1,"dance#poring::OnMyMobDead";
	end;

OnMyMobDead:
	mapannounce "job_duncer"," Magnifique, bravo! ",bc_map;
	end;

OnDisable:
	killmonsterall "job_duncer";
	end;

}

// Backup Dancers
//============================================================ 
job_duncer,63,110,4	script	Danseuse::BackDancer	724,{
	end;

OnSmile:
	Emotion e_no1;
	donpcevent "Backdancer#2::OnSmile";
	donpcevent "Backdancer#3::OnSmile";
	donpcevent "Backdancer#4::OnSmile";
	end;

OnOmg:
	Emotion e_omg;
	donpcevent "Backdancer#2::OnOmg";
	donpcevent "Backdancer#3::OnOmg";
	donpcevent "Backdancer#4::OnOmg";
	end;
}

job_duncer,66,113,4	script	Danseuse::Backdancer#2	724,{
	end;

OnSmile:
	Emotion e_no1;
	end;

OnOmg:
	Emotion e_omg;
	end;
}

job_duncer,72,113,4	script	Danseuse::Backdancer#3	724,{
	end;

OnSmile:
	Emotion e_no1;
	end;

OnOmg:
	Emotion e_omg;
	end;
}

job_duncer,75,110,4	script	Danseuse::Backdancer#4	724,{
	end;

OnSmile:
	Emotion e_no1;
	end;

OnOmg:
	Emotion e_omg;
	end;
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.1 Removed the warp I left here my accident, added a check for using 
//=     Improved Concentration and arrow shower, people could get away with it
//=     [Fredzilla]
//= 1.0 I tried to keep as much the same from the Jap version as possible
//=     this turned out to be quite hard, but on the whole it is the same script
//=     I have added some new things, and changed some of the used commands,
//=     along with some optimization. [Fredzilla]
//= 1.5 Added Baby Class Support [Fredzilla]
//= 1.6 Added a func which prevent advanced classes passing
//= 2nd Job Quests again. It also guides adv classes where
//= to go. [Lupus]
//= 2.0 Changed numbers to constants. [Vicious]
//= 2.1 Script check #1. [Lance]
//= 2.2 Fixed unpassable part, thx2 Alis [Lupus]
//= 2.3 Updated initnpctimer to attach player to jobDq script [Skotlex]
//= 2.4 Attached chrid to be able obtain player's SP on mobs death, optimized a bit [Lupus]
//= 2.5 Final fix of chrid issues, changed 'Improved
//=	Concentration' to 'Attention Concentrate' [Lupus]
//= 3.0 Some optimization, typos, added missing dialogues (4Kb lighter) [Lupus]
//= 3.1 Implemented JOBLVL as in other quests [Lupus]
