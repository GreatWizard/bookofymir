//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//===== eAthena Script ======================================= 
//= Star Gladiator Job Quest
//===== By: ================================================== 
//= Celestria, Samuray22
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Job Change quest for Star Gladiator class.
//===== Additional Comments: ================================= 
//= 1.1 Updated to the Aegis Standards. [Samuray22]
//= 1.1a Corrected a minor error respect to the header. [Samuray22]
//= 1.1b Corrected NPC name length warning. [L0ne_W0lf]
//= 1.1c Changed item names to item IDs. [Samuray22]
//= 1.2 Another round of updates to the script. [L0ne_W0lf]
//=	Removes the use of labels, and correcets other errors.
//= 1.3 Added Quest Log commands. [Kisuka]
//============================================================

payon,215,102,3	script	Moohyun#job_star	828,3,3,{
	if (Class == Job_Taekwon) {
		if (STGL_Q == 0) {
			mes "[Moohyun]";
			if (Sex)
				mes "Hé, le Taekwon!";
			else
				mes "Hé, la Taekwon!";
			mes "Venez par ici une minute. J'ai une proposition à vous faire, si vous voulez bien m'écouter!";
			next;
			if (select( "Bien sûr.","Non merci!") == 1) {
				mes "[Moohyun]";
				mes "D'accord, quel est votre nom "+( sex ? "jeune homme" : "jeune femme" )+"?";
				next;
				mes "[" + strcharinfo(0) + "]";
				mes "" + strcharinfo(0) + ".";
				next;
				mes "[Moohyun]";
				mes "Erf! Vous pourriez y mettre un peu de coeur, de chaleur humaine! Vous me dites ça de façon si sèche... Mais dites-moi, avez-vous déjà pensé à ce que vous voudriez devenir?";
				next;
				mes "[Moohyun]";
				mes "Si vous n'avez pas encore pris votre décision, pourquoi ne songeriez-vous pas à devenir ^4D4DFF"+ ( sex ? "un Guerrier" : "une Guerrière" ) +" du Soleil, de la Lune, et des Étoiles^000000? Réfléchissez-y.";
				next;
				mes "[" + strcharinfo(0) + "]";
				mes ( sex ? "Guerrier" : "Guerrière" ) +" de quoi? Oo";
				mes "Je n'ai jamais entendu parler de ce truc! Par contre, je sais que je peux devenir "+( sex ? "Maître" : "Maîtresse" )+" Taekwon ou "+ ( sex ? " Lieur" : "Lieuse" ) +" d'Âmes.";
				next;
				mes "[Moohyun]";
				mes "Oh mais c'est cela: les Maîtres Taekwon sont des Guerriers du Soleil, de la Lune, et des Étoiles. Ils sont empreints du pouvoir du cosmos! Ça en jette, non? Mais dites-moi, seriez -vous intéressé"+( sex ? "" : "e" )+" par l'idée de devenir "+( sex ? "un Maître" : "une Maîtresse" )+" Taekwon?";
				next;
				if (select("Oh oui!:Non, pas vraiment.") == 1) {
					if (JobLevel > 39) {
						mes "[Moohyun]";
						mes "Fantastique~ Je savais que vous verriez les choses sous le même angle que moi. Bien, vous semblez prêt"+ ( sex ? "" : "e" ) +". Il vous suffit maintenant de trouver un Maître qui pourra vous entraîner.";
						next;
						mes "[Moohyun]";
						mes "Voulez-vous vraiment ^4D4dffdevenir "+ ( sex ? "un Guerrier" : "une Guerrière" ) +" Stellaire^000000? Je vous présenterai quelqu'un qui pourra vous aider si c'est le cas.";
						next;
						if (select("Oui, je le veux.","Laissez-moi y réfléchir...") == 1) {
							mes "[Moohyun]";
							mes "Excellent! Comme l'avez surement déjà deviné, les Guerriers Stellaires ne sont pas organisés en guilde. Il nous est donc difficile de tous nous rassembler, mais nous avons aussi moins de contraintes et une plus grande liberté.";
							next;
							mes "[Moohyun]";
							mes "Alors, qui est dans les environs en ce moment...? Hum... Ah, vous devriez aller trouver ^4D4DFFMoogang^000000. C'est l'un des rares Guerriers Stellaires qui accepte de recevoir des apprentis. Il vous guidera pendant votre apprentissage.";
							next;
							mes "[Moohyun]";
							mes "Bien. Vous trouverez Moogang à Comodo, dans l'endroit le plus proche des cieux. Je vais vous écrire une lettre de recommandation que je lui enverrai.";
							set STGL_Q,1;
							setquest 7007;
							close;
						}					
						mes "[Moohyun]";
						mes "D'accord. Changer de profession n'est pas une mince affaire, alors vous devriez y réfléchir longuement. Mais laissez-moi cependant vous dire que vous ne le regretteriez pas si vous embrassiez la voie des Guerriers du Cosmos."; // Biiiiioman, Biiiiioman... Pardon [NoH]
						close;
					}
					mes "[Moohyun]";
					mes "Fantastique~";
					mes "Mais il va d'abord falloir que vous atteignez au minimum le niveau de Métier 40 avant pouvoir de commencer votre entraînement. Vous n'en comprendriez pas les subtilités sinon.";
					next;
					mes "[Moohyun]";
					mes "Je sais que vous pouvez y parvenir. Ça ne devrait pas prendre trop de temps. Promettez-moi de revenir pour que je vous aide à devenir "+( sex ? "un Guerrier" : "une Guerrière" )+" Stellaire~";
					close;
				}
				mes "[Moohyun]";
				mes "Vraiment? Pourtant je pense que la Voie du Guerrier Stellaire vous conviendrait mieux que celle du Lieur d'Âmes. Mais la décision vous appartient...";
				next;
				mes "[Moohyun]";
				mes "Revenez me voir si jamais vous changez d'avis. Vous pourriez faire "+ ( sex ? "un grand Guerrier" : "une grande Guerrière" ) +" Stellaire, vous savez?";
				close;
			}
			mes "[Moohyun]";
			mes "Ah, ne le prenez pas comme ça. Ça ne vous coûte rien d'écouter... Allez, laissez-moi au moins une chance...";
			close;
		}
		else if (STGL_Q == 1) {
			mes "[Moohyun]";
			mes "J'ai déjà envoyé une lettre de recommandation pour vous à Moogang, alors allez le retrouver à Comodo. Il commencera votre entraînement pour devenir "+ ( sex ? "Guerrier" : "Guerrière" ) +" Stellaire.";
			close;
		}
		else if (STGL_Q == 7) {
			mes "[Moohyun]";
			mes "Alors, comment se passe votre entraînement? Oh, mais vous n'avez pas l'air en forme? Est-ce que vous auriez des difficultés?";
			next;
			if (select("Non, ça va.:A propos de la devinette de Beeryu...") == 1) {
				mes "[Moohyun]";
				mes "C'est une bonne chose que d'être indépendant"+( sex ? "" : "e" )+" et de résoudre soi-même vos problèmes, mais vous devriez tout de même apprendre à demander de l'aide quand vous en avez vraiment besoin. Il n'y a pas de honte à avoir...";
				close;		
			}
			mes "[Moohyun]";
			mes "Ah, ça. Beeryu vous a donné une énigme à résoudre... Eh bien tout d'abord, il vous faudra lui apporter quelque chose de vraiment important, mais ce n'est pas un objet matériel.";
			next;
			mes "[Moohyun]";
			mes "Il vous faudra lui prouver quelque chose. Dites-moi... Quand vous rencontrez des obstacles dans la vie, comment réagissez-vous? Que ressentez-vous au plus profond de votre coeur?";
			next;
			mes "[Moohyun]";
			mes "Je sais que Beeryu vous a demandé de lui démontrer votre patience, mais il y a quelque de plus important derrière tout cela. Que vous dites-vous lorsque la situation semble sans issue?";
			next;
			if (select("Je n'abandonnerai pas!:Je... Je ne sais pas.") == 1) {
				mes "[" + strcharinfo(0) + "]";
				mes "Je n'abandonnerai pas! Je réaliserai mes rêves, quoi qu'il en coûte, ou combien de temps ça prendra!";
				next;
				mes "[Moohyun]";
				mes "Oui, c'est cela! Lorsque vous avez une volonté de fer, vous avez la patience d'endurer toute sorte de choses. Démontrez à Beeryu votre résolution...";
				next;
				mes "[Moohyun]";
				mes "Bien, je pense que vous êtes maintenant prêt"+ ( sex ? "" : "e" ) +". Retournez voir Moogang et dirigez-vous vers la Salle de la Lune. Nous pourrons certainement nous saluer d'égal à égal, de Guerrier Stellaire à Guerrier Stellaire la prochaine fois que nous nous verrons.";
				set STGL_Q,8;
				close;
			}
			mes "[Moohyun]";
			mes "Vous... Vous ne savez pas? Si vous rencontrez des obstacles, essayez de surmonter votre peur, confrontez vos ennemis, et faites face à la vie! La réponse viendra alors naturellement.";
			next;
			mes "[Moohyun]";
			mes "Hum... Pourquoi n'iriez-vous pas méditer un moment sur la valeur du courage? Oui, cela vous aidera sans doute.";
			close;
		}
		else if (STGL_Q == 8) {
			mes "[Moohyun]";
			mes "Bien, je pense que vous êtes maintenant prêt"+ ( sex ? "" : "e" ) +". Retournez voir Moogang et dirigez-vous vers la Salle de la Lune. Nous pourrons certainement nous saluer d'égal à égal, de Guerrier Stellaire à Guerrier Stellaire la prochaine fois que nous nous verrons.";
			close;
		}
		mes "[Moohyun]";
		mes "Alors, comment ça a été? Je crois en vous. Je sais que vous deviendrez "+ ( sex ? "un grand Guerrier" : "une grande Guerrière" ) +" Stellaire et que vous accomplirez de grandes choses.";
		close;
	}
	else if (BaseJob == Job_Star_Gladiator) {
		mes "[Moohyun]";
		mes "Hé, comment cela va depuis le temps? Je vois que vous êtes entré"+ ( sex ? "" : "e") +" en communion avec la Nature. Le pouvoir du Cosmos n'est-il pas extraordinaire?";
		close;
	}
	else if (Class== Job_Novice) {
		mes "[Moohyun]";
		mes "Hé, gamin! Ça t'intéresserait d'apprendre le Tae Kwon Do? Je te garantis que tu deviendras super-balèze si tu t'y mets!";
		next;
		mes "[Moohyun]";
		mes "Voyons... Il y a un type du nom de Phoenix qui peut t'apprendre les techniques de base. Il devrait être là, quelque part...";
		next;
		mes "[Moohyun]";
		mes "Bref, vas t'exercer au Tae Kwon Do pendant un moment, et reviens me voir quand tu seras prêt"+ ( sex ? "" : "e" ) +" à percer les mystères subtils de notre art et à tenter de maîtriser plus que ton corps et ton esprit.";
		close;
	}
	mes "[Moohyun]";
	mes "Tu veux quoi, mec?";
	if (Sex) {
		mes "Tu veux te mettre aux arts martiaux, c'est ça? Désolé, mais c'est trop tard pour toi, l'ami~";
	}
	else {
		mes "Oh, pardon... Je n'avais pas... Je veux dire... Vous êtes si charmante! Désolé d'avoir été aussi direct.";
	}
	next;
	mes "[Moohyun]";
	mes "Quoi qu'il en soit... Vous pensez peut-être que je ne suis qu'un petit caïd, et j'avoue que je me comporte comme tel. Mais je suis en fait un Guerrier du Soleil, de la Lune, et des Étoiles.";
	next;
	mes "[Moohyun]";
	mes "C'est trop tard pour vous, mais si jamais vous connaissez quelqu'un qui veut devenir Guerrier Stellaire, envoyez-le-moi. Je le présenterai aux bonnes personnes~";
	close;

OnTouch:
	if (Class == Job_Taekwon) {
		if (STGL_Q == 0) {
			mes "[Moohyun]";
			mes "Hum...? Oh, vous êtes exactement le genre de personne que je recherche. Approchez, j'ai une offre à vous faire, si vous voulez bien m'écouter...";
			close;
		}
	}
	else if (BaseJob == Job_Star_Gladiator) {
		mes "[Moohyun]";
		mes "Comment va la vie, "+ ( sex ? "Guerrier" : "Guerrière" ) +" Stellaire? Continuez d'utiliser votre pouvoir pour défendre des causes justes. Puissiez-vous portez fièrement le titre de "+ ( sex ? "Guerrier" : "Guerrière" ) +" Stellaire où que vous alliez.";
		close;
	}
	mes "[Moohyun]";
	mes "Oh, oh~";
	mes "Reculez-vous un peu, vous voulez bien? Vous me cachez le soleil!";
	close;
}
	
comodo,172,230,3	script	Maître Errant::Wandering Master#job_sta	730,{
	if (Class == Job_Taekwon) {
		if (STGL_Q == 1) {
			mes "[Moogang]";
			mes strcharinfo(0) + "...";
			mes "Je vous attendais. Moohyun m'a prévenu de votre venue, et de votre désir de devenir "+ ( sex ? "un Guerrier" : "une Guerrière" ) +" Stellaire. Je vais faire confiance en son jugement...";
			next;
			mes "[Moogang]";
			mes "Oui, Moohyun est très doué pour reconnaître la force intérieure chez les autres. Vous devriez vous en sortir. Êtes-vous prêt"+ ( sex ? "" : "e" ) +" pour la première épreuve, et pour mettre vos poings et vos pieds au service du Grand Cosmos?";
			next;
			if (select("Oui, laissez-moi passer l'épreuve!:Attendez, je dois y réfléchir!") == 1) {
				if (SkillPoint) {
					mes "[Moogang]";
					mes "Hum? Il vous reste des Points de Compétence? Utilisez-les pour apprendre quelques nouvelles techniques de Tae Kwon Do s'il vous plaît, et revenez me voir ensuite.";
					close;
				}
				mes "[Moogang]";
				mes "Le Tae Kwon Do vous apprend les bases. Le corps, l'âme, et l'esprit... Il ne s'agit pas d'entités séparées, mais d'un tout. Votre moi est en harmonie avec lui-même, mais l'est-il aussi avec le monde extérieur?";
				next;
				mes "[Moogang]";
				mes "Fermez les yeux. Ressentez la brise légère. Écartez les bras et étreignez les cieux. Pouvez-vous le ressentir? Pouvez-vous ressentir le flux discontinu du Cosmos?";
				next;
				mes "[Moogang]";
				mes "Les Lois de la Nature ne peuvent pas être transgressées, mais une fois que vous la sentirez vivre en vous, vous serez à même de ressentir la chaleur du Soleil, de percevoir la lumière de la Lune, et de vous fondre avec les Étoiles. Cette épreuve vous apprendra à entrer en harmonie avec le monde...";
				next;
				mes "[Moogang]";
				mes "Allez de l'avant, et imprégnez-vous du pouvoir de la Nature existant en chaque chose. Apportez-moi le Vent! Apportez-moi l'Eau! Apportez-moi le Feu! Apportez-moi la Terre!";
				next;
				mes "[Moogang]";
				mes "En d'autres mots, il me faudrait...";
				mes "^4D4DFFUn fragment de Foudre Condensée^000000,";
				mes "^4D4DFFUne Mère Nature^000000,";
				mes "^4D4DFFUne Glace Mystique^000000 et";
				mes "^4D4DFFUn Coeur Ardent^000000.";
				mes "Partez, maintenant...";
				set STGL_Q,2;
				changequest 7007,7008;
				close;
			}
			mes "[Moogang]";
			mes "Je respecte votre choix, même si je ne vois aucune raison d'hésiter. Mais à l'instar des phases de la lune, tout grand bouleversement doit suivre le cours de la Nature.";
			close;
		}
		else if (STGL_Q == 2) {
			if (countitem(996) > 0 && countitem(997) > 0 && countitem(995) > 0 && countitem(994) > 0) {
				mes "[Moogang]";
				mes "Ah, vous avez complété la tâche que je vous avais assignée. Très bien. Ressentez-vous le lien entre les éléments en tenant dans vos mains ces fragments de Terre, d'Air, d'Eau, et de Feu?";
				next;
				mes "[Moogang]";
				mes "Le Vent peut rafraîchir grâce à de douces brises, comme il peut dévaster les terres avec des ouragans. La Terre est l'élément sur lequel toute créature vivante vit, mais elle peut aussi engloutir des bâtiments entiers.";
				next;
				mes "[Moogang]";
				mes "De l'Eau naît la vie, mais elle peut aussi la reprendre dans ses flots. Le Feu peut apporter chaleur et réconfort, mais peut aussi réduire la vie en cendres. Telle est la Nature.";
				next;
				mes "[Moogang]";
				mes "Il s'agit du pouvoir caché de la Nature. Comme pour n'importe quelle force, les éléments peuvent être utilisés pour faire le bien comme le mal. Comprenez-vous maintenant pourquoi? Il s'agit de l'équilibre de l'univers.";
				next;
				mes "[Moogang]";
				mes "Cependant, les éléments originels de l'univers composent le Cosmos: le Soleil, la Lune, et les Étoiles. Méditez sur cette vérité pendant un moment...";
				next;
				mes "[Moogang]";
				mes "Avec la compréhension de ce qu'est profondément la Nature vient le respect de toutes choses. Êtes-vous maintenant prêt"+ ( sex ? "" : "e" ) +" à passer la prochaine épreuve? Suivez-moi si c'est le cas, je vous conduirai dans notre sanctuaire.";
				delitem 996,1; //Rough_Wind
				delitem 997,1; //Great_Nature
				delitem 995,1; //Mistic_Frozen
				delitem 994,1; //Flame_Heart
				set STGL_Q,3;
				changequest 7008,7009;
				close;
			}
			mes "[Moogang]";
			mes "Pour votre première épreuve de Guerrier Stellaire, il vous faudra m'apporter des fragments de chacun des éléments. Je veux que vous ressentiez l'harmonie entre ceux-ci.";
			next;
			mes "[Moogang]";
			mes "Il me faudrait...";
			mes "^4D4DFFUn fragment de Foudre Condensée^000000,";
			mes "^4D4DFFUne Mère Nature^000000,";
			mes "^4D4DFFUne Glace Mystique^000000 et";
			mes "^4D4DFFUn Coeur Ardent^000000.";
			mes "Partez, maintenant...";
			close;
		}
		else if (STGL_Q == 3) {
			mes "[Moogang]";
			mes "Êtes-vous prêt"+ ( sex ? "" : "e" ) +" à passer la prochaine épreuve? Suivez-moi si c'est le cas, je vous conduirai dans notre sanctuaire.";
			next;
			if (select("Attendez, j'ai besoin de plus de temps!:Oui, je suis prêt"+ ( sex ? "" : "e" ) +".") == 1) {
				mes "[Moogang]";
				mes "Je vois. Dans ce cas, prévenez-moi quand vous voudrez continuer.";
				close;
			}
			mes "[Moogang]";
			mes "Adressez-vous à Daru une fois arrivé"+ ( sex ? "" : "e" ) +" à l'autel. Il vous guidera. Revenez me voir quand vous aurez passé l'épreuve.";
			close2;
			warp "job_star",34,12;
			end;
		}
		else if (STGL_Q > 3 && STGL_Q < 7) {
			mes "[Moogang]";
			mes "Comme c'est étrange... Vous n'avez pas pu compléter l'épreuve en si peu de temps. Ah, il a dû se passer quelque chose. Je vais vous reconduire auprès de Daru.";
			close2;
			warp "job_star",34,12;
			end;
		}
		else if (STGL_Q == 7) {
			mes "[Moogang]";
			mes "Hum... Ça n'a pas l'air d'aller. Ah, vous devez être en train de penser à l'énigme de Beeryu, n'est-ce pas? Personne d'autre ne peut vous apporte la réponse...";
			next;
			mes "[Moogang]";
			mes "Hum... Vous feriez peut-être mieux d'aller consulter ^4D4DFFMoohyun^000000 à Payon, afin de comprendre la valeur de la patience, et l'épreuve de Beeryu.";
			next;
			mes "[Moogang]";
			mes "Revenez me voir quand vous aurez compris la véritable valeur de la patience. Je vous reconduirai dans la Salle de la Lune pour l'épreuve.";
			close;
		}
		else if (STGL_Q == 8) {
			mes "[Moogang]";
			mes "Ah, je vois que Moohyun vous a aidé à atteindre un nouveau niveau de compréhension. Très bien, très bien. Laissez-moi vous reconduire dans la Salle de la Lune où Beeryu vous attend...";
			close2;
			warp "job_star",100,13;
			end;
		}
		else if (STGL_Q == 9 || STGL_Q == 11) {
			mes "[Moogang]";
			mes "Voulez-vous retourner dans la Salle des Étoiles afin d'achever votre entraînement en tant que "+ ( sex ? "Guerrier" : "Guerrière" ) +" Stellaire?";
			next;
			if (select("Oui, s'il vous plaît.:Plus tard peut-être.") == 1) {
				mes "[Moogang]";
				mes "Gardez votre motivation... Je suis certain que vous deviendrez "+ ( sex ? "un Guerrier" : "une Guerrière" ) +" Stellaire, et peut-être plus rapidement que vous ne le pensez.";
				close2;
				warp "job_star",166,29;
				end;
			}
			mes "[Moogang]";
			mes "Je vois. Dans ce cas, revenez me voir lorsque votre corps, votre esprit, et son âme seront prêts pour cette épreuve.";
			close;
		}
		else if (STGL_Q == 10) {
			mes "[Moogang]";
			mes "Ah, vous êtes venu"+ ( sex ? "" : "e" ) +" parler à Cheehee. Voulez-vous que je vous renvoie dans la Salle des Étoiles?";
			next;
			if (select("Oui, s'il vous plaît.:Peut-être plus tard.") == 1) {
				mes "[Moogang]";
				mes "Gardez votre motivation... Je suis certain que vous deviendrez "+ ( sex ? "un Guerrier" : "une Guerrière" ) +" Stellaire, et peut-être plus rapidement que vous ne le pensez.";
				close2;
				warp "job_star",166,29;
				end;
			}
			mes "[Moogang]";
			mes "Je vois. Dans ce cas, revenez me voir lorsque votre corps, votre esprit, et son âme seront prêts pour cette épreuve.";
			close;
		}
		else if (STGL_Q == 12) {
			mes "[Moogang]";
			mes "Ah, vous revoilà, et bien plus à l'écoute de la Nature qu'autrefois. Oui, je peux le lire dans votre regard. Mais dites-moi, qu'avez-vous appris?";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "Je peux ressentir les liens entre le Soleil, la Lune, et les Étoiles. Tous apportent la lumière, mais de ces différents aspects naissent différents pouvoirs.";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "Du Soleil nait la chaleur, et le réconfort, et il est la source de la vie. La lumière de la Lune nous apporte paix et sérénité, tandis que du miroitement des Étoiles provient l'espoir, même dans les ténèbres de la nuit.";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "Je connais aussi la chaleur destructrice su Soleil, la solitude de la Lune, et la tristesse des Étoiles. Je comprends maintenant la marche du Cosmos!";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "De la rage du Cosmos peut surgir le plus terrifiant des démons, mais de son amour peuvent naître la lumière sacrée et la bénédiction de l'infinitude...";
			next;
			mes "[Moogang]";
			mes "Je ne pourrais pas vous en demander plus. Vous êtes déjà "+ ( sex ? "un Guerrier" : "une Guerrière" ) +" du Soleil, de la Lune, et des Étoiles. Bienvenue parmi les adeptes de notre art, mon ami"+ ( sex ? "" : "e" ) +".";
			completequest 7011;
			callfunc "Job_Change",Job_Star_Gladiator;
			callfunc "F_ClearJobVar";		//clears all job variables for the current player
			next;
			mes "[Moogang]";
			mes "A partir de ce jour, prenez vos décisions de façon très réfléchie, car elles détermineront le reste de votre vie. Et n'oubliez jamais ce chant...";
			next;
			mes "[Moogang]";
			mes "\"^4D4DFFLe Soleil brille les jours pairs.";
			mes "La Lune resplendit les jours impairs.";
			mes "Et tous les cinq jours,";
			mes "Les Étoiles scintillent et nous montrent leur amour.^000000\""; // C'te rime à la con... [NoH]
			next;
			mes "[Moogang]";
			mes "C'est tout ce que je puis vous dire. N'oubliez jamais que nous autres, Guerriers Stellaires, ne pourrions exister sans le Soleil, la Lune, et les Étoiles...";
			close;
		}
		mes "[Moogang]";
		mes "Oh, " + ( isday() ? "bonjour" : "bonsoir" ) + ". Vous étudiez le Tae Kwon Do, n'est-ce pas? Ravi de vous rencontrer. Appelez-moi Moogang. J'étais comme vous autrefois...";
		next;
		mes "[Moogang]";
		mes "Hum... Avez-vous déjà pensé à devenir... Non, attendez.... Hum...";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "Hein...?";
		next;
		mes "[Moogang]";
		mes "Oh, j'ai toujours un peu de mal à parler aux autres personnes. Mais pourquoi n'iriez-vous pas parler à mon cher ami Moohyun?";
		next;
		mes "[Moogang]";
		mes "C'est juste que... Je suis tellement excité! Vous êtes peut-être "+ ( sex ? "le Guerrier" : "la Guerrière" ) +" que je cherchais!";
		close;
	}
	else if (BaseJob == Job_Star_Gladiator) {
		mes "[Moogang]";
		mes "Oh, je suis content de vous revoir. Alors, où étiez-vous? J'imagine que vous étiez dans un endroit bénit par le Soleil, la Lune, et les Étoiles, pas vrai?";
		next;
		mes "[Moogang]";
		mes "Gardez mon conseil à l'esprit, mon ami"+ ( sex ? "" : "e" ) +", et réfléchissez bien avant de prendre des décisions. Je ne voudrais pas que vous le regrettiez plus tard...";
		next;
		mes "[Moogang]";
		mes "Ah, si jamais les Salles du Soleil, de la Lune, ou des Étoiles vous manquent, je peux vous y renvoyer quand vous voulez. Voudriez-vous y retourner?";
		next;
		switch(select("La prochaine fois peut-être.:Dans la Salle du Soleil!:Dans la Salle de la Lune!:Dans la Salle des Étoiles!")) {
		case 1:
			mes "[Moogang]";
			mes "Très bien. Je resterai là. Revenez me voir si jamais vous voulez retourner en ces lieux. À bientôt, "+strcharinfo(0)+"~";
			close;
		case 2:
			mes "[Moogang]";
			mes "Ah, la réconfortante chaleur du Soleil vous manque, n'est-ce pas? Je vais vous y conduire sans attendre...";
			close2;
			warp "job_star",34,12;
			end;
		case 3:
			mes "[Moogang]";
			mes "Hum? Vous ressentez le besoin d'être apaisé"+ ( sex ? "" : "e" ) +" par la lumière réconfortante de la Lune, c'est cela? J'espère qu'elle vous aidera à trouver la paix intérieure...";
			close2;
			warp "job_star",100,13;
			end;
		case 4:
			mes "[Moogang]";
			mes "Ah, je ne compte plus les raisons pour lesquelles quelqu'un voudrait contempler le scintillement des étoiles.... Je vous conduis dans cette salle sans attendre~";
			close2;
			warp "job_star",166,29;
			end;
		}
	}
	mes "[Expert des Arts Martiaux]";
	mes "Aimez-vous l'escalade? L'air frais, la sensation de liberté, la beauté de la Nature...";
	next;
	mes "[Expert des Arts Martiaux]";
	mes "Je ne peux bien sûr apprécier la lune et les étoiles autant que vous le pouvez... Mais que les éléments vous protègent dans vos aventures~";
	close;
}

job_star,29,33,0	script	Daru#job_star	59,{
	if (Class == Job_Taekwon) {
		if (STGL_Q == 3) {
			mes "[Daru]";
			mes "Ah, " + ( isday() ? "bonjour" : "bonsoir" ) + ". Je m'appelle Daru, et je serai votre guide dans cette salle. Ouvrez les yeux, et répondez à mes questions. Commençons, maintenant.";
			next;
			mes "[Daru]";
			mes "Dites-moi. Que voyez-vous";
			next;
			switch(select("Le Soleil.:La Lune.:Les Étoiles.:Je n'en sais rien.")) {
			case 1:
				mes "[Daru]";
				mes "Oui! Il s'agit du soleil! J'imagine que vous pourriez vous représenter l'humanité comme étant les enfants du Soleil. Bien, très bien...";
				set .@sun_point,.@sun_point+10;
				break;
			case 2:
				mes "[Daru]";
				mes "La... La Lune?! Hum... C'est la Salle du Soleil, vous savez...";
				break;
			case 3:
				mes "[Daru]";
				mes "Les Étoiles... J'imagine que vous vous représentez le Soleil comme l'une des millions d'étoiles dans l'univers...";
				set .@sun_point,.@sun_point+5;
				break;
			case 4:
				mes "[Daru]";
				mes "Vous... Vous ne savez pas? Vous devriez ouvrir vos yeux, ainsi que votre esprit. Vous n'auriez rien à perde à risquer une réponse...";
			}
			next;
			mes "[Daru]";
			mes "Rien en ce monde ne pourrait vivre sans le Soleil. Mais savez-vous ce qui dépend le plus de celui-ci?";
			next;
			switch(select("La chaleur.:Le réconfort.:La lumière.:Rien.")) {
			case 1:
				mes "[Daru]";
				mes "C'est exact. Sans la chaleur du soleil, notre monde ne serait pas simplement les royaume des glaces, mais il ne connaîtrait également aucune forme de vie.";
				set .@sun_point,.@sun_point+10;
				break;
			case 2:
				mes "[Daru]";
				mes "Le réconfort...?";
				mes "Ah, oui. La chaleur du soleil nous apporte du réconfort. Et sans cela, la vie vaut-elle vraiment d'être vécue? C'est une bonne réponse.";
				set .@sun_point,.@sun_point+10;
				break;
			case 3:
				mes "[Daru]";
				mes "Oui, nous ne pourrions rien voir sans la glorieuse lumière du Soleil. Nous ne saurions rien. Nous ne serions rien.";
				set .@sun_point,.@sun_point+10;
				break;
			case 4:
				mes "[Daru]";
				mes "Hum... La réponse devrait venir de votre coeur, plutôt que de votre esprit. Tout le monde a besoin de quelque chose venant du Soleil. Voyons...";
				break;
			}
			next;
			mes "[Daru]";
			mes "Il y a un chant qui dit...";
			mes "\"^4D4DFFLe Soleil brille les jours pairs.";
			mes "La Lune resplendit les jours impairs.";
			mes "Et tous les cinq jours,";
			mes "Les Étoiles scintillent et nous montrent leur amour.^000000\""; // C'te rime à la con... [NoH]
			next;
			mes "[Daru]";
			mes "\"^4D4DFFLe désert est un endroit solaire.";
			mes "Sa chaleur embrasse le sable et la terre.";
			mes "Les marais sont des endroits lunaires.";
			mes "Les loups qui y rôdent connaissent cet air.";
			mes "Les profondes grottes sont des endroits stellaires,";
			Mes "Et dans leurs entrailles les Chevaliers battent le fer.^000000\"";
			next;
			mes "[Daru]";
			mes "Bien, maintenant, lequel de ces couples resplendit des milles feux du Soleil? Réfléchissez-y bien...";
			next;
			if (select("Le Marais et les Chevaliers.:Le Désert et le Sable.:Le Désert et les Chevaliers.:Les Grottes Profondes et les Loups.") == 2)
				set .@sun_point,.@sun_point+10;
			mes "[Daru]";
			mes "Bien. Ce sera tout. Je ne peux certainement pas apprendre tout ce qu'il y a à savoir sur vous au travers de ces quelques questions, mais cela devrait suffire pour l'instant.";
			next;
			if (.@sun_point == 30) {
				mes "[Daru]";
				mes "J'admets que je suis impressionné par votre compréhension du Soleil. Il est la source de la vie, le berceau de la chaleur et du réconfort. Laissez-moi maintenant vous conduire dans la Salle de la Lune.";
				set STGL_Q,4;
				close2;
				warp "job_star",100,13;
				end;
			}
			else if (.@sun_point == 25) {
				mes "[Daru]";
				mes "Même si vous n'avez pas une parfaite compréhension du Soleil, vous comprenez l'idée qui se cache derrière. Vous ressentez qu'il est la source de la vie, de la chaleur, et du réconfort.";
				next;
				mes "[Daru]";
				mes "Et c'est grâce à ce savoir que vous allez pouvoir apprendre à entrer en harmonie avec le Cosmos. Suivez-moi de ce côté, maintenant. Je vais vous conduire dans la Salle de la Lune...";
				set STGL_Q,4;
				close2;
				warp "job_star",100,13;
				end;
			}
			mes "[Daru]";
			mes "Hum... Si vous ne comprenez pas la place du Soleil dans l'Univers et dans le monde des hommes, vous ne pourrez jamais entrer en harmonie avec la Nature, avec le Cosmos.";
			next;
			mes "[Daru]";
			mes "Je vous conseille d'aller méditer profondément sur les vérités fondamentales de la Nature avant de revenir me voir. Contemplez le pouvoir infini du Soleil...";
			set STGL_Q,5;
			close;
		}
		else if (STGL_Q == 4) {
			mes "[Daru]";
			mes "Vous n'avez plus besoin de rester ici. Laissez-moi vous conduire dans la Salle de la Lune.";
			close2;
			warp "job_star",100,13;
			end;
		}
		else if (STGL_Q == 5) {
			if (rand(1,5) == 3) {
				mes "[Daru]";
				mes "Hum... Je pense que vous avez assez médité sur la grandeur du Soleil et son importance pour les êtres humains et le monde. Bien. Je vais maintenant vous conduire dans la Salle de la Lune.";
				set STGL_Q,4;
				close2;
				warp "job_star",100,13;
				end;
			}
			mes "[Daru]";
			mes "Détendez chaque muscle de votre corps. Fermez les yeux. Ressentez la chaleur du Soleil sur vos paupières tandis que vous méditez sur son rôle dans l'univers et votre place dans le Cosmos.";
			close;
		}
		else if (STGL_Q == 6) {
			mes "[Daru]";
			mes "Hum? Vous n'avez plus rien à faire dans la Salle du Soleil. Laissez-moi maintenant vous conduire dans celle des Étoiles...";
			close2;
			warp "job_star",166,29;
			end;
		}
		else if (STGL_Q == 7) {
			mes "[Daru]";
			mes "Ah, Beeryu doit vous avoir donné une énigme à résoudre. Eh bien, il va vous falloir retourner en ville pour trouver la réponse. Je vous y renvoie.~";
			close2;
			warp "payon",164,58;
			end;
		}
		else if (STGL_Q == 8) {
			mes "[Daru]";
			mes "Hum? Beeryu vous attend dans la Salle de la Lune. Je vous y reconduis tout de suite.";
			close2;
			warp "job_star",100,13;
			end;
		}
		else if (STGL_Q > 8 && STGL_Q < 12) {
			mes "[Daru]";
			mes "Hum... Je comprends que vous aimiez autant la Salle du Soleil, mais il est temps pour vous de retourner dans la Salle des Étoiles. Je vous y renvoie...";
			close2;
			warp "job_star",166,29;
			end;
		}
		mes "[Daru]";
		mes "Hum... Je comprends que vous aimiez autant la Salle du Soleil, mais vous avez une tâche importante à accomplir. Je vous renvoie en ville...";
		close2;
		warp "payon",164,58;
		end;
	}
	mes "[Daru]";
	mes "Ne dérangez pas ceux qui passent cette épreuve s'il vous plaît. Voudriez-vous retourner en ville?";
	next;
	if (select("Retourner à Payon.:Annuler.") == 1) {
		mes "[Daru]";
		mes "D'accord. Laissez-moi vous y reconduire dans ce cas.";
		close2;
		warp "payon",164,58;
		end;
	}
	mes "[Daru]";
	mes "Bien. Prenez le temps de ressentir la splendeur du Soleil pendant que vous restez ici.";
	close;
}

job_star,95,33,0	script	Beeryu#job_star	106,{
	if (Class == Job_Taekwon) {
		if (STGL_Q == 4) {
			mes "[Beeryu]";
			mes "Bienvenue dans la Salle de la Lune. Je m'appelle Beeryu, et je serai votre guide dans ce sanctuaire. Je vais vous poser une série de questions. Réfléchissez bien et donnez-moi des réponses honnêtes.";
			next;
			mes "[Beeryu]";
			mes "De quelle lune avons-nous le plus besoin, et désirons-nous avant toute chose? Pensez aux différents aspects de la lune, et à la façon dont son apparence peut changer certaines nuits.";
			next;
			switch(select("La Lune Rouge.:La Lune Bleue.:La Lune d'Or.:...?")) {
			case 1:
				mes "[Beeryu]";
				mes "La Lune Rouge..?";
				mes "C'est un présage funeste, qui transforme souvent le ciel en une marée de sang. De la Lune Rouge naissent de sombres sentiments que nous ne voudrions connaître...";
				break;
			case 2:
				mes "[Beeryu]";
				mes "La Lune Bleue...?";
				mes "Ah, oui. C'est une lune calme et paisible, dont la lumière douce nous aide à réfléchir clairement. C'est la lune du repos paisible...";
				set .@moon_point,.@moon_point+10;
				break;
			case 3:
				mes "[Beeryu]";
				mes "La Lune d'Or...?";
				mes "Ah, c'est une lune d'abondance et de richesses. Aspirer à la prospérité est une chose naturelle, et chercher à accomplir ses aspirations est l'un des piliers de la vie.";
				set .@moon_point,.@moon_point+10;
				break;
			case 4:
				mes "[Beeryu]";
				mes "Hum...?";
				mes "Vous n'avez pas d'opinion à propos de la Lune? Vous devriez reprendre confiance en vous-même et me dire quel aspect de la Lune vous préférez...";
			}
			next;
			mes "[Beeryu]";
			mes "Dans quel but vous utiliseriez-vous des pouvoirs de la Lune si vous pouviez les maîtriser?";
			next;
			switch(select("La Justice:L'Amélioration de Soi:la Préservation de la Nature:La Vengeance")) {
			case 1:
				mes "[Beeryu]";
				mes "Oui. Il est toujours d'actualité de se battre pour la Justice. Cependant, souvenez-vous bien que la véritable Justice ne saurait aller sans la Force et la Compassion.";
				set .@moon_point,.@moon_point+10;
				break;
			case 2:
				mes "[Beeryu]";
				mes "L'Amélioration de Soi? C'est une réponse acceptable, mais vous devez toujours garder à l'esprit les raisons qui font que vous voulez devenir plus fort"+ ( sex ? "" : "e" ) +", ou vous vous perdrez en route.";
				set .@moon_point,.@moon_point+10;
				break;
			case 3:
				mes "[Beeryu]";
				mes "La protection de la Nature est une tâche qui incombe à chaque être humain. Cependant, je m'attendais à une autre réponse, venant d'"+ ( sex ? "un expert" : "une experte" ) +" des arts martiaux...";
				break;
			case 4:
				mes "[Beeryu]";
				mes "Céder à la vengeance est parfois tentant, surtout lorsque ce sentiment s'accompagne d'un désir de justice. Cependant, la vengeance est quelque chose d'intrinsèquement ignoble.";
				next;
				mes "[Beeryu]";
				mes "Posez-vous cette question: que feriez-vous après avoir étanché votre soif de vengeance? Faire exploser sa rage cause plus de dégâts à soi-même qu'à ses ennemis...";
				break;
			}
			next;
			mes "[Beeryu]";
			mes "Écoutez attentivement les paroles de ce chant. Je suis certain que Daru vous l'a déjà chanté...";
			next;
			mes "[Beeryu]";
			mes "\"^4D4DFFLe Soleil brille les jours pairs.";
			mes "La Lune resplendit les jours impairs.";
			mes "Et tous les cinq jours,";
			mes "Les Étoiles scintillent et nous montrent leur amour.^000000\""; // C'te rime à la con... [NoH]
			next;
			mes "[Beeryu]";
			mes "\"^4D4DFFLe désert est un endroit solaire.";
			mes "Sa chaleur embrasse le sable et la terre.";
			mes "Les marais sont des endroits lunaires.";
			mes "Les loups qui y rôdent connaissent cet air.";
			mes "Les profondes grottes sont des endroits stellaires,";
			Mes "Et dans leurs entrailles les Chevaliers battent le fer.^000000\"";
			next;
			mes "[Beeryu]";
			mes "D'après ce paroles, quel couple évoque le plus la lumière de la Lune?";
			next;
			if (select("Les Marécages et les Chevaliers.:Le Désert et le Sable.:Les Profondes Grottes et les Chevaliers.:Les Marécages et les Loups.") == 4)
				set .@moon_point,.@moon_point+10;
			mes "[Beeryu]";
			mes "Bien. Je vous ai posé les questions que je voulais vous poser.";
			if (.@moon_point > 20) {
				mes "Vous devez maintenant apprendre à connaître les liens entre la lumière de la Lune et ses ombres. Ce ne sera qu'alors que vous deviendrez un Maître dans cet art.";
				next;
				mes "[Beeryu]";
				mes "La douce lumière de la Lune illumine même le plus sombre des cieux. Les ombres de la Lune compensent la glorieuse lumière du Soleil. La sagesse et le pouvoir. Le calme et la rage. Méditez sur ceci...";
				next;
				mes "[Beeryu]";
				mes "J'ai foi en vous. Je sais que vous deviendrez "+ ( sex ? "un grand Guerrier" : "une grande Guerrière" ) +" Stellaire. Il est maintenant temps de vous rendre dans la Salle des Étoiles. Suiviez-moi, je vous prie...";
				set STGL_Q,6;
				close2;
				warp "job_star",166,29;
				end;
			}
			mes "Vous devez maintenant apprendre le calme et la patience, à l'image du rayonnement de la Lune.";
			next;
			mes "[Beeryu]";
			mes "Réfléchissez-y sérieusement. "+ ( sex ? "Un véritable Guerrier" : "Une véritable Guerrière" ) +" Stellaire sait faire prendre de calme et de discernement dans n'importe quelle situation.";
			next;
			mes "[Beeryu]";
			mes "Je voudrais maintenant que vous saisissiez cette opportunité et que vous méditiez jusqu'à entrer en communion avec la Lune. Respirez profondément. Faites le vide dans votre tête et freinez votre esprit...";
			next;
			mes "[Beeryu]";
			mes "Je vais maintenant vous renvoyer à Payon. Essayez d'y apprendre la ^4D4DFFsignification véritable de la patience^000000. Retourner voir Moogang lorsque vous serez prêt"+( sex ? "" : "e" )+". Il vous reconduira ici.";
			set STGL_Q,7;
			close2;
			warp "payon",164,58;
			end;
		}
		else if (STGL_Q == 5) {
			mes "[Beeryu]";
			mes "Hum... Vous devez d'abord passer la première épreuve dans la Salle du Soleil avant que je puisse vous tester. Laissez-moi vous reconduire dans l'autre salle...";
			close2;
			warp "job_star",34,12;
			end;
		}
		else if (STGL_Q == 6) {
			mes "[Beeryu]";
			mes "Que faites-vous encore ici? La lumière de la pleine lune apporte calme et réconfort, mais vous devez aller de l'avant si vous souhaitez devenir "+ ( sex ? "un Guerrier" : "une Guerrière" ) +" Stellaire. Suivez-moi. Je vais vous accompagner jusqu'à la Salle des Étoiles.";
			close2;
			warp "job_star",166,29;
			end;
		}
		else if (STGL_Q == 7) {
			mes "[Beeryu]";
			mes "Je veux que vous m'apportiez la preuve que vous comprenez la valeur véritable de la patience associée à la Lune. Vous ne pourrez pas devenir "+ ( sex ? "un Guerrier" : "une Guerrière" ) +" Stellaire si vous ne comprenez pas cet état d'esprit...";
			next;
			mes "[Beeryu]";
			mes "Je veux que vous me montriez une preuve concrète et indiscutable. Je vais maintenant vous renvoyer en ville...";
			close2;
			warp "payon",164,58;
			end;
		}
		else if (STGL_Q == 8) {
			mes "[Beeryu]";
			mes "Ah, vous revoilà enfin. Je peux voir dans votre façon d'être que vous avez renforcé votre détermination. Bien. J'espère que vous connaissez maintenant le sens du mot \"patience\"...";
			next;
			mes "[Beeryu]";
			mes "La patience et la détermination sont indispensables pour mener une vie sans regrets. Vous devez croire en vous-même, tout en gardant de la considération pour les autres dans l'harmonie cosmique.";
			next;
			mes "[Beeryu]";
			mes "Apprenez à contrôler vos pouvoirs à travers votre entraînement spirituel. Apprenez à faire preuve d'honneur sans succomber à la vanité ou à l'orgueil. Vous êtes prêt"+( sex ? "" : "e" )+" à entrer dans la Salle des Étoiles. Je vais vous y conduire sans attendre.";
			set STGL_Q,6;
			close2;
			warp "job_star",166,29;
			end;
		}
		else if (STGL_Q > 8 && STGL_Q < 12) {
			mes "[Beeryu]";
			mes "Vous devriez être en train de suivre les enseignement de Cheehee dans la Salle des Étoiles. J'espère que vous deviendrez bientôt "+ ( sex ? "un Guerrier" : "une Guerrière" ) +" Stellaire...";
			close2;
			warp "job_star",166,29;
			end;
		}
		mes "[Beeryu]";
		mes "Que faites-vous encore ici? Vous avez une tâche importante à accomplir. Je vous renvoie en ville...";
		close2;
		warp "payon",164,58;
		end;
	}
	mes "[Beeryu]";
	mes "Ne dérangez pas ceux qui passent cette épreuve s'il vous plaît. Voudriez-vous retourner en ville?";
	next;
	if (select("Retourner à Payon.:Annuler.") == 1) {
		mes "[Beeryu]";
		mes "D'accord. Laissez-moi vous y reconduire dans ce cas.";
		close2;
		warp "payon",164,58;
		end;
	}
	mes "[Beeryu]";
	mes "......";
	mes ".........";
	close;
}

job_star,161,33,0	script	Cheehee#job_star	77,{
	if (Class == Job_Taekwon) {
		if (STGL_Q == 6) {
			mes "[Cheehee]";
			mes "Ne trouvez-vous pas les étoiles magnifiques? Elles sont comme les larmes d'une fillette versées sur le noir infini des cieux nocturnes...";
			set STGL_Q,9;
			close;
		}
		else if (STGL_Q == 9) {
			if (rand(1,5) == 3) {
				mes "[Cheehee]";
				mes "L'épreuve que je vous ai préparée est très simple. Apportez-moi simplement des objets trouvés sur cette terre provenant des Étoiles. Apportez-moi un fragment d'étoile, et de la poussière d'étoile...";
				set STGL_Q,10;
				changequest 7009,7010;
				close;
			}
			mes "[Cheehee]";
			mes "......";
			mes ".........";
			next;
			mes "^3355FFCheehee est comme hypnotisée par la voûte céleste. C'est comme il elle était en train de compter chaque étoile scintillant dans les cieux.^000000";
			close;
		}
		else if (STGL_Q == 10) {
			if (countitem(1000) > 0 && countitem(1001) > 0) {
				mes "[Cheehee]";
				mes "Oh? Vous m'avez apporté ce que je vous avais demandé. Saviez-vous que l'on utilisait l'esprit des étoiles pour renforcer les armes et armures des guerriers avant la bataille?";
				next;
				mes "[Cheehee]";
				mes "Les Étoiles sont liées aux rêves, aux souhaits, aux espoirs, à la magie, et à la romance. Il arrive parfois que des Étoiles naisse la tristesse, mais c'est un type de tristesse doux, presque réconfortant...";
				delitem 1000,1; //Star_Crumb
				delitem 1001,1; //Sparkling_Dust
				set STGL_Q,11;
				close;
			}
			mes "[Cheehee]";
			mes "Les fragments d'étoile... La poussière d'étoile... Vous ne les trouverez ici si vous ne les avez avec vous. Allez, maintenant, et rapportez-moi ce que je vous ai demandé.";
			close2;
			warp "payon",164,58;
			end;
		}
		else if (STGL_Q == 11) {
			mes "[Cheehee]";
			mes "Avez-vous déjà pensé à la mélancolie qui pouvait naître des Étoiles? Ce chant vous aidera peut-être à mieux la percevoir, même si vous l'avez certainement déjà entendu...";
			next;
			mes "[Cheehee]";
			mes "\"^4D4DFFLe Soleil brille les jours pairs.";
			mes "La Lune resplendit les jours impairs.";
			mes "Et tous les cinq jours,";
			mes "Les Étoiles scintillent et nous montrent leur amour.^000000\""; // C'te rime à la con... [NoH]
			next;
			mes "[Cheehee]";
			mes "\"^4D4DFFLe désert est un endroit solaire.";
			mes "Sa chaleur embrasse le sable et la terre.";
			mes "Les marais sont des endroits lunaires.";
			mes "Les loups qui y rôdent connaissent cet air.";
			mes "Les profondes grottes sont des endroits stellaires,";
			Mes "Et dans leurs entrailles les Chevaliers battent le fer.^000000\"";
			next;
			mes "[Cheehee]";
			mes "Maintenant...";
			mes "Lequel de ces couples resplendit dans la lumière stellaire?";
			next;
			if (select("Les Profondes Grottes et le Sable.:Le Désert et le Sable.:Les Profondes Grottes et les Chevaliers.:Le Désert et les Chevaliers.") == 3) {
				mes "[Cheehee]";
				mes "Vous avez raison. Tous les cinq jours, les Chevaliers des Profondes Grottes resplendissent sous la lumière stellaire.";
				next;
				mes "[Cheehee]";
				mes "Si vous réussissez à comprendre les paroles de ce chant, vous devriez percevoir l'essence-même du Tae Kwon Do. Mais souvenez-vous que la lumière a différents aspects...";
				next;
				mes "[Cheehee]";
				mes "La grandeur du Soleil... La tranquillité de la Lune... Le scintillement mélancolique des Étoiles... Tous sont différents, et uniques. Imprégnez-vous de la lumière des Étoiles autant que vous le souhaitez.";
				next;
				mes "[Cheehee]";
				mes "Allez reparler à Moogang lorsque vous serez prêt"+ ( sex ? "" : "e" ) +". Je lui ferai savoir que vous avez passé toutes les épreuves.";
				set STGL_Q,12;
				changequest 7010,7011;
				close;
			}
			mes "[Cheehee]";
			mes "Hum... Vous devez en apprendre plus sur la nature des Étoiles. Vous devez comprendre le Cosmos si vous voulez devenir "+ ( sex ? "un Guerrier" : "une Guerrière" ) +" Stellaire.";
			next;
			mes "[Cheehee]";
			mes "En comprenant les paroles de ce chant, vous comprendrez l'essence-même des Guerriers Stellaires. Et gardez à l'esprit que la lumière a plusieurs aspects...";
			next;
			mes "[Cheehee]";
			mes "La grandeur du Soleil... La tranquillité de la Lune... Le scintillement mélancolique des Étoiles... Tous sont différents, et uniques. Imprégnez-vous de la lumière des Étoiles autant que vous le souhaitez.";
			next;
			mes "[Cheehee]";
			mes "Allez reparler à Moogang lorsque vous serez prêt"+ ( sex ? "" : "e" ) +". Je lui ferai savoir que vous avez passé toutes les épreuves.";
			set STGL_Q,12;
			changequest 7010,7011;
			close;
		}
		else if (STGL_Q == 12) {
			mes "[Cheehee]";
			mes "Voulez-vous quitter la Salle des Étoiles?";
			next;
			if (select("Oui:Non") == 1) {
				mes "[Cheehee]";
				mes "Bien. Dans ce cas, je vais vous reconduire à Payon, la ville la plus proche. À bientôt...";
				close2;
				warp "payon",164,58;
				end;
			}
			mes "[Cheehee]";
			mes "Prenez votre temps et imprégnez-vous de la lumière des Étoiles...";
			close;
		}
		mes "[Cheehee]";
		mes "Les Étoiles sont une chose magnifique. Voudriez-vous retourner à Payon maintenant?";
		next;
		if (select("Oui:Non") == 1) {
			mes "[Cheehee]";
			mes "Bien. Laissez-moi vous raccompagner à Payon.";
			close2;
			warp "payon",164,58;
			end;
		}
		mes "[Cheehee]";
		mes "Prenez votre temps et imprégnez-vous de la lumière des Étoiles...";
		close;
	}
	mes "[Cheehee]";
	mes "Les Étoiles sont une chose magnifique. Voudriez-vous retourner à Payon maintenant?";
	next;
	if (select("Oui:Non") == 1) {
		mes "[Cheehee]";
		mes "Bien. Laissez-moi vous raccompagner à Payon.";
		close2;
		warp "payon",164,58;
		end;
	}
	mes "[Cheehee]";
	mes "Prenez votre temps et imprégnez-vous de la lumière des Étoiles...";
	close;
}

//============================================================ 
// Old changelog
//============================================================ 
//= A temp Star Gladiator Job Changer based on the kRO quest.
//= Quest info from RagnaInfo.
//= Sprites from kRO screenshots, bar two which I just had to guess at.
//= 1.0 Optimized, updated [Lupus]
//============================================================ 
