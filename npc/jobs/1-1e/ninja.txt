//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Tanos, corrigé par Etna
//===== eAthena Script ======================================= 
//= Ninja Job Quest
//===== By: ================================================== 
//= Legionaire, Kisuka
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Ninja Job Change Quest
//===== Additional Comments: ================================= 
//= 1.1 Optimized, updated, checked [Lupus]
//= 1.2 Added Akagi [Playtester]
//= 1.3 Updated dialogs to official ones and optimized [SinSloth]
//= 1.4 Fixed a minor bug [SinSloth]
//= 1.5 Updated to match AEGIS script and Added Quest Log commands. [Kisuka]
//= 1.6 Fixed AEGIS amatsu warp typo. [Kisuka]
//============================================================ 

alberta,30,65,3	script	Akagi	730,{
	if (Class == Job_Novice) {
		if (JobLevel == 10) {
			mes "[Akagi]";
			mes "Humm...";
			mes "Tu dois être venu, car tu sentais que quelqu'un t'attendait ici. Dis moi, voudrais-tu suivre le Chemin de la Patience?";
			next;
			if(select("Non:Oui") == 1) {
				mes "[Akagi]";
				mes "Je vois.";
				mes "A chacun son choix, je suppose. Fais attention à toi.";
				close;
			}
			mes "[Akagi]";
			mes "Très bien.";
			mes "Alors, laisse-moi te guider sur cette voie dès maintenant...";
			close2;
			switch(rand(3)) {
				case 1:
					warp "amatsu",170,229;
					end;
				case 2:
					warp "amatsu",216,188;
					end;
				default:
					warp "amatsu",178,176;
					end;
			}
		}else{
			mes "[Akagi]";
			mes "Hum? Je ne peux t'être d'aucune utilité tant que tu n'es pas devenu un peu plus fort...";
			close;
		}
	}else{
		mes "[Akagi]";
		mes "Humm...";
		mes "Toi et moi...";
		mes "Nous sommes égaux en terme d'habilité au combat. Nous pourrons peut être nous entrainer ensemble de temps en temps.";
		close;
	}
}

que_ng,30,65,3	script	Cougar#nq	730,{
	if (Upper == 2) {
		mes "[Cougar]";
		mes "J... Je n'ai jamais vu un enfant aussi fort que toi!";
		mes "V-vas-t-en, erreur de la nature!";
		close;
	}
	if (Class == Job_Novice) {
		if (JobLevel < 10) {
			mes "[Cougar]";
			mes "Hum? Tu es venu afin de devenir Ninja?";
			mes "Tu n'es pas assez expérimenté, alors reviens quand tu seras plus habitué à combattre les monstres.";
			close;
		}
		if (NINJ_Q == 0) {
			mes "["+strcharinfo(0)+"]";
			mes "Excusez-moi.";
			mes "B-Bonjour?";
			next;
			mes "[Cougar]";
			mes "...............................";
			mes "Comment as-tu fait ça?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Fait quoi? Je n'ai absolument rien fait, je ne pense pas...";
			next;
			mes "[Cougar]";
			mes "C-Comment peux-tu me voir? Je suis sensé être invisible à l'oeil nu. Ah, je sais. Joe le Chat Sauvage a dû t'envoyer pour me tuer! Je ne perdrais pas face à toi! Meurs!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "A-attendez! Je ne sais même pas qui est ce Joe le Chat Sauvage! Calmez-vous, il n'y a aucune raison d'être violent!";
			next;
			mes "[Cougar]";
			mes "Quoi...?";
			mes "Comment as-tu esquivé toutes mes attaques? Tu as du talent, je dois le reconnaitre.";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "...............................";
			mes "Je suis venu ici en espérant pouvoir devenir Ninja.";
			next;
			mes "[Cougar]";
			mes "...Oh. C'est tout?";
			mes "Hmm, tu as un bon potentiel, mais je ne peux pas t'aider pour le moment. J'ai trop d'ennemis, et je ne peux pas baisser ma garde, ne serait-ce qu'une seconde.";
			next;
			mes "[Cougar]";
			mes "Ce Joe le Chat Sauvage est vraiment sans pitié...! Il pourrait débarquer à tout moment! Il ferait tout pour remporter la victoire sur ses ennemis!";
			next;
			mes "[Cougar]";
			mes "Attends, je viens juste de penser à quelque chose. Tu peux peut-être m'aider. Fais ce que je te demande, et je t'apprendrais quelques techniques si tu veux vraiment devenir un Ninja.";
			next;
			if(select("Bien sûr!:Non, merci.") == 2) {
				mes "[Cougar]";
				mes "Hum? Bon, très bien. Cependant, je ne vois pas pourquoi nous ne pouvons pas nous entraider en cette situation difficile...";
				close;	
			}
			mes "[Cougar]";
			mes "Super! Maintenant, j'aimerais demander à Joe le Chat Sauvage s'il serait d'accord pour faire une trêve temporaire. Je sais que nous sommes tous deux sans armes, alors nous devrions d'abord nous équiper convenablement.";
			next;
			mes "[Cougar]";
			mes "Prends cette lettre, et donne-la à Joe le Chat Sauvage, à Einbroch. C'est un maître du camouflage, donc garde les yeux bien ouverts. Ah, et cherche le dans un endroit élevé.";
			next;
			mes "[Cougar]";
			mes "Ouais, Joe le Chat Sauvage a toujours aimé se cacher dans les endroits élevés. Bref, une fois que tu lui auras donné cette lettre, reviens et fais moi savoir sa réponse.";
			set NINJ_Q,1;
			setquest 6015;
			close;
		}
		else if(NINJ_Q == 1) {
			mes "[Cougar]";
			mes "Ci cette tâche n'est pas urgente: s'il te plait, dépêche-toi d'aller à Einbroch et de donner ma lettre à Joe le Chat Sauvage.";
			close;
		}
		else if(NINJ_Q == 2) {
			mes "[Cougar]";
			mes "As-tu donné ma lettre à Joe le Chat Sauvage? J'ai toujours besoin de connaitre sa réponse à ma proposition de trêve. Bref, vas voir si tu peux lui soutirer une réponse.";
			close;
		}
		else if(NINJ_Q == 3) {
			mes "[Cougar]";
			mes "Tu es revenu. Alors, Joe le Chat Sauvage t'as renvoyé ici avec sa réponse? Bien, bien, alors maintenant, lis-la moi s'il te plait.";
			next;
			mes "[Cougar]";
			mes "Quoi...?! Comment peut-il rejeter ma proposition de trêve?! Ça ne peut vouloir dire qu'une chose: il a dû réussir à se fabriquer un autre Kunai. Rah! Je dois rattraper mon retard, ou je suis foutu!";
			next;
			mes "[Cougar]";
			mes "Écoute, tu dois m'aider encore une fois! J'ai besoin que tu me ramènes quelques matériaux pour que je puisse fabriquer mon propre Kunai, afin de combattre Joe le Chat Sauvage. Après cela, je ferai de toi un Ninja.";
			next;
			mes "[Cougar]";
			mes "Tout ce que tu as à faire, c'est de me ramener";
			mes "^3355FF5 Cyfars^000000 et";
			mes "^3355FF1 Phracon^000000.";
			mes "S'il te plait, ramènes-les moi aussi vite que tu peux!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Huh? C'est drôle, Joe le Chat Sauvage m'a demandé de lui ramener exactement les mêmes matériaux.";
			next;
			mes "[Cougar]";
			mes "Malédiction! Alors, ça veut dire... Ça veut dire que tu as aidé Joe à fabriquer son Kunai! Non! J'aurais jamais dû te parler de ça! Eh bien, c'est trop tard maintenant. Il n'y a plus qu'à se dépêcher!";
			set NINJ_Q,4;
			changequest 6017,6018;
			close;
		}
		else if(NINJ_Q == 4) {
			if (countitem(7053) < 5 || countitem(1010) < 1) {
				mes "[Cougar]";
				mes "Dépêche-toi de me ramener";
				mes "^3355FF5 Cyfars^000000 et";
				mes "^3355FF1 Phracon^000000,";
				mes "afin que je puisse fabriquer mon propre Kunai, et l'utiliser contre Joe le Chat Sauvage!";
				close;
			}
			if (Skillpoint != 0) {
				mes "[Cougar]";
				mes "Whoa, whoa...";
				mes "Il te reste encore quelques Points de Compétences. Tu ferais mieux de tous les utiliser avant de changer de profession, d'accord?";
				close;
			}
			mes "[Cougar]";
			mes "Ah, tu es revenu avec tout ce dont j'ai besoin. Tu as été plus vite que ce que je pensais, hein? Bien, comme promis, je vais faire de toi un Ninja.";
			next;
			mes "[Cougar]";
			mes "Laisse-moi me présenter comme il se doit. Je suis le Grand Ninja Cougar de la Touga Ninja Corps, et je suis en charge de l'équipe de recherche partie trouver Sir Kazma.";
			next;
			mes "[Cougar]";
			mes "Sir Kazma est le chef de mon village, mais il s'est enfui. Cela a conduit à un conflit interne au sein de la Ninja Corps. Les choses sont très instables en ce moment...";
			next;
			mes "[Cougar]";
			mes "Je n'ai pas voulu t'accepter en tant que Ninja dans un premier temps à cause de cette situation difficile. Cependant, tu as prouvé que tu mérites amplement de rejoindre le rang des Ninjas.";
			next;
			mes "[Cougar]";
			mes "Selon cette lettre, même Joe a une haute opinion de toi. Souviens-toi simplement qu'en tant que Ninja, ta mission doit être la plus grande de tes priorités. Mais ne laisse pas les objectifs d'une mission remplacer ta conscience.";
			next;
			mes "[Cougar]";
			mes "''La discrétion avant tout.''";
			mes "Pour garder nos secrets dans l'ombre, tu ne peux acheter ou vendre des armes Ninja qu'auprès des vendeurs agréés. Garde bien ça à l'esprit.";
			next;
			mes "[Cougar]";
			mes "A partir d'aujourd'hui, tu es désormais un fier membre de la Touga Ninja Corps. Sois aussi agile que le vent, et aussi silencieux que l'ombre.";
			callfunc "Job_Change",Job_Ninja;
			set NINJ_Q,5;
			getitem 13010,1;	// Asura
			completequest 6018;
			close;
		}
		else {
			mes "[Cougar]";
			mes "Comment vas-tu?";
			mes "Entraîne-toi dur: tu doit être capable de disparaître sans laisser de trace. Si tu peux faire ça, tu auras gagné le respect de tous les Ninjas~";
			close;
		}
	}else{
		if (Class == Job_Ninja) {
			mes "[Cougar]";
			mes "Comment vas-tu?";
			mes "Entraîne-toi dur: tu doit être capable de disparaître sans laisser de trace. Si tu peux faire ça, tu auras gagné le respect de tous les Ninjas~";
			close;
		}else{
			mes "[Cougar]";
			mes "Quoi...?";
			mes "Comment as-tu pu me trouver, caché dans l'ombre?! Tu dois être bien plus qu'un simple aventurier, eh?";
			close;
		}
	}
}

einbroch,184,194,3	script	Homme Mystérieux#nq	 	881,{
	if (NINJ_Q == 1) {
		mes "[Homme Mystérieux]";
		mes "J'ai voyagé dans bien des contrées, mais je n'avais jamais vu un immeuble aussi haut que la Tour d'Einbroch. Tous les bâtiments de ma ville natale sont minuscules en comparaison...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Oh, êtes-vous d'Amatsu? Je recherche quelqu'un de là-bas, nommé Joe le Chat Sauvage.";
		next;
		mes "[Homme Mystérieux]";
		mes "...Non. Non, Je viens d'Izlude et je suis a Einbroch car j'ai besoins de quelques matériaux. Dis moi, pourquoi cherches-tu ce Joe le Chat Sauvage?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Eh bien, je dois lui remettre cette lettre et obtenir sa réponse afin de pouvoir devenir un Ninja.";
		next;
		mes "[Homme Mystérieux]";
		mes "Vraiment? Maintenant que j'y pense, j'ai dû le croiser une ou deux fois dans cette ville. Bien qu'il préfère qu'on l'appelle ''Joe le Léopard Rouge'' plutôt que ''Joe le Chat Sauvage.''";
		next;
		mes "[Homme Mystérieux]";
		mes "J'aimerais vraiment t'aider à le trouver, mais d'abord j'ai besoin de quelques matériaux. Si cela ne t'embête pas, pourrais-tu m'aider à les rassembler? Ensuite, je t'aiderai à trouver Joe.";
		next;
		if(select("Ne vous inquiétez pas, je vais le trouver seul.:Bien sûr, je vais vous aider.") == 1) {
			mes "[Homme Mystérieux]";
			mes "Tu es sûr...?";
			mes "Joe le Léopard Rouge est un maître du camouflage. Tu auras besoin d'aide si tu veux le trouver...";
			close;
		}
		mes "[Homme Mystérieux]";
		mes "Bien, je suis heureux d'entendre ça. Aide-moi à trouver";
		mes "^3355FF5 Cyfars^000000 et";
		mes "^3355FF1 Phracon^000000 s'il te plait.";
		set NINJ_Q,2;
		changequest 6015,6016;
		close;
	}
	else if(NINJ_Q == 2) {
		if (countitem(7053) < 5 || countitem(1010) < 1) {
			mes "[Homme Mystérieux]";
			mes "Ramène-moi";
			mes "^3355FF5 Cyfars^000000 et";
			mes "^3355FF1 Phracon^000000";
			mes "dès que tu pourras. Ensuite, je t'aiderai à trouver Joe le Léopard Rouge.";
			close;	
		}
		mes "[Homme Mystérieux]";
		mes "Bien, bien. Tu as ramené les matériaux... Maintenant, c'est à mon tour de t'aider. Voila, laisse-moi voir cette lettre.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "?????!!";
		next;
		mes "[Homme Mystérieux]";
		mes "Ben quoi? N'as-tu pas une lettre de Cougar pour moi?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Êtes-vous...";
		mes "Êtes-vous Joe le Chat Sauvage?";
		next;
		mes "[Homme Mystérieux]";
		mes "...Oui, mais je préfère qu'on m'appelle Joe le Léopard Rouge. Cougar t'envoie, c'est ça? C'est le seul qui m'appelle comme ça. Alors tu veux être un Ninja, hein? Hum, très bien...";
		next;
		mes "[Joe le Léopard Rouge]";
		mes "Si tu veux devenir un Ninja, il faudra toujours que tu fasses attention à ce que tu vois, et en quoi tu crois. N'oublie pas que si tes secrets sont découverts, tu ne seras plus un Ninja.";
		next;
		mes "[Joe le Léopard Rouge]";
		mes "Souviens-toi de toujours te déplacer rapidement, et de toujours disparaitre sans laisser de trace. Rester caché dans l'ombre est vraiment notre pouvoir ultime.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Je vois...";
		mes "...........";
		next;
		mes "[Joe le Léopard Rouge]";
		mes "Pour l'instant, laisse-moi lire cette lettre. Voyons voir... Hum. Je pensais que Cougar voulait me défier à nouveau, mais il souhaite une trêve temporaire, en fait! Hah!";
		next;
		mes "[Joe le Léopard Rouge]";
		mes "Merci pour ton aide. J'ai les matériaux qu'il me faut. Je n'ai plus qu'a fabriquer mon Kunai! Hahaha! Je ne ferais jamais de trêve si j'ai l'avantage!";
		next;
		mes "[Joe le Léopard Rouge]";
		mes "Laisse-moi lui écrire ma réponse. Je vais aussi lui faire part de mes recommandations... Je pense que tu ferais un très bon Ninja, même si je t'ai berné tout à l'heure. Heh heh heh!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "......";
		mes ".........";
		mes "............";
		next;
		mes "[Joe le Léopard Rouge]";
		mes "Voilà!";
		mes "Ramène cette lettre à Cougar s'il te plait. Ça prendra du temps de retourner à Amatsu, alors laisse-moi t'y envoyer directement...";
		delitem 1010,1;	// Phracon
		delitem 7053,5;	// Cyfar
		set NINJ_Q,3;
		changequest 6016,6017;
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if(NINJ_Q == 3) {
		mes "[Joe le Léopard Rouge]";
		mes "Eh? Je ne suis pas sûr de ce qui s'est passé, mais il semble que tu n'aies pas donné ma réponse a Cougar. Puis-je te renvoyer à Amatsu dès maintenant?";
		next;
		if(select("Non, merci.:Oui, s'il vous plait.") == 1) {
			mes "[Joe le Léopard Rouge]";
			mes "Très bien.";
			mes "J'essayais juste de te faire économiser un peu de temps.";
			close;
		}
		mes "[Joe le Léopard Rouge]";
		mes "Bien. A bientôt dans ce cas.";
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if(NINJ_Q == 4) {
		mes "[Joe le Léopard Rouge]";
		mes "Cougar t'as demandé de lui ramener des matériaux aussi? Oh, et bien, je suppose que je ne peux pas lui reprocher. Au moins, je pourrai le battre à la loyale~";
		close;
	}
	else if(NINJ_Q == 5 && Class == Job_Ninja) {
		mes "[Joe le Léopard Rouge]";
		mes "Oh, tu es un Ninja~";
		mes "J'espère que tu continueras à t'entrainer pour ainsi maitriser autant de compétences de Ninja que possible. Souviens-toi de toujours te fondre dans l'ombre.";
		close;
	}
	else {
		mes "[Touriste]";
		mes "J'ai voyagé dans bien des contrées, mais je n'avais jamais vu un immeuble aussi haut que la Tour d'Einbroch. Tous les bâtiments de ma ville natale sont minuscules en comparaison...";
		close;
	}
}
