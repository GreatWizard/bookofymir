//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Tanos, corrigé par Etna
//===== eAthena Script ======================================= 
//= Ninja Job Quest
//===== By: ================================================== 
//= Legionaire, Kisuka
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Ninja Job Change Quest
//===== Additional Comments: ================================= 
//= 1.1 Optimized, updated, checked [Lupus]
//= 1.2 Added Akagi [Playtester]
//= 1.3 Updated dialogs to official ones and optimized [SinSloth]
//= 1.4 Fixed a minor bug [SinSloth]
//= 1.5 Updated to match AEGIS script and Added Quest Log commands. [Kisuka]
//============================================================ 

alberta,30,65,3	script	Akagi	730,{
	if (Class == Job_Novice) {
		if (JobLevel == 10) {
			mes "[Akagi]";
			mes "Humm...";
			mes "Tu dois être venu, car tu sentais que quelqu'un t'attendait ici. Dis moi, cherches-tu le chemin de la patience?";
			next;
			if(select("Non:Oui") == 1) {
				mes "[Akagi]";
				mes "Je vois.";
				mes "A chacun son choix, je suppose. Fais attention à toi.";
				close;
			}
			mes "[Akagi]";
			mes "Très bien.";
			mes "Alors, laisse-moi te guider sur cette voie dès maintenant...";
			close2;
			switch(rand(3)) {
				case 1:
					warp "amatsu",170,229;
					end;
				case 2:
					warp "amatsu",216,188;
					end;
				default:
					warp "amatsu",178,167;
					end;
			}
		}else{
			mes "[Akagi]";
			mes "Hum? Je ne peux t'être d'aucune utilité tant que tu ne seras pas un peu plus fort...";
			close;
		}
	}else{
		mes "[Akagi]";
		mes "Humm...";
		mes "Toi et moi...";
		mes "Nous sommes égaux en terme d'habilité au combat. Nous pourrons peut être nous entrainer ensemble de temps en temps.";
		close;
	}
}

que_ng,30,65,3	script	Cougar#nq	730,{
	if (Upper == 2) {
		mes "[Cougar]";
		mes "J... Je n'ai jamais vu un enfant aussi fort que toi!";
		mes "V-vas-t-en, erreur de la nature!";
		close;
	}
	if (Class == Job_Novice) {
		if (JobLevel < 10) {
			mes "[Cougar]";
			mes "Hm? Tu es venu afin de devenir Ninja?";
			mes "Tu n'es pas assez expérimenté, alors reviens quand tu seras plus habitué à combattre les monstres.";
			close;
		}
		if (NINJ_Q == 0) {
			mes "["+strcharinfo(0)+"]";
			mes "Excusez-moi.";
			mes "B-Bonjour?";
			next;
			mes "[Cougar]";
			mes "...............................";
			mes "Comment as-tu fais ça?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Fais quoi? Je n'ai absolument rien fait, je ne pense pas...";
			next;
			mes "[Cougar]";
			mes "C-Comment peux-tu me voir? Je suis supposé être invisible à l'oeil nu. Ah, je sais. Wildcat Joe a dû t'envoyer pour me tuer! Je ne perdrais pas face à toi! Meurs!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "A-attendez! Je ne sais même pas qui est ce Wildcat Joe! Calmez-vous, il n'y a aucune raison d'être violent!";
			next;
			mes "[Cougar]";
			mes "Quoi...?";
			mes "Comment as-tu esquivé toutes mes attaques? Tu as du talent, je dois le reconnaitre.";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "...............................";
			mes "Je suis venu ici en espérant pouvoir devenir Ninja.";
			next;
			mes "[Cougar]";
			mes "...Oh. C'est tout?";
			mes "Hmm, tu as un bon potentiel, mais je ne peux pas t'aider pour le moment. J'ai trop d'ennemis, et je ne peux pas baisser ma garde, ne serait-ce qu'une seconde.";
			next;
			mes "[Cougar]";
			mes "Ce Wildcat Joe est vraiment sans pitié...! Il pourrait débarquer à tout moment! Il ferait tout pour remporter la victoire sur ses ennemis!";
			next;
			mes "[Cougar]";
			mes "Attends, je viens juste de penser à quelque chose. Tu peux peut-être m'aider. Fais ce que je te demande, et je t'apprendrais quelques techniques si tu veux vraiment devenir un Ninja.";
			next;
			if(select("Bien sûr!:Non, merci.") == 2) {
				mes "[Cougar]";
				mes "Hm? Bon, très bien. Cependant, je ne vois pas pourquoi nous ne pouvons pas nous entraider en cette situation difficile...";
				close;	
			}
			mes "[Cougar]";
			mes "Super! Maintenant, j'aimerais demander à Wildcat Joe s'il serait d'accord pour faire une trève temporaire. Je sais que nous sommes tous deux sans armes, alors nous devrions d'abord nous équipper convenablement.";
			next;
			mes "[Cougar]";
			mes "Prends cette lettre, et donne-la à Wildcat Joe, à Einbroch. C'est un maître du camouflage, donc garde les yeux bien ouverts. Ah, et cherche le dans un endroit élevé.";
			next;
			mes "[Cougar]";
			mes "Ouais, Wildcat Joe a toujours aimé se cacher dans les endroits élevés. Bref, une fois que tu lui auras donné cette lettre, reviens et fais moi savoir sa réponse.";
			set NINJ_Q,1;
			setquest 6015;
			close;
		}
		else if(NINJ_Q == 1) {
			mes "[Cougar]";
			mes "Même si cette tâche n'est pas urgente, s'il te plait, dépèche-toi d'aller à Einbroch et de donner ma lettre à Wildcat Joe.";
			close;
		}
		else if(NINJ_Q == 2) {
			mes "[Cougar]";
			mes "As-tu donné ma lettre à Wildcat Joe? J'ai toujours besoin de connaitre sa réponse à ma proposition de trève. Bref, va voir si tu peux lui soutirer une réponse.";
			close;
		}
		else if(NINJ_Q == 3) {
			mes "[Cougar]";
			mes "Tu es revenu. Alors, Wildcat Joe t'as renvoyé ici avec sa réponse? Bien, bien, alors maintenant, lis-la moi s'il te plait.";
			next;
			mes "[Cougar]";
			mes "Quoi...?! Comment peut-il rejeter ma proposition de trève?! Ça ne peut vouloir dire qu'une chose: il a du réussir à se fabriquer un autre Kunai. Rah! Je dois rattraper mon retard, ou je suis foutu!";
			next;
			mes "[Cougar]";
			mes "Écoute, tu dois m'aider encore une fois! J'ai besoin que tu me ramènes quelques matériaux pour que je puisse fabriquer mon propre Kunai, afin de combattre Wildcat Joe. Après celà, je ferais de toi un Ninja.";
			next;
			mes "[Cougar]";
			mes "Tout ce que tu as à faire, c'est de me ramener";
			mes "^3355FF5 Cyfars^000000 et";
			mes "^3355FF1 Phracon^000000.";
			mes "S'il te plait, ramènes-les moi aussi vite que tu peux!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Huh? C'est drôle, Wildcat Joe m'a demandé de lui ramener exactement les mêmes matériaux.";
			next;
			mes "[Cougar]";
			mes "Malédiction! Alors, ca veut dire... Que tu as aidé Joe à fabriquer son Kunai! Non! J'aurais du te parler de ça plus tôt! Et bien, c'est trop tard maintenant. Il n'y a plus qu'à se d-dépêcher!";
			set NINJ_Q,4;
			changequest 6017,6018;
			close;
		}
		else if(NINJ_Q == 4) {
			if (countitem(7053) < 5 || countitem(1010) < 1) {
				mes "[Cougar]";
				mes "Dépèche toi de me ramener";
				mes "^3355FF5 Cyfars^000000 et";
				mes "^3355FF1 Phracon^000000,";
				mes "afin que je puisse fabriquer mon propre Kunai, et l'utiliser contre Wildcat Joe!";
				close;
			}
			if (Skillpoint != 0) {
				mes "[Cougar]";
				mes "Whoa, whoa...";
				mes "Il te reste encore quelques Points de Compétences. Tu ferais mieux de tous les utiliser avant de changer de job, d'accord?";
				close;
			}
			mes "[Cougar]";
			mes "Ah, tu es revenu avec tout ce dont j'ai besoin. Tu as été plus vite que ce que je pensais, hein? Bien, comme promis, je vais faire de toi un Ninja.";
			next;
			mes "[Cougar]";
			mes "Laisse-moi me présenter comme il se doit. Je suis le Grand Ninja Cougar de la Touga Ninja Corps, et je suis en charge de l'équipe de recherche parti trouver Sir Kazma.";
			next;
			mes "[Cougar]";
			mes "Sir Kazma est le chef de mon village, mais il s'est enfui. Cela a résulté en un conflit interne au sein de la Ninja Corps. Les choses sont très instables en ce moment...";
			next;
			mes "[Cougar]";
			mes "Je n'ai pas voulu t'accepter en tant que Ninja à cause de cette situation difficile. Cependant, tu as prouvé que tu mérites amplement de rejoindre le rang des Ninjas.";
			next;
			mes "[Cougar]";
			mes "Selon cette lettre, même Joe a une haute opinion de toi. Souviens-toi juste qu'en tant que Ninja, ta mission est ta plus grande priorité. Mais ne laisse pas les objectifs d'une mission remplacer ta consciense.";
			next;
			mes "[Cougar]";
			mes "''La discretion avant tout.''";
			mes "Pour garder nos secrets dans l'ombre, tu ne peux acheter ou vendre des armes Ninja qu'auprès des vendeurs agréés. Garde bien ça à l'esprit.";
			next;
			mes "[Cougar]";
			mes "A partir d'aujourd'hui, tu es désormais un fier membre de la Touga Ninja Corps. Sois aussi agile que le vent, et aussi silencieux que l'ombre.";
			callfunc "Job_Change",Job_Ninja;
			set NINJ_Q,5;
			getitem 13010,1;	// Asura
			completequest 6018;
			close;
		}
		else {
			mes "[Cougar]";
			mes "Comment as-tu été?";
			mes "Entraîne-toi dur:";
			mes "tu veux être capable de disparaître sans laisser de trace. Si tu peux faire ça, tu auras le respect de tous les Ninjas~";
			close;
		}
	}else{
		if (Class == Job_Ninja) {
			mes "[Cougar]";
			mes "Comment as-tu été?";
			mes "Entraîne-toi dur:";
			mes "tu veux être capable de disparaître sans laisser de trace. Si tu peux faire ça, tu auras le respect de tous les Ninjas~";
			close;
		}else{
			mes "[Cougar]";
			mes "Quoi...?";
			mes "Comment as-tu pu me trouver, caché dans l'ombre?! Tu dois être bien plus qu'un simple aventurier, eh?";
			close;
		}
	}
}

einbroch,184,194,3	script	Homme Mystérieux#nq	 	881,{
	if (NINJ_Q == 1) {
		mes "[Homme Mystérieux]";
		mes "J'ai voyagé dans bien des contrées, mais je n'avais jamais vu un immeuble aussi haut que la Tour d'Einbroch. Tous les bâtiments de ma ville natale sont minuscules en comparaison...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Oh, êtes-vous d'Amatsu? Je recherche quelqu'un de là-bas, nommé Wildcat Joe.";
		next;
		mes "[Homme Mystérieux]";
		mes "...Non. Non, Je viens d'Izlude et je suis a Einbroch car j'ai besoins de quelques matériaux. Dis moi, pourquoi cherches-tu ce Wildcat Joe?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Eh bien, je dois lui remettre cette lettre et obtenir sa réponse afin de pouvoir devenir un Ninja.";
		next;
		mes "[Homme Mystérieux]";
		mes "Vraiment? Maintenant que j'y pense, j'ai du le croiser une ou deux fois dans cette ville. Bien qu'il préfère qu'on l'appelle ''Red Leopard Joe,'' plutôt que ''Wildcat Joe.''";
		next;
		mes "[Homme Mystérieux]";
		mes "J'aimerais vraiment t'aider à le trouver, mais d'abord j'ai besoin de quelques matériaux. Si cela ne t'embète pas, pourrais-tu m'aider à les rassembler? Ensuite, je t'aiderais à trouver Joe.";
		next;
		if(select("Ne vous inquiétez pas, je vais le trouver seul.:Bien sûr, je vais vous aider.") == 1) {
			mes "[Homme Mystérieux]";
			mes "Tu es sûr...?";
			mes "Red Leopard Joe est un maître du camouflage. Tu auras besoin d'aide si tu veux le trouver...";
			close;
		}
		mes "[Homme Mystérieux]";
		mes "Bien, je suis heureux d'entendre ça. Aide-moi à trouver";
		mes "^3355FF5 Cyfars^000000 et";
		mes "^3355FF1 Phracon^000000 s'il te plait.";
		set NINJ_Q,2;
		changequest 6015,6016;
		close;
	}
	else if(NINJ_Q == 2) {
		if (countitem(7053) < 5 || countitem(1010) < 1) {
			mes "[Homme Mystérieux]";
			mes "Ramène-moi";
			mes "^3355FF5 Cyfars^000000 et";
			mes "^3355FF1 Phracon^000000";
			mes "dès que tu pourras. Ensuite, je t'aiderais à trouver Red Leopard Joe.";
			close;	
		}
		mes "[Homme Mystérieux]";
		mes "Bien, bien. Tu as ramené les matériaux... Maintenant, c'est à mon tour de t'aider. Voila, laisse-moi voir cette lettre.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "?????!!";
		next;
		mes "[Homme Mystérieux]";
		mes "Ben quoi? N'as-tu pas une lettre de Cougar pour moi?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Etes-vous...";
		mes "Etes-vous Wildcat Joe?";
		next;
		mes "[Homme Mystérieux]";
		mes "...Oui, mais je préfère qu'on m'appelle Red Leopard Joe. Cougar t'as envoyé à moi, c'est ça? C'est le seul qui m'appelle comme ça. Alors tu veux être un Ninja, eh? Hmm, très bien...";
		next;
		mes "[Red Leopard Joe]";
		mes "Si tu veux devenir un Ninja, il faudra toujours que tu fasses attention à ce que tu vois, et en quoi tu crois.  N'oublie pas que si tes secrets sont découverts, tu ne seras plus un Ninja.";
		next;
		mes "[Red Leopard Joe]";
		mes "Souviens toi de toujours te déplacer rapidement, et de toujours disparaitre sans laisser de trace. Rester cacher dans l'ombre est vraiment notre pouvoir ultime.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Je vois...";
		mes "...........";
		next;
		mes "[Red Leopard Joe]";
		mes "Pour l'instant, laisse-moi lire cette lettre. Voyons voir... Hm. Je pensais que Cougar voulait me défier à nouveau, mais il souhaite une trève temporaire, en fait! Hah!";
		next;
		mes "[Red Leopard Joe]";
		mes "Merci pour ton aide. J'ai les matériaux qu'il me faut. Je n'ai plus qu'a fabriquer mon Kunai! Hahaha! Je ne ferais jamais de trève si j'ai l'avantage!";
		next;
		mes "[Red Leopard Joe]";
		mes "Laisse-moi lui écrire ma réponse. Je vais aussi lui faire part de mes recommendations... Je pense que tu ferais un très bon Ninja, même si je t'ai abusé tout à l'heure. Heh heh heh!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "......";
		mes ".........";
		mes "............";
		next;
		mes "[Red Leopard Joe]";
		mes "Voilà!";
		mes "Ramène cette lettre à Cougar s'il te plait. Ça prendra du temps de retourner à Amatsu, alors laisse-moi t'y envoyer directement...";
		delitem 1010,1;	// Phracon
		delitem 7053,5;	// Cyfar
		set NINJ_Q,3;
		changequest 6016,6017;
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if(NINJ_Q == 3) {
		mes "[Red Leopard Joe]";
		mes "Eh? Je ne suis pas sûr de ce qui s'est passé, mais il semble que tu n'aies pas donné ma réponse a Cougar. Puis-je te renvoyer à Amatsu dès maintenant?";
		next;
		if(select("Non, merci.:Oui, s'il vous plait.") == 1) {
			mes "[Red Leopard Joe]";
			mes "Très bien.";
			mes "J'essayais juste de te faire économiser un peu de temps.";
			close;
		}
		mes "[Red Leopard Joe]";
		mes "Okay, alors.";
		mes "A bientôt.";
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if(NINJ_Q == 4) {
		mes "[Red Leopard Joe]";
		mes "Cougar t'as demandé de lui ramener des matériaux aussi? Oh, et bien, je suppose que je ne peux pas le blâmer. Au moins, je pourrai le battre à la loyale~";
		close;
	}
	else if(NINJ_Q == 5 && Class == Job_Ninja) {
		mes "[Red Leopard Joe]";
		mes "Oh, tu es un Ninja~";
		mes "J'espère que tu continueras à t'entrainer pour ainsi maitriser autant de compétences de Ninja que possible. Souviens toi de toujours te fondre dans l'ombre.";
		close;
	}
	else {
		mes "[Touriste]";
		mes "J'ai voyagé dans bien des contrées, mais je n'avais jamais vu un immeuble aussi haut que la Tour d'Einbroch. Tous les bâtiments de ma ville natale sont minuscules à côté...";
		close;
	}
}