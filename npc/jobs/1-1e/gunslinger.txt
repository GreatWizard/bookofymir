//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par KhaOs, corrigé par Etna et Fern
//===== eAthena Script ======================================= 
//= Gunslinger Job Quest
//===== By: ================================================== 
//= erKURITA, RockmanEXE, Kisuka
//===== Current Version: ===================================== 
//= 2.2
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Gunslinger Job Change Quest
//===== Additional Comments: ================================= 
//= 1.0 Made the NPC [erKURITA]
//= 1.5 Couple fixes to the npc, aswell as adding the missing
//=	  3 green herbs. [erKURITA]
//= 1.6 job number->const, commonized variable name,
//= 	optimized [Lupus]
//= 1.7 Now uses only ONE variable GUNS_Q [Lupus]
//= 1.7a Grammar fixes, few spelling fixes, etc [CBMaster]
//= 1.7b Parenthesis fixes [KarLaeda]
//= 1.8 Fixed the reward you can get [Playtester]
//= 1.9 Removed some 'clever' script constructs [ultramage]
//= 2.0 Updated to official version - Thanks to Omega for
//= his first try [SinSloth] 1.10b - removed .GATs [Lupus]
//= 2.1 Taureau Éclairé now deletes all items. Thanks to yyCC. [L0ne_W0lf]
//= 2.1a Corrected a Typo error ";;". [Samuray22]
//= 2.1b Corrected some typos errors. (bugreport:1549) [Samuray22]
//= 2.1c Fixed some missing/messed up item checks. (bugreport:2693) [Kisuka]
//= 2.2 Updated to match AEGIS script and Added Quest Log commands. [Kisuka]
//============================================================ 

que_ng,152,167,3	script	Maître Miller	901,{
	if (Upper == 2) {
		mes "[Maître Miller]";
		mes "Oh, quel adorable bambin~";
		mes "Où est ta maman?";
		mes "Cet endroit est dangereux, alors s'il te plait rentre vite chez toi, d'accord?";
		close;
	}
	if (Class == Job_Novice) {
		if (JobLevel < 10 || getskilllv("NV_BASIC") < 9) {
			mes "[Maître Miller]";
			mes "Tu voudrais devenir Gunslinger, hein? Tu as du potentiel, mais pas encore assez d'expérience. Entraîne-toi juste encore un peu, et reviens me voir ensuite, entendu?";
			close;
		}
		if (GUNS_Q == 0) {
			mes "[Maître Miller]";
			mes "Je suis Miller, Maître des Gunslinger, et gardien de Dame Celena.";
			mes "Maintenant, de quoi as-tu besoin? Je n'ai pas de temps à te consacrer, alors j'espère que c'est important!";
			next;
			if(select("Rien.:Je veux devenir un Gunslinger.") == 1) {
				mes "[Maître Miller]";
				mes "Ne gaspille pas mon temps!";
				mes "Si tu veux devenir Gunslinger, alors reviens me parler.";
close;
}
mes "[Maître Miller]";
mes "Hum. tu es un peu jeune, mais tes yeux me disent que tu es assez déterminé.";
mes "Tu auras besoin de passer notre entretien et de suivre nos cours d'éducation pour devenir Gunslinger.";
mes "Désires-tu t'engager dans cette voie?";
next;
if(select("Laissez-moi y réfléchir un peu.:Bien sûr!") == 1) {
	mes "[Maître Miller]";
	mes "Je comprends. Si tu te décides à devenir Gunslinger, alors reviens me voir. Je te prendrai en charge.";
	close;
}
mes "[Maître Miller]";
mes "Bien, Bien. Bon alors, tu vas pouvoir commencer. Prends cette lettre et amène-la à M. Taureau Éclairé à Payon. C'est un chaman qui jugera si tu es qualifié ou non pour devenir Gunslinger.";
set GUNS_Q,1;
setquest 6020;
close;
}
else if(GUNS_Q == 1) {
mes "[Maître Miller]";
mes "Prends cette lettre de recommandation que j'ai écrite pour toi et apporte-la à Taureau Éclairé à Payon. Il te testera pour voir si tu as vraiment le potentiel requis pour devenir Gunslinger.";
			close;
		}
		else if(GUNS_Q == 2) {
			mes "[Maître Miller]";
			mes "Hum... Taureau Éclairé t'as demandé de rassembler des objets afin de prouver ta valeur?";
			mes "Hum. Je suppose que cela fait partie de son test de qualification.";
			close;
		}
		else if(GUNS_Q == 3) {
			mes "[Maître Miller]";
			mes "Taureau Éclairé t'a demandé de lui rapporter un peu de lait? Il doit vraiment t'apprécier pour te demander de lui rendre autant de services. Bonne chance, l'ami.";
			close;
		}
		else if(GUNS_Q == 4) {
			mes "[Maître Miller]";
			mes "Je m'attends à avoir de bonnes nouvelles à ton sujet d'ici peu. Tu sais, je n'ai aucun doute sur le fait que tu deviendras un Gunslinger.";
			close;
		}
		else if(GUNS_Q == 5) {
			if (Skillpoint != 0) {
				mes "[Maître Miller]";
				mes "Hé, il te reste des Points de Compétences. Tu dois tous les utiliser avant de revenir me parler à nouveau.";
				close;
			}
			mes "[Maître Miller]";
			mes "Oh, tu as apporté un justificatif de Taureau Éclairé! Cela faisait longtemps qu'il n'en avait pas donné un à quelqu'un. Je suis fier de toi!";
			next;
			mes "[Maître Miller]";
			mes "Si Taureau Éclairé approuve, alors je n'ai aucune raison de te rejeter. Comme tout s'est bien passé, je vais te promouvoir Gunslinger. Mais d'abord, laisse moi t'expliquer notre métier plus en détails.";
			next;
			mes "[Maître Miller]";
			mes "En tant que Gunslinger, tu devras toujours garder ton arme à feu sur toi. La Guilde des Gunslinger garde une trace de toutes les armes à feux et balles. Ainsi, tu ne pourras t'en procurer qu'auprès des membres de la Guilde.";
			next;
			mes "[Maître Miller]";
			mes "Ne t'en fais pas, les Gunslinger de la Guilde se trouvent presque partout ces jours-ci. De toute façon, c'est la voie que nous ordonne de suivre notre Chef de Guilde, Dame Celena.";
			next;
			mes "[Maître Miller]";
			mes "Tu auras peut être la chance de la rencontrer un de ces jours. De toute façon, en ce moment nous devons réguler les Armes à Feux et les Balles, afin de les garder loin des gens mal intentionnés ou irresponsables.";
			next;
			mes "[Maître Miller]";
			mes "En tout cas, c'est toujours un plaisir pour moi de parler à un autre Gunslinger, alors restons en contact. Puisse le pouvoir de la terre te protéger dans toutes tes aventures~";
			callfunc "Job_Change",Job_Gunslinger;
			set GUNS_Q,6;
			completequest 6024;
			if (rand(1,2) == 1) {
				getitem 13100,1;	// Six_Shooter
			}else{
				getitem 13150,1;	// Branch
			}
			close;
		}
	}else{
		if (Class == Job_Gunslinger) {
			mes "[Maître Miller]";
			mes "Oh! Ça faisait longtemps que je ne t'avais pas vu, l'ami. Comment vas-tu? J'espère que tu entretiens bien ton Arme à Feu. Prends soin d'elle, et elle prendra soin de toi. Retiens bien cela.";
			close;
		}else{
			mes "[Maître Miller]";
			mes "Si tu n'as rien à me demander, alors s'il te plait, passes ton chemin.";
			close;
		}
	}
}

payon,184,65,3	script	Taureau Éclairé	866,{
	if (GUNS_Q == 1) {
		mes "[Taureau Éclairé]";
		mes "Bonjour, jeune loup.";
		mes "Que m'as-tu apporté là?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "M. Miller m'a demandé de vous remettre cette lettre. En réalité, j'aimerais vivement devenir Gunslinger...";
		next;
		mes "[Taureau Éclairé]";
		mes "Miller, tu dis?";
		mes "Hum, Renard Noir ne recommande pas quelqu'un s'il ne croit pas qu'il fera un bon Gunslinger. Oui, je pense savoir pourquoi il t'a envoyé me voir.";
		next;
		mes "[Taureau Éclairé]";
		mes "Je peux le voir dans tes yeux: tu as un coeur généreux et un fort sens des responsabilités. Tout ce dont tu as besoin, c'est de la bénédiction de la Terre pour te protéger.";
		next;
		mes "[Taureau Éclairé]";
		mes "Je peux te donner un justificatif qui prouvera ton désir de devenir un guerrier de la terre au Maître des Gunslinger.";
		mes "J'aurais besoin de ces objets...";
		next;
		mes "[Taureau Éclairé]";
		mes "^3355FF1 Tronc^000000,";
		mes "^3355FF3 Peluches^000000,";
		mes "^3355FF3 Zargons^000000,";
		mes "^3355FF10 Coquilles^000000,";
		mes "^3355FF3 Herbes Vertes^000000, et";
		mes "^3355FF3 Coquilles Arc-en-Ciel^000000.";
		next;
		mes "[Taureau Éclairé]";
		mes "Quand je t'aurai délivré le justificatif, tu pourras l'apporter à Renard Noir qui t'aidera à devenir Gunslinger.";
		set GUNS_Q,2;
		changequest 6020,6021;
		close;
	}
	else if(GUNS_Q == 2) {
		if (countitem(912) < 3 || countitem(914) < 3 || countitem(1019) < 1 || countitem(935) < 10 || countitem(511) < 3 || countitem(1013) < 3) {
			mes "[Taureau Éclairé]";
			mes "Je peux te délivrer un justificatif qui prouvera ton désir de devenir un guerrier de la terre au Maître des Gunslinger.";
			mes "J'aurais besoin de ces objets...";
			next;
			mes "[Taureau Éclairé]";
			mes "^3355FF1 Tronc^000000,";
			mes "^3355FF3 Peluches^000000,";
			mes "^3355FF3 Zargons^000000,";
			mes "^3355FF10 Coquilles^000000,";
			mes "^3355FF3 Herbes Vertes^000000, et";
			mes "^3355FF3 Coquilles Arc-en-Ciel^000000.";
			next;
			mes "[Taureau Éclairé]";
			mes "Quand je t'aurai délivré le justificatif, tu pourras l'apporter à Renard Noir qui t'aidera à devenir Gunslinger.";
			close;
		}
		delitem 912,3;	// Zargon
		delitem 914,3;	// Fluff
		delitem 1019,1;	// Wooden_Block
		delitem 935,10;	// Shell
		delitem 511,3;	// Green_Herb
		delitem 1013,3;	// Colorful_Shell
		set GUNS_Q,3;
		changequest 6021,6022;
		mes "[Taureau Éclairé]";
		mes "Ah, tu es revenu avec tout ce dont j'ai besoin. S'il te plait, donne-moi un peu de temps pour préparer le justificatif.";
		mes "Reviens dans un petit moment, j'en aurai fini avec ça.";
		close;
	}
	else if(GUNS_Q == 3) {
		mes "[Taureau Éclairé]";
		mes "Oh, tu es arrivé juste à temps. Ça fait un moment que je n'ai pas fait l'un de ces justificatifs, alors je suis un peu rouillé. Ça me rappelle ma jeunesse.";
		next;
		mes "[Taureau Éclairé]";
		mes "Je sers à ce poste en choisissant les destinataires dignes de recevoir le justificatif des Gunslinger depuis quelques décennies maintenant. Mais avant cela, j'étais un jeune aventurier tout comme toi.";
		next;
		mes "[Taureau Éclairé]";
		mes "Je me souviens, comme si c'était hier, du jour où j'ai reçu mon propre petit justificatif pour devenir Gunslinger, un guerrier de la terre. C'est quand j'ai rencontré le père de Celena... Comment le temps peut-il passer si rapidement?";
		next;
		mes "[Taureau Éclairé]";
		mes "Ah... J'apprécie vraiment Celena et Renard Noir pour toute l'aide qu'ils m'ont apporté dans le recrutement des jeunes Gunslingers. Je suis très vieux maintenant, et je ne peux pas tout faire tout seul. *Soupir* C'est la vie...";
		next;
		mes "[Taureau Éclairé]";
		mes "Avant que tu ne partes, puis-je te demander de me rendre un petit service? J'ai soif et je voudrais bien un verre de lait froid. Peux-tu m'en apporter s'il te plait?";
		set GUNS_Q,4;
		changequest 6022,6023;
		close;
	}
	else if(GUNS_Q == 4) {
		if (countitem(519) < 1) {
			mes "[Taureau Éclairé]";
			mes "Je suis un vieil homme qui va bientôt être réuni avec la terre mère. Pourrais-tu rendre un petit service à un vieux Gunslinger et m'apporter un verre de lait froid?";
			close;		
		}
		delitem 519,1;	// Milk
		set GUNS_Q,5;
		changequest 6023,6024;
		mes "[Taureau Éclairé]";
		mes "Oh, merci pour ta générosité! Je vois que tu m'as apporté du Lait. Ahhhh, Délicieux~";
		next;
		mes "[Taureau Éclairé]";
		mes "J'admire la patience, la douceur et la bonté dont tu as fait preuve en me l'apportant.";
		mes "Oui, ce sont des qualités que nous voulons que tous les Gunslinger aient.";
		next;
		mes "[Taureau Éclairé]";
		mes "Maintenant, prends ce justificatif pour Miller, Renard Noir, avec toute mon approbation. J'espère que tu utiliseras ton Arme à Feu pour faire respecter la justice comme un noble guerrier de la terre.";
		next;
		mes "[Taureau Éclairé]";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Maaaaarrraaa Neeey~";
		mes "Yippee Yippee Yai Yocaiyay~";
		close;
	}
	else if(GUNS_Q == 5) {
		mes "[Taureau Éclairé]";
		mes "S'il te plait, prends ce justificatif pour Miller, le Renard Noir, avec toute mon approbation. J'espère que tu utiliseras ton Arme à Feu pour faire respecter la justice comme un noble guerrier de la terre.";
		next;
		mes "[Taureau Éclairé]";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Maaaaarrraaa Neeey~";
		mes "Yippee Yippee Yai Yocaiyay~";
		close;
	}
	else if(GUNS_Q == 6) {
		mes "[Taureau Éclairé]";
		mes "AAh, ça fait longtemps que je ne t'avais pas vu. J'espère que tu deviens un loup futé, et que tu utilises tes pouvoirs de Gunslinger pour protéger ce qui est bon et juste..";
		close;
	}
	else {
		mes "[Taureau Éclairé]";
		mes "Zzzzzz~";
		mes "^333333*Pff!*^000000";
		close;
	}
}
