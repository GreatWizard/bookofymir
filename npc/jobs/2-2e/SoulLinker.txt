//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena, corrigé par Morrigan
//Myllena: j'ai conservé certains textes de l'ancienne version
//===== eAthena Script ======================================= 
//= Soul Linker Job Quest
//===== By: ================================================== 
//= Celestria & Samuray22
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Job quest for Soul Linker class.
//= [Vars Used]
//= SOUL_Q = Job Change Phase. (Max 4)
//===== Additional Comments: ================================= 
//= 1.2 Rescripted to Aegis 10.3 standards. [Samuray22]
//= 1.3 Slight updates and fixes to the script. [L0ne_W0lf]
//=	Removes the use of labels, and corrects other errors.
//=	Removed the GM-use only NPC from the sec_in map. 
//=	No longer used a global var, uses an NPC var instead.
//= 1.3a Deleted Empty Color Tag. (bugreport:1572) [Samuray22]
//= 1.4 Replaced effect numerics with constants. [L0ne_W0lf]
//= 1.5 Added Quest Log commands. [Kisuka]
//============================================================ 

morocc_in,174,30,6	script	Enfant#link1	716,{
	if (Class == Job_Soul_Linker) {
		mes "[Maia]";
		mes "Je vous souhaite bonne chance dans vos voyages. Plus vous maîtrisez les compétences des Lieurs d'Âmes, plus votre esprit sera capable d'aider vos alliés...";
		close;
	}
	if (Class == Job_Star_Gladiator) {
		mes "[Enfant]";
		if (Sex == 1) {
			mes "Ne seriez-vous pas un guerrier du Soleil?";
		}
		else {
			mes "Ne seriez-vous pas un guerrier de la Lune?";
		}
		mes "Vous m'êtes comme familier"+ ( sex ? "" : "e" ) +"... Après tout, les bases de vos capacités sont les mêmes que celle des Taekwon Do, n'est-ce pas?";
		close;
	}
	if (Class != Job_Taekwon) {
		mes "[Enfant]";
		mes "Hum? Je n'ai rien à vous offrir. Mais si vous connaissez des personnes qui pratiquent le Taekwon Do, ils peuvent être intéressés par mes enseignements.";
		close;
	}
	if (JobLevel < 40) {
		mes "[Enfant]";
		mes "Alors vous étudiez le Taekwon Do? C'est bien, c'est très bien. Continuez de peaufiner vos compétences et de vous entraîner.";
		close;
	}
	else if (JobLevel > 39) {
		if (SOUL_Q == 0) {
			mes "[Enfant]";
			mes "...";
			mes "Hé, vous.";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "Quelqu'un m'a parlé?";
			next;
			mes "[Enfant]";
			mes "Oui, je vous ai appelé. Ne me faites pas hausser la voix, et approchez-vous.";
			next;
			if (select("Tu n'es qu'un gamin malpoli!:L'ignorer.") == 1) {
				mes "[Enfant]";
				mes "Vous êtes chanceux que je m'intéresse à vous! J'ai peut-être l'air d'un enfant, mais j'ai plus de trois cents ans!";
				emotion e_pif;
				next;
				mes "[Enfant]";
				mes "Maintenant écoutez...";
				mes "Je sais que vous êtes un disciple des arts Taekwon Do.";
				mes "C'est un art de combat très respectable, mais j'ai une proposition à vous faire, si vous voulez bien l'écouter.";
				emotion e_heh;
				next;
				mes "[Enfant]";
				mes "Je vous regarde, et je vois que vous êtes quelqu'un de très spirituel. Vous avez un très grand potentiel, et je ne voudrais pas voir ce potentiel gâché. Pourquoi ne deviendriez-vous pas un Lieur d'Âmes? ...";
				next;
				if (select("Ha! Quel gamin stupide~:Lieur d'Âmes?") == 1) {
					mes "[Enfant]";
					mes "Vous... Vous ne me croyez pas? Je suis on ne peut plus sérieux. Ne pouvez-vous pas oublier le fait que j'ai l'air d'un enfant juste une minute? Tss, ... ces jeunes.";
					close;
				}
				mes "[Enfant]";
				mes "Les Lieurs d'Âmes communiquent avec les esprits des guerriers déchus, qui souhaitent pourtant continuer de se battre dans le monde des vivants. Les esprits de ces guerriers ne peuvent combattre tel quel dans notre monde.";
				next;
				mes "[Enfant]";
				mes "Cependant, si votre esprit s'y prête, ces âmes peuvent être attirées vers vous. Avec suffisamment d'entraînement, vous pouvez imprégner temporairement vos alliés du pouvoir de ces âmes.";
				next;
				mes "[Enfant]";
				mes "Vous ne pourrez cependant vous imprégner vous-même du pouvoir de ces âmes. De plus, il vous faudra maîtriser de nombreuses compétences avant de pouvoir manier le pouvoir de ces âmes à votre guise.";
				next;
				mes "[Enfant]";
				mes "Vous devrez entrer dans un monde totalement différent si vous souhaitez devenir Lieur d'Âmes. Mais je sais que pour vous, cela sera possible. Alors, qu'en dites-vous?";
				next;
				if (select("Ça ne me dit rien pour l'instant...:Très bien, que dois-je faire?") == 1) {
					mes "[Enfant]";
					mes "Ah, d'accord. Eh bien, si vous décidez de devenir un Lieur d'Âmes, revenez me voir quand vous voulez.";
					close;
				}
				set SOUL_Q,1;
				setquest 6005;
				mes "[Enfant]";
				mes "Alors vous souhaitez devenir un Lieur d'Âmes? Super!";
				mes "Très bien, pour l'instant vous devrez me rapporter certains objets. Ne vous inquiétez pas, je vous expliquerai plus tard pourquoi j'en ai besoin.";
				next;
				mes "[Enfant]";
				mes "Maintenant rapportez-moi";
				mes "^0000FF1 Diamant 3 Carats^000000,";
				mes "^0000FF1 Coeur Immortel^000000 et";
				mes "^0000FF1 Rose Éternelle^000000.";
				mes "Et faites vite s'il vous plaît.";
				close;
			}
			mes "[Enfant]";
			mes "Hum...?";
			mes "Attendez, où allez-vous? Je... Je ne vous parle plus!";
			close;
		}
		else if (SOUL_Q == 1) {
			if (Class == Job_Taekwon) {
				mes "[Enfant]";
				mes "Oh, vous êtes de retour?";
				mes "Alors, avez-vous bien apporté";
				mes "^0000FF1 Diamant 3 Carats^000000,";
				mes "^0000FF1 Coeur Immortel^000000 et";
				mes "^0000FF1 Rose Éternelle^000000.";
				mes "Comme je vous l'ai demandé?";
				next;
				if (select("Oui.:Non, pas encore...") == 1) {
					if (countitem(732) > 0 && countitem(929) > 0 && countitem(748) > 0) {
						delitem 732,1; //Crystal_Jewel__
						delitem 929,1; //Immortal_Heart
						delitem 748,1; //Witherless_Rose
						set SOUL_Q,2;
						changequest 6005,6006;
						mes "[Enfant]";
						mes "Parfait, vous avez apporté tout ce qu'il fallait. Avant de commencer, permettez-moi de me présenter. Mon nom est Maia, et telle que vous me voyez, j'ai plus de trois cent ans.";
						next;
						mes "[Maia]";
						mes "J'ai été chargée de recruter et de former de nouveaux Lieurs d'Âmes. C'est pour cette raison que je suis encore de ce monde.";
						next;
						mes "[Maia]";
						mes "Quoi qu'il en soit, je dois effectuer quelques préparations avec les matériaux que vous avez apportés. Revenez d'ici quelques minutes s'il vous plaît.";
						close;
					}
					mes "[Enfant]";
					mes "Hum...?";
					mes "Je pense que vous avez oublié certaines choses. La prochaine fois, assurez-vous d'avoir tout ce que je vous demande.";
					emotion e_pif;
					next;
					mes "[Enfant]";
					mes "J'aurais besoin de:";
					mes "^0000FF1 Diamant 3 Carats^000000,";
					mes "^0000FF1 Coeur Immortel^000000 et";
					mes "^0000FF1 Rose Éternelle^000000.";
					close;
				}
				mes "[Enfant]";
				mes "Hum. D'accord, assurez-vous d'apporter tous ces matériaux la prochaine fois.";
				close;
			}
			set SOUL_Q,0;
			mes "[Enfant]";
			mes "Vous êtes "+ ( sex ? "devenu un Guerrier" : "devenue une Guerrière" ) +" du Soleil, de la Lune et des Étoiles... ? Je ne pensais pas que vous en auriez le potentiel. Je suppose que je ne peux pas vous faire de reproches...";
			close;
		}
		else if (SOUL_Q == 2) {
			if (SkillPoint) {
				mes "[Maia]";
				mes "Il vous reste des Points de Compétence à attribuer.";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Maia]";
				mes "Quelqu'un est actuellement en train d'accomplir la cérémonie pour devenir Lieur d'Âmes. Pourriez-vous attendre que cette personne ait terminé? Revenez dans un moment.";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Maia]";
			mes "Très bien, j'ai terminé les préparations. Nous allons maintenant commencer la cérémonie qui vous fera devenir un Lieur d'Âmes.";
			mes "Fermez les yeux...";
			close2;
			warp "job_soul",30,30;
			end;
		}
		else if (SOUL_Q > 2) {
			mes "[Maia]";
			mes "Êtes-vous prêt"+ ( sex ? "" : "e" ) +" à entrer dans les profondeurs de votre esprit?";
			next;
			if (select("Non:Oui") == 1) {
				mes "[Maia]";
				mes "Très bien, revenez quand vous serez prêt"+ ( sex ? "" : "e" ) +". Je vous attends ici.";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Maia]";
				mes "Quelqu'un est actuellement en train d'accomplir la cérémonie pour devenir Lieur d'Âmes. Pourriez-vous attendre que cette personne ait terminé? Revenez dans un moment.";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Maia]";
			mes "Très bien, fermez vos yeux et détendez-vous. Nous allons plonger dans les profondeurs de votre esprit.";
			close2;
			warp "job_soul",30,30;
			end;
		}
	}

OnInit:
	set .SoulLinkerTest,0;
	end;
}

job_soul,30,31,0	script	Maia#link2::SLTester	-1,3,3,{
OnTouch:
	if (Class == Job_Taekwon) {
		if (JobLevel < 40) {
			set getvariableofnpc(.SoulLinkerTest,"Enfant#link1"),0;
			mes "[Maia]";
			mes "Hum? Comment êtes-vous arrivé"+ ( sex ? "" : "e" ) +" ici? Vous n'êtes pas encore qualifié"+ ( sex ? "" : "e" ) +" pour assister à cette cérémonie. Je vais vous ramener à Morroc...";
			close2;
			warp "morocc",157,47;
			end;
		}
		if (SOUL_Q == 2) {
			mes "[Maia]";
			mes "Reconnaissez-vous cet endroit? Nous sommes actuellement à l'intérieur de votre esprit. Les âmes des guerriers qui sont morts attendent que vous en appeliez à leur pouvoir.";
			next;
			mes "[Maia]";
			mes "Pour l'instant il ne sont que quelques-uns, mais si vous continuez de vous entraîner, vous deviendrez capable de faire appel à de nombreuses âmes, en tant que Lieur d'Âmes.";
			next;
			set SOUL_Q,3;
			changequest 6006,6007;
			mes "[Maia]";
			mes "Nous ne pouvons rester ici que trois minutes. Je vous suggère de parler à ces âmes, pendant que vous en avez l'opportunité.";
			close;
		}
		else if (SOUL_Q == 3) {
			mes "[Maia]";
			mes "Écoutez ce que les âmes souhaitent vous dire.";
			close;
		}
		else if (SOUL_Q == 4) {
			mes "[Maia]";
			mes "Vous êtes maintenant prêt"+ ( sex ? "" : "e" ) +" à devenir un Lieur d'Âmes. Cependant, si vous souhaitez parler à nouveau aux âmes, vous pouvez le faire.";
			next;
			if (select("Discuter avec les âmes:Devenir Lieur d'Âmes") == 1) {
				mes "[Maia]";
				mes "Très bien. Dépêchez vous, nous ne pouvons rester ici qu'un temps limité...";
				close;
			}
			mes "[Maia]";
			mes "Nous allons maintenant commencer la cérémonie. Les objets que vous avez apportés nous permettront de vous apprendre à invoquer le pouvoir des guerriers déchus afin de soutenir vos amis pendant les combats.";
			next;
			mes "[Maia]";
			mes "La Rose Éternelle vous protègera...";
			donpcevent "Maia#link2::Oneff";
			next;
			mes "[Maia]";
			mes "La Rose Éternelle vous protègera...";
			mes "Le Coeur Immortel se substituera à votre coeur s'il cesse de battre.";
			next;
			mes "[Maia]";
			mes "La Rose Éternelle vous protègera...";
			mes "Le Coeur Immortel se substituera à votre coeur s'il cesse de battre.";
			mes "Le Diamant se transformera en poussière à la place de votre corps de mortel.";
			next;
			mes "[Maia]";
			mes "Les morts qui souhaitent continuer de se battre... combattront pour vous! Utilisez vos pouvoirs de Lieur d'Âmes avec sagesse, dans des buts justes.";
			next;
			completequest 6008;
			callfunc "Job_Change",Job_Soul_Linker;
			callfunc "F_ClearJobVar";		// clears all job variables for the current player
			set SOUL_Q,0;
			mes "[Maia]";
			mes "Je vous souhaite bonne chance dans votre nouvelle vie. Entourez-vous de fidèles alliés, et les esprits vous protègeront. Adieu, mon ami"+ ( sex ? "" : "e" ) +".";
			close2;
			set getvariableofnpc(.SoulLinkerTest,"Enfant#link1"),0;
			donpcevent "Timer#link3::OnDisable";
			warp "morocc",157,47;
			end;
		}
		set getvariableofnpc(.SoulLinkerTest,"Enfant#link1"),0;
		mes "[Maia]";
		mes "Hum...? Votre temps n'est pas encore venu. Vous ne devriez pas vous trouver ici. Je vous renvoie à Morroc...";
		close2;
		warp "morocc",157,47;
		end;
	}
	set getvariableofnpc(.SoulLinkerTest,"Enfant#link1"),0;
	if (Class == Job_Soul_Linker) {
		mes "[Maia]";
		mes "Le temps est venu pour vous de vous aventurer dans le vaste monde! Nous aurons vraiment besoin de plus de Lieurs d'Âmes pour remporter la bataille face aux forces du Mal...";
	}
	else {
		mes "[Maia]";
		mes "Étrange...";
		mes "Vous n'êtes pas censé"+ ( sex ? "" : "e" ) +" être ici. Laissez-moi vous reconduire à Morroc...";
	}
	close2;
	warp "morocc",157,47;
	end;

Oneff:
	specialeffect EF_MAPPILLAR2;
	end;
}

job_soul,35,30,6	duplicate(SLTester)	Maia#link6	716

job_soul,30,35,6	script	Âme#link4	827,{
	if (SOUL_Q == 2) {
		mes "[Âme d'un Moine]";
		mes "Qui suis-je...?";
		mes "Je pense... je pense que vous devriez parler à Maia...";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Âme d'un Moine]";
		mes "Vous êtes ici dans un lieu où les âmes perdues se rendent lorsqu'elles pensent avoir encore quelque chose à accomplir ou transmettre en ce bas monde.";
		next;
		mes "[Âme d'un Moine]";
		mes "Je suis ici parce que je voulais protéger mes proches... mais mes bras n'ont pas été assez forts.";
		next;
		mes "[Âme d'un Moine]";
		mes "Je me suis perdu en même temps que j'ai perdu les gens que j'aimais...";
		next;
		mes "[Âme d'un Moine]";
		mes "Maintenant, je flotte ici entre la vie et la mort, ressassant mon passé et regrettant de ne pas avoir réussi à être plus fort.";
		next;
		mes "[Âme d'un Moine]";
		mes "Mais avec le temps, j'ai appris à mieux accepter mon passé.";
		next;
		mes "[Âme d'un Moine]";
		mes "Vous... Vous avez l'air fort"+ ( sex ? "" : "e" ) +". Si seulement je pouvais vous transmettre la force d'esprit que j'ai réussi à acquérir depuis tout ce temps passé ici...";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[Âme d'un Moine]";
		mes "Merci de m'avoir écouté.";
		close;
	}
	mes "[Âme d'un Moine]";
	mes "...";
	close;
}

job_soul,30,25,7	script	Âme#link5	754,{
	if (SOUL_Q == 2) {
		mes "[Âme d'un Sage]";
		mes "Parlez d'abord aux autres s'il vous plaît.";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Âme d'un Sage]";
		mes "Étudier. Étudier. Étudier.";
		mes "Tout ce que j'ai fait dans ma vie c'est étudier.";
		next;
		mes "[Âme d'un Sage]";
		mes "Oh! Les choses que j'ai apprises m'ont bien servies. Mon vocabulaire est varié, je connais les noms de toutes les plantes locales, et appris plusieurs langues.";
		next;
		mes "[Âme d'un Sage]";
		mes "Mais tout le temps que j'ai passé à étudier était du temps que je ne pouvais pas passer ni avec mes amis ni avec ma famille et mes proches.";
		next;
		mes "[Âme d'un Sage]";
		mes "Ce n'est que le jour où je suis tombé malade par manque de sommeil et que personne n'est venu à mon chevet, que j'ai réalisé la solitude dans laquelle je m'étais enfermé.";
		next;
		mes "[Âme d'un Sage]";
		mes "Tout ceci pour vous rappeler que rien n'est plus important que la famille ou les amis.";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[Âme d'un Sage]";
		mes "Merci de m'avoir écouté.";
		close;
	}
	mes "[Âme d'un Sage]";
	mes "...";
	close;
}

job_soul,25,30,5	script	Âme#link7	744,{
	if (SOUL_Q == 2) {
		mes "[Âme d'une Alchimiste]";
		mes "Parlez d'abord aux autres s'il vous plaît.";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Âme d'une Alchimiste]";
		mes "Vous savez pourquoi vous êtes ici...";
		next;
		mes "[Âme d'une Alchimiste]";
		mes "Moi, je suis ici parce que j'ai voulu guérir quelqu'un qui avait une maladie très grave. Je lui ai administré mes herbes et mes potions, mais...";
		next;
		mes "[Âme d'une Alchimiste]";
		mes "J'étais inexpérimentée, et j'ai accidentellement mélangé des ingrédients sans vraiment faire attention...";
		next;
		mes "[Âme d'une Alchimiste]";
		mes "Ceux que j'essayais de sauver ont souffert et ont péri à cause de mon incompétence.";
		next;
		mes "[Âme d'une Alchimiste]";
		mes "J'ai passé des années à être furieuse contre moi-même à cause de cette erreur.";
		next;
		mes "[Âme d'une Alchimiste]";
		mes "Bien sûr, tout le monde fait des erreurs... Mais lorsqu'elles coûtent la vie à des personnes, jamais on ne se pardonne.";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[Âme d'une Alchimiste]";
		mes "Merci de m'avoir écoutée.";
		close;
	}
	mes "[Âme d'une Alchimiste]";
	mes "...";
	close;
}

job_soul,1,5,0	script	Timer#link3	111,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	set getvariableofnpc(.SoulLinkerTest,"Enfant#link1"),0;
	end;

OnTimer60000:
OnTimer120000:
	if (getmapusers("job_soul") == 0) {
		stopnpctimer;
		set getvariableofnpc(.SoulLinkerTest,"Enfant#link1"),0;
	}
	end;

OnTimer180000:
OnTimer181000:
OnTimer182000:
	mapwarp "job_soul","morocc",157,47;
	end;

OnTimer183000:
	mapwarp "job_soul","morocc",157,47;
	set getvariableofnpc(.SoulLinkerTest,"Enfant#link1"),0;
	stopnpctimer;
}

//============================================================ 
// Old changelog
//============================================================ 
//= A temp Soul Linker Job Changer based on the kRO quest.
//= Quest info from RagnaInfo. Sprites from kRO screenshots
//= 1.0	Optimized and updated [Lupus]
//= 1.1 Fixed NPC names according to iRO [Lupus]
//============================================================ 
