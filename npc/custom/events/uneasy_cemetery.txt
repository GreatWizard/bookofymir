//===== eAthena Script ======================================= 
//= Uneasy Prontera Cemetery Quest (original script!)
//===== By: ================================================== 
//= Lupus
//===== Current Version: ===================================== 
//= 1.2a (Tested and fully working!)
//===== Compatible With: ===================================== 
//= eAthena Version 1.0
//===== Description: ========================================= 
//= A periodical quest of the Uneasy Cemetery (Kill undead / Prevent their appearance)
//= Every day, at the midnight Prontera receive a wave of Undeads.
//= They come from Uneasy Cemetery of Prontera. To protect the players
//= from the undeads terror you may either kill the enemy. Or supply Mother Mathana
//= with needed amount of Holy Water. Every citizen can take his part in the 
//= saving of Prontera city. After some days of quiet life... the Cemetery strikes back.
//===== Additional Comments: ================================= 
//= 1.1 More advanced ver. Added some bonus the the one who'd kill the last walking undead
//= 1.2 Added coords to the script to make label OmMobDead working
//= 1.2a Changed item names to item IDs. [Samuray22]
//============================================================ 


prontera,3,3,3	script	Uneasy_Check	-1,{
	end;

OnHour00:
	set $UNEASY_DL,$UNEASY_DL-1;
	set $UNEASY_BL,$UNEASY_BL+30; //add need of HW for 30 bottles per day
	if ($UNEASY_BL>666) set $UNEASY_BL,666; //keep needed bottles not <=666
	if ($UNEASY_DL < 0) goto L_Start_Undead;
//The Cemetery is OK yet.
	disablenpc "Mère Mathana";
	end;
OnInit:
	if ($UNEASY_DL >= 0) disablenpc "Mère Mathana";
	end;

OnHour06:
	killmonsterall "prontera";	//The Sun kills undead in the morning
	end;

OnHour01:
	if ($@UNEASY_MOB > 0) mapannounce "prontera","[Mère Mathana]: Au nom d' Odin, détruisez ces vermines mort-vivantes!",0;
	end;

OnZombieDead:
	set $@UNEASY_MOB,$@UNEASY_MOB-1;
	if ($@UNEASY_MOB>0) end;
	set $UNEASY_DL,0;
	set $UNEASY_H$,strcharinfo(0);
	if (Sex==1) mapannounce "prontera","[Mère Mathana]: Le preux "+$UNEASY_H$+" vient d'exterminer le dernier mort-vivant de Prontera!",0;
	if (Sex==0) mapannounce "prontera","[Mère Mathana]: Dame "+$UNEASY_H$+" vient d'exterminer le dernier mort-vivant de Prontera!",0;
	set JobExp,JobExp+100;
	set BaseExp,BaseExp+50;
	end;

L_Start_Undead:
	killmonsterall "prontera";	//kills any left monsters
	enablenpc "Mère Mathana";
//call some monsters in the city
	set $@UNEASY_MOB, 65;
	areamonster "prontera",0,0,0,0,"--ja--",1015,30,"Uneasy_Check::OnZombieDead";
//in the Cemetery
	monster "prontera",268,349,"--ja--",1015,30,"Uneasy_Check::OnZombieDead";
	monster "prontera",269,350,"--ja--",1036,5,"Uneasy_Check::OnZombieDead";
//announce
	mapannounce "prontera","[Mère Mathana]: Une aura maléfique émane du cimetière! Au nom d'Odin, rendez-vous au Sanctuaire et sauvez la ville de Prontera!",0;
}

prontera,257,313,5	script	Mère Mathana	79,{
	mes "[Mère Mathana]";
	if ($UNEASY_DL <= 0) goto L_Undead_Walk;
	mes "J'ai peur qu'il y ait quelque chose de maléfique rôdant dans notre vieux cimetière...";
	if ($UNEASY_H$==strcharinfo(0)) mes "Mais grâce à vous, "+$UNEASY_H$+", nous pourrons dormir paisiblement " + $UNEASY_DL + " nuits!";
	if ($UNEASY_H$!=strcharinfo(0)) mes "But grâce à l'aide de "+$UNEASY_H$+", nous pourrons dormir paisiblement " + $UNEASY_DL + " nuits!";
	emotion 0;
	close;

L_Undead_Walk:
	if ($UNEASY_DL == 0) mes "J'ai peur qu'ils ne reviennent demain soir!";
	if ($UNEASY_DL == 0 && $UNEASY_H$==strcharinfo(0)) mes "Merci, "+$UNEASY_H$+"! Nous nous occuperons du reste jusqu'au prochain crépuscule.";
	if ($UNEASY_DL == 0 && $UNEASY_H$!=strcharinfo(0)) mes "Mais grâce à l'aide de "+$UNEASY_H$+", nous sommes tranquilles jusqu'au prochain crépuscule.";
	mes "Afin d'apaiser ces âmes tourmentées, il faudrait asperger leur tombe d'Eau Bénite. Malheureusement, nous en sommes à court.";
	mes "Est-ce que vous pourriez nous trouver de l'Eau Bénite?";
	next;
	menu "Oui, prenez toutes mon Eau Bénite!",-, "Désolé, j'en ai besoin.",M_NO, "Je n'en ai aucune.",M_DONT_HAVE;

	if ( countitem(523)<1 ) goto M_DONT_HAVE;
	set $UNEASY_BL,$UNEASY_BL-countitem(523);
	delitem 523,countitem(523);

	if ( $UNEASY_BL > 0 ) goto L_NEED_MORE;
//set quiet days!!! no more undead for this period!
	set $UNEASY_DL,5+((0-$UNEASY_BL)/30);
	set $UNEASY_H$,strcharinfo(0);
	mes "[Mère Mathana]";
	mes "Merci, "+$UNEASY_H$+"! Maintenant, nous avons assez d'Eau Bénite!";
	next;
	mes "[Mère Mathana]";
	mes "Grâce à celle que nous avons versé sur les tombes, nous avons " + $UNEASY_DL + " nuits tranquilles devant nous!";
	next;
	killmonsterall "prontera";	//kills any left monsters
	mes "[Mère Mathana]";
	mes "Vous voyez, "+ $UNEASY_H$ +"? Ils s'en sont tous allé maintenant!";
	next;
	mes "[Mère Mathana]";
	mes "Notre Eglise va vous remercier personnellement...";
	next;
	if (Sex==1) mapannounce "prontera","[Mère Mathana]: Au nom d'Odin nous décernons au preux "+$UNEASY_H$+" le titre de Sauveur Prontera!",0;
	if (Sex==0) mapannounce "prontera","[Mère Mathana]: Au nom d'Odin nous décernons à la belle "+$UNEASY_H$+" le titre de Sauveur Prontera!",0;
	mes "[Mère Mathana]";
	mes "Au nom d'Odin nous vous bénissons et vous décernons ce modeste présent provenant du cellier de Mareusis.";
	getitem 505,1; //Blue_Potion
	set JobExp,JobExp+100;
	set BaseExp,BaseExp+50;
	close;

L_NEED_MORE:
	mes "[Mère Mathana]";
	mes "Merci, brave "+strcharinfo(0)+", mais nous avons encore besoin de " + $UNEASY_BL + " bouteilles d'Eau Bénite de plus.";
	close;

M_NO:
	mes "[Mère Mathana]";
	mes "J'ai peur que les créatures de ce vieux cimetière ne deviennent rapidement incontrolables... S'il-vous-plaît, apportez-nous toutes les bouteilles d'Eau Bénite que vous pourrez trouver.";
	close;

M_DONT_HAVE:
	mes "[Mère Mathana]";
	mes "Helas! Nous avons encore besoin de " + $UNEASY_BL + " bouteilles d'Eau Bénite de plus...";
	mes "J'ai peur que les créatures de ce vieux cimetière ne deviennent rapidement incontrolables...";
	mes "Je vous en supplie, au nom d'Odin, aidez la ville de Prontera.";
	close;
}
