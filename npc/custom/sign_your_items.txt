//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= * Sign Your Items *
//===== By: ================================================== 
//= Lupus
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= eAthena 1
//===== Description: ========================================= 
//= Write you name on your rare equipment or weapon ^_-
//= Original X-Mas mini-quest(could be used as a permanent one)
//===== Additional Comments: ================================= 
//= 1.0 First release
//============================================================

prt_in,24,61,7	script	Perchik	47,{
	mes "[Perchik]";
	if(BaseJob==Job_Novice || BaseLevel<50) {
		mes "Désolé, je n'aide pas les mioches. Vas donc tuer quelques Porings.";
		emotion e_sry;
		close;
	}
	mes "Bonjour! Je peux ^000090apposer votre nom^000000 sur l'un de vos objets si vous voulez.";
	next;
	menu "En savoir plus...",-, "Apposer mon nom.",M_DO;

	mes "[Perchik]";
	mes "Je peux apposer votre nom sur n'importe lequel de vos équipements ou armes, du moment qu'il ne possède pas de slot.";
	emotion e_ic;
	next;
	mes "[Perchik]";
	mes "Il y a une semaine, mon chef m'a dit d'envoyer balader les Novices. Je ne sais pas trop pourquoi.";
	next;
	mes "[Perchik]";
	mes "Pour mon travail, j'accepte le paiement en ^0000803Gift Box^000000 (les grises)";
	mes "et je demande ^FF00005000 Zenys^000000 par raffinement de votre objet.";
	next;
	emotion e_cry;
	mes "[Perchik]";
	mes "Et oui, c'est cher, mais j'ai 12 enfants affamés à nourrir, et une femme très en colère...";
	next;
	mes "[Perchik]";
	mes "A moins que ce soit... 12 enfants très en colère, et une femme affamée...";
	emotion e_hmm;
	close;

M_DO:
	mes "[Perchik]";
	mes "Montrez moi l'objet que vous voulez signer...";
M_MENU:
	next;
	menu getequipname(1),M_PART1,getequipname(9),M_PART9,getequipname(10),M_PART10,getequipname(2),M_PART2,getequipname(4),M_PART4,
	     getequipname(3),M_PART3,getequipname(5),M_PART5,getequipname(6),M_PART6,getequipname(7),M_PART7,getequipname(8),M_PART8;

	//Head Gear
	M_PART1:
		set @part,1;
		if (getequipisequiped(1)) goto L_CHECK1;
		mes "[Perchik]";
		mes "A part un crâne avec un début de calvitie, je ne vois rien sur quoi je puisse travailler.";
		emotion 6;
		goto M_MENU;
	M_PART9:
		set @part,9;
		if (getequipisequiped(9)) goto L_CHECK1;
		mes "[Perchik]";
		mes "Des lunettes... Mettez-en, moi je n'en vois pas.";
		emotion 20;
		goto M_MENU;
	M_PART10:
		set @part,10;
		if (getequipisequiped(10)) goto L_CHECK1;
		mes "[Perchik]";
		mes "Bas les masques!";
		emotion 20;
		goto M_MENU;
	//Armor
	M_PART2:
		set @part,2;
		if (getequipisequiped(2)) goto L_CHECK1;
		mes "[Perchik]";
		mes "Votre ventre... ? Vous avez une petite brioche.";
		emotion 6;
		goto M_MENU;
	//Left Hand
	M_PART3:
		set @part,3;
		if (getequipisequiped(3)) goto L_CHECK1;
		mes "[Perchik]";
		mes "Signer votre main gauche? Je ne suis pas une célébrité, vous savez...";
		emotion 4;
		goto M_MENU;
	//Right Hand
	M_PART4:
		set @part,4;
		if (getequipisequiped(4)) goto L_CHECK1;
		mes "[Perchik]";
		mes "Signer votre main droite? Je ne suis pas une célébrité, vous savez...";
		emotion 4;
		goto M_MENU;
	//Garment
	M_PART5:
		set @part,5;
		if (getequipisequiped(5)) goto L_CHECK1;
		mes "[Perchik]";
		mes "Mais... Vous êtes nu!";
		goto M_MENU;
	//Foot Gear
	M_PART6:
		set @part,6;
		if (getequipisequiped(6)) goto L_CHECK1;
		mes "[Perchik]";
		mes "Eeerk!! Vous ne trouvez pas que ça sent un peu des pieds par ici?";
		emotion 16;
		goto M_MENU;
	//Accessory1
	M_PART7:
		set @part,7;
		if (getequipisequiped(7)) goto L_CHECK1;
		mes "[Perchik]";
		mes "Hum... Vous avez une jolie poche, en effet.";
		emotion 20;
		goto M_MENU;
	//Accessory2
	M_PART8:
		set @part,8;
		if (getequipisequiped(8)) goto L_CHECK1;
		mes "[Perchik]";
		mes "Hum... Vous avez une jolie poche, en effet.";
		emotion 20;
		goto M_MENU;

L_CHECK1:
	set @ref,0;
	set @id,getequipid(@part);
	if(getequipisidentify(@part)) goto L_CHECK2;
	mes "[Perchik]";
	mes "Tricheeeeeeuuuur!";
	npctalk "Tricheur!";
	emotion e_wah;
	close;

L_CHECK2:
	set @ref,getequiprefinerycnt(@part);
	mes "[Perchik]";
	mes "Voulez-vous apposer votre nom sur votre "+getitemname(@id)+"... ?";
	if(BaseJob==Job_Novice) {
		mes "Mais tu es un Novice! Mon chef m'a dit d'envoyer balader les petits mioches dans ton genre.";
		emotion e_sry;
		goto M_MENU;
	}
	set @slot0,getequipcardid(@part,0);
	set @slot1,getequipcardid(@part,1);
	set @slot2,getequipcardid(@part,2);
	set @slot3,getequipcardid(@part,3);

	if(@slot0==255 || @slot0==254 || @slot0<0) {
		mes "Désolé, mais cet objet possède déjà un nom.";
		mes "Je ne peux pas travailler là dessus.";
		emotion e_hmm;
		goto M_MENU;
	}
	if(@slot0>4000 && @slot0<5000) {
		mes "Une carte? Ici?!";
		mes "Comme je vous l'ai dit, je ne signe pas les objets qui ont des cartes.";
		emotion e_hmm;
		goto M_MENU;
	}
	if(getiteminfo(@id,10)) {
		mes "Désolé, je ne signe pas les objets qui possèdent un slot.";
		emotion e_sry;
		goto M_MENU;
	}

	if(@ref)mes "Cet objet a été raffiné "+@ref+" fois... Donc ajoutez ^FF00005000 Zenys^000000 par niveau de raffinement!";
	mes "Cela nous fait donc ^0000803Gift Boxes^000000 (grises)";
	if(@ref)mes "et ^FF0000"+(5000*@ref)+" Zenys^000000.";
	next;
	menu "Ok!",-, "S'en aller.",M_END;
	if(Zeny>=(5000*@ref) && countitem(644)>=3) {
		delitem 644,3;
		set Zeny,Zeny-(5000*@ref);
		goto L_MAKE;
	}
	mes "[Perchik]";
	mes "Je ne travaille pas gratuitement, désolé.";
	emotion e_sry;
	close;

L_MAKE:
	if(countitem2(@id,1,@ref,0,@slot0,@slot1,@slot2,@slot3)==0) {
		mes "[Perchik]";
		mes "Où est votre "+getitemname(@id)+"?";
		npctalk "Tricheur!";
		logmes "CHEATER: Tried to sign an item not having it: "+getitemname(@id);
		emotion e_wah;
		close;
	}
	delitem2 @id,1,1,@ref,0,@slot0,@slot1,@slot2,@slot3;

	mes "[Perchik]";
	mes "Et voilà!";
	emotion e_proud;

	getitem2 @id,1,1,@ref,0,254,0,getcharid(0)&0xffff,(getcharid(0)>>16)&0xffff;
	close;

M_END:
	mes "[Perchik]";
	mes "A plus tard...";
	emotion e_yawn;
	close;
}