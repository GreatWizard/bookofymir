//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//===== eAthena Script =======================================
//= Morroc Raceway Script
//===== By: ==================================================
//= acky (god@acky.com)
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= Any eAthena Version
//===== Description: =========================================
//= Lets players race around Morroc (pvp_y_1-5)
//===== Additional Comments: =================================
//= If there are more than 3 players, at least 3 people
//= must finish before a new race can be started.
//=
//= If there are less than 3 players, at least 1 person
//= must finish before a new race can be started.
//=
//= Removed permanent global variables
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Replaced effect numerics with constants. [Samuray22]
//============================================================

//Warps you into race way
morocc,166,105,6	script	Personnel de la Course#1	116,{
mes "[Personnel de la Course]";
mes "Voulez-vous participer à la ^0000FFCourse de Morroc^000000?";
next;
menu "Oui",L_Warp,"Non",-;
mes "[Personnel de la Course]";
mes "D'accord. Revenez quand vous voulez si vous changez d'avis.";
close;

L_Warp:
warp "pvp_y_1-5",165,256;
close;
}

//Warps you out of raceway
pvp_y_1-5,169,265,5	script	Personnel de la Course#2	116,{
mes "[Personnel de la Course]";
mes "Bienvenue à la Course de Morroc!";
next;
menu "Informations",-,"Quitter",L_Warp,"Annuler",L_Cancel;
mes "[Personnel de la Course]";
mes "Quelqu'un doit parler au Donneur de Départ pour lancer la course.";
next;
mes "[Personnel de la Course]";
mes "Une fois qu'elle aura commencé, vous devrez courir à travers Morroc dans le sens inverse des aiguilles d'une montre.";
next;
mes "[Personnel de la Course]";
mes "Vous devrez passer par tous les points de contrôle - pas de triche!";
close;

L_Warp:
warp "morocc",165,101;

L_Cancel:
mes "[Personnel de la Course]";
mes "A bientôt!";
close;
}

//Counts down and starts race
pvp_y_1-5,145,269,5	script	Donneur de Départ	733,{
if ($@race != 0) goto L_Started;
if ($@counting != 0) goto L_Started;
if ($@racecount == 1) goto L_Started;
L_Menu:
mes "[Donneur de Départ]";
mes "Veuillez rester derrière la ligne.";
menu "Donner le départ",L_Count,"Annuler",-;
close;

	L_Count:
		set $@counting,1;
		mes "La course va commencer...";
		addtimer 1000, "Donneur de Départ::OnCount1000";
		addtimer 2000, "Donneur de Départ::OnCount2000";
		addtimer 3000, "Donneur de Départ::OnCount3000";
		addtimer 4000, "Donneur de Départ::OnCount4000";
		announce strcharinfo(0) + " vient de lancer le compte-à-rebours. Soyez prêts pour la course!",1;
		close;


	OnCount1000:
		announce "[3]",1;
		end;

	OnCount2000:
		announce "[2]",1;
		end;

	OnCount3000:
		announce "[1]",1;
		end;

	OnCount4000:
		emotion 27;
		specialeffect EF_CHIMTO;
		announce "[PARTEZ!]",1;
		set $@race,1;
		set $@position,0;
		set $@counting,0;
		set $@raceid,rand(100000,999999);
		end;


L_Started:
if ((getmapusers("pvp_y_1-5") < 3) && ($@position > 0)) goto L_Menu;
if ($@position > 2) goto L_Menu;
mes "[Donneur de Départ]";
mes "Une course est déjà en cours.";
close;

OnInit:
set $@race,0;
set $@position,0;
set $@racecount,0;
end;
}

//Checkpoint 1
pvp_y_1-5,144,262,5	script	Point de Contrôle [1]	111,0,5,{
end;
OnTouch:
if (@raceid != $@raceid) goto L_Started;
if (@race == 6) goto L_Finished;
if ($@race == 1) goto L_Started;
mes "La course n'a pas encore commencé. Veuillez reculer.";
close;
L_Started:
set @race,1;
set @raceid,$@raceid;
end;
L_Finished:
mes "Vous venez de terminer la course!";
close;
}

//Checkpoint 2
pvp_y_1-5,73,247,5	script	Point de Contrôle [2]	111,6,6,{
end;
OnTouch:
if (@race != 1) goto L_Miss;
set @race,2;
announce "[" + strcharinfo(0) +"] a franchi le point de contrôle [1]",1;
end;
L_Miss:
mes "Vous n'êtes pas passé par tous les points de contrôle. Veuillez faire demi-tour.";
close;
}

//Checkpoint 3
pvp_y_1-5,77,44,5	script	Point de Contrôle [3]	111,6,6,{
end;
OnTouch:
if (@race != 2) goto L_Miss;
set @race,3;
announce "[" + strcharinfo(0) +"] a franchi le point de contrôle [2]",1;
end;
L_Miss:
mes "Vous n'êtes pas passé par tous les points de contrôle. Veuillez faire demi-tour.";
close;
}

//Checkpoint 3
pvp_y_1-5,249,60,5	script	Point de Contrôle [4]	111,6,6,{
end;
OnTouch:
if (@race != 3) goto L_Miss;
set @race,4;
announce "[" + strcharinfo(0) +"] a franchi le point de contrôle [3]",1;
end;
L_Miss:
mes "Vous n'êtes pas passé par tous les points de contrôle. Veuillez faire demi-tour.";
close;
}

//Checkpoint 4
pvp_y_1-5,255,256,5	script	Point de Contrôle [5]	111,6,6,{
end;
OnTouch:
if (@race != 4) goto L_Miss;
set @race,5;
announce "[" + strcharinfo(0) +"] a franchi le point de contrôle [4]",1;
end;
L_Miss:
mes "Vous n'êtes pas passé par tous les points de contrôle. Veuillez faire demi-tour.";
close;
}

//Finish Line
pvp_y_1-5,174,244,5	script	Ligne d'Arrivée	111,6,6,{
end;
OnTouch:
if (@raceid != $@raceid) goto L_WrongRace;
if (@race != 5) goto L_Miss;
set @race,6;
set $@position,$@position+1;
announce "[" + strcharinfo(0) +"] vient de franchir la Ligne d'Arrivée! [Position: " + $@position + "]",1;
end;
L_Miss:
mes "Vous n'êtes pas passé par tous les points de contrôle. Veuillez faire demi-tour.";
close;
L_WrongRace:
mes "Vous n'êtes pas inscrit pour cette course.";
close;
}

//Check Point Marker Flags
pvp_y_1-5,144,267,4	script	Point de Contrôle [1]#01	722,{
end;
}
pvp_y_1-5,144,257,4	script	Point de Contrôle [1]#02	722,{
end;
}
pvp_y_1-5,70,252,3	script	Point de Contrôle [2]#01	722,{
end;
}
pvp_y_1-5,77,243,3	script	Point de Contrôle [2]#02	722,{
end;
}
pvp_y_1-5,81,48,1	script	Point de Contrôle [3]#01	722,{
end;
}
pvp_y_1-5,72,40,1	script	Point de Contrôle [3]#02	722,{
end;
}
pvp_y_1-5,244,65,7	script	Point de Contrôle [4]#01	722,{
end;
}
pvp_y_1-5,252,57,7	script	Point de Contrôle [4]#02	722,{
end;
}
pvp_y_1-5,259,260,5	script	Point de Contrôle [5]#01	722,{
end;
}
pvp_y_1-5,251,252,5	script	Point de Contrôle [5]#02	722,{
end;
}
pvp_y_1-5,174,249,4	script	Ligne d'Arrivée#01	722,{
end;
}
pvp_y_1-5,174,238,4	script	Ligne d'Arrivée#02	722,{
end;
}
