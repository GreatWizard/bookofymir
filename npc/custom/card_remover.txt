//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Etna, corrigé par Myllena
//===== eAthena Script ======================================= 
//= Card Removal NPC
//===== By: ================================================== 
//= TyrNemesis^
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Removes cards from equipped items.
//===== Additional Comments: ================================= 
//= 1.0 First version. [TyrNemesis^]
//= 1.2 Optimized and fixed getequipname menu. [Kisuka]
//============================================================ 

prt_in,28,73,4	script	Vieille Femme Savante::eAcustom	78,{
	mes "[Vieille Femme Savante]";
	mes ( isday() ? "Bonjour" : "Bonsoir" ) + ", mon p'tit. J'ai le pouvoir de retirer les cartes que vous avez serties sur votre équipement. Est-ce que ce service vous intéresse?";
	next;
	switch(select("Oui, tout à fait.:Que facturez-vous?:Non merci.")) {
		case 1:
			mes "[Vieille Femme Savante]";
			mes "Très bien. Quel objet dois-je travailler?";
			next;

			setarray .@position$[1], "Tête (haut)","Armure","Main gauche","Main droite","Épaules","Chaussures","Accessoire gauche","Accessoire droit","Tête (milieu)","Tête (bas)";
			set .@menu$,"";
			for( set .@i,1; .@i <= 10; set .@i,.@i+1 )
			{
				if( getequipisequiped(.@i) )
					set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";

				set .@menu$, .@menu$ + ":";
			}
			set .@part,select(.@menu$);
			if(!getequipisequiped(.@part)) {
				mes "[Vieille Femme Savante]";
				mes "Mon p'tit... Vous ne portez rien à cet emplacement qui nécessite de vous retirer des cartes.";
				close;
			}
			if(getequipcardcnt(.@part) == 0) {
				mes "[Vieille Femme Savante]";
				mes "Mon p'tit... Il n'y a aucune carte sertie sur cet objet. Je ne peux rien faire avec ça, je suis désolée.";
				close;
			}
			set .@cardcount,getequipcardcnt(.@part);
			
			if (!checkweight(1202,(.@cardcount+1))) { 
				mes "^3355FFUn instant! Je ne peux vous offrir aucun de mes services car vous portez trop de choses. Déposez vos objets superflus dans votre Stok Kafra et revenez ensuite~";
				close;
			}
			mes "[Vieille Femme Savante]";
			mes "Cet objet est serti de " + .@cardcount + " carte(s). Pour exercer ma magie, j'ai besoin de " + (200000+(.@cardcount * 25000)) + " Zenys, d'un ^0000FFMorceau d'Étoile^000000, et d'une ^0000FFGemme Jaune^000000.";
			next;
			if(select("Très bien. Faites-le!:Laissez tomber!") == 2) {
				mes "[Vieille Femme Savante]";
				mes "Très bien. Revenez quand vous aurez besoin de mes services.";
				close;
			}
			if((zeny < (200000+(.@cardcount * 25000))) || (countitem(1000) < 1) || (countitem(715) < 1)) {
				mes "[Vieille Femme Savante]";
				mes "Vous n'avez pas tous ce dont j'ai besoin pour exercer ma magie, mon enfant. Revenez quand vous aurez le nécessaire.";
				close;
			}
			mes "[Vieille Femme Savante]";
			mes "Avant de commencer, je dois vous avertir... je peux échouer. Dans ce cas, il se peut que les cartes, l'objet, ou les deux soient détruits. Je ne fais aucun remboursement. Ceci étant dit, quel est le plus important pour vous: les cartes, ou l'objet?";
			next;
			switch(select("J'ai changé d'avis à ce sujet.:L'objet.:Les cartes.")) {
				case 1:
					mes "[Vieille Femme Savante]";
					mes "Très bien. Revenez quand vous aurez besoin de mes services.";
					close;
				case 2:
					set .@failtype,1;
					break;
				case 3:
					set .@failtype,2;
			}
			mes "[Vieille Femme Savante]";
			mes "Très bien. Je vais commencer.";
			set zeny,zeny - (200000+(.@cardcount * 25000));
			delitem 1000,1;
			delitem 715,1;
			
			// Replace the constants in the next 3 lines with failure chance values defined in refine_db.txt
			// First value = Total failure chance (item and cards destroyed)
			// Second value = Partial failure chance (one or the other is destroyed, player decides which one is safe)
			// Third value = Harmless failure chance (all that's lost is your investment)

			set .@failchance,rand(100);
		/*	
			if(.@failchance < 2) {
				next;
				failedremovecards .@part,0;
				mes "[Vieille Femme Savante]";
				mes "Le processus a été un échec total. J'ai bien peur que l'objet et les cartes n'aient été détruits.";
				close;
			}

			if(.@failchance < 8) {
				if (.@failtype == 1) {
					next;
					failedremovecards .@part,1;					
					mes "[Vieille Femme Savante]";
					mes "Bien que j'aie réussi à retirer les cartes de l'objet, elles ont été détruites au cours du processus. L'objet, cependant, est intact.";
					close;
				}

				if (.@failtype == 2) {
					next;
					failedremovecards .@part,2;
					mes "[Vieille Femme Savante]";
					mes "C'est très regrettable. J'ai réussi à enlever les cartes, mais l'objet lui-même a été détruit au cours du processus.";
					close;	
				}
			}
		*/

			if(.@failchance < 10) {
				next;
				failedremovecards .@part,3;				
				mes "[Vieille Femme Savante]";
				mes "Je n'ai pas réussi à enlever les cartes. Heureusement pour vous, l'objet et les cartes sont intactes.";
				close;	
			}
			next;
			successremovecards .@part;
			mes "[Vieille Femme Savante]";
			mes "Le processus a été un succès. Voici vos cartes et votre objet. Adieu!";
			close;			
		case 2:
			mes "[Vieille Femme Savante]";
			mes "Je facture un montant forfaitaire de 200 000 Zenys, plus 25 000 Zenys pour chaque carte que j'enlève de l'objet. De plus, j'ai besoin d'un Morceau d'Étoile et d'une Gemme Jaune pour exercer ma magie. ";
			close;
		case 3:
			mes "[Vieille Femme Savante]";
			mes "Très bien. Revenez quand vous aurez besoin de mes services";
			close;
	}
}
