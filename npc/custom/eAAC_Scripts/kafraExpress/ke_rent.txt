//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Kafra Express - Rental Module
//===== By: ================================================== 
//= Skotlex 
//===== Current Version: ===================================== 
//= 1.8
//===== Compatible With: ===================================== 
//= eAthena SVN R3424+
//===== Description: ========================================= 
//= Part of the Kafra Express Script Package.
//= Rents PecoPecos, Falcons, Carts 
//===== Additional Comments: ================================= 
//= See config.txt for configuration.
//============================================================ 

-	script	keInit_rent	-1,{
OnInit:	//Load Config
	donpcevent "keConfig::OnLoadRent";
	end;
}

function	script	F_keRent	{
	set @cartCost,callfunc("F_keCost",$@kert_cartCost,100);
	if(@kert_cartOnly) {
		set @kmenu, 2;
	} else {
		set @falconCost,callfunc("F_keCost",$@kert_falconCost,100);
		set @pecoCost,callfunc("F_keCost",$@kert_pecoCost,100);
		set @kmenu, select (
			"- Annuler",
			"- Louer une Charrette ("+@cartCost+"z)",
			"- Louer un Faucon ("+@falconCost+"z)",
			"- Louer un Peco Peco ("+@pecoCost+"z)"
		);
	}
	switch (@kmenu) {
	case 2: //Cart
		if (getskilllv("MC_PUSHCART")==0) {
			callfunc "F_keIntro", -1, "Désolée, vous devez avoir appris la compétence 'Permis Charrette' pour pouvoir louer une Charrette.";
		} else 
		if (checkcart()) {
			callfunc "F_keIntro", -1, "Vous possédez déjà une Charrette.";
		} else
		if (!(callfunc("F_keCharge",$@kert_cartCost,100,1))) {
			callfunc "F_keIntro", e_an, "Désolée, mais vous n'avez pas assez d'argent.";
		} else {
			setcart;
			emotion e_ok;
		}
		break;
	case 3: //Falcon
		if (getskilllv("HT_FALCON")==0) {
			callfunc "F_keIntro", -1, "Désolée, vous devez avoir appris la compétence 'Falcon Taming' pour pouvoir louer un Faucon.";
		} else
		if (checkfalcon()) {
			callfunc "F_keIntro", -1, "Vous possédez déjà un Faucon.";
		} else
		if (!(callfunc("F_keCharge",$@kert_falconCost,100,1))) {
			callfunc "F_keIntro", e_an, "Désolée, mais vous n'avez pas assez d'argent.";
		} else {
			setfalcon;
			emotion e_ok;
		}
		break;
	case 4: //pecopeco
		if (getskilllv("KN_RIDING")==0) {
			callfunc "F_keIntro", -1, "Désolée, vous devez avoir appris la compétence 'Équitation' pour pouvoir louer un Peco Peco.";
		} else
		if (checkriding()) {
			callfunc "F_keIntro", -1, "Vous possédez déjà un Peco Peco.";
		} else
		if (!(callfunc("F_keCharge",$@kert_pecoCost,100,1))) {
			callfunc "F_keIntro", e_an, "Désolée, mais vous n'avez pas assez d'argent.";
		} else {
			setriding;
			emotion e_ok;
		}
		break;
	}
	return;
}
