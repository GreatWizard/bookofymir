//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Etna
//===== eAthena Script ======================================= 
//= Bandit Beard Quest
//===== By: ================================================== 
//= Mega Man Expert & Lupus
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 0.5.2 +
//===== Description: ========================================= 
//= A harmless quest for a simply item w/o any bonuses "Bandit Beard"
//===== Additional Comments: ================================= 
//= Fully working.
//= 1.0 First release
//= 1.1 Fixed some exploits, bugs and typos. Optimized 8) [Lupus]
//= 1.1b fixed some typos
//= 1.2 fixed donpcevent label [Lupus]
//= 1.3 Removed duplicates [L0ne_W0lf]
//============================================================

umbala,126,129,4	script	Homme barbu#bandit	120,{
	if(BEARD_QUEST == 4 ) goto L_MAKE;
	if(BEARD_QUEST >= 5 ) goto L_DONE;
	if(BEARD_QUEST >= 1 ) goto L_THREAD;

	mes "[Homme barbu]";
	mes "Barbe! Qui veut une magnifique Barbe de Bandit?";
	mes "Voulez-vous une barbe?";
	next;
	menu "Oui, je voudrais des poils sur le visage!",-,"Non merci, ça fait négligé!",M_NO;

	mes "[Homme barbu]";
	mes "HoHo~ Vous voulez des moustaches.";
	emotion 18;
	mes "Eh bien, je pourrai vous donner ma barbe, mais elle n'est pas bon marché. En fait, il ne m'en reste qu'une seule... mais je ne la vends pas. Mais ne vous fâchez pas! Je peux vous en fabriquer une.";
	next;
	set BEARD_QUEST,1;
L_THREAD:
	mes "[Homme barbu]";
	mes "J'ai besoin de ^8080FFFil^000000 pour la barbe.";
	mes "Allez voir mon collègue d'Izlude pour le ^8080FFFil^000000.";
	next;

	mes "[Homme barbu]";
	mes "J'aurais également besoin des choses suivantes pour la barbe:";
L_LIST:
	mes "^8080FF1 Élastique^000000";
	mes "^8080FF5 Peaux de Bêtes^000000";
	mes "^8080FF1 Teinture Noire^000000";
	mes "^8080FF1 Barbe de Grand-Père^000000";
	mes "^8080FF100 Morves^000000";
	mes "^8080FF1 Antiagent^000000";
	mes "^8080FF1 Diamant 2 Carats^000000";
	close;

M_NO:
	mes "[Homme barbu]";
	mes "Très bien, comme vous voulez!";
	emotion 7;
	close;

L_DONE:
	mes "[Homme barbu]";
	mes "Désolé, je peux seulement en faire une par personne.";
	mes "Nous ne voulons pas voir des barbes partout. Pas vrai?";
	emotion 29;
	close;

L_MAKE:
	mes "[Homme barbu]";
	mes "Faisons votre barbe!";
	next;
	if(countitem(7200) < 1 || countitem(919) < 1 ||	countitem(983) < 1 || countitem(2241) < 1 ||
	countitem(938) < 100 || countitem(973) < 1 || countitem(731) < 1) goto L_NOITEMS;
	delitem 7200, 1;
	delitem 919, 1;
	delitem 983, 1;
	delitem 2241, 1;
	delitem 938, 100;
	delitem 973, 1;
	delitem 731, 1;
	mes "^8080FF~Vous le voyez découper et coudre de la barbe~^000000";
	next;
	mes "^8080FF~Il vous donne la barbe une fois terminée~^000000";
	next;
	getitem 2237, 1;
	set BEARD_QUEST, 5;
	mes "[Homme barbu]";
	mes "Bonne journée!";
	emotion 29;
	close;

L_NOITEMS:
	mes "[Homme barbu]";
	mes "Diable! Je ne peux pas faire une barbe sans les objets!";
	mes "Revoici la liste:";
	emotion 23;
	goto L_LIST;
}  

//Master Tailor----------
izlude_in,123,175,4	script	Maître Tailleur#bandit	50,{
	mes "[Maître Tailleur]";
	mes "Bonsoir! Je suis le maître tailleur!";
	if(BEARD_QUEST != 1 ) close;

	next;
	mes "[Maître Tailleur]";
	mes "Comment? Un homme barbu vous envoie...";
	mes "Et bien, je suis navré, je n'ai plus de ^8080FFFil^000000.";
	mes "Mais j'ai l'adresse de mon fournisseur.";
	mes "C'est l'entrepôt de la Corporation Karfa à Al De Baran.";
	mes "L'adresse est: Al De Baran 59,221.";
	mes "À plus tard.";
	set BEARD_QUEST, 2;
	close;
}
// Karfa Thread Clerk---
aldeba_in,70,179,5	script	Employée Karfa#bandit	113,{
	mes "[Employée Karfa]";
	if(BEARD_QUEST == 3 ) goto L_REPEAT;
	if(BEARD_QUEST > 3 ) goto L_DONE;

	mes "Que puis-je pour vous?";
	next;
	if(BEARD_QUEST == 2 ) goto L_DELIVERY;

	menu "Qu'y a t-il dans ces caisses?",-,"Rien.",M_NOPE;

	mes "[Employée Karfa]";
	mes "Elles sont vides.";
M_NOPE:
	close;

L_DELIVERY:

	mes "[Employée Karfa]";
	mes "Désolée mais nous n'avons pas reçu de ^8080FFFil^000000.";
	mes "Nos expéditions sont perturbées en cette période d'insécurité.";
	mes "Vous savez, nous nous procurons le ^8080FFFil^000000 à Alberta, mais les monstres attaquent nos négociants.";
	next;
	mes "[Employée Karfa]";
	mes "Avant, les monstres ne faisaient pas celà.";
	mes "C'est peut être dû à cette canicule.";
	mes "Sil vous plait, aider nous!";
	set BEARD_QUEST,3;
	next;
	mes "[Employée Karfa]";
L_REPEAT:
	mes "Oh d'ailleurs, le vendeur de Fil se trouve aux coordonnées 60 et 100 d'Alberta...";
L_DONE:
	mes "Merci!";
	close;
}

//Thread Merchant ------
alberta,65,123,6	script	Homme#bandit	85,{
	if(BEARD_QUEST == 3 && $@beardMobD) goto L_KILLED;
	if(BEARD_QUEST > 3) goto L_DONE;

	mes "[Marchand de Fil]";
	mes "ARGGG!! Je ne pourrai jamais faire mes livraisons à Al De Baran!";
	mes "Je continue de me faire assaillir par les insectes.";
	mes "Ils volent toutes mes provisions et le ^8080FFFil^000000.";
	next;
	mes "[Marchand de Fil]";

	if(BEARD_QUEST != 3) mes "Que dois-je faire?";
	if(BEARD_QUEST != 3) close;

	if($@beardmob > 0) mes "Les tuer! Encore et encore!!!";
	if($@beardmob > 0) close;
	mes "Êtes-vous ici pour m'aider?";
	emotion 1;
	next;
	menu "Oui",-,"Non",M_NO;

	mes "[Marchand de Fil]";
	mes "Oh merci mon Dieu!";
	mes "Si vous pouviez seulement tuer les monstres-insectes, je pourrais effectuer ma livraison.";
	next;
	mes "[Marchand de Fil]";
	mes "Oh NON! Les voici qui arrivent!!";
	emotion 19;
	donpcevent "BRDQ_MOBS::OnStart";
	close;

L_KILLED:
	set $@beardMobD,0;
	mes "[Marchand de Fil]";
	mes "Merci d'avoir tué ces monstres-insectes.";
	mes "En témoignage de ma gratitude voici une boîte pleine de ^8080FFFil^000000.";
	set BEARD_QUEST,4;
	close;

L_DONE:
	mes "[Marchand de Fil]";
	mes "Merci encore!";
	emotion 15;
	close;

M_NO:
	mes "[Marchand de Fil]";
	mes "Que faire...";
	close;
}

//mobsummons -------
alberta,1,1,1	script	BRDQ_MOBS	-1,{
OnStart:
	set $@beardMob,10;
	set $@beardMobD,0;
	monster "alberta",65,120,"--ja--",1054,1,"BRDQ_MOBS::OnDie";
	monster "alberta",66,120,"--ja--",1054,1,"BRDQ_MOBS::OnDie";
	monster "alberta",67,120,"--ja--",1054,1,"BRDQ_MOBS::OnDie";
	monster "alberta",68,120,"--ja--",1054,1,"BRDQ_MOBS::OnDie";
	monster "alberta",66,121,"--ja--",1054,1,"BRDQ_MOBS::OnDie";
	monster "alberta",67,121,"--ja--",1054,1,"BRDQ_MOBS::OnDie";
	monster "alberta",68,121,"--ja--",1054,1,"BRDQ_MOBS::OnDie";
	monster "alberta",66,122,"--ja--",1054,1,"BRDQ_MOBS::OnDie";
	monster "alberta",67,122,"--ja--",1054,1,"BRDQ_MOBS::OnDie";
	monster "alberta",68,122,"--ja--",1054,1,"BRDQ_MOBS::OnDie";
	end;
OnDie:
	set $@beardmob, $@beardmob - 1;
	if($@beardmob > 0) end;
	set $@beardMobD,1;
	end;
On1201:
On0001:
	set $@beardMob,0;
	set $@beardMobD,0;
	end;
}
