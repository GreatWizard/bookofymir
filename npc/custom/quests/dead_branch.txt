//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Etna
//===== eAthena Script ======================================= 
//= Dead Branch (+Bloody Branch) Quest
//===== By: ================================================== 
//= GM-Yevon
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= eAthena 0.5.2 +
//===== Description: ========================================= 
// Simple item trade-in quest, a person can get a Dead Branch
// by simply providing the items: Log x3 (7201), Wooden Heart x1 (7189), 
// Trunk x5 (1019), and Wooden Gnarl x1 (7222).
//===== Additional Comments: ================================= 
//= Fully working.
//= 1.1 Optimized, added Bloody Branch with 0.01% chance [Lupus]
//============================================================

niflheim,204,179,3	script	Monsieur Mobry	121,{

	mes "[Monsieur Mobry]";
	mes "Les arbres ont un esprit, vous savez...";
	next;

	mes "[Monsieur Mobry]";
	mes "En fait, je connais un secret au sujet de ces esprits qui peut en quelque sorte... peut-être vous aider...";
	next;

	mes "[Monsieur Mobry]";
	mes "Je peux créer une branche pleine de vie... Enfin, si vous pouvez me fournir le bon matériel...";
	next;
	goto L_MENU;

L_MENU:
	menu "Que dois-je faire?",-,"Où puis-je trouver le matériel?",M_FIND,"Fabriquez-moi une Branche Morte!",M_CREATE,"Oubliez ça...",M_END;

	mes "[Monsieur Mobry]";
	mes "Mwehehe... Je vois que vous souhaitez contrôler les esprits des arbres, pas vrai?... Ok... J'ai besoin de:";
	mes "^1354453 Bûches^000000";
	mes "^1354451 Coeur de Bois^000000";
	mes "^1354455 Troncs^000000";
	mes "^1354451 Tronçon de Bois^000000";
	next;
	goto L_MENU;

M_FIND:
	mes "[Monsieur Mobry]";
	mes "Vous pouvez trouver ces objets sur certains monstres...";
	mes "^135445Les Bûches peuvent être trouvé plus loin sur les Golems de Bois.^000000";
	mes "^135445Le Coeur de Bois est l'essence des Golems de Bois.^000000";
	mes "^135445Les Troncs? Sérieusement, pensez aux bois...^000000";
	mes "^135445Le Tronçon de Bois... Gibbet...^000000";
	next;
	goto L_MENU;

L_NOLOG:
	mes "[Monsieur Mobry]";
	mes "Les ^135445Bûches^000000 forment le corps... Il m'en faut plusieurs.";
	mes "Apportez-moi trois rondins et je vous ferai l'esprit...";
	close;

L_NOHEART:
	mes "[Monsieur Mobry]";
	mes "Les ^135445Coeurs de Bois^000000 sont l'essence de l'esprit.";
	mes "Apportez-moi un coeur en bois et je vous ferai l'esprit...";
	close;

L_NOTRUNK:
	mes "[Monsieur Mobry]";
	mes "Comment? Vous n'avez même pas réussi à trouver ^1354455 Troncs^000000?";
	mes "Allez tuer des Saules Anciens ou autre chose... Bon sang... pas de troncs heh... pathétique.";
	close;

L_NOGNARL:
	mes "[Monsieur Mobry]";
	mes "Comment puis-je faire sans ^135445Tronçon de Bois^000000?";
	mes "J'ai dit Gibbet... Ils sont tous autour de cet endroit...";
	close;

M_CREATE:
	mes "[Monsieur Mobry]";
	mes "Eh bien... laissez-moi regarder ce que vous m'avez apporté...";
	next;
	if(countitem(7201)<3) goto L_NOLOG;
	if(countitem(7189)<1) goto L_NOHEART;
	if(countitem(1019)<5) goto L_NOTRUNK;
	if(countitem(7222)<1) goto L_NOGNARL;
	delitem 7201,3;
	delitem 7189,1;
	delitem 1019,5;
	delitem 7222,1;
	mes "[Mister Mobry]";
	mes "Heh... Espérons que vous vous amusiez bien avec les esprits des arbres... Soyez prudent tout de même...";
	mes "Heh... Alors, ferez-vous attention? Enfin après tout... Que m'importe si vous mourez..... Ne faites pas attention.";
	if(rand(1000)==0) goto L_GIVE2;
	getitem 604,1; //Dead Branch
	close;
L_GIVE2:
	getitem 12103,1; //Bloody Branch
	close;

M_END:
	mes "[Monsieur Mobry]";
	mes "Aucun esprit pour vous... alors au revoir!";
	close;
}
