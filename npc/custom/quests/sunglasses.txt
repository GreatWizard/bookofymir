//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Etna et Nimos
//===== eAthena Script =======================================
//= Quest For Slotted Sunglasses
//===== By: ==================================================
//= Aegis - amichan
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: =====================================
//= Any eAthena Version;
//===== Description: =========================================
//= Quest to get Slotted Sunglasses
//===== Additional Comments: =================================
//= 1.0 by Aegis 1.1 by aichan 1.2 by x[tsk] 1.3 by Darkchild
//= 1.5 Fixed Exploit [Lupus]
//= 1.6 Fixed Exploit [KarLaeda]
//= 1.7 Relocated 'Maseph' to moc_fild07. [Kisuka]
//============================================================

// quest will reset it self after 1 pair of Slotted Sunglasses is made. 
// 1st part of the quest

alberta,88,193,5	script	Indic	73,{ // 'Commerçant de lunettes de soleil' c'est beaucoup trop long et ça ne colle pas avec le npc qui fournit seulement un renseignement. Je me suis donc permise de changer le nom du npc (Etna)
	if(SG_QUEST1 == 1) goto L_SG_Q1_Done;
	mes "[Indic]";
	mes "Bonjour! Que puis-je faire pour vous?";
	next;
	menu "Il paraît que vous faites des ^0000FFSunglasses[1]^000000.",-,"Rien, désolé de vous avoir dérangé.",L_SG_No;

	mes "[Indic]";
	mes "Je ne les fabrique pas, mais je peux vous dire où trouver la personne qui s'en charge. Contre de petits honoraires...";
	next;
	menu "Combien?",L_SG_1,"Pas question, je la trouverai, moi-même!",-;

	mes "[Indic]";
	mes "Comme vous voulez, mais le fabricant ne vous fera pas de ^0000FFSunglasses[1]^000000 à moins de savoir que vous veniez.";
	mes "Je pourrais l'avertir que vous allez le voir, mais bon...";
	next;
	mes "[Indic]";
	mes "Revenez me voir, quand vous aurez abandonné vos recherches. Hahaha!";
	close;
L_SG_1:
	mes "[Indic]";
	mes "Pour que je vous donne des informations sur les ^0000FFSunglasses[1]^000000 vous devez m'obtenir:";
	mes "^0000881 Diamant 1 Carat^000000,"; //myllena: traduction des objets
	mes "^00008850 Plumes^000000, ";
	mes "et ^000088100000 Zenys^000000.";
	next;
	menu "Bien, voici!",L_SG_1_Check,"C'est trop!",-;

	mes "[Indic]";
	mes "Comme vous voulez.";
	close;
L_SG_1_Check:
	if (countitem(730) < 1) goto L_SG_Diamond;
	if (countitem(949) < 50) goto L_SG_Feathers;
	if (zeny<100000) goto L_SG_Funds;
	delitem 730,1;
	delitem 949,50;
	set zeny,zeny-100000;
	set SG_QUEST1,1;
	mes "[Indic]";
	mes "Super! Maintenant, écoutez attentivement.";
	next;
	mes "[Indic]";
	mes "Cherchez une femme nommée Maseph, quelque part au nord de Morroc.";
	mes "Je vais lui envoyer un message pour lui faire savoir que vous venez.";
	close;
L_SG_Diamond:
	mes "[Indic]";
	mes "Vous n'avez pas le ^000088Diamant 1 Carat^000000. Revenez me voir quand vous l'aurez.";
	close;
L_SG_Feathers:
	mes "[Indic]";
	mes "Vous n'avez pas les ^00008850 Plumes^000000. Revenez me voir quand vous les aurez.";
	close;
L_SG_Funds:
	mes "[Indic]";
	mes "Vous n'avez pas les ^000088100000 Zenys^000000. Revenez me voir quand vous les aurez.";
	close;
L_SG_Q1_Done:
	mes "[Indic]";
	mes "Je ne peux rien vous dire de plus.";
	next;
	mes "[Indic]";
	mes "Allez voir Maseph. Elle est quelque part au nord de Morroc.";
	close;
L_SG_No:
	mes "[Indic]";
	mes "Revenez me voir quand vous serez prêt.";
	close;
}
 
// 2nd part of the quest

moc_fild07,334,186,5	script	Maseph	702,{
	if(SG_QUEST1 == 1) goto L_SG_2;
	mes "[Maseph]";
	mes "Belle journée, n'est-ce pas?";
	close;
L_SG_2:
	mes "[Maseph]";
	mes "Salut!";
	next;
	mes "[Maseph]";
	mes "Vous êtes venus pour des ^000088Sunglasses[1]^000000, n'est-ce pas?";
	next;
	menu "Oui.",L_SG_2_Start,"Non, désolé de vous avoir dérangé.",-;

	mes "[Maseph]";
	mes "Vous voilà, alors!";
	close;
L_SG_2_Start:
	mes "[Maseph]";
	mes "Bon, pour vous en fabriquer, j'ai besoin d'une paire de ^000088Sunglasses^000000 et 400000 Zenys.";
	next;
	menu "Vous pouvez y aller!",L_SG_Q2_Check,"Non, merci.",-;

	mes "[Maseph]";
	mes "Comme vous voulez.";
	close;
L_SG_Q2_Check:
	if(countitem(2201) < 1) goto L_SG_Sunglasses;
	if(zeny<400000) goto L_SG_Funds;
	delitem 2201,1;
	set zeny,zeny-400000;
	mes "[Maseph]";
	mes "Merci. Je vais les faire tout de suite.";
	next;
	mes "[Maseph]";
	mes "Voilà mon ami!";
	getitem 2202,1;
// quest reset
	set SG_QUEST1,0;
	next;
	mes "[Maseph]";
	mes "Appréciez vos ^000088Sunglasses[1]^000000.";
	close;
L_SG_Sunglasses:
	mes "[Maseph]";
	mes "J'ai besoin d'une paire de ^000088Sunglasses^000000. Revenez me voir quand vous en aurez une.";
	close;
L_SG_Funds:
	mes "[Maseph]";
	mes "Vous n'avez pas assez d'argent. Revenez me voir quand vous en aurez.";
	close;
}