//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Moscovia Kafras
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Kafras for the town of Moscovia.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//============================================================ 

moscovia,223,191,4	script	Employée Kafra#mosk	114,{
	cutin "kafra_04",2;
	mes "[Employée Kafra]";
	mes "La Corporation Kafra vous accompagne où que vous alliez pour vous fournir les meilleurs services.";
	mes "En quoi puis-je vous aider?";
	next;
	switch(select("Sauvegarder:Ouvrir mon Stock:Louer une Charrette:Demander d'Autres Informations:Annuler")) {
		case 1:
			mes "[Employée Kafra]";
			mes "Votre Point de Sauvegarde a été enregistré ici, à Moscovia.";
			mes "Merci d'avoir utilisé les Services Kafras.";
			savepoint "moscovia",221,194;
			close2;
			cutin "", 255;
			end;
		case 2:
			if(basicskillcheck() && getskilllv("NV_BASIC") < 6){
				mes "[Employée Kafra]";
				mes "Je suis désolée, mais vous devez être un Novice de Niveau de Métier 6 au minimum pour pouvoir consulter votre stock.";
				close2;
				cutin "", 255;
				end;
			}
			if(countitem(7059)) delitem 7059,1;
			else {
				if(Zeny<80){
					mes "[Employée Kafra]";
					mes "Je suis désolée, mais vous n'avez pas assez d'argent pour utiliser le service de Stockage.";
					mes "Le prix d'accès au Stock est de 80 Zenys.";
					close2;
					cutin "", 255;
					end;
				}
				set Zeny, Zeny-80;
				set RESRVPTS, RESRVPTS + (80/5);
			}
			mes "[Employée Kafra]";
			mes "Merci d'avoir utilisé les Services Kafra!";
			mes "Nous espérons vous revoir très bientôt.";
			
			callfunc("F_CheckKafCode");	//check your storage password, if set
			
			close2;
			openstorage;
			cutin "", 255;
			end;
		case 3:
			if(baseClass != Job_Merchant){
				mes "[Employée Kafra]";
				mes "Je suis désolée, mais la location de Charrettes n'est proposée qu'aux Marchands, Forgerons et Alchimistes.";
				close2;
				cutin "", 255;
				end;
			}

			else if(checkcart() == 1){
				mes "[Employée Kafra]";
				mes "Désolée... Mais vous avez déjà une Charrette....";
				close2;
				cutin "", 255;
				end;
			}

			if(countitem(7061) > 0) delitem 7061,1;
			else {
				mes "[Employée Kafra]";
				mes "La location de Charrette coûte 700 Zenys.";
				mes "Acceptez-vous ce tarif?";
				next;
				if(select("Oui, merci.:Non.") == 1) {
					if(Zeny<700){
						mes "[Employée Kafra]";
						mes "Je suis désolée, mais vous n'avez pas assez d'argent pour payer les 700 Zenys nécessaires à la location d'une Charrette.";
						close2;
						cutin "", 255;
						end;
					}
					set Zeny,Zeny-700;
					set RESRVPTS, RESRVPTS + 48;
				}else{
					close2;
					cutin "", 255;
					end;
				}
			}
			setcart;
			close2;
			cutin "", 255;
			end;
		case 4:
			if(select("Consulter le nombre de Points Kafra.:Annuler.") == 2) {
				cutin "", 255;
				close;
			}
			mes "[Employée Kafra]";
			mes "Voyons voir...";
			mes strcharinfo(0) + "...";
			mes "Ah, vous avez un total de " +RESRVPTS+ " Points Kafra.";
			next;
			mes "[Employée Kafra]";
			mes "Vous pouvez échanger ces points contre de nombreux lots, au Bureau Central des Kafras à Al De Baran.";
			close2;
			cutin "", 255;
			end;
		case 5:
			mes "[Employée Kafra]";
			mes "Merci d'avoir utilisé les Services Kafra!";
			mes "Nous espérons vous revoir très bientôt.";
			close2;
			cutin "", 255;
			end;
	}
}