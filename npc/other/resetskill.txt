//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script =======================================
//= Hypnotiseur
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= [Aegis conversion]
//= Permenent first class, base level 40 and under skill resetter.
//= First Class characters and expanded classes only.
//= Baby Classes and First Class trans can't use this service.
//===== Additional Comments: =================================
//= 1.0 First version. [L0ne_W0lf]
//============================================================
prontera,146,232,4	script	Hypnotiseur#2	124,{
	mes "[Hypnotiseur]";
	mes "Salutations, aventurier.";
	mes "Je suis un membre de l'Académie d'Hypnotisme en détachement à Prontera, afin de proposer nos services de Remise à Zéro des Compétences à ceux qui le souhaiteraient et à un bon prix, c'est à dire gratuitement!";
	next;
	mes "[Hypnotiseur]";
	mes "Bien que le nombre de Remise à Zéro des Compétences est illimité, deux conditions doivent être cependant requises.";
	mes "Tout d'abord, vous devez posséder un ^FF0000Niveau de Base inférieur à 40^000000.";
	next;
	mes "[Hypnotiseur]";
	mes "Deuxièmement, seules les personnes exerçant les métiers:";
	mes "^FF0000Epéiste, Acolyte, Mage,";
	mes "Voleur, Archer, Marchand,";
	mes "Taekwon, Gunslinger ou Ninja^000000";
	mes "peuvent bénéficier de ce service.";
	mes "Avez-vous des questions?";
	next;
	switch(select("Qu'est-ce qu'une Remise à Zéro?:Je souhaite faire une Remise à Zéro.:Rien, merci.")) {

	case 1:
		mes "[Hypnotiseur]";
		mes "Le service de Remise à Zéro des Compétences permet aux aventuriers de redistribuer leurs Points de Compétences s'ils ne sont pas satisfaits de leurs compétences actuelles.";
		next;
		mes "[Hypnotiseur]";
		mes "Avant de procéder à une Remise à Zéro des Compétences, vous ne devez rien avoir dans votre inventaire, par exemple en déposant les objets dans le Stock Kafra.";
		next;
		mes "[Hypnotiseur]";
		mes "Ah, si vous êtes marchand il vous faudra également retirer votre ^FF0000Charrette^000000.";
		close;

	case 2:
		mes "[Hypnotiseur]";
		mes "Êtes-vous certain de vouloir utiliser notre service de ^FF0000Remise à Zéro des Compétences^000000?";
		next;
		if (select("Oui:Non") == 1) {
			if (BaseLevel > 40) {
				mes "[Hypnotiseur]";
				mes "Je suis désolé, mais votre Niveau de Base est supérieur à 40, ce qui est la limite pour pouvoir profiter du service de Remise à Zéro des Compétences.";
				close;
			}
			if ((Class > 0 && Class < 7 && Upper == 0) || (Class == Job_Gunslinger) || (Class == Job_Ninja) || (Class == Job_Taekwon)) {
				if (checkcart()) {
					mes "[Hypnotiseur]";
					mes "Oh! Merci de retirez votre Charrette avant de procéder à la Remise à Zéro des Compétences.";
					mes "Merci de votre coopération~";
					close;
				}
				if (Weight) {
					mes "[Hypnotiseur]";
					mes "Afin de pouvoir procéder à la Remise à Zéro des Compétences, je vous demanderai de bien vouloir stocker les objets de votre inventaire dans votre Stock Kafra. Le poids de votre inventaire doit être ^FF0000égal à 0^000000.";
					next;
					mes "[Hypnotiseur]";
					mes "Merci de votre compréhension.";
					close;
				}
				mes "[Hypnotiseur]";
				mes "Merci d'avoir utilisé nos services. Veuillez à redistribuer vos points de compétences de façon réfléchie.";
				mes "Bonne chance dans vos aventures. Faites attention à vous au cours de vos voyages~";
				ResetSkill;
				close;
			}
			mes "[Hypnotiseur]";
			mes "Je suis désolé, mais votre profession ne permet pas l'accès à nos services de Remise à Zéro des Compétences.";
			next;
			mes "[Hypnotiseur]";
			mes "seules les personnes exerçant les métiers:";
			mes "^FF0000Epéiste, Acolyte,";
			mes "Mage, Voleur, Archer,";
			mes "Marchand, Taekwon,";
			mes "Gunslinger et Ninja^000000";
			mes "peuvent bénéficier de ce service.";
			mes "Merci de votre compréhension.";
			close;
		}
		mes "[Hypnotiseur]";
		mes "Merci et bonne chance dans vos aventures. Faites attention à vous au cours de vos voyages~";
		close;

	case 3:
		mes "[Hypnotiseur]";
		mes "Merci et bonne chance dans vos aventures. Faites attention à vous au cours de vos voyages~";
		close;
	}

}
