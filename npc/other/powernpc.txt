//= Le Livre d'Ymir http://www.bookofymir.net/
//= traduit par No Healing
//===== eAthena Script =======================================
//= Official Gonryun Power Arena
//===== By: ==================================================
//= KarLaeda (Aegis script cnversion)
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= Athena SVN 9000+
//===== Additional Comments: =================================
//= 1.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//============================================================


gonryun,180,118,4	script	M. Muscle#gnp	85,{
	if (checkweight(1201,1) == 0) {
		mes "- Une petite minute !! -";
		mes "- Vous portez trop d'objets -";
		mes "- sur vous en ce moment. -";
		mes "- Réessayez quand vous vous -";
		mes "- serez allégé un peu. -";
		close;
	}
	if (BaseJob == Job_Novice) {
		mes "[Songmoodoo]";
		mes "Les enfants ne sont pas admis à l'intérieur.";
		close;
	}
	if (in_battle == 3) set in_battle,1;
	if (in_battle < 1) {
		mes "[Songmoodoo]";
		mes "Eh eh~ Ce n'est pas quelque chose qu'on voit tous les jours.";
		mes "Oh~ Vous, vous avez l'air balèze! Vous voulez essayer?";
		next;
		switch(select("Pourquoi pas?:?????:Sans façons.")){
		case 1:
			mes "[Songmoodoo]";
			mes "Haha~ Je savais que vous essaeriez.";
			mes "Si vous êtes capable de soulever ceci, alors je vous dirais quelque chose d'intéressant.";
			mes "Prêt?";
			next;
			mes "^0000FFSongmoodoo vous désigne un rocher et vous demande de le soulever.";
			if (BaseJob < Job_Knight) {
				if (checkweight(7049,690)) {
					mes "^0000FFVous soulevez lentement le rocher.^000000";
					set in_battle,1;
					getitem 7049,690; // Stone
					next;
					emotion e_no1;
					mes "[Songmoodoo]";
					mes "Ouah~ Excellent~";
					mes "Je vous ferai voir un endroit sympa la prochaine fois qu'on se verra.";
					mes "A une prochaine fois~";
				} else {
					mes "Cela semble trop lourd pour vous.^000000";
					next;
					mes "[Songmoodoo]";
					mes "Vous manquez toujours d'entraînement à ce que je vois. Revenez quand vous vous serez exercé un peu plus.";
				}
			} else if (BaseClass == Job_Swordman || BaseClass == Job_Thief || BaseClass == Job_Merchant || BaseJob == Job_Monk) {
				if (checkweight(7049,1100)) {
					mes "^0000FFVous soulevez lentement le rocher.^000000";
					set in_battle,1;
					getitem 7049,1100; // Stone
					next;
					emotion e_no1;
					mes "[Songmoodoo]";
					mes "Ouah~ Excellent~";
					mes "Je vous ferai voir un endroit sympa la prochaine fois qu'on se verra.";
					mes "A une prochaine fois~";
				} else {
					mes "Cela semble trop lourd pour vous.^000000";
					next;
					mes "[Songmoodoo]";
					mes "Vous manquez toujours d'entraînement à ce que je vois. Revenez quand vous vous serez exercé un peu plus.";
				}
			} else {
				if (checkweight(7049,730)) {
					mes "^0000FFVous soulevez lentement le rocher.^000000";
					set in_battle,1;
					getitem 7049,730; // Stone
					next;
					emotion e_no1;
					mes "[Songmoodoo]";
					mes "Ouah~ Excellent~";
					mes "Je vous ferai voir un endroit sympa la prochaine fois qu'on se verra.";
					mes "A une prochaine fois~";
				} else {
					mes "Cela semble trop lourd pour vous.^000000";
					next;
					mes "[Songmoodoo]";
					mes "Vous manquez toujours d'entraînement à ce que je vois. Revenez quand vous vous serez exercé un peu plus.";
				}
			}
			break;
		case 2:
			mes "[Songmoodoo]";
			mes "Rien de bien méchant. Si vous pensez que vous êtes fort, ça pourrait être l'occasion de vivre quelque chose de nouveau. Vous voulez essayer?";
			break;
		case 3:
			mes "[Songmoodoo]";
			mes "J'imagine que ce n'est pas le cas~";
			mes "....";
			emotion e_pif;
			break;
		}
	} else if (in_battle == 1) {
		mes "[Songmoodoo]";
		mes "Ça fait plaisir de vous revoir!";
		mes "Vous voulez y aller?";
		next;
		if (select("Bien sûr:Une prochaine fois peut-être.") == 1) {
			mes "[Songmoodoo]";
			mes "Bien! Haha. J'adore votre assurance! Bonne chance à vous~";
			close2;
			warp "gon_test",53,6;
			end;
		} else {
			mes "[Songmoodoo]";
			mes "Bon... D'accord. On se verra la prochaine fois alors.";
		}
	} else {
		mes "[Songmoodoo]";
		mes "Vous avez fui de là-bas? J'imagine que vous n'étiez pas assez fort, c'est ça? Bah, je vous laisserai une autre chance. A la prochaine!";
		emotion e_pif;
		set in_battle,1;
	}
	close;
}

gon_test,50,14,4	script	Administrateur#gnp	780,{
	mes "[Administrateur]";
	if (BaseJob == Job_Novice) {
		mes "Les enfants ne sont pas admis à l'intérieur.";
		close;
	}
	if ($@in_battle) {
		mes "Désolé, quelqu'un est déjà dans l'arène. Vous devriez réessayer plus tard.";
		close;
	}
	mes "Êtes-vous prêt?";
	mes "";
	mes "Souvenez-vous, vous devez payer 500 Zenys pour pouvoir combattre.";
	next;
	if (select("Oui, laissez-moi combattre!:Un instant, s'il-vous-plaît.") == 2) {
		mes "[Administrateur]";
		mes "D'accord. A plus tard!.";
		close;
	}
	if (Zeny < 500) {
		mes "[Administrateur]";
		mes "Je suis désolé mais vous ne semblez pas avoir assez d'argent sur vous.";
		close;
	}
	set Zeny,Zeny - 500;
	set in_battle,1;
	warp "gon_test",42,86;
	set $@in_battle,1;
	setnpctimer 0,"Summoner#gnp";
	startnpctimer "Summoner#gnp";
	end;
}

gon_test,42,89,4	script	Invocateur#gnp	774,{
	mes "[SongYeunWoo]";
	if (in_battle == 3) {
		mes "Veuillez revenir me voir après vous être inscrit.";
		close;
	} else if (in_battle == 2) {
		mes "Vous avez déjà un match en cours. Vous ne pouvez pas en avoir plusieurs en même temps. Vous devez quitter l'arène si vous voulez affronter d'autres monstres.";
		close;
	}
	mes "Bienvenue.";
	mes "Quels monstres dois-je faire venir?";
	next;
	set @gnpGroup,select("Groupe 1:Groupe 2:Group 3:Groupe 4:Groupe 5:Groupe 6:Groupe 7:Groupe 8:Groupe 9:Groupe 10:Groupe 11");
	set @gnpGroupMob$,.gnpMobsName$[(@gnpGroup-1)*6];
	for (set @i,1; @i < 6; set @i, @i+1) {
		set @gnpGroupMob$, @gnpGroupMob$ + ":" + .gnpMobsName$[(@gnpGroup-1)*6 + @i];
	}
	set @gnpMob,select(@gnpGroupMob$);
	mes "[SongYeunWoo]";
	mes "Que le combat commence!";
	close2;
	set in_battle,2;
	if (getnpctimer(0) < 180000) monster "gon_test",56,86,.gnpMobsName$[(@gnpGroup-1)*6 + @gnpMob - 1],.gnpMobsId[(@gnpGroup-1)*6 + @gnpMob - 1],1,"Invocateur#gnp::OnGnpMobDead";
//  do init timer on mob select ?
//	setnpctimer 0;
	end;

	OnInit:
		initnpctimer;
		stopnpctimer;
// Certains noms on été complètement changés (+ limite de 23 caractères à respecter) [No Healing]
		setarray .gnpMobsName$[0],"Spécialité Philippine","Sirotoxisme","Employé du Don du Sang","Tarentule","DangRangKwon","Molaire du Désert";
		setarray .gnpMobsId[0],1419,1428,1434,1430,1457,1432;
		setarray .gnpMobsName$[6],"Squelette de Porc","Ours Cannibale","Mineur","Chien de Combat","Princesse des Sirènes","Fils Unique";
		setarray .gnpMobsId[6],1462,1442,1469,1460,1425,1472;
		setarray .gnpMobsName$[12],"SamYeupchoom","Chien de Chasse","Briseur de Noix","Hipocampe","JAKK","Cadavre";
		setarray .gnpMobsId[12],1454,1455,1443,1426,1436,1423;
		setarray .gnpMobsName$[18],"Marduk","Pousse d'Oignon","Ver","Poupée Parlante","Fille aux Allumettes","Démon Rouge";
		setarray .gnpMobsId[18],1458,1440,1429,1459,1444,1422;
		setarray .gnpMobsName$[24],"Naga","Moisissure","Missile Sol-Air","Aryong","Abiryong","Bactérie";
		setarray .gnpMobsId[24],1421,1481,1424,1465,1466,1433;
		setarray .gnpMobsName$[30],"Réclamations","Archer Obèse","Petite Chèvre noire","Pervers","Trésor","Corne Verte";
		setarray .gnpMobsId[30],1427,1473,1431,1446,1474,1471;
		setarray .gnpMobsName$[36],"Ouragan","Face de Porc","Seigneur du Labyrinthe","Chevalier de la Rancune","Archer de la Rancune","Papillon";
		setarray .gnpMobsId[36],1450,1439,1461,1467,1453,1479;
		setarray .gnpMobsName$[42],"Grosses Lèvres","Wendigo","Carte","Monstre Tentaculaire","Réveil Musclé","Démon de la Croix";
		setarray .gnpMobsId[42],1451,1475,1437,1441,1476,1435;
		setarray .gnpMobsName$[48],"Asticot","Grand Miroir","Rentrée Littéraire","Doigts de Fée","Sac-à-main","Boite de Conserve";
		setarray .gnpMobsId[48],1477,1448,1478,1489,1488,1438;
		setarray .gnpMobsName$[54],"Reine","Homme de Feu","Lame du Bourreau","Dragon Mutant","Roi Lion","Épée Longue";
		setarray .gnpMobsId[54],1482,1464,1487,1449,1456,1486;
		setarray .gnpMobsName$[60],"Roi des Moineaux","Bourreau","Guerrier","Guantanamo","Épée Géante";
		setarray .gnpMobsId[60],1447,1483,1490,1484,1485;
	end;

	OnTimer120000:
		areaannounce "gon_test",41,81,74,92,"Il vous reste 1 minute.",0;
	end;

	OnTimer180000:
		killmonster "gon_test","Invocateur#gnp::OnGnpMobDead";
	end;

	OnTimer182000:
		areaannounce "gon_test",41,81,74,92,"Le temps est écoulé.",0;
	end;

	OnGnpMobDead:
		setnpctimer 0;
		set in_battle,1;
		areaannounce "gon_test",41,81,74,92,"Merci de votre participation! Nous espérons vous revoir prochainement.",0;
		sleep 4000;
	OnTimer184000:
		stopnpctimer;
		areawarp "gon_test",41,81,74,92,"gon_test",44,4;
		set $@in_battle,0;
	end;
}

// Nom du NPC changé à cause de la longueur [No Healing]
gon_test,46,14,3	script	Guide de l'Arène	770,{
	mes "[SongHeeYeon]";
	mes ".....";
	mes "Bonjour à vous~";
	mes "Vous êtes dans une arène de combat. Avez-vous des questions?";
	next;
	switch(select("Une arène de combat?:Vous voulez prendre un verre?:Entrer.:Sortir.:Non.")){
	case 1:
		mes "[SongHeeYeon]";
		mes "Tout comme l'indique le nom de ce lieu, il s'agit d'un terrain d'affrontement.";
		mes "Nous avons de nombreux monstres sur différents niveaux.";
		next;
		mes "[SongHeeYeon]";
		mes "Des Monstres de Première Catégorie sont déjà en train de se préparer.";
		mes "Nous vous demanderons une petite participation pour pouvoir entrer.";
		break;
	case 2:
		mes "[SongHeeYeon]";
		mes "Euh... Pardon?";
		mes "Je... Je suis désolée mais j'ai du travail en ce moment...";
		mes "Toutes mes excuses...";
		break;
	case 3:
		mes "[SongHeeYeon]";
		mes "Très bien. Passez un bon moment à l'intérieur!";
		close2;
		set in_battle,3;
		warp "gon_test",25,98;
		end;
	case 4:
		mes "[SongHeeYeon]";
		mes "Très bien. Merci de votre participation, et revenez bientôt~";
		close2;
		warp "gonryun",177,112;
		end;
	case 5:
		mes "[SongHeeYeon]";
		mes "........";
		mes "Au revoir..";
		break;
	}
	close;
}

gon_test,70,103,3	script	ChowAnAn#gnp	773,{
	mes "[ChowAnAn]";
	mes "Voulez-vous partir?";
	next;
	if (select("Oui.:Non.") == 1) {
		mes "[ChowAnAn]";
		mes "Très bien. Revenez nous voir!";
		close2;
		set in_battle,1;
		warp "gon_test",44,4;
		end;
	}
	mes "[ChowAnAn]";
	mes "Très bien.";
	close;
}
