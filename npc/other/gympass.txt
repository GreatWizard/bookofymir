//= Le Livre d'Ymir http://www.bookofymir.net/
//= traduit par No Healing
//===== eAthena Script ======================================= 
//= Gym Pass Cash Item NPC
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Exchange Gym Passes to learn "Enlarge Weight Limit"
//= iRO NPC situated in Payon beside Kafra Shop.
//===== Additional Comments: ================================= 
//= 1.0 First version [L0ne_W0lf]
//= 1.1 Fixed the NPC saying you "00" [L0ne_W0lf]
//= 1.2 Replaced effect numerics with constants. [Samuray22]
//= 1.3 Updated script, and fixed potential errors. [L0ne_W0lf]
//============================================================ 

payon,173,141,4	script	Ripped Cabus#GymPass	899,{
	mes "[Ripped]";
	mes "Salut à vous!";
	mes "Les gens ne font plus autant d'efforts de nos jours. Même si vous gagnez votre vie en combattant, certaines parties de votre corps peuvent s'être ramollies par endroit. Vous voyez ce que je veux dire?";
	next;
	mes "[Ripped]";
	mes "Mais si vous vous entraînez avec moi je peux vous garantir que vous serez capable de soulever et de transporter bien plus de choses.";
	mes "Il faudra juste que vous me présentiez une ^FF0000Carte de Membre de Club de Gym^000000 chaque fois que vous voudrez vous entraîner, et nous pourrons commencer.";
	next;
	mes "[Ripped]";
	mes "Mais ne vous excitez pas trop: quel que soit le temps que je passe à vous entraîner, vous pourriez toujours vouloir en faire plus.";
	mes "Vous avez déjà entendu parler de quelqu'un qui en avait trop fait? Non? C'est parce qu'ils sont tous morts! Vous voyez?";
	next;
	mes "[Ripped]";
	mes "Voyons... pour vous je dirais que je pourrai vous entraîner ^FF000010 fois^000000 pour augmenter votre capacité à porter des objets sans trop de risques.";
	mes "Alors... Êtes-vous prêt à transpirer?";
	next;
	switch(select("Oui.:Non.:Hum, je me suis ramolli.")) {
	case 1:
		if (gympassmemory < 10) {
			set .@add_carry,gympassmemory + 1;
			set .@remain_carry,10 - .@add_carry;
			if (countitem(7776) > 0) {
				mes "[Ripped]";
				mes "Oh, génial, je vois que vous avez apporté votre Carte de Membre de Club de Gym.";
				mes "Bon, faites la même chose que moi jusqu'à ce que vous sentiez vraiment vos muscles travailler.";
				mes "Prêt? On commence!";
				next;
				specialeffect2 EF_EARTHSPIKE;
				next;
				specialeffect2 EF_DEVIL;
				next;
				specialeffect2 EF_COIN;
				next;
				specialeffect2 EF_SIGHTRASHER;
				next;
				mes "[Ripped]";
				mes "Voilà, maintenant vous devriez être capable de porter plus d'objets.";
				mes "Voyons, à ce rythme nous pourrions avoir ^FF00000" + .@remain_carry + "^000000 séances supplémentaires.";
				delitem 7776,1; //Max_Weight_Up_Scroll
				set gympassmemory,.@add_carry;
				skill "ALL_INCCARRY",.@add_carry,0;
				close;
			}
			else {
				mes "[Ripped]";
				mes "Qu'est-ce que je vous ai dit, l'ami?";
				mes "Vous devez me présenter votre ^FF0000Carte de Membre de Club de Gym^000000 si vous voulez faire une séance de remise en forme et vous entraîner à porter plus de choses.";
				close;
			}
		}
		else {
			mes "[Ripped]";
			mes "Je ne pense pas que je puisse vous entraîner plus que cela, l'ami!";
			mes "Regardez votre musculature: ce serait dangereux pour votre corps de vouloir en faire plus. Et puis je vous ai dit ce qui se passait quand on essayait de dépasser ses limites, pas vrai?";
			close;
		}
	case 2:
		mes "[Ripped]";
		mes "C'est bien dommage. Eh bien, revenez me voir si vous changez d'avis. Et parlez de moi à vos amis: s’ils se sont ramollis, je les aiderais à les remettre en forme!";
		close;
	case 3:
		if (gympassmemory > 0) {
			mes "[Ripped]";
			mes "Qu'est-ce qui s'est passé? Vous avez laissé vos dorsaux s'atrophier? Heureusement pour eux, il y a ce qu'on appelle la mémoire musculaire. Ça ne devrait pas prendre trop de temps de reconstruire votre musculature...";
			next;
			specialeffect2 EF_EARTHSPIKE;
			next;
			specialeffect2 EF_DEVIL;
			next;
			specialeffect2 EF_COIN;
			next;
			specialeffect2 EF_SIGHTRASHER;
			next;
			mes "[Ripped]";
			mes "Qu'est-ce que vous en dites? Vous avez retrouvé votre musculature aussi facilement que ça! Évitez de vous laisser aller à l'avenir, d'accord?";
			skill "ALL_INCCARRY",gympassmemory,0;
			close;
		}
		else {
			mes "[Ripped]";
			mes "Euh... On ne s'est jamais entraînés ensemble, ça j'en suis sûr!";
			close;
		}
	}
}
