//= Le Livre d'Ymir http://www.bookofymir.net/
//= traduit par No Healing
//===== eAthena Script ========================================
//= Izlude Arena Room
//===== By: ==================================================
//= SinSloth
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= Any Athena Version
//===== Description: =========================================
//= Izlude Battle Arena Main Room
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Fixed a small reward typo (was giving potato.) [L0ne_W0lf]
//============================================================

arena_room,94,93,5	script	Vendigos	97,{

	mes "[Vendigos]";
	mes "Bienvenue dans l'enceinte de nos Arènes! Je m'appelle Vendigos. Je suis ici pour vous renseigner.";
	next;
	mes "[Vendigos]";
	mes "N'hésitez pas si vous avez des questions.";
L_Menu:
	next;
	switch( select( "Comment participer?","A propos des Points de l'Arène","Mon nombre actuel de Points de l'Arène","Annuler" ) )
	{
		case 1:
			mes "[Vendigos]";
			mes "Il y a deux types d'arènes: les Arènes ^3131FFIndividuelles^000000 et les Arènes ^3131FFpar Equipe^000000.";
			next;
			mes "[Vendigos]";
			mes "Le ^3131FFMode Individuel^000000 comprend quatre niveaux différents, basés sur celui du personnage (du niveau 50 au niveau 80). Les équipes de 5 membres peuvent s'inscrire en ^3131FFMode par Equipe^000000.";
			next;
			mes "[Vendigos]";
			mes "En ^3131FFMode Individuel^000000, les personnages ne pourront pas participer si ils ont 20 niveaux ou plus que le niveau requis.";
			next;
			mes "[Vendigos]";
			mes "Les responsables de chacune des arènes se trouvent dans des salles tout autour de celle-ci. Vous pourrez attendre qu'une arène se libère dans une Salle d'Attente, et vous serez alors automatiquement téléporté vers celle-ci.";
			break;
		
		case 2:
			mes "[Vendigos]";
			mes "Laissez-moi vous parler des ^3131FFPoints de l'Arène^000000. Il s'agit de récompenses que nous donnons aux joueurs qui ont réussi à ^3131FFcompléter toutes les salles^000000. Un joueur peut accumuler un maximum de 30.000 points.";
			next;
			mes "[Vendigos]";
			mes "Vous gagnerez aussi quelques points, même si vous n'arrivez pas au bout, ou si une erreur survient dans l'arène.";
			next;
			mes "[Vendigos]";
			mes "Vous pouvez échanger ces points contre un certain nombre d'objets ou de services, comme des photographies souvenir, en allant trouver mon collègue Givu.";
			break;

		case 3:
			mes "[Vendigos]";
			mes "Laissez-moi regarder dans le registre... Ah, voilà! "+strcharinfo(0)+".";
			next;
			mes "[Vendigos]";
			mes "Vous avez un total de ^3131FF"+arena_point+"^000000 points.";
			next;
			mes "[Vendigos]";
			mes "Je peux, si vous le voulez, vous renseigner sur leur utilisation. Vous n'avez qu'à sélectionner dans le menu: ^3131FF'A propos des Points de l'Arène'^000000.";
			break;

		case 4:
			mes "[Vendigos]";
			mes "Très bien. Passez une bonne journée.";
			close;
	}
	next;
	mes "[Vendigos]";
	mes "Puis-je faire quelque chose d'autre pour vous?";
	goto L_Menu;
	
Onlinerec_50:
	mapannounce "arena_room",""+$arena_50topn$+" vient d'établir un nouveau record dans l'Arène de Niveau 50. Félicitations!",0;
	end;
	
Onlinerec_60:
	mapannounce "arena_room",""+$arena_60topn$+" vient d'établir un nouveau record dans l'Arène de Niveau 60. Félicitations!",0;
	end;
	
Onlinerec_70:
	mapannounce "arena_room",""+$arena_70topn$+" vient d'établir un nouveau record dans l'Arène de Niveau 70. Félicitations!",0;
	end;
Onlinerec_80:
	mapannounce "arena_room",""+$arena_80topn$+" vient d'établir un nouveau record dans l'Arène de Niveau 80. Félicitations!",0;
	end;
	
Onlinerec_pt:
	mapannounce "arena_room","L'équipe "+$arena_pttopn$+" vient d'établir un nouveau record dans l'Arène. Félicitations à eux!",0;
	end;

//OnCommand: "linerec_vs"
// var vs_winner = GetGlobalStr "040611_arn_vstopn"
// broadcastinmap "Party "+vs_winner+" has renewed the top record in the Arena Time Force Battle. Congratulations!"
// end;

}

arena_room,105,82,5	script	Records de l'Arène::Arena Record Staff	57,{

	mes "[Owen Kheuv]";
	mes "Bonjour, je m'appelle Owen Kheuv. Je suis en charge de la tenue des registres de chaque arène, et de l'enregistrement des records.";
	next;
	mes "[Owen Kheuv]";
	mes "Quel record vous intéresse-t-il? Veuillez en choisir un dans la liste ci-dessous.";
	next;
	switch( select( "Arène de Niveau 50","Arène de Niveau 60","Arène de Niveau 70","Arène de Niveau 80","Arène par Equipe" ) )
	{
		case 1:
			mes "[Owen Kheuv]";
			mes "Le record de l'Arène de Niveau 50 est actuellement détenu par ^3131FF"+$arena_50topn$+"^000000, et est de ^3131FF"+$top_50min+"^000000 minutes ^3131FF"+$top_50sec+"^000000 secondes.";
			close;

		case 2:
			mes "[Owen Kheuv]";
			mes "Le record de l'Arène de Niveau 60 est actuellement détenu par ^3131FF"+$arena_60topn$+"^000000, et est de ^3131FF"+$top_60min+"^000000 minutes ^3131FF"+$top_60sec+"^000000 secondes.";
			close;

		case 3:
			mes "[Owen Kheuv]";
			mes "Le record de l'Arène de Niveau 70 est actuellement détenu par ^3131FF"+$arena_70topn$+"^000000, et est de ^3131FF"+$top_70min+"^000000 minutes ^3131FF"+$top_70sec+"^000000 secondes.";
			close;

		case 4:
			mes "[Owen Kheuv]";
			mes "Le record de l'Arène de Niveau 80 est actuellement détenu par ^3131FF"+$arena_80topn$+"^000000, et est de ^3131FF"+$top_80min+"^000000 minutes ^3131FF"+$top_80sec+"^000000 secondes.";
			close;

		case 5:
			mes "[Owen Kheuv]";
			mes "Le record de l'Arène par Equipe est actuellement détenu par l'équipe ^3131FF"+$arena_pttopn$+"^000000, et est de ^3131FF"+$top_ptmin+"^000000 minutes ^3131FF"+$top_ptsec+"^000000 secondes.";
			close;
	}
}

arena_room,68,135,3	script	Guide Pat::Helper Pat	726,{

	mes "[Pat]";
	mes "Bien le bonjour. Je suis le guide des Arènes de Niveau 50.";
	next;
	mes "[Pat]";
	mes "L'^3131FFArène de Niveau 50^000000 est accessible aux personnages de niveau ^FF000050^000000 à ^FF000069^000000.";
	next;
	mes "[Pat]";
	mes "Les restrictions de niveaux sont faites pour empêcher ceux qui ont un niveau trop élevé de chambouler les arènes pour débutants. J'espère que vous comprenez.";
	mes "Nous vous demanderons également une participation de 1.000 zenys lors de l'inscription.";
	next;
	mes "[Pat]";
	mes "Laissez-moi vous parler des règles de l'arène.";
	next;
	mes "[Pat]";
	mes "^3131FFVous devrez patienter dans la salle d'attente jusqu'à ce que ce soit votre tour.^000000";
	mes "Lorsqu'il viendra, vous serez automatiquement transporté dans l'arène.";
	next;
	mes "[Pat]";
	mes "Le chronomètre se déclenchera dès que vous serez dans l'arène.";
	mes "Suivez bien les instructions de ^3131FFLentinard^000000. ";
	next;
	mes "[Pat]";
	mes "Vous pourrez y rester pendant ^3131FF5 minutes^000000.";
	next;
	mes "[Pat]";
	mes "Après avoir passé chacune des salles et tué le dernier Boss, vous serez reconduit vers une nouvelle ^3131FFsalle d'attente^000000. Vous ne pourrez y rester qu'^3131FFune minute^000000. Vous devrez alors vous dépêcher de récupérer vos Points de l'Arène avant de partir.";
	next;
	mes "[Pat]";
	mes "Passé ce délai, vous serez reconduit à l'extérieur et vous ne pourrez plus recevoir de Points de l'Arène. Ne l'oubliez pas.";
	next;
	mes "[Pat]";
	mes "J'espère que vous passerez un agréable moment.";
	close;
}

arena_room,41,93,5	script	Guide Ben::Helper Ben	750,{

	mes "[Ben]";
	mes "Bien le bonjour. Je suis le guide des Arènes de Niveau 60.";
	next;
	mes "[Ben]";
	mes "L'^3131FFArène de Niveau 60^000000 est accessible aux personnages de niveau ^FF000060^000000 à ^FF000079^000000.";
	next;
	mes "[Ben]";
	mes "Les restrictions de niveaux sont faites pour empêcher ceux qui ont un niveau trop élevé de chambouler les arènes pour débutants. J'espère que vous comprenez.";
	mes "Nous vous demanderons également une participation de 1,000 zenys lors de l'inscription.";
	next;
	mes "[Ben]";
	mes "Laissez-moi vous parler des règles de l'arène.";
	next;
	mes "[Ben]";
	mes "^3131FFVous devrez patienter dans la salle d'attente jusqu'à ce que ce soit votre tour.^000000";
	mes "Lorsqu'il viendra, vous serez automatiquement transporté dans l'arène.";
	next;
	mes "[Ben]";
	mes "Le chronomètre se déclenchera dès que vous serez dans l'arène.";
	mes "Suivez bien les instructions de ^3131FFMinilover^000000.";
	next;
	mes "[Ben]";
	mes "Vous pourrez y rester pendant ^3131FF6 minutes^000000.";
	next;
	mes "[Ben]";
	mes "Après avoir passé chacune des salles et tué le dernier Boss, vous serez reconduit vers une nouvelle ^3131FFsalle d'attente^000000. Vous ne pourrez y rester qu'^3131FFune minute^000000. Vous devrez alors vous dépêcher de récupérer vos Points de l'Arène avant de partir.";
	next;
	mes "[Ben]";
	mes "Passé ce délai, vous serez reconduit à l'extérieur et vous ne pourrez plus recevoir de Points de l'Arène. Ne l'oubliez pas.";
	next;
	mes "[Ben]";
	mes "J'espère que vous passerez un agréable moment.";
	close;
}

arena_room,53,49,5	script	Guide Vicious::Helper Vicious	730,{

	mes "[Vicious]";
	mes "Bien le bonjour. Je suis le guide des Arènes de Niveau 70.";
	mes "(...et vraiment j'me demande pourquoi je fais ce boulot débile... *gromelle*..)";
	next;
	mes "[Vicious]";
	mes "L'^3131FFArène de Niveau 70^000000 est accessible aux personnages de niveau ^FF000070^000000 à ^FF000089^000000.";
	next;
	mes "[Vicious]";
	mes "Les restrictions de niveaux sont faites pour empêcher ceux qui ont un niveau trop élevé de chambouler les arènes pour débutants. J'espère que vous comprenez.";
	mes "Nous vous demanderons également une participation de 1,000 zenys lors de l'inscription.";
	next;
	mes "[Vicious]";
	mes "Laissez-moi vous parler des règles de l'arène.";
	next;
	mes "[Vicious]";
	mes "^3131FFVous devrez patienter dans la salle d'attente jusqu'à ce que ce soit votre tour.^000000";
	mes "Lorsqu'il viendra, vous serez automatiquement transporté dans l'arène.";
	next;
	mes "[Vicious]";
	mes "Le chronomètre se déclenchera dès que vous serez dans l'arène.";
	mes "Suivez bien les instructions de ^3131FFCadilac^000000.";
	next;
	mes "[Vicious]";
	mes "Vous pourrez y rester pendant ^3131FF7 minutes^000000.";
	next;
	mes "[Vicious]";
	mes "Après avoir passé chacune des salles et tué le dernier Boss, vous serez reconduit vers une nouvelle ^3131FFsalle d'attente^000000. Vous ne pourrez y rester qu'^3131FFune minute^000000. Vous devrez alors vous dépêcher de récupérer vos Points de l'Arène avant de partir.";
	next;
	mes "[Vicious]";
	mes "Passé ce délai, vous serez reconduit à l'extérieur et vous ne pourrez plus recevoir de Points de l'Arène. Ne l'oubliez pas.";
	next;
	mes "[Vicious]";
	mes "Bon, c'est tout. Faites bien attention à vous.";
	close;
}

arena_room,147,49,3	script	Guide Epin::Helper Epin	727,{

	mes "[Epin]";
	mes "Bien le bonjour. Je suis le guide des Arènes de Niveau 80.";
	mes "J'aime la lecture et j'ai 19 ans... Quoi...? Ca ne vous intéresse pas...?";
	next;
	emotion e_cry,0;
	mes "[Epin]";
	mes "Bon, bon...";
	next;
	mes "[Epin]";
	mes "L'^3131FFArène de Niveau 80^000000 est accessible aux personnages de niveau ^FF000080^000000 et plus.";
	next;
	mes "[Epin]";
	mes "Les restrictions de niveaux sont faites pour empêcher ceux qui ont un niveau trop élevé de chambouler les arènes pour débutants. J'espère que vous comprenez.";
	mes "Nous vous demanderons également une participation de 1,000 zenys lors de l'inscription.";
	next;
	mes "[Epin]";
	mes "Laissez-moi vous parler des règles de l'arène.";
	next;
	mes "[Epin]";
	mes "^3131FFVous devrez patienter dans la salle d'attente jusqu'à ce que ce soit votre tour.^000000";
	mes "Lorsqu'il viendra, vous serez automatiquement transporté dans l'arène.";
	next;
	mes "[Epin]";
	mes "Le chronomètre se déclenchera dès que vous serez dans l'arène.";
	mes "Suivez bien les instructions de ^3131FFActus^000000.";
	next;
	mes "[Epin]";
	mes "Vous pourrez y rester pendant ^3131FF8 minutes^000000.";
	next;
	mes "[Epin]";
	mes "Après avoir passé chacune des salles et tué le dernier Boss, vous serez reconduit vers une nouvelle ^3131FFsalle d'attente^000000. Vous ne pourrez y rester qu'^3131FFune minute^000000. Vous devrez alors vous dépêcher de récupérer vos Points de l'Arène avant de partir.";
	next;
	mes "[Epin]";
	mes "Passé ce délai, vous serez reconduit à l'extérieur et vous ne pourrez plus recevoir de Points de l'Arène. Ne l'oubliez pas.";
	next;
	mes "[Epin]";
	mes "J'espère que vous passerez un agréable moment.";
	close;
}

arena_room,158,93,5	script	Guide Lunic::Helper Lunic	828,{

	mes "[Lunic]";
	mes "Bienvenue dans l'Arène par Equipe. J'espère que vous prendrez le temps d'écouter attentivement ma présentation: cette arène est quelque peu différente des autres.";
	next;
	mes "[Lunic]";
	mes "L'^3131FFArène par Equipe^000000 est accessible aux joueurs de niveau ^FF000010^000000 et plus.";
	next;
	mes "[Lunic]";
	mes "Le combat ne pourra pas commencer tant que 5 membres de l'équipe ne seront pas entrés dans la salle. Nous vous demanderons aussi une participation de 1.000 zenys lors de l'inscription.";
	next;
	mes "[Lunic]";
	mes "Laissez-moi vous parler des règles de l'arène.";
	next;
	mes "[Lunic]";
	mes "^3131FFVous et les membres de votre groupe doivent entrer dans la salle d'attente^000000.";
	mes "Vous devez bien sûr faire partie d'une équipe pour cela. Vous pourriez toujours participer dans le cas contraire, mais ça serait beaucoup moins pratique.";
	next;
	mes "[Lunic]";
	mes "Gardez aussi à l'esprit que les joueurs qui seront conduits dans l'arène seront les ^FF00005 premiers inscrits^000000, pas forcément les ^FF0000membres de l'équipe^000000.";
	next;
//	mes "[Lunic]";
//	mes "And please form a party with ^3131FF5 players^000000 before you enter a chat room.";
//	next;
	mes "[Lunic]";
	mes "Vous serez alors téléporté vers une petite salle où vous trouverez un de mes collègues, ainsi qu'un portail que vous pourrez emprunter pour sortir si vous n'étiez pas avec les membres de votre groupe. Vous serez alors reconduit vers la salle d'attente.";
	next;
	mes "[Lunic]";
	mes "Lorsque vous serez avec les membres de votre équipe, vous devrez parler ^FF0000un par un^000000 au guide présent.";
	next;
	mes "[Lunic]";
	mes "Il vous conduira alors dans la véritable arène. Si l'un des membres n'a pas les fonds pour s'acquitter du droit d'entrée, il sera reconduit vers la sortie.";
	next;
	mes "[Lunic]";
	mes "Faites bien attention aussi, car vous ne pourrez rester dans cette salle que pendant ^FF0000une minute^000000.";
	next;
	mes "[Lunic]";
	mes "Le chronomètre se déclenchera dès que vous serez dans l'arène.";
	mes "Suivez bien les instructions de ^3131FFSlipslowrun^000000. ";
	next;
	mes "[Lunic]";
	mes "Vous aurez alors ^3131FF10 minutes^000000 pour traverser toutes les salles. Mais attention, ce ne sera pas aussi simple qu'il peut paraître. Employez ce temps à bon escient.";
	next;
	mes "[Lunic]";
	mes "Après avoir passé chacune des salles et tué le dernier Boss, vous serez reconduit vers une nouvelle ^3131FFsalle d'attente^000000. Vous ne pourrez y rester qu'^3131FFune minute^000000. Vous devrez alors vous dépêcher de récupérer vos Points de l'Arène avant de partir.";
	next;
	mes "[Lunic]";
	mes "Passé ce délai, vous serez reconduit à l'extérieur et vous ne pourrez plus recevoir de Points de l'Arène. Ne l'oubliez pas.";
	next;
	mes "[Lunic]";
	mes "Si l'équipe bat un record, ^3131FFle chef d'équipe^000000 pourra faire valider le ^3131FFnom de l'équipe^000000.";
	next;
	mes "[Lunic]";
	mes "Cependant, si plusieurs équipes participent, ou si des joueurs n'en font pas partie, il sera impossible de valider ce record. C'est pourquoi nous vous conseillons vivement de former un groupe avant d'entrer dans l'arène.";
	next;
	mes "[Lunic]";
	mes "Merci de m'avoir écouté. J'espère que vous passerez un agréable moment.";
	close;
}

arena_room,158,82,1	script	Guide Lonik::Helper Lonik	828,{

	emotion e_no1,0;
	mes "[Lonik]";
	mes "Tada! Me voilà!";
	next;
	mes "[Lonik]";
	mes "Vous seriez curieux de savoir si il y a quelqu'un à l'intérieur, n'est-ce pas?";
	next;
	mes "[Lonik]";
	mes "Eh bien la réponse est...";
	mes ".............";
	if(getmapusers("force_1-2"))
	{
		emotion e_oh,0;
		mes "Oui!";
		mes "Il y a quelqu'un à l'intérieur.";
		next;
		mes "[Lonik]";
		mes "Vous feriez mieux de patienter un petit peu!";
	}
	else
	{
		emotion e_x,0;
		mes "Non! Vous pouvez y aller. Bonne chance!";
	}
	close;
}

sec_in02,72,180,3	script	Gestion de l'Arène::Arena Manager#arena	802,{
	if (!getgmlevel()) end; // Bugfix: petite sécurité si les joueurs ont accès à sec_in02 (vu que c'est un bug officiel, ça ne sera pas fixé sur eA *sigh*). [No Healing]
	input .@arena;
	if(!.@arena)
	{
		mes "[Gestion de l'Arène]";
		mes "Opération annulée.";
		close;
	}
	else if(.@arena == 1357)
	{
		mes "[Gestion de l'Arène]";
		mes "Veuillez choisir une option.";
		next;
		switch( select( "Remettre l'arène à zéro","Modifier les records" ) )
		{
			case 1:
				switch( select( "Niveau 50","Niveau 60","Niveau 70","Niveau 80","Mode par Equipe" ) ) 
				{
					case 1:
						set .@arena,50;
						break;
					case 2:
						set .@arena,60;
						break;
					case 3:
						set .@arena,70;
						break;
					case 4:
						set .@arena,80;
						break;
					case 5:
						mes "[Gestion de l'Arène]";
						mes "== Attention ==";
						mes "Vous avez décidé de remettre à zéro toutes les courses. Confirmez-vous?";
						next;
						switch( select( "Oui","Non" ) )
						{
							case 1:
								donpcevent "Ponox::Onstart";
								mes "[Gestion de l'Arène]";
								mes "Les arènes ont été remises à zéro.";
								close;

							case 2:
								mes "[Gestion de l'Arène]";
								mes "Opération annulée.";
								close;
						}
				}
				mes "[Gestion de l'Arène]";
				mes "== Attention ==";
				mes "Vous avez décidé de remettre à zéro l'arène "+.@arena+". Confirmez-vous?";
				next;
				switch( select( "Oui","Non" ) )
				{
					case 1:
						donpcevent "lvl "+.@arena+"s Waiting Room::Onstart";
						mes "[Gestion de l'Arène]";
						mes "L'arène a été remise à zéro.";
						close;
					
					case 2:
						mes "[Gestion de l'Arène]";
						mes "Opération annulée.";
						close;
				}
				
			case 2:
				mes "[Gestion de l'Arène]";
				mes "== Attention ==";
				mes "^CE0000Vous avez choisi de modifier les records. Soyez conscient que cette option n'est à utiliser qu'en dernier recours!^000000";
				next;
				mes "[Gestion de l'Arène]";
				mes "Etes-vous certain de vouloir continuer?";
				next;
				switch( select( "Non","Oui" ) )
				{
					case 1:
						mes "[Gestion de l'Arène]";
						mes "Ouf~ :)";
						close;

					case 2:
						switch( select( "Niveau 50","Niveau 60","Niveau 70","Niveau 80","Mode par Equipe" ) )
						{
							case 1:
								mes "[Gestion de l'Arène]";
								mes "Entrez 0 pour annuler. Dans le cas contraire, entrez un nombre à 2 chiffres maximum (minutes puis secondes).";
								next;
								input .@arenamin;
								set $top_50min,.@arenamin;
								input .@arenasec;
								set $top_50sec,.@arenasec;
								mes "[Gestion de l'Arène]";
								mes "Le record de temps pour le niveau 50 vient d'être changé en ^FF0000"+$top_50min+"^000000 minutes et ^FF0000"+$top_50sec+"^000000 secondes.";
								close;
							case 2:
								mes "[Gestion de l'Arène]";
								mes "Entrez 0 pour annuler. Dans le cas contraire, entrez un nombre à 2 chiffres maximum (minutes puis secondes).";
								next;
								input .@arenamin;
								set $top_60min,.@arenamin;
								input .@arenasec;
								set $top_60sec,.@arenasec;
								mes "[Gestion de l'Arène]";
								mes "Le record de temps pour le niveau 60 vient d'être changé en ^FF0000"+$top_60min+"^000000 minutes et ^FF0000"+$top_60sec+"^000000 secondes.";
								close;
							case 3:
								mes "[Gestion de l'Arène]";
								mes "Entrez 0 pour annuler. Dans le cas contraire, entrez un nombre à 2 chiffres maximum (minutes puis secondes).";
								next;
								input .@arenamin;
								set $top_70min,.@arenamin;
								input .@arenasec;
								set $top_70sec,.@arenasec;
								mes "[Gestion de l'Arène]";
								mes "Le record de temps pour le niveau 70 vient d'être changé en ^FF0000"+$top_70min+"^000000 minutes et ^FF0000"+$top_70sec+"^000000 secondes.";
								close;
							case 4:
								mes "[Gestion de l'Arène]";
								mes "Entrez 0 pour annuler. Dans le cas contraire, entrez un nombre à 2 chiffres maximum (minutes puis secondes).";
								next;
								input .@arenamin;
								set $top_80min,.@arenamin;
								input .@arenasec;
								set $top_80sec,.@arenasec;
								mes "[Gestion de l'Arène]";
								mes "Le record de temps pour le niveau 80 vient d'être changé en ^FF0000"+$top_80min+"^000000 minutes et ^FF0000"+$top_80sec+"^000000 secondes.";
								close;
							case 5:
								mes "[Gestion de l'Arène]";
								mes "Entrez 0 pour annuler. Dans le cas contraire, entrez un nombre à 2 chiffres maximum (minutes puis secondes).";
								next;
								input .@arenamin;
								set $top_ptmin,.@arenamin;
								input .@arenasec;
								set $top_ptsec,.@arenasec;
								mes "[Gestion de l'Arène]";
								mes "Le record de temps par équipe vient d'être changé en ^FF0000"+$top_ptmin+"^000000 minutes et ^FF0000"+$top_ptsec+"^000000 secondes.";
								close;
						}
				}
		}
	}
	mes "Mot de passe incorrect.";
	close;
}

sec_in02,79,171,3	script	Préposé aux Récompenses::Reward Manager#arena	802,{

	if (!getgmlevel()) end; // Bugfix [No Healing]
	input .@arena;
	if(!.@arena)
	{
		mes "[Préposé aux Récompenses]";
		mes "Opération annulée.";
		close;
	}
	else if(.@arena == 1357)
	{
		mes "[Préposé aux Récompenses]";
		mes "Voulez-vous désactiver l'accès au NPC des récompenses?";
		next;
		switch( select( "Annuler","Oui","L'activer" ) )
		{
			case 1:
				mes "[Préposé aux Récompenses]";
				mes "Opération annulée.";
				close;
		
			case 2:
				mes "[Préposé aux Récompenses]";
				mes "NPC désactivé.";
				disablenpc "Teleporter#arena";
				close;
				
			case 3:
				mes "[Préposé aux Récompenses]";
				mes "NPC activé.";
				enablenpc "Teleporter#arena";
				close;
		}
	}
	mes "[Reward Manager]";
	mes "Mot de passe incorrect.";
	close;
}

arena_room,105,93,5	script	Préposé aux Récompenses::Teleporter#arena	55,{

	mes "[Préposé aux Récompenses]";
	mes "Je peux vous conduire vers la salle des récompenses. Désirez-vous vous y rendre?";
	next;
	switch( select( "Oui.","Non." ) )
	{
		case 1:
			mes "[Préposé aux Récompenses]";
			mes "Bien, veuillez me suivre.";
			close2;
			warp "prt_are_in",60,14;
			end;

		case 2:
			mes "[Préposé aux Récompenses]";
			mes "Comme vous voulez. N'hésitez pas à revenir me voir si vous changez d'avis.";
			close;
	}
}

prt_are_in,98,14,3	script	Givu#arena	728,{


	if(checkweight(526,5) != 1)
	{
		mes "- Une petite minute! Vous portez trop d'objets sur vous. Revenez me voir après vous être débarrassé de quelques uns. -";
		close;
	}
	mes "[Givu]";
	mes "Bien le bonjour, et bienvenue dans nos arènes! Je m'appelle Givu, et je suis en charge du programme de récompenses.";
	next;
	mes "[Givu]";
	mes "Vous avez la possibilité d'échanger vos Points de l'Arène contre diverses choses. Choisissez ce que vous voulez dans la liste ci-dessous.";
	next;
	switch( select( "Les échanger contre un Consommable","Les échanger contre de l'Expérience","Prendre une photo souvenir","Consulter son nombre actuel de Points" ) )
	{
		case 1:
			mes "[Givu]";
			mes "Quel consommable désirez-vous?";
			next;
			switch( select( "Honey","Royal Jelly","Mastela Fruit","Condensed White Potion","Anodyne","Yggdrasil Seed","Yggdrasil Berry","Old Blue Box","Old Purple Box","Old Card Album" ) )
			{
				case 1: callfunc "Func_Are_Rew",518,5,20;
				case 2: callfunc "Func_Are_Rew",526,5,30;
				case 3: callfunc "Func_Are_Rew",522,5,30;
				case 4: callfunc "Func_Are_Rew",547,8,30;
				case 5: callfunc "Func_Are_Rew",605,3,20;
				case 6: callfunc "Func_Are_Rew",608,1,20;
				case 7: callfunc "Func_Are_Rew",607,1,40;
				case 8: callfunc "Func_Are_Rew",603,1,100;
				case 9: callfunc "Func_Are_Rew",617,1,300;
				case 10: callfunc "Func_Are_Rew",616,1,1000;
			}
			
		case 2:
			mes "[Givu]";
			mes "Voulez-vous échanger vos Points de l'Arène contre des Points d'Expérience? Ce service coûte 40 Points de l'Arène.";
			next;
			switch( select( "Annuler","Oui" ) )
			{
				case 1:
					mes "[Givu]";
					mes "Comme vous voulez.";
					close;
				
				case 2:
					if(arena_point < 40)
					{
						mes "[Givu]";
						mes "Vous n'avez pas assez de Points de l'Arène. Vous devriez consulter votre compte actuel.";
						close;
					}
					set arena_point,arena_point -40;
					if(BaseLevel < 70) getexp 3000,0;
					else if(BaseLevel < 80) getexp 9000,0;
					else if(BaseLevel < 90) getexp 10000,0;
					else if(BaseLevel < 100) getexp 30000,0;
					mes "[Givu]";
					mes "Vous avez reçu les Points d'Expérience. Merci.";
					close;
			}

		case 3:
			mes "[Givu]";
			mes "Vous avez choisi d'utiliser nos services de photos souvenir.";
			next;
			switch( select( "A propos des photos souvenir","Prendre une photo" ) )
			{
				case 1:
					mes "[Givu]";
					mes "Vous voyez les escaliers à ma droite?";
					next;
					mes "[Givu]";
					mes "Vous pourrez y prendre une capture d'écran avec le monstre ou le NPC de votre choix.";
					next;
					mes "[Givu]";
					mes "Après que vous ayez choisi avec qui vous voulez poser, celui-ci fera des émotions pendant ^FF0000 1 minute^000000.";
					next;
					mes "[Givu]";
					mes "Ne ratez pas l'opportunité d'être photographié auprès de votre monstre préféré!";
					close;

				case 2:
					if($arenapic)
					{
						mes "[Givu]";
						mes "Je vais vous demander une petite minute. Quelqu'un est déjà en train de prendre des photos.";
						close;
					}
					mes "[Givu]";
					mes "Veuillez choisir un NPC dans la liste ci-dessous. Celui apparaîtra et vous aurez 1 minute pour prendre des photos. Ce service coûte 10 Points de l'Arène.";
					next;
					switch( select( "Baphomet","Dark Lord","Doppelganger","Eddga","Dracula","Samurai","Stormy Knight","Phreeoni","De jolies femmes","Valkyrie" ) )
					{
						case 1: set $@arenapic,1039; break;
						case 2: set $@arenapic,1272; break;
						case 3: set $@arenapic,1046; break;
						case 4: set $@arenapic,1115; break;
						case 5: set $@arenapic,1389; break;
						case 6: set $@arenapic,1492; break;
						case 7: set $@arenapic,1251; break;
						case 8: set $@arenapic,1159; break;
						case 9: set $@arenapic,6969; break;
						case 10: set $@arenapic,7777; break;
					}
					if(arena_point < 10)
					{
						mes "[Givu]";
						mes "Vous n'avez pas assez de Points de l'Arène. Vous devriez consulter votre compte actuel.";
						close;
					}
					mes "[Givu]";
					if($@arenapic == 6969) mes "Voulez-vous vous faire prendre en photo en compagnie de Jolies Filles?";
					else if($@arenapic == 7777) mes "Voulez-vous vous faire prendre en photo en compagnie d'une Valkyrie?";
					else mes "Voulez-vous vous faire prendre en photo en compagnie d'un "+getmonsterinfo($@arenapic,0)+"?";
					next;
					switch( select( "Oui","Non" ) )
					{
						case 1:
							set arena_point,arena_point -10;
							set $arenapic,1;
							enablenpc "#arena_"+$@arenapic+"";
							movenpc "#arena_"+$@arenapic+"",96,28;
							if($@arenapic == 6969)
							{
								enablenpc "#arena_ss_2";
								enablenpc "#arena_ss_3";
								enablenpc "#arena_ss_4";
							}
							donpcevent "npctime#arena::OnStart";
							mes "[Givu]";
							mes "Merci.";
							close;

						case 2:
							mes "[Givu]";
							mes "Avez-vous besoin d'un peu plus de temps pour vous décider?";
							close;
					}
			}
			
		case 4:
			mes "[Givu]";
			mes "Laissez-moi regarder dans le registre... Ah, voilà! "+strcharinfo(0)+".";
			next;
			mes "[Givu]";
			mes ""+strcharinfo(0)+", vous avez un total de ^3131FF"+arena_point+"^000000 points.";
			close;
	}

OnInit:
	set $arenapic,0;
	end;
}

prt_are_in,1,1,5	script	#arena_1039	1039,{
	end;
	
OnInit:
	disablenpc "#arena_6969";
	disablenpc "#arena_ss_2";
	disablenpc "#arena_ss_3";
	disablenpc "#arena_ss_4";
	disablenpc "#arena_1272";
	disablenpc "#arena_1046";
	disablenpc "#arena_1115";
	disablenpc "#arena_1389";
	disablenpc "#arena_1492";
	disablenpc "#arena_1251";
	disablenpc "#arena_1159";
	disablenpc "#arena_7777";
	disablenpc "#arena_1039";
	end;

OnHeart:
	emotion e_lv,0;
	end;
OnSci:
	emotion e_scissors,0;
	end;
OnBest:
	emotion e_no1,0;
	end;
OnOmg:
	emotion e_omg,0;
	end;
OnKik:
	emotion e_gg,0;
	end;
OnKis:
	emotion e_kis,0;
	end;
}

prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_6969	714
prt_are_in,94,26,5	duplicate(#arena_1039)	#arena_ss_2	714
prt_are_in,100,28,3	duplicate(#arena_1039)	#arena_ss_3	714
prt_are_in,102,26,3	duplicate(#arena_1039)	#arena_ss_4	714
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1272	1272
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1046	1046
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1115	1115
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1389	1389
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1492	1492
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1251	1251
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_1159	1159
prt_are_in,1,1,5	duplicate(#arena_1039)	#arena_7777	811

prt_are_in,130,30,3	script	npctime#arena	139,{

OnStop:
	stopnpctimer;
	end;
	
OnStart:
	initnpctimer;
	end;
	
OnTimer2000:
	mapannounce "prt_are_in","Givu : Essayez d'utiliser les bonnes émotions à temps~",1,0xFFCE00;
	end;
	
OnTimer5000:
	mapannounce "prt_are_in","Emoticône: /lv",1,0xFFCE00;
	end;
	
OnTimer7000:
	mapannounce "prt_are_in"," 3 ",1,0xFFCE00;
	end;
	
OnTimer8000:
	mapannounce "prt_are_in"," 2 ",1,0xFFCE00;
	end;
	
OnTimer9000:
	mapannounce "prt_are_in"," 1 ",1,0xFFCE00;
	end;
	
OnTimer10000:
	mapannounce "prt_are_in"," Prêt? ",1,0xFFCE00;
	donpcevent "#arena_"+$@arenapic+"::OnHeart";
	if($@arenapic == 6969)
	{
		donpcevent "#arena_ss_2::OnHeart";
		donpcevent "#arena_ss_3::OnHeart";
		donpcevent "#arena_ss_4::OnHeart";
	}
	end;
	
OnTimer15000:
	mapannounce "prt_are_in","Emoticône: /gawi",1,0xFFCE00;
	end;
	
OnTimer17000:
	mapannounce "prt_are_in"," 3 ",1,0xFFCE00;
	end;
	
OnTimer18000:
	mapannounce "prt_are_in"," 2 ",1,0xFFCE00;
	end;
	
OnTimer19000:
	mapannounce "prt_are_in"," 1 ",1,0xFFCE00;
	end;
	
OnTimer20000:
	mapannounce "prt_are_in"," Prêt? ",1,0xFFCE00;
	donpcevent "#arena_"+$@arenapic+"::OnSci";
	if($@arenapic == 6969)
	{
		donpcevent "#arena_ss_2::OnSci";
		donpcevent "#arena_ss_3::OnSci";
		donpcevent "#arena_ss_4::OnSci";
	}
	end;
	
OnTimer25000:
	mapannounce "prt_are_in","Emoticône: /no1",1,0xFFCE00;
	end;
	
OnTimer27000:
	mapannounce "prt_are_in"," 3 ",1,0xFFCE00;
	end;
	
OnTimer28000:
	mapannounce "prt_are_in"," 2 ",1,0xFFCE00;
	end;
	
OnTimer29000:
	mapannounce "prt_are_in"," 1 ",1,0xFFCE00;
	end;
	
OnTimer30000:
	mapannounce "prt_are_in"," Prêt? ",1,0xFFCE00;
	donpcevent "#arena_"+$@arenapic+"::OnBest";
	if($@arenapic == 6969)
	{
		donpcevent "#arena_ss_2::OnBest";
		donpcevent "#arena_ss_3::OnBest";
		donpcevent "#arena_ss_4::OnBest";
	}
	end;
	
OnTimer35000:
	mapannounce "prt_are_in","Emoticône: /huk",1,0xFFCE00;
	end;
	
OnTimer37000:
	mapannounce "prt_are_in"," 3 ",1,0xFFCE00;
	end;
	
OnTimer38000:
	mapannounce "prt_are_in"," 2 ",1,0xFFCE00;
	end;
	
OnTimer39000:
	mapannounce "prt_are_in"," 1 ",1,0xFFCE00;
	end;
	
OnTimer40000:
	mapannounce "prt_are_in"," Prêt? ",1,0xFFCE00;
	donpcevent "#arena_"+$@arenapic+"::OnOmg";
	if($@arenapic == 6969)
	{
		donpcevent "#arena_ss_2::OnOmg";
		donpcevent "#arena_ss_3::OnOmg";
		donpcevent "#arena_ss_4::OnOmg";
	}
	end;
	
OnTimer45000:
	mapannounce "prt_are_in","Emoticône: /gg",1,0xFFCE00;
	end;
	
OnTimer47000:
	mapannounce "prt_are_in"," 3 ",1,0xFFCE00;
	end;
	
OnTimer48000:
	mapannounce "prt_are_in"," 2 ",1,0xFFCE00;
	end;
	
OnTimer49000:
	mapannounce "prt_are_in"," 1 ",1,0xFFCE00;
	end;
	
OnTimer50000:
	mapannounce "prt_are_in"," Prêt? ",1,0xFFCE00;
	donpcevent "#arena_"+$@arenapic+"::OnKik";
	if($@arenapic == 6969)
	{
		donpcevent "#arena_ss_2::OnKik";
		donpcevent "#arena_ss_3::OnKik";
		donpcevent "#arena_ss_4::OnKik";
	}
	end;
	
OnTimer55000:
	mapannounce "prt_are_in","Emoticône: /kis",1,0xFFCE00;
	end;
	
OnTimer57000:
	mapannounce "prt_are_in"," 3 ",1,0xFFCE00;
	end;

OnTimer58000:
	mapannounce "prt_are_in"," 2 ",1,0xFFCE00;
	end;
	
OnTimer59000:
	mapannounce "prt_are_in"," 1 ",1,0xFFCE00;
	end;
	
OnTimer60000:
	mapannounce "prt_are_in"," Prêt? ",1,0xFFCE00;
	donpcevent "#arena_"+$@arenapic+"::OnKis";
	if($@arenapic == 6969)
	{
		donpcevent "#arena_ss_2::OnKis";
		donpcevent "#arena_ss_3::OnKis";
		donpcevent "#arena_ss_4::OnKis";
	}
	end;
	
OnTimer62000:
	mapannounce "prt_are_in","Et voilà, c'est terminé! Merci d'avoir utilisé nos service.",1,0xFFCE00;
	movenpc "#arena_"+$@arenapic+"",1,1;
	donpcevent "#arena_1039::OnInit";
	set $arenapic,0;
	stopnpctimer;
	end;
}

sec_in02,72,171,3	script	Préposé aux Photos::Picture Manager#arena	802,{

	if (!getgmlevel()) end; // Bugfix [No Healing]
	input .@arena;
	if(!.@arena)
	{
		mes "[Préposé aux Photos]";
		mes "Opération annulée.";
		close;
	}
	else if(.@arena == 1357)
	{
		mes "[Préposé aux Photos]";
		mes "Voulez-vous remettre à zéro la variable des photos des arènes?";
		next;
		switch( select( "Annuler","Oui" ) )
		{
			case 1:
				mes "[Préposé aux Photos]";
				mes "Opération annulée.";
				close;

			case 2:
				mes "[Préposé aux Photos]";
				mes "Variable des photos remise à zéro.";
				set $arenapic,0;
				close;
		}
	}
	mes "[Préposé aux Photos]";
	mes "Mot de Passe incorrect.";
	close;
}

sec_in02,76,176,5	script	Compte des Joueurs::Live Broadcast#arena	802,{

	mes "[Compte des Joueurs]";
	mes "Il y a actuellement "+getmapusers("force_1-1")+" joueurs dans l'arène Nv 50.";
	mes "Il y a actuellement "+getmapusers("force_2-1")+" joueurs dans l'arène Nv 60.";
	mes "Il y a actuellement "+getmapusers("force_3-1")+" joueurs dans l'arène Nv 70.";
	mes "Il y a actuellement "+getmapusers("force_4-1")+" joueurs dans l'arène Nv 80.";
	mes "Il y a actuellement "+getmapusers("force_1-2")+" joueurs dans l'arène par équipes.";
	mes "Il y a actuellement "+getmapusers("arena_room")+" joueurs dans la salle d'attente.";
	close;
}

function	script	Func_Are_Rew	{
				
	mes "[Givu]";
	mes "Voulez-vous convertir vos Points de l'Arène en "+getitemname(getarg(0))+"s?";
	mes "Vous pouvez échanger ^3131FF"+getarg(2)+" Points de l'Arène contre "+getarg(1)+" "+getitemname(getarg(0))+"(s)^000000. Entrez 0 pour annuler. Dans le cas contraire, indiquez combien de ^3131FFfois^000000 vous désirez faire l'échange et dépenser vos Points de l'Arène.";
	next;
	input .@reward;
	if(!.@reward)
	{
		mes "[Givu]";
		mes "Vous avez annulé l'échange.";
		close;
	
	}
	else if(.@reward > 1500)
	{
		mes "[Givu]";
		mes "Je suis désolée, je ne peux pas échanger autant d'objets à la fois.";
		close;
	}
	if(arena_point < .@reward * getarg(2))
	{
		mes "[Givu]";
		mes "Vous n'avez pas assez de Points de l'Arène. Vous devriez consulter votre compte actuel.";
		close;
	}
	set arena_point,arena_point - (.@reward * getarg(2));
	getitem getarg(0),.@reward * getarg(1);
	mes "[Givu]";
	mes "Merci, et à bientôt!";
	close;
}

arena_room,63,126,0	script	LevelCheck#50	139,5,5,{

OnTouch:
	if(BaseLevel < 50 || BaseLevel > 69)
	{
		mes "[Guide Pat]";
		mes "Je suis désolé mais cette arène est réservée aux joueurs de niveau ^FF000050^000000 à ^FF000069^000000. Je vais devoir vous demander de quitter cette salle, et de vous diriger vers l'arène correspondant à votre niveau. Amusez-vous bien!";
		close2;
		warp "arena_room",100,75;
	}
	else if(Zeny < 1000)
	{
		mes "[Guide Pat]";
		mes "Oh! Vous avez le niveau requis pour participer mais il vous manque les 1000 zenys de frais d'inscription. N'hésitez pas à revenir quand vous les aurez!";
		close2;
		warp "arena_room",100,75;
	}
	end;
}

arena_room,49,87,0	script	LevelCheck#60	139,5,5,{

OnTouch:
	if(BaseLevel < 60 || BaseLevel > 79)
	{
		mes "[Guide Ben]";
		mes "Je suis désolé mais cette arène est réservée aux joueurs de niveau ^FF000060^000000 à ^FF000079^000000. Je vais devoir vous demander de quitter cette salle, et de vous diriger vers l'arène correspondant à votre niveau. Amusez-vous bien!";
		close2;
		warp "arena_room",100,75;
	}
	else if(Zeny < 1000)
	{
		mes "[Guide Ben]";
		mes "Oh! Vous avez le niveau requis pour participer mais il vous manque les 1000 zenys de frais d'inscription. N'hésitez pas à revenir quand vous les aurez!";
		close2;
		warp "arena_room",100,75;
	}
	end;
}

arena_room,63,53,0	script	LevelCheck#70	139,5,5,{

OnTouch:
	if(BaseLevel < 70 || BaseLevel > 89)
	{
		mes "[Guide Vicious]";
		mes "Je suis désolé mais cette arène est réservée aux joueurs de niveau ^FF000070^000000 à ^FF000089^000000. Je vais devoir vous demander de quitter cette salle, et de vous diriger vers l'arène correspondant à votre niveau. Amusez-vous bien!";
		close2;
		warp "arena_room",100,75;
	}
	else if(Zeny < 1000)
	{
		mes "[Guide Vicious]";
		mes "Oh! Vous avez le niveau requis pour participer mais il vous manque les 1000 zenys de frais d'inscription. N'hésitez pas à revenir quand vous les aurez!";
		close2;
		warp "arena_room",100,75;
	}
	end;
}

arena_room,136,53,0	script	LevelCheck#80	139,5,5,{

OnTouch:
	if(BaseLevel < 80)
	{
		mes "[Guide Epin]";
		mes "Je suis désolé mais cette arène est réservée aux joueurs de niveau ^FF000080^000000 et plus. Je vais devoir vous demander de quitter cette salle, et de vous diriger vers l'arène correspondant à votre niveau. Amusez-vous bien!";
		close2;
		warp "arena_room",100,75;
	}
	else if(Zeny < 1000)
	{
		mes "[Guide Epin]";
		mes "Oh! Vous avez le niveau requis pour participer mais il vous manque les 1000 zenys de frais d'inscription. N'hésitez pas à revenir quand vous les aurez!";
		close2;
		warp "arena_room",100,75;
	}
	end;

}
