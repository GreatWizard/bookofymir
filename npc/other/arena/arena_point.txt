//= Le Livre d'Ymir http://www.bookofymir.net/
//= traduit par No Healing
//===== eAthena Script =======================================
//= Point Exchanger
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Point exchanger: Turbo track points -> Arena Points
//===== Additional Comments: =================================
//= 1.0 First version. [L0ne_W0lf]
//============================================================

prt_are_in,103,11,3	script	Conversion des Points::Arena Point Manager	732,{
	mes "[Conversion des Points]";
	mes "J'espère que vous avez passé un agréable moment dans l'arène. Si vous avez accumulé des points au Turbo Track d'Aldebarn, je peux les convertir en Points de l'Arène.";
	next;
//	switch(select("Consulter son nombre de points:Convertir ses points:^660000Conversion Info^000000")) { // Bugfix: 3ème option virée: elle ne renvoie à rien (cf: les commentaires officiels dans le Turbo track) [No Healing]
	switch(select("Consulter son nombre de points:Convertir ses points")) {
	case 1:
		mes "[Conversion des Points]";
		mes strcharinfo(0) + ", vous avez actuellement " + arena_point + " Points de l'Arène et " + tt_point + " Points Turbo Track.";
		close;
	case 2:
		mes "[Conversion des Points]";
		mes strcharinfo(0) + ", vous avez actuellement " + arena_point + " Points de l'Arène et " + tt_point + " Points Turbo Track.";
		next;
		mes "[Conversion des Points]";
		mes "Veuillez choisir dans la liste ci-dessous le nombre de points à convertir. Gardez bien à l'esprit que si vous choisissez d'en convertir plus de 10, vous ne pourrez en convertir que des ^4D4DFFmultiples de 10^000000.";
		next;
		switch(select("2 Points Turbo Track -> 1 Points de l'Arène:4 Points Turbo Track -> 2 Points de l'Arène:6 Points Turbo Track -> 3 Points de l'Arène:8 Points Turbo Track -> 4 Points de l'Arène:10 Points Turbo Track et plus:Annuler")) {
		case 1:
			callsub S_ExchangePoints,28999,2,1;
		case 2:
			callsub S_ExchangePoints,28998,4,2;
		case 3:
			callsub S_ExchangePoints,28997,6,2;
		case 4:
			callsub S_ExchangePoints,28996,8,4;
		case 5:
			mes "[Conversion des Points]";
			mes "Veuillez indiquer combien de fois vous voulez convertir 10 Points Turbo Track en Points de l'Arène (entre 1 et 20, '^3355FF0^000000' pour annuler).";
			next;
			input .@input;
			if (.@input == 0) {
				mes "[Conversion des Points]";
				mes "Vous avez choisi d'annuler l'échange.";
				close;
			}
			else if (.@input > 20) {
				mes "[Conversion des Points]";
				mes "Le nombre dépasse la limite autorisée, je suis désolé. Veuillez entrer un nombre inférieur ou égal à 20.";
				close;
			}
			else {
				set .@want_point1,10 * .@input;
				set .@want_point,5 * .@input;
				set .@my_arena_all,arena_point + .@want_point;
				set .@my_turbo_all,tt_point - .@want_point1;
				if (.@my_arena_all > 28999) {
					mes "[Conversion des Points]";
					mes "Je suis désolé mais vous dépasseriez le nombre maximum de Points de l'Arène autorisé si je faisais la conversion. Vous ne pouvez pas avoir plus de 29,000 Points de l'Arène.";
					next;
					mes "[Conversion des Points]";
					mes "Vous devriez d'abord utiliser quelques-uns de vos points. Merci de votre participation.";
					close;
				}
				if (.@my_turbo_all < 0) {
					mes "[Conversion des Points]";
					mes "Je suis désolé mais vous n'avez pas assez de Points Turbo Track pour faire la conversion.";
					close;
				}
				else {
					mes "[Conversion des Points]";
					mes "Vous venez de convertir 10 Points Turbo Track en 5 Points de l'Arène " + .@input + " fois de suite. Un total de " + .@want_point1 + " Points Turbo Track ont été convertis en " + .@want_point +" Points de l'Arène.";
					set tt_point,tt_point - .@want_point1;
					set arena_point,arena_point+.@want_point;
					next;
					mes "[Conversion des Points]";
					mes strcharinfo(0) + ", vous avez actuellement ^4682B4" + arena_point + "^000000 Points de l'Arène, et ^00688B" + tt_point + "^000000 Points Turbo Track. Merci pour votre participation.";
					close;
				}
			}
		case 6:
			mes "[Conversion des Points]";
			mes "Vous avez choisi d'annuler l'échange.";
			close;
		}
	case 3:
		// Again. no dialog for this option.
		close;
	}
	close;

S_ExchangePoints:
	if (arena_point > getarg(0) ) {
		mes "[Conversion des Points]";
		mes "Je suis désolé mais vous dépasseriez le nombre maximum de Points de l'Arène autorisé si je faisais la conversion. Vous ne pouvez pas avoir plus de 29,000 Points de l'Arène.";
		next;
		mes "[Conversion des Points]";
		mes "Vous devriez d'abord utiliser quelques-uns de vos points. Merci de votre participation.";
		close;
	}
	if (tt_point >= getarg(1)) {
		mes "[Conversion des Points]";
		mes strcharinfo(0) + ", vous venez de convertir "+getarg(1)+" Point Turbo Track en "+getarg(2)+" Point(s) de l'Arène.";
		set tt_point,tt_point-getarg(1);
		set arena_point,arena_point+getarg(2);
		next;
		mes "[Conversion des Points]";
		mes strcharinfo(0) + ", vous avez actuellement ^4682B4" + arena_point + "^000000 Points de l'Arène, et ^00688B" + tt_point + "^000000 Points Turbo Track. Merci pour votre participation.";
		close;
	}
	else {
		mes "[Conversion des Points]";
		mes "Je suis désolé mais vous n'avez pas assez de Points Turbo Track. Il vous faut au moins 2 points pour faire la conversion.";
		close;
	}
}
