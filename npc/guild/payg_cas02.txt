//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script =======================================
//= War of Emperium (Payon)
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= NPCs that relate to Payon Guild Castle 2
//===== Additional Comments: =================================
//= 1.0 First Version. No longer uses functions. [L0ne_W0lf]
//= 1.1 Corrected flag-in spawn point. [L0ne_W0lf]
//= 1.2 Removed dialog from interior flags. [L0ne_W0lf]
//============================================================

// Guild Flag (Town)
//============================================================
payon,166,173,3	script	Scarlet Palace#f2-1::ScarletPalace	722,{
	end;

OnRecvCastlePy02:
	FlagEmblem GetCastleData("payg_cas02",1);
	end;
}

payon,97,322,4	duplicate(ScarletPalace)	Scarlet Palace#f2-2	722

// Guild Flag (Outside Castle)
//============================================================
pay_gld,292,112,6	script	Scarlet Palace#f2-3::ScarletPalace2	722,{
	set .@GID, GetCastleData("payg_cas02",1);
	if (.@GID == 0) {
		mes " [ Edit du Divin Royaume de Rune Midgard ]";
		mes " ";
		mes "1. Sous la Divine Ordonnance du Roi de Rune Midgard, ce château n'appartient pour l'instant à aucune guilde.";
		mes " ";
		mes "2. Celui qui éliminera tous les gardiens qui protègent ce château et qui détruira l'Emperium, sera reconnu par le Roi de Rune Midgard en tant qu'humble possesseur du château.";
	}
	else {
		if (getcharid(2) == .@GID) {
			mes "Brave guerrier... voulez vous retourner dans votre humble base?";
			next;
			if (select("Retourner au château.:Annuler.") == 1) {
				if (getcharid(2) == GetCastleData("payg_cas02",1)) {
					warp "payg_cas02",22,275;
					end;
				}
			}
			close;
		}
		mes " [ Edit du Divin Royaume de Rune Midgard ]";
		mes " ";
		mes "1. Sous la Divine Ordonnance du Roi de Rune Midgard, ce château appartient à la Guilde ^ff0000" + GetGuildName(.@GID) + "^000000.";
		mes " ";
		mes "2. Le Chef de la Guilde ^ff0000"+ GetGuildName(.@GID) + "^000000 est";
		mes "^FF0000" + GetGuildMaster(.@GID) + "^000000";
		mes "A quiconque souhaitant remettre en cause ce fait, qu'il lève son épée et se batte pour l'Honneur.";
	}
	close;

OnRecvCastlePy02:
	FlagEmblem GetCastleData("payg_cas02",1);
	end;
}

pay_gld,292,120,6	duplicate(ScarletPalace2)	Scarlet Palace#f2-4	722
pay_gld,291,135,6	duplicate(ScarletPalace2)	Scarlet Palace#f2-5	722
pay_gld,271,163,0	duplicate(ScarletPalace2)	Scarlet Palace#f2-6	722

// Guild Flag (Inside Castle)
//============================================================
payg_cas02,254,40,6	script	Scarlet Palace#f2-7::ScarletPalace3	722,{
	end;

OnRecvCastlePy02:
	FlagEmblem GetCastleData("payg_cas02",1);
	end;
}

payg_cas02,254,48,6	duplicate(ScarletPalace3)	Scarlet Palace#f2-8	722
payg_cas02,202,49,0	duplicate(ScarletPalace3)	Scarlet Palace#f2-9	722
payg_cas02,209,49,0	duplicate(ScarletPalace3)	Scarlet Palace#f2-10	722
payg_cas02,59,282,4	duplicate(ScarletPalace3)	Scarlet Palace#f2-11	722
payg_cas02,70,282,4	duplicate(ScarletPalace3)	Scarlet Palace#f2-12	722

// AGIT Manager
//============================================================
payg_cas02,39,25,0	duplicate(Gld_Agit_Manager)	Agit#payg_cas02	-1

// Steward
//============================================================
payg_cas02,22,260,7	duplicate(Gld_Mngr_Template)	Cherieos#payg_cas02	55

// Guardians
//============================================================
payg_cas02,22,260,7	duplicate(Gld_Guard_Template)	Gardien#payg_cas02	-1

// Kafra Staff
//============================================================
payg_cas02,22,275,5	duplicate(Gld_Kafra_Template)	Kafra#payg_cas02	117

// Master's Room Exit
//============================================================
payg_cas02,149,148,0	script	#switch_Py02	111,{
	mes " ";
	mes "Vous remarquez un petit levier...";
	mes "Voulez-vous l'activer?";
	next;
	if(select("Oui.:Non.") == 2) close;
	warp "payg_cas02",22,261;
	end;
}

// Guild Treasure Protection
//============================================================
payg_cas02,145,144,0	duplicate(Gld_Trea_Protect)	ban_warp#payg_cas02	-1,6,6

// Guild Treasure Spawns
//============================================================
payg_cas02,145,144,0	duplicate(Gld_Trea_Spawn)	Coffre#payg_cas02	-1

// Guild Dungeon Switch
//============================================================
payg_cas02,278,247,0	duplicate(Gld_Dun_Template)	#payg_cas02	111
