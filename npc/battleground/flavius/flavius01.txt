//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing, corrigé par Myllena
//===== eAthena Script ======================================= 
// BattleGround System - Flavius
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: ========================================= 
//= [AEGIS Conversion]
//= Flavius Battleground.
//= - Winning Team: 9 badges
//= - Losing Team: 3 badge
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Fixed pink crystal spawning as blue. [L0ne_W0lf]
//= 1.2 Updated 'waitingroom' to support required zeny/lvl. [Kisuka]
//============================================================

// Waiting Room NPCs
//============================================================
bat_room,86,227,4	script	Lieutenant Ator	418,{
	end;
OnInit:
	waitingroom "Combat à 10 Joueurs",10,"start#bat_b01::OnReadyCheck",1,0,80,99;
	end;
OnEnterBG:
	set $@FlaviusBG1_id1, waitingroom2bg("bat_b01",10,290,"start#bat_b01::OnGuillaumeQuit","");
	end;
}

bat_room,85,204,0	script	Lieutenant Thelokus	414,{
	end;
OnInit:
	waitingroom "Combat à 10 Joueurs",10,"start#bat_b01::OnReadyCheck",1,0,80,99;
	end;
OnEnterBG:
	set $@FlaviusBG1_id2, waitingroom2bg("bat_b01",390,10,"start#bat_b01::OnCroixQuit","");
	end;
}

bat_room,85,220,0	warp	back_bgroomb01a	1,1,bat_room,154,150
bat_room,85,211,0	warp	back_bgroomb01b	1,1,bat_room,154,150

bat_room,2,151,3	script	#bat_b01_timer	844,{
	end;

OnInit:
OnEnable:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	initnpctimer;
	set .@chk_bat_a01,getmapusers("bat_b01");
	if (.@chk_bat_a01 < 1) {
		set $@FlaviusBG1, 0;
		if( $@FlaviusBG1_id1 ) { bg_destroy $@FlaviusBG1_id1; set $@FlaviusBG1_id1, 0; }
		if( $@FlaviusBG1_id2 ) { bg_destroy $@FlaviusBG1_id2; set $@FlaviusBG1_id2, 0; }
		donpcevent "start#bat_b01::OnReadyCheck";
	}
	end;
}


// Flavius Battleground Engine
//============================================================
bat_b01,15,15,3	script	start#bat_b01	844,{
OnInit:
	mapwarp "bat_b01","bat_room",154,150;
	end;

OnReadyCheck:
	if( $@FlaviusBG1 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Lieutenant Ator");
	set .@Croix, getwaitingroomstate(0,"Lieutenant Thelokus");
	if( !.@Guillaume && !.@Croix ) {
		donpcevent "#bat_b01_timer::OnStop";
		end;
	}
	if( .@Guillaume < 10 || .@Croix < 10 )
		end;
	set $@FlaviusBG1, 1;
	set $@FlaviusBG1_Victory, 0;
	set $@Croix_ScoreBG1, 0;
	set $@Guill_ScoreBG1, 0;
	bg_updatescore "bat_b01",$@Guill_ScoreBG1,$@Croix_ScoreBG1;

	donpcevent "Lieutenant Ator::OnEnterBG";
	donpcevent "Lieutenant Thelokus::OnEnterBG";
	donpcevent "OBJ#bat_b01_a::Onkill";
	donpcevent "OBJ#bat_b01_a::OnEnable";
	donpcevent "OBJ#bat_b01_b::Onkill";
	donpcevent "OBJ#bat_b01_b::OnEnable";
	donpcevent "guardian#bat_b01_a::Onkill";
	donpcevent "guardian#bat_b01_b::Onkill";
	donpcevent "guardian#bat_b01_a::OnEnable";
	donpcevent "guardian#bat_b01_b::OnEnable";
	donpcevent "cell#bat_b01_a::Onred";
	donpcevent "cell#bat_b01_b::Onred";
	donpcevent "time#bat_b01::OnEnable";
	disablenpc "Guillaume Vintenar#b01_a";
	disablenpc "Croix Vintenar#b01_b";
	disablenpc "Vintenar#bat_b01_aover";
	disablenpc "Vintenar#bat_b01_bover";
	bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
	bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
	donpcevent "countdown#bat_b01::OnEnable";
	initnpctimer;
	end;

OnReset:
	donpcevent "OBJ#bat_b01_a::Onkill";
	donpcevent "OBJ#bat_b01_a::OnEnable";
	donpcevent "OBJ#bat_b01_b::Onkill";
	donpcevent "OBJ#bat_b01_b::OnEnable";
	donpcevent "guardian#bat_b01_a::Onkill";
	donpcevent "guardian#bat_b01_b::Onkill";
	donpcevent "guardian#bat_b01_a::OnEnable";
	donpcevent "guardian#bat_b01_b::OnEnable";
	donpcevent "cell#bat_b01_a::Onred";
	donpcevent "cell#bat_b01_b::Onred";
	donpcevent "time#bat_b01::OnEnable";
	disablenpc "Guillaume Vintenar#b01_a";
	disablenpc "Croix Vintenar#b01_b";
	disablenpc "Vintenar#bat_b01_aover";
	disablenpc "Vintenar#bat_b01_bover";
	bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
	bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
	end;

OnGuillaumeQuit:
OnCroixQuit:
	bg_leave;
	end;

OnTimer10000:
	stopnpctimer;
	donpcevent "#bat_b01_timer::OnEnable";
	end;
}

bat_b01,1,1,3	script	OBJ#bat_b01_a	844,{
OnEnable:
	bg_monster $@FlaviusBG1_id1,"bat_b01",61,150,"Cristal de Guillaume",1915,"OBJ#bat_b01_a::OnMyMobDead";
	end;

Onkill:
	killmonster "bat_b01","OBJ#bat_b01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b01_a::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "Le Cristal de Guillaume vient d'être détruit!",bc_map,"0xFFCE00";
		if ($@Croix_ScoreBG1 > 0) {
			set $@FlaviusBG1_Victory,2;
			set $@Croix_ScoreBG1,$@Croix_ScoreBG1+1;
			enablenpc "Guillaume Vintenar#b01_a";
			enablenpc "Croix Vintenar#b01_b";
			donpcevent "time#bat_b01::Onstop";
		}
		else {
			set $@Croix_ScoreBG1,1;
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::onReset";
		}
		donpcevent "#bat_b01_timer::OnStop";
		bg_updatescore "bat_b01",$@Guill_ScoreBG1,$@Croix_ScoreBG1;
		bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
		bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		donpcevent "#bat_b01_timer::OnEnable";
	}
	end;
}

bat_b01,1,2,3	script	OBJ#bat_b01_b	844,{
OnEnable:
	bg_monster $@FlaviusBG1_id2,"bat_b01",328,150,"Cristal de Croix",1914,"OBJ#bat_b01_b::OnMyMobDead";
	end;

Onkill:
	killmonster "bat_b01","OBJ#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b01_b::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "Le Cristal de Croix vient d'être détruit!",bc_map,"0xFFCE00";
		if ($@Guill_ScoreBG1 > 0) {
			set $@FlaviusBG1_Victory,1;
			set $@Guill_ScoreBG1,$@Guill_ScoreBG1+1;
			enablenpc "Guillaume Vintenar#b01_a";
			enablenpc "Croix Vintenar#b01_b";
			donpcevent "time#bat_b01::Onstop";
		}
		else {
			set $@Guill_ScoreBG1,1;
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::onReset";
		}
		donpcevent "#bat_b01_timer::OnStop";
		bg_updatescore "bat_b01",$@Guill_ScoreBG1,$@Croix_ScoreBG1;
		bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
		bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		donpcevent "#bat_b01_timer::OnEnable";
	}
	end;
}

bat_b01,1,3,3	script	guardian#bat_b01_a	844,{
OnEnable:
	bg_monster $@FlaviusBG1_id1,"bat_b01",108,159,"Garde de Guillaume",1949,"guardian#bat_b01_a::OnMyMobDead";
	bg_monster $@FlaviusBG1_id1,"bat_b01",108,141,"Garde de Guillaume",1949,"guardian#bat_b01_a::OnMyMobDead";
	end;

Onkill:
	killmonster "bat_b01","guardian#bat_b01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardian#bat_b01_a::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b01_a::Ongreen";
		mapannounce "bat_b01", "Le Cristal de Guillaume est maintenant vulnérable aux attaques!",bc_map,"0xFFCE00";
	}
	end;
}

bat_b01,1,3,3	script	guardian#bat_b01_b	844,{
OnEnable:
	bg_monster $@FlaviusBG1_id2,"bat_b01",307,160,"Garde de Croix",1949,"guardian#bat_b01_b::OnMyMobDead";
	bg_monster $@FlaviusBG1_id2,"bat_b01",307,138,"Garde de Croix",1949,"guardian#bat_b01_b::OnMyMobDead";
	end;

Onkill:
	killmonster "bat_b01","guardian#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardian#bat_b01_b::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b01_b::Ongreen";
		mapannounce "bat_b01", "Le Cristal de Croix est maintenant vulnérable aux attaques!",bc_map,"0xFFCE00";
	}
	end;
}

bat_b01,1,4,3	script	cell#bat_b01_a	844,{
Onred:
	setwall "bat_b01",60,149,2,6,0,"Guillaume_wall1-1";
	setwall "bat_b01",62,149,2,0,0,"Guillaume_wall1-2";
	setwall "bat_b01",62,151,2,2,0,"Guillaume_wall1-3";
	setwall "bat_b01",60,151,2,4,0,"Guillaume_wall1-4";
	end;

Ongreen:
	delwall "Guillaume_wall1-1";
	delwall "Guillaume_wall1-2";
	delwall "Guillaume_wall1-3";
	delwall "Guillaume_wall1-4";
	end;
}

bat_b01,1,5,3	script	cell#bat_b01_b	844,{
Onred:
	setwall "bat_b01",327,149,2,6,0,"Croix_wall1-1";
	setwall "bat_b01",329,149,2,0,0,"Croix_wall1-2";
	setwall "bat_b01",329,151,2,2,0,"Croix_wall1-3";
	setwall "bat_b01",327,151,2,4,0,"Croix_wall1-4";
	end;

Ongreen:
	delwall "Croix_wall1-1";
	delwall "Croix_wall1-2";
	delwall "Croix_wall1-3";
	delwall "Croix_wall1-4";
	end;
}

bat_b01,1,6,1	script	time#bat_b01	844,{
OnEnable:
	donpcevent "Rebouteux#b01_a::OnEnable";
	donpcevent "Rebouteux#b01_b::OnEnable";
	end;

Onstop:
	donpcevent "Rebouteux#b01_a::OnStop";
	donpcevent "Rebouteux#b01_b::OnStop";
	end;
}

bat_b01,10,294,3	script	Rebouteux#b01_a	95,{
	specialeffect2 EF_HEAL;
	mes "[Rebouteux]";
	mes "Serre bien les dents mon p'tit gars, et prends une bonne bouffée d'air. Ça risque de faire un peu mal...";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_b01_rp1_a_warp";
	end;

OnTimer26000:
	disablenpc "bat_b01_rp1_a_warp";
	end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Rebouteux#b01_a::onEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Rebouteux#b01_a";
	end;

Onstop:
	disablenpc "bat_b01_rp1_a_warp";
	disablenpc "Rebouteux#b01_a";
	stopnpctimer;
	end;
}

bat_b01,10,290,0	script	bat_b01_rp1_a_warp	45,10,10,{
OnInit:
	disablenpc "bat_b01_rp1_a_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_b01",87,73;
	end;
}

bat_b01,389,14,3	script	Rebouteux#b01_b	95,{
	specialeffect2 EF_HEAL;
	mes "[Rebouteux]";
	mes "Serre bien les dents mon p'tit gars, et prends une bonne bouffée d'air. Ça risque de faire un peu mal...";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_b01_rp1_b_warp";
	end;

OnTimer26000:
	disablenpc "bat_b01_rp1_b_warp";
	end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Rebouteux#b01_b::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Rebouteux#b01_b";
	end;

Onstop:
	disablenpc "bat_b01_rp1_b_warp";
	disablenpc "Rebouteux#b01_b";
	stopnpctimer;
	end;
}

bat_b01,389,10,0	script	bat_b01_rp1_b_warp	45,9,9,{
OnInit:
	disablenpc "bat_b01_rp1_a_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_b01",312,225;
	end;
}

bat_b01,87,76,0	script	A_CODE#bat_b01	-1,5,5,{
OnTouch:
	if (checkquest(2070) < 0)
		setquest 2070;
	end;
}

bat_b01,312,224,0	script	B_CODE#bat_b01	-1,5,5,{
OnTouch:
	if (checkquest(2070) < 0)
		setquest 2070;
	end;
}

bat_b01,10,294,3	script	Guillaume Vintenar#b01_a	934,{
	if ($@FlaviusBG1_id1 == getcharid(4)) {
		if ($@FlaviusBG1_Victory == 1) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 8) {
				mes "[Axl Rose]";
				mes "Guillaume le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,9; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Guillaume le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts.";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Guillaume Vintenar#b01_a";
	end;
}

bat_b01,389,14,3	script	Croix Vintenar#b01_b	934,{
	if ($@FlaviusBG1_id2 == getcharid(4)) {
		if ($@FlaviusBG1_Victory == 2) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 8) {
				mes "[Swandery]";
				mes "Croix le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,9; //BF_Badge2
			}
			else {
				mes "[Swandery]";
				mes "Croix le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Croix Vintenar#b01_b";
	end;
}

bat_b01,1,5,3	script	countdown#bat_b01	844,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

Onstop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_b01", "Guillaume Vintenar Axl Rose : À l'attaque! Détruisons le Cristal de Croix!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_b01", "Croix Vintenar Swandery : Même si l'armée de Guillaume lutte pour nous vaincre, la victoire sera nôtre. Montrons-leur notre puissance!",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_b01", "Marollo VII : Guillaume Marollo, Croix Marollo! Et vous, leurs mercenaires!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_b01", "Marollo VII : Vos deux camps possèdent chacun une force louable, il sera difficile de trancher en la faveur de l'un ou l'autre.",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_b01", "Marollo VII : Cette bataille de Flavius est pure perte de temps. Je déciderai donc de l'équipe victorieuse en fonction de votre progression.",bc_map,"0x99CC00";
	end;

OnTimer1822000:
	mapannounce "bat_b01", "Marollo VII : Si vous n'accepetez pas ces règles, allez donc vous battre dans une autre vallée!",bc_map,"0x99CC00";
	end;

OnTimer1825000:
	mapannounce "bat_b01", "Axl Rose, Swandery : Oui, c'est compris.",bc_map,"0x99CC00";
	end;

OnTimer1830000:
	donpcevent "time#bat_b01::Onstop";
	bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
	bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
	enablenpc "Vintenar#bat_b01_aover";
	enablenpc "Vintenar#bat_b01_bover";
	end;

OnTimer1900000:
	mapwarp "bat_b01","bat_room",154,150;
	donpcevent "countdown#bat_b01::Onstop";
	end;
}

bat_b01,81,83,3	script	Camp de Guillaume#flag21	973,{ end; }
bat_b01,94,83,3	script	Camp de Guillaume#flag22	973,{ end; }
bat_b01,81,66,3	script	Camp de Guillaume#flag23	973,{ end; }
bat_b01,94,66,3	script	Camp de Guillaume#flag24	973,{ end; }
bat_b01,139,142,3	script	Camp de Guillaume#flag25	973,{ end; }
bat_b01,139,158,3	script	Camp de Guillaume#flag26	973,{ end; }
bat_b01,110,161,3	script	Camp de Guillaume#flag27	973,{ end; }
bat_b01,110,137,3	script	Camp de Guillaume#flag28	973,{ end; }
bat_b01,63,135,3	script	Camp de Guillaume#flag29	973,{ end; }
bat_b01,63,165,3	script	Camp de Guillaume#flag30	973,{ end; }
bat_b01,10,296,3	script	Camp de Guillaume#flag31	973,{ end; }

bat_b01,306,233,3	script	Camp de Croix#flag21	974,{ end; }
bat_b01,317,233,3	script	Camp de Croix#flag22	974,{ end; }
bat_b01,306,216,3	script	Camp de Croix#flag23	974,{ end; }
bat_b01,317,216,3	script	Camp de Croix#flag24	974,{ end; }
bat_b01,257,158,3	script	Camp de Croix#flag25	974,{ end; }
bat_b01,257,141,3	script	Camp de Croix#flag26	974,{ end; }
bat_b01,297,164,3	script	Camp de Croix#flag27	974,{ end; }
bat_b01,297,136,3	script	Camp de Croix#flag28	974,{ end; }
bat_b01,336,161,3	script	Camp de Croix#flag29	974,{ end; }
bat_b01,336,139,3	script	Camp de Croix#flag30	974,{ end; }
bat_b01,389,16,3	script	Camp de Croix#flag31	974,{ end; }

bat_b01,10,294,3	script	Vintenar#bat_b01_aover	419,{
	set .@A_B_gap,$@Guill_ScoreBG1 - $@Croix_ScoreBG1;
	if ($@FlaviusBG1_id1 == getcharid(4)) {
		if (.@A_B_gap > 0) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 8) {
				mes "[Axl Rose]";
				mes "Guillaume le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,9; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Guillaume le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else if (.@A_B_gap == 0) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts.";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts.";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
	}
	else {
		mes "[Axl Rose]";
		mes "Que faites-vous ici, mercenaire de Croix? Vous n'avez rien à faire ici, nous vous ferons mordre la poussière!";
		close;
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Vintenar#bat_b01_aover";
	end;
}

bat_b01,389,14,3	script	Vintenar#bat_b01_bover	415,{
	set .@A_B_gap,$@Guill_ScoreBG1 - $@Croix_ScoreBG1;
	if ($@FlaviusBG1_id2 == getcharid(4)) {
		if (.@A_B_gap > 0) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else if (.@A_B_gap == 0) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 8) {
				mes "[Swandery]";
				mes "Croix le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,9; //BF_Badge2
			}
			else {
				mes "[Swandery]";
				mes "Croix le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
	}
	else {
		mes "[Swandery]";
		mes "Que faites-vous ici, mercenaire de Guillaume? Vous n'avez rien à faire ici, nous vous ferons mordre la poussière!";
		close;
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Vintenar#bat_b01_bover";
	end;
}

/*
bat_b01,1,10,3	script	Tout arrêter#b01	81,{
	input .@input,0,2000;
	if (.@input == 0) {
		mes "Annulé.";
		close;
	}
	else if (.@input == 1854) {
		mes "En quoi puis-je vous aider?";
		next;
		switch(select("Tout arrêter.:Annuler.")) {
		case 1:
			mes "Au revoir.";
			close2;
			mapwarp "bat_b01","bat_room",154,150;
			end;
		case 2:
			mes "Annulé.";
			close;
		}
	}
}
*/
