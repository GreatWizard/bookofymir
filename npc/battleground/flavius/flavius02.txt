//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing, corrigé par Myllena
//===== eAthena Script ======================================= 
// BattleGround System - Flavius Second
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: ========================================= 
//= [AEGIS Conversion]
//= Flavius Battleground.
//= - Winning Team: 9 badges
//= - Losing Team: 3 badge
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Fixed NPCs being called for waiting rooms. (bugreport:4395)
//= 1.2 Fixed pink crystal spawning as blue. [L0ne_W0lf]
//= 1.3 Updated 'waitingroom' to support required zeny/lvl. [Kisuka]
//============================================================

// Waiting Room NPCs
//============================================================
bat_room,142,227,4	script	Lieutenant Huvas	418,{
	end;
OnInit:
	waitingroom "Combat à 10 Joueurs",10,"start#bat_b02::OnReadyCheck",1,0,80,99;
	end;
OnEnterBG:
	set $@FlaviusBG2_id1, waitingroom2bg("bat_b02",10,290,"start#bat_b02::OnGuillaumeQuit","");
	end;
}

bat_room,142,204,0	script	Lieutenant Yukon	414,{
	end;
OnInit:
	waitingroom "Combat à 10 Joueurs",10,"start#bat_b02::OnReadyCheck",1,0,80,99;
	end;
OnEnterBG:
	set $@FlaviusBG2_id2, waitingroom2bg("bat_b02",390,10,"start#bat_b02::OnCroixQuit","");
	end;
}

bat_room,141,220,0	warp	back_bgroomb02a	1,1,bat_room,154,150
bat_room,141,211,0	warp	back_bgroomb02b	1,1,bat_room,154,150

bat_room,2,151,3	script	#bat_b02_timer	844,{
	end;

OnInit:
OnEnable:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	initnpctimer;
	set .@chk_bat_a01,getmapusers("bat_b02");
	if (.@chk_bat_a01 < 1) {
		set $@FlaviusBG2, 0;
		if( $@FlaviusBG2_id1 ) { bg_destroy $@FlaviusBG2_id1; set $@FlaviusBG2_id1, 0; }
		if( $@FlaviusBG2_id2 ) { bg_destroy $@FlaviusBG2_id2; set $@FlaviusBG2_id2, 0; }
		donpcevent "start#bat_b02::OnReadyCheck";
	}
	end;
}


// Flavius Battleground Engine
//============================================================
bat_b02,15,15,3	script	start#bat_b02	844,{
OnInit:
	mapwarp "bat_b02","bat_room",154,150;
	end;

OnReadyCheck:
	if( $@FlaviusBG2 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Lieutenant Huvas");
	set .@Croix, getwaitingroomstate(0,"Lieutenant Yukon");
	if( !.@Guillaume && !.@Croix ) {
		donpcevent "#bat_b02_timer::OnStop";
		end;
	}
	if( .@Guillaume < 10 || .@Croix < 10 )
		end;
	set $@FlaviusBG2, 1;
	set $@FlaviusBG2_Victory, 0;
	set $@Croix_ScoreBG2, 0;
	set $@Guill_ScoreBG2, 0;

	bg_updatescore "bat_b02",$@Guill_ScoreBG2,$@Croix_ScoreBG2;
	donpcevent "Lieutenant Huvas::OnEnterBG";
	donpcevent "Lieutenant Yukon::OnEnterBG";
	donpcevent "OBJ#bat_b02_a::Onkill";
	donpcevent "OBJ#bat_b02_a::OnEnable";
	donpcevent "OBJ#bat_b02_b::Onkill";
	donpcevent "OBJ#bat_b02_b::OnEnable";
	donpcevent "guardian#bat_b02_a::Onkill";
	donpcevent "guardian#bat_b02_b::Onkill";
	donpcevent "guardian#bat_b02_a::OnEnable";
	donpcevent "guardian#bat_b02_b::OnEnable";
	donpcevent "cell#bat_b02_a::Onred";
	donpcevent "cell#bat_b02_b::Onred";
	donpcevent "time#bat_b02::OnEnable";
	disablenpc "Guillaume Vintenar#b02_a";
	disablenpc "Croix Vintenar#b02_b";
	disablenpc "Vintenar#bat_b02_aover";
	disablenpc "Vintenar#bat_b02_bover";
	bg_warp $@FlaviusBG2_id1,"bat_b02",87,75;
	bg_warp $@FlaviusBG2_id2,"bat_b02",311,224;
	donpcevent "countdown#bat_b02::OnEnable";
	initnpctimer;
	end;

OnReset:
	donpcevent "OBJ#bat_b02_a::Onkill";
	donpcevent "OBJ#bat_b02_a::OnEnable";
	donpcevent "OBJ#bat_b02_b::Onkill";
	donpcevent "OBJ#bat_b02_b::OnEnable";
	donpcevent "guardian#bat_b02_a::Onkill";
	donpcevent "guardian#bat_b02_b::Onkill";
	donpcevent "guardian#bat_b02_a::OnEnable";
	donpcevent "guardian#bat_b02_b::OnEnable";
	donpcevent "cell#bat_b02_a::Onred";
	donpcevent "cell#bat_b02_b::Onred";
	donpcevent "time#bat_b02::OnEnable";
	disablenpc "Guillaume Vintenar#b02_a";
	disablenpc "Croix Vintenar#b02_b";
	disablenpc "Vintenar#bat_b02_aover";
	disablenpc "Vintenar#bat_b02_bover";
	bg_warp $@FlaviusBG2_id1,"bat_b02",87,75;
	bg_warp $@FlaviusBG2_id2,"bat_b02",311,224;
	end;

OnGuillaumeQuit:
OnCroixQuit:
	bg_leave;
	end;

OnTimer10000:
	stopnpctimer;
	donpcevent "#bat_b02_timer::OnEnable";
	end;
}

bat_b02,1,1,3	script	OBJ#bat_b02_a	844,{
OnEnable:
	bg_monster $@FlaviusBG2_id1,"bat_b02",61,150,"Cristal de Guillaume",1915,"OBJ#bat_b02_a::OnMyMobDead";
	end;

Onkill:
	killmonster "bat_b02","OBJ#bat_b02_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b02","OBJ#bat_b02_a::OnMyMobDead") < 1) {
		mapannounce "bat_b02", "Le Cristal de Guillaume vient d'être détruit!",bc_map,"0xFFCE00";
		if ($@Croix_ScoreBG2 > 0) {
			set $@FlaviusBG2_Victory,2;
			set $@Croix_ScoreBG2,$@Croix_ScoreBG2+1;
			enablenpc "Guillaume Vintenar#b02_a";
			enablenpc "Croix Vintenar#b02_b";
			donpcevent "time#bat_b02::Onstop";
		}
		else {
			set $@Croix_ScoreBG2,1;
			donpcevent "time#bat_b02::OnEnable";
			donpcevent "start#bat_b02::onReset";
		}
		donpcevent "#bat_b02_timer::OnStop";
		bg_updatescore "bat_b02",$@Guill_ScoreBG2,$@Croix_ScoreBG2;
		bg_warp $@FlaviusBG2_id1,"bat_b02",10,290;
		bg_warp $@FlaviusBG2_id2,"bat_b02",390,10;
		donpcevent "#bat_b02_timer::OnEnable";
	}
	end;
}

bat_b02,1,2,3	script	OBJ#bat_b02_b	844,{
OnEnable:
	bg_monster $@FlaviusBG2_id2,"bat_b02",328,150,"Cristal de Croix",1914,"OBJ#bat_b02_b::OnMyMobDead";
	end;

Onkill:
	killmonster "bat_b02","OBJ#bat_b02_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b02","OBJ#bat_b02_b::OnMyMobDead") < 1) {
		mapannounce "bat_b02", "Le Cristal de Croix vient d'être détruit!",bc_map,"0xFFCE00";
		if ($@Guill_ScoreBG2 > 0) {
			set $@FlaviusBG2_Victory,1;
			set $@Guill_ScoreBG2,$@Guill_ScoreBG2+1;
			enablenpc "Guillaume Vintenar#b02_a";
			enablenpc "Croix Vintenar#b02_b";
			donpcevent "time#bat_b02::Onstop";
		}
		else {
			set $@Guill_ScoreBG2,1;
			donpcevent "time#bat_b02::OnEnable";
			donpcevent "start#bat_b02::onReset";
		}
		donpcevent "#bat_b02_timer::OnStop";
		bg_updatescore "bat_b02",$@Guill_ScoreBG2,$@Croix_ScoreBG2;
		bg_warp $@FlaviusBG2_id1,"bat_b02",10,290;
		bg_warp $@FlaviusBG2_id2,"bat_b02",390,10;
		donpcevent "#bat_b02_timer::OnEnable";
	}
	end;
}

bat_b02,1,3,3	script	guardian#bat_b02_a	844,{
OnEnable:
	bg_monster $@FlaviusBG2_id1,"bat_b02",108,159,"Garde de Guillaume",1949,"guardian#bat_b02_a::OnMyMobDead";
	bg_monster $@FlaviusBG2_id1,"bat_b02",108,141,"Garde de Guillaume",1949,"guardian#bat_b02_a::OnMyMobDead";
	end;

Onkill:
	killmonster "bat_b02","guardian#bat_b02_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b02","guardian#bat_b02_a::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b02_a::Ongreen";
		mapannounce "bat_b02", "Le Cristal de Guillaume est maintenant vulnérable aux attaques!",bc_map,"0xFFCE00";
	}
	end;
}

bat_b02,1,3,3	script	guardian#bat_b02_b	844,{
OnEnable:
	bg_monster $@FlaviusBG2_id2,"bat_b02",307,160,"Garde de Croix",1949,"guardian#bat_b02_b::OnMyMobDead";
	bg_monster $@FlaviusBG2_id2,"bat_b02",307,138,"Garde de Croix",1949,"guardian#bat_b02_b::OnMyMobDead";
	end;

Onkill:
	killmonster "bat_b02","guardian#bat_b02_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b02","guardian#bat_b02_b::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b02_b::Ongreen";
		mapannounce "bat_b02", "Le Cristal de Croix est maintenant vulnérable aux attaques!",bc_map,"0xFFCE00";
	}
	end;
}

bat_b02,1,4,3	script	cell#bat_b02_a	844,{
Onred:
	setwall "bat_b02",60,149,2,6,0,"Guillaume_wall1-1";
	setwall "bat_b02",62,149,2,0,0,"Guillaume_wall1-2";
	setwall "bat_b02",62,151,2,2,0,"Guillaume_wall1-3";
	setwall "bat_b02",60,151,2,4,0,"Guillaume_wall1-4";
	end;

Ongreen:
	delwall "Guillaume_wall1-1";
	delwall "Guillaume_wall1-2";
	delwall "Guillaume_wall1-3";
	delwall "Guillaume_wall1-4";
	end;
}

bat_b02,1,5,3	script	cell#bat_b02_b	844,{
Onred:
	setwall "bat_b02",327,149,2,6,0,"Croix_wall1-1";
	setwall "bat_b02",329,149,2,0,0,"Croix_wall1-2";
	setwall "bat_b02",329,151,2,2,0,"Croix_wall1-3";
	setwall "bat_b02",327,151,2,4,0,"Croix_wall1-4";
	end;

Ongreen:
	delwall "Croix_wall1-1";
	delwall "Croix_wall1-2";
	delwall "Croix_wall1-3";
	delwall "Croix_wall1-4";
	end;
}

bat_b02,1,6,1	script	time#bat_b02	844,{
OnEnable:
	donpcevent "Rebouteux#b02_a::OnEnable";
	donpcevent "Rebouteux#b02_b::OnEnable";
	end;

Onstop:
	donpcevent "Rebouteux#b02_a::OnStop";
	donpcevent "Rebouteux#b02_b::OnStop";
	end;
}

bat_b02,10,294,3	script	Rebouteux#b02_a	95,{
	specialeffect2 EF_HEAL;
	mes "[Rebouteux]";
	mes "Serre bien les dents mon p'tit gars, et prends une bonne bouffée d'air. Ça risque de faire un peu mal...";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_b02_rp1_a_warp";
	end;

OnTimer26000:
	disablenpc "bat_b02_rp1_a_warp";
	end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Rebouteux#b02_a::onEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Rebouteux#b02_a";
	end;

Onstop:
	disablenpc "bat_b02_rp1_a_warp";
	disablenpc "Rebouteux#b02_a";
	stopnpctimer;
	end;
}

bat_b02,10,290,0	script	bat_b02_rp1_a_warp	45,10,10,{
OnInit:
	disablenpc "bat_b02_rp1_a_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_b02",87,73;
	end;
}

bat_b02,389,14,3	script	Rebouteux#b02_b	95,{
	specialeffect2 EF_HEAL;
	mes "[Rebouteux]";
	mes "Serre bien les dents mon p'tit gars, et prends une bonne bouffée d'air. Ça risque de faire un peu mal...";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_b02_rp1_b_warp";
	end;

OnTimer26000:
	disablenpc "bat_b02_rp1_b_warp";
	end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Rebouteux#b02_b::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Rebouteux#b02_b";
	end;

Onstop:
	disablenpc "bat_b02_rp1_b_warp";
	disablenpc "Rebouteux#b02_b";
	stopnpctimer;
	end;
}

bat_b02,389,10,0	script	bat_b02_rp1_b_warp	45,9,9,{
OnInit:
	disablenpc "bat_b02_rp1_a_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_b02",312,225;
	end;
}

bat_b02,87,76,0	script	A_CODE#bat_b02	-1,5,5,{
OnTouch:
	if (checkquest(2070) < 0)
		//setquest 2070;
	end;
}

bat_b02,312,224,0	script	B_CODE#bat_b02	-1,5,5,{
OnTouch:
	if (checkquest(2070) < 0)
		//setquest 2070;
	end;
}

bat_b02,10,294,3	script	Guillaume Vintenar#b02_a	934,{
	if ($@FlaviusBG2_id1 == getcharid(4)) {
		if ($@FlaviusBG2_Victory == 1) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 8) {
				mes "[Axl Rose]";
				mes "Guillaume le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,9; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Guillaume le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts.";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Guillaume Vintenar#b02_a";
	end;
}

bat_b02,389,14,3	script	Croix Vintenar#b02_b	934,{
	if ($@FlaviusBG2_id2 == getcharid(4)) {
		if ($@FlaviusBG2_Victory == 2) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 8) {
				mes "[Swandery]";
				mes "Croix le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,9; //BF_Badge2
			}
			else {
				mes "[Swandery]";
				mes "Croix le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Croix Vintenar#b02_b";
	end;
}

bat_b02,1,5,3	script	countdown#bat_b02	844,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

Onstop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_b01", "Guillaume Vintenar Axl Rose : À l'attaque! Détruisons le Cristal de Croix!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_b01", "Croix Vintenar Swandery : Même si l'armée de Guillaume lutte pour nous vaincre, la victoire sera nôtre. Montrons-leur notre puissance!",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_b01", "Marollo VII : Guillaume Marollo, Croix Marollo! Et vous, leurs mercenaires!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_b01", "Marollo VII : Vos deux camps possèdent chacun une force louable, il sera difficile de trancher en la faveur de l'un ou l'autre.",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_b01", "Marollo VII : Cette bataille de Flavius est pure perte de temps. Je déciderai donc de l'équipe victorieuse en fonction de votre progression.",bc_map,"0x99CC00";
	end;

OnTimer1822000:
	mapannounce "bat_b01", "Marollo VII : Si vous n'accepetez pas ces règles, allez donc vous battre dans une autre vallée!",bc_map,"0x99CC00";
	end;

OnTimer1825000:
	mapannounce "bat_b01", "Axl Rose, Swandery : Oui, c'est compris.",bc_map,"0x99CC00";
	end;

OnTimer1830000:
	donpcevent "time#bat_b02::Onstop";
	bg_warp $@FlaviusBG2_id1,"bat_b02",10,290;
	bg_warp $@FlaviusBG2_id2,"bat_b02",390,10;
	enablenpc "Vintenar#bat_b02_aover";
	enablenpc "Vintenar#bat_b02_bover";
	end;

OnTimer1900000:
	mapwarp "bat_b02","bat_room",154,150;
	donpcevent "countdown#bat_b02::Onstop";
	end;
}

bat_b02,81,83,3	script	Camp de Guillaume#flag32	973,{ end; }
bat_b02,94,83,3	script	Camp de Guillaume#flag33	973,{ end; }
bat_b02,81,66,3	script	Camp de Guillaume#flag34	973,{ end; }
bat_b02,94,66,3	script	Camp de Guillaume#flag35	973,{ end; }
bat_b02,139,142,3	script	Camp de Guillaume#flag36	973,{ end; }
bat_b02,139,158,3	script	Camp de Guillaume#flag37	973,{ end; }
bat_b02,110,161,3	script	Camp de Guillaume#flag38	973,{ end; }
bat_b02,110,137,3	script	Camp de Guillaume#flag39	973,{ end; }
bat_b02,63,135,3	script	Camp de Guillaume#flag40	973,{ end; }
bat_b02,63,165,3	script	Camp de Guillaume#flag41	973,{ end; }
bat_b02,10,296,3	script	Camp de Guillaume#flag42	973,{ end; }

bat_b02,306,233,3	script	Camp de Croix#flag32	974,{ end; }
bat_b02,317,233,3	script	Camp de Croix#flag33	974,{ end; }
bat_b02,306,216,3	script	Camp de Croix#flag34	974,{ end; }
bat_b02,317,216,3	script	Camp de Croix#flag35	974,{ end; }
bat_b02,257,158,3	script	Camp de Croix#flag36	974,{ end; }
bat_b02,257,141,3	script	Camp de Croix#flag37	974,{ end; }
bat_b02,297,164,3	script	Camp de Croix#flag38	974,{ end; }
bat_b02,297,136,3	script	Camp de Croix#flag39	974,{ end; }
bat_b02,336,161,3	script	Camp de Croix#flag40	974,{ end; }
bat_b02,336,139,3	script	Camp de Croix#flag41	974,{ end; }
bat_b02,389,16,3	script	Camp de Croix#flag42	974,{ end; }

bat_b02,10,294,3	script	Vintenar#bat_b02_aover	419,{
	set .@A_B_gap,$@Guill_ScoreBG2 - $@Croix_ScoreBG2;
	if ($@FlaviusBG2_id1 == getcharid(4)) {
		if (.@A_B_gap > 0) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 8) {
				mes "[Axl Rose]";
				mes "Guillaume le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,9; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Guillaume le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else if (.@A_B_gap == 0) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts.";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts.";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
	}
	else {
		mes "[Axl Rose]";
		mes "Que faites-vous ici, mercenaire de Croix? Vous n'avez rien à faire ici, nous vous ferons mordre la poussière!";
		close;
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Vintenar#bat_b02_aover";
	end;
}

bat_b02,389,14,3	script	Vintenar#bat_b02_bover	415,{
	set .@A_B_gap,$@Guill_ScoreBG2 - $@Croix_ScoreBG2;
	if ($@FlaviusBG2_id2 == getcharid(4)) {
		if (.@A_B_gap > 0) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else if (.@A_B_gap == 0) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 8) {
				mes "[Swandery]";
				mes "Croix le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,9; //BF_Badge2
			}
			else {
				mes "[Swandery]";
				mes "Croix le Très Saint!!";
				mes "Festoyons à notre glorieuse victoire!";
				mes "" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
	}
	else {
		mes "[Swandery]";
		mes "Que faites-vous ici, mercenaire de Guillaume? Vous n'avez rien à faire ici, nous vous ferons mordre la poussière!";
		close;
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Vintenar#bat_b02_bover";
	end;
}

/*
bat_b02,1,10,3	script	Tout arrêter#b02	81,{
	input .@input,0,2000;
	if (.@input == 0) {
		mes "Annulé.";
		close;
	}
	else if (.@input == 1854) {
		mes "En quoi puis-je vous aider?";
		next;
		switch(select("Tout arrêter.:Annuler.")) {
		case 1:
			mes "Au revoir.";
			close2;
			mapwarp "bat_b02","bat_room",154,150;
			end;
		case 2:
			mes "Annulé.";
			close;
		}
	}
}
*/
