//===== eAthena Script ======================================= 
// BattleGround System - Tierra Gorge
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: ========================================= 
//= [AEGIS Conversion]
//= Tierra Gorge Battleground.
//= - Winning Team: 3 badges
//= - Losing Team: 1 badge
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Corrected setwalls for barricades. [L0ne_W0lf]
//= 1.2 Updated 'waitingroom' to support required zeny/lvl. [Kisuka]
//============================================================

// Waiting Room NPCs
//============================================================
bat_room,57,227,5	script	Lieutenant Kalos	418,{
	end;

OnInit:
	waitingroom "Combat à 10 Joueurs",10,"start#bat_a01::OnReadyCheck",1,0,80,99;
	end;

OnEnterBG:
	set $@TierraBG1_id1, waitingroom2bg("bat_a01",50,374,"start#bat_a01::OnGuillaumeQuit","");
	end;
}

bat_room,58,204,1	script	Lieutenant Eyor	414,{
	end;

OnInit:
	waitingroom "Combat à 10 Joueurs",10,"start#bat_a01::OnReadyCheck",1,0,80,99;
	end;

OnEnterBG:
	set $@TierraBG1_id2, waitingroom2bg("bat_a01",42,16,"start#bat_a01::OnCroixQuit","");
	end;
}

bat_room,1,151,3	script	#bat_a01_timer	844,{
	end;

onInit:
OnEnable:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	initnpctimer;
	set .@chk_bat_a01,getmapusers("bat_a01");
	if (.@chk_bat_a01 < 1) {
		set $@TierraBG1,0; set $@TierraBG1_Victory, 0;
		if( $@TierraBG1_id1 ) { bg_destroy $@TierraBG1_id1; set $@TierraBG1_id1, 0; }
		if( $@TierraBG1_id2 ) { bg_destroy $@TierraBG1_id2; set $@TierraBG1_id2, 0; }
		donpcevent "start#bat_a01::OnReadyCheck";
	}
	end;
}

bat_a01,352,342,0	script	#bat_a01_quest_a	-1,5,5,{
	end;

OnTouch:
	if (checkquest(2069) < 0)
		setquest 2069;
	end;
}

bat_a01,353,52,0	script	#bat_a01_quest_b	-1,5,5,{
	end;

OnTouch:
	if (checkquest(2069) < 0)
		setquest 2069;
	end;
}

bat_room,57,220,0	warp	back_bgrooma01a	1,1,bat_room,154,150
bat_room,57,211,0	warp	back_bgrooma01b	1,1,bat_room,154,150

// Tierra Gorge Battleground Engine
//============================================================
bat_a01,15,15,3	script	start#bat_a01	844,{
OnInit:
	mapwarp "bat_a01","bat_room",154,150;
	end;

OnEnable:
	donpcevent "OBJ#bat_a01_a::Onkill";
	donpcevent "OBJ#bat_a01_a::OnEnable";
	donpcevent "OBJ#bat_a01_b::Onkill";
	donpcevent "OBJ#bat_a01_b::OnEnable";
	donpcevent "barricade#bat_a01_a::Onkill";
	donpcevent "barricade#bat_a01_a::OnEnable";
	donpcevent "barricade#bat_a01_b::Onkill";
	donpcevent "barricade#bat_a01_b::OnEnable";
	donpcevent "OBJ#bat_a01_n::Onkill";
	donpcevent "OBJ#bat_a01_n::OnEnable";
	donpcevent "NOBJ_mob#bat_a01_a::Onkill";
	donpcevent "NOBJ_mob#bat_a01_b::Onkill";
	donpcevent "Battle Therapist#a01_a::OnEnable";
	donpcevent "Battle Therapist#a01_b::OnEnable";
	donpcevent "countdown#bat_a01::OnEnable";
	disablenpc "Guillaume Blacksmith#a01";
	disablenpc "Croix Blacksmith#bat_a01";
	disablenpc "Guillaume Vintenar#a01_a";
	disablenpc "Croix Vintenar#a01_b";
	end;

OnGuillaumeQuit:
OnCroixQuit:
	bg_leave;
	end;

OnReadyCheck:
	if( $@TierraBG1 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Lieutenant Kalos");
	set .@Croix, getwaitingroomstate(0,"Lieutenant Eyor");
	if( !.@Guillaume && !.@Croix ) {
		donpcevent "#bat_a01_timer::OnStop";
		end;
	}
	else if( .@Guillaume < 10 || .@Croix < 10 )
		end;
	set $@TierraBG1,1;
	donpcevent "Lieutenant Kalos::OnEnterBG";
	donpcevent "Lieutenant Eyor::OnEnterBG";
	donpcevent "start#bat_a01::onEnable";
	bg_warp $@TierraBG1_id1,"bat_a01",352,342;
	bg_warp $@TierraBG1_id2,"bat_a01",353,52;
	initnpctimer;
	end;

OnTimer10000:
	stopnpctimer;
	donpcevent "#bat_a01_timer::OnEnable";
	end;
}

bat_a01,15,16,3	script	OBJ#bat_a01_a	844,{
OnEnable:
	bg_monster $@TierraBG1_id1,"bat_a01",177,345,"Dépôt de Nourriture",1909,"OBJ#bat_a01_a::OnMyMobDead";
	end;

Onkill:
	killmonster "bat_a01","OBJ#bat_a01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","OBJ#bat_a01_a::OnMyMobDead") < 1) {
		donpcevent "Battle Therapist#a01_a::OnStop";
		donpcevent "Battle Therapist#a01_b::OnStop";
		set $@TierraBG1_Victory, 2;
		enablenpc "Guillaume Vintenar#a01_a";
		enablenpc "Croix Vintenar#a01_b";
		mapannounce "bat_a01", "Aide de Camp de Croix Swandery: Nous avons détruit les réserves de nourriture de Guillaume. Hourra, braves guerriers!",bc_map,"0xFFCE00";
		bg_warp $@TierraBG1_id1,"bat_a01",50,374;
		bg_warp $@TierraBG1_id2,"bat_a01",42,16;
	}
	end;
}

bat_a01,15,17,3	script	OBJ#bat_a01_b	844,{
OnEnable:
	bg_monster $@TierraBG1_id2,"bat_a01",167,50,"Réserve de Nourriture",1910,"OBJ#bat_a01_b::OnMyMobDead";
	end;

Onkill:
	killmonster "bat_a01","OBJ#bat_a01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","OBJ#bat_a01_b::OnMyMobDead") < 1) {
		donpcevent "Battle Therapist#a01_a::OnStop";
		donpcevent "Battle Therapist#a01_b::OnStop";
		set $@TierraBG1_Victory, 1;
		enablenpc "Guillaume Vintenar#a01_a";
		enablenpc "Croix Vintenar#a01_b";
		mapannounce "bat_a01", "Aide de Camp de Guillaume Axl Rose: Nous avons détruit les réserves de nourriture de Croix. Hourra, braves guerriers!",bc_map,"0xFFCE00";
		bg_warp $@TierraBG1_id1,"bat_a01",50,374;
		bg_warp $@TierraBG1_id2,"bat_a01",42,16;
	}
	end;
}

bat_a01,15,18,3	script	barricade#bat_a01_a	844,{
OnEnable:
	for( set .@i,185; .@i < 202; set .@i,.@i+1 )
		bg_monster $@TierraBG1_id1,"bat_a01",.@i,266,"Barricade",1906,"barricade#bat_a01_a::OnMyMobDead";
	setwall "bat_a01",186,266,16,6,1,"bat_a01_c1";
	end;

Onkill:
	killmonster "bat_a01","barricade#bat_a01_a::OnMyMobDead";
	delwall "bat_a01_c1";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","barricade#bat_a01_a::OnMyMobDead") < 17) {
		killmonster "bat_a01","barricade#bat_a01_a::OnMyMobDead";
		delwall "bat_a01_c1";
		enablenpc "Guillaume Blacksmith#a01";
		mapannounce "bat_a01", "Aide de Camp de Guillaume Axl Rose: La baricade de la valée vient d'être détruite! Mais où est donc le forgeron? Il nous faut la reconstruire!",bc_map,"0xFFCE00";
	}
	end;
}

bat_a01,15,19,3	script	barricade#bat_a01_b	844,{
OnEnable:
	for( set .@i,169; .@i < 186; set .@i,.@i+1 )
		bg_monster $@TierraBG1_id2,"bat_a01",.@i,129,"Barricade",1906,"barricade#bat_a01_b::OnMyMobDead";
	setwall "bat_a01",170,129,16,6,1,"bat_a01_g1";
	end;

Onkill:
	killmonster "bat_a01","barricade#bat_a01_b::OnMyMobDead";
	delwall "bat_a01_g1";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","barricade#bat_a01_b::OnMyMobDead") < 17) {
		killmonster "bat_a01","barricade#bat_a01_b::OnMyMobDead";
		delwall "bat_a01_g1";
		enablenpc "Croix Blacksmith#bat_a01";
		mapannounce "bat_a01", "Aide de Camp de Croix Swandery: La baricade de la valée vient d'être détruite! Mais où est donc le forgeron? Il nous faut la reconstruire!",bc_map,"0xFFCE00";
	}
	end;
}

bat_a01,15,20,3	script	OBJ#bat_a01_n	844,{
OnEnable:
	monster "bat_a01",273,203,"Drapeau de Neutralité",1911,1,"OBJ#bat_a01_n::OnMyMobDead";
	end;

Onkill:
	killmonster "bat_a01","OBJ#bat_a01_n::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","OBJ#bat_a01_n::OnMyMobDead") < 17) {
		bg_team_setxy getcharid(4),56,212;
		if (getcharid(4) == $@TierraBG1_id1) {
			donpcevent "NOBJ_mob#bat_a01_a::OnEnable";
			mapannounce "bat_a01", "Guillaume a capturé une Bannière de Neutralité et gagne l'avantage.",bc_map,"0xFFCE00";
		}
		else {
			donpcevent "NOBJ_mob#bat_a01_b::OnEnable";
			mapannounce "bat_a01", "Croix a capturé une Bannière de Neutralité et gagne l'avantage.",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a01,15,21,3	script	NOBJ_mob#bat_a01_a	844,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a01_b::Onkill";
	bg_monster $@TierraBG1_id1,"bat_a01",272,204,"Gardien de Guillaume",1949,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@TierraBG1_id1,"bat_a01",272,213,"Gardien de Guillaume",1949,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@TierraBG1_id1,"bat_a01",273,197,"Gardien de Guillaume",1950,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	end;

Onkill:
	killmonster "bat_a01","NOBJ_mob#bat_a01_a::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a01,15,22,3	script	NOBJ_mob#bat_a01_b	844,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a01_a::Onkill";
	bg_monster $@TierraBG1_id2,"bat_a01",272,204,"Gardien de Croix",1949,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@TierraBG1_id2,"bat_a01",272,213,"Gardien de Croix",1949,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@TierraBG1_id2,"bat_a01",273,197,"Gardien de Croix",1950,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	end;

Onkill:
	killmonster "bat_a01","NOBJ_mob#bat_a01_b::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a01,185,270,1	script	Forgeron de Guillaume::Guillaume Blacksmith#a01	851,{
	if (getcharid(4) == $@TierraBG1_id1) {
		mes "[Forgeron de Guillaume]";
		mes "Nous sommes dans un état d'urgence! La barricade vient d'être détruite!";
		mes "Nous pouvons la réparer, mais il nous faut au plus vite ^3131FF50 Pierres, 3 Tendons d'Ours, 500 Fragments de Métal, 30 Eluniums Bruts et 100 Ligots d'Or.^000000 Nous avons déjà tout cela, à l'exception de 50 Pierres!";
		next;
		switch(select("La réparer.:La laisser telle qu'elle.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Forgeron de Guillaume]";
				mes "Vous avez assez de pierres! Bien, réparons-la!";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Forgeron de Guillaume]";
				mes "Combinez l'Or et les Pierres! Attention au dosage!";
				next;
				mes "[Forgeron de Guillaume]";
				mes "Combinez l'Or et les Pierres! Attention au dosage! Formez la barricade!";
				next;
				mes "[Forgeron de Guillaume]";
				mes "Combinez l'Or et les Pierres! Attention au dosage! Formez la barricade! Renforcez-la avec l'Elunium Brut!";
				next;
				mes "[Forgeron de Guillaume]";
				mes "Combinez l'Or et les Pierres! Attention au dosage! Formez la barricade! Renforcez-la avec l'Elunium Brut! Utilisez les Fragments de Métal!";
				next;
				mes "[Forgeron de Guillaume]";
				mes "Combinez l'Or et les Pierres! Attention au dosage! Formez la barricade! Renforcez-la avec l'Elunium Brut! Utilisez les Fragments de Métal! Brêlez ça avec les Tendons d'Ours";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Forgeron de Guillaume]";
				mes "C'est fait! Nous pouvons enfin respirer.";
				delitem 7049,50; //Stone
				donpcevent "barricade#bat_a01_a::OnEnable";
				close2;
				disablenpc "Guillaume Blacksmith#a01";
				end;
			}
			else {
				mes "[Forgeron de Guillaume]";
				mes "Vous n'avez pas assez de pierres!";
				next;
				mes "[Forgeron de Guillaume]";
				mes "^3131FFIl nous faut 50 pierres.^000000";
				mes "Dépêchez-vous, nous sommes débordés!";
				close;
			}
		case 2:
			mes "[Forgeron de Guillaume]";
			mes "Les ennemis arrivent! Évacuons cet endroit!";
			close;
		}
	}
	else {
		mes "[Forgeron de Guillaume]";
		mes "Les ennemis arrivent!";
		close;
	}

OnInit:
	disablenpc "Guillaume Blacksmith#a01";
	end;
}

bat_a01,170,121,5	script	Forgeron de Croix::Croix Blacksmith#bat_a01	851,{
	if (getcharid(4) == $@TierraBG1_id2) {
		mes "[Forgeron de Croix]";
		mes "Nous sommes dans un état d'urgence! La barricade vient d'être détruite!";
		mes "Nous pouvons la réparer, mais il nous faut au plus vite ^3131FF50 Pierres, 3 Tendons d'Ours, 500 Fragments de Métal, 30 Eluniums Bruts et 100 Ligots d'Or.^000000 Nous avons déjà tout cela, à l'exception de 50 Pierres!";
		next;
		switch(select("La réparer.:La laisser telle qu'elle.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Forgeron de Croix]";
				mes "Vous avez assez de pierres! Bien, réparons-la!";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Forgeron de Croix]";
				mes "Combinez l'Or et les Pierres! Attention au dosage!";
				next;
				mes "[Forgeron de Croix]";
				mes "Combinez l'Or et les Pierres! Attention au dosage! Formez la barricade!";
				next;
				mes "[Forgeron de Croix]";
				mes "Combinez l'Or et les Pierres! Attention au dosage! Formez la barricade! Renforcez-la avec l'Elunium Brut!";
				next;
				mes "[Forgeron de Croix]";
				mes "Combinez l'Or et les Pierres! Attention au dosage! Formez la barricade! Renforcez-la avec l'Elunium Brut! Utilisez les Fragments de Métal!";
				next;
				mes "[Forgeron de Croix]";
				mes "Combinez l'Or et les Pierres! Attention au dosage! Formez la barricade! Renforcez-la avec l'Elunium Brut! Utilisez les Fragments de Métal! Brêlez ça avec les Tendons d'Ours";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Forgeron de Croix]";
				mes "C'est fait! Nous pouvons enfin respirer.";
				delitem 7049,50; //Stone
				donpcevent "barricade#bat_a01_b::OnEnable";
				close2;
				disablenpc "Croix Blacksmith#bat_a01";
				end;
			}
			else {
				mes "[Forgeron de Croix]";
				mes "Vous n'avez pas assez de pierres!";
				next;
				mes "[Forgeron de Croix]";
				mes "^3131FFIl nous faut 50 pierres.^000000";
				mes "Dépêchez-vous, nous sommes débordés!";
				close;
			}
		case 2:
			mes "[Forgeron de Croix]";
			mes "Les ennemis arrivent! Évacuons cet endroit!";
			close;
		}
	}
	else {
		mes "[Forgeron de Croix]";
		mes "Les ennemis arrivent!";
		close;
	}

OnInit:
	disablenpc "Croix Blacksmith#bat_a01";
	end;
}

bat_a01,53,377,3	script	Rebouteux::Battle Therapist#a01_a	95,{
	specialeffect2 EF_HEAL;
	mes "[Rebouteux]";
	mes "Serre bien les dents "+ ( sex ? "mon p'tit gars" : "ma p'tite dame" ) +", et prends une bonne bouffée d'air. Ça risque de faire un peu mal...";
	close;
	end;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Battle Therapist#a01_a";
	end;

Onstop:
	disablenpc "bat_a01_rp1_a_warp";
	disablenpc "Battle Therapist#a01_a";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a01_rp1_a_warp";
	end;

OnTimer26000:
	disablenpc "bat_a01_rp1_a_warp";
	end;

OnTimer26500:
	donpcevent "Battle Therapist#a01_a::OnEnable";
	end;
}

bat_a01,51,375,0	script	bat_a01_rp1_a_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a01_rp1_a_warp";
	end;

OnTouch_:
	percentheal 100,100;
	warp "bat_a01",352,342;
	end;
}

bat_a01,45,19,3	script	Rebouteux::Battle Therapist#a01_b	95,{
	specialeffect2 EF_HEAL;
	mes "[Rebouteux]";
	mes "Serre bien les dents "+ ( sex ? "mon p'tit gars" : "ma p'tite dame" ) +", et prends une bonne bouffée d'air. Ça risque de faire un peu mal...";
	close;
	end;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Battle Therapist#a01_b";
	end;

Onstop:
	disablenpc "bat_a01_rp1_b_warp";
	disablenpc "Battle Therapist#a01_b";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a01_rp1_b_warp";
	end;

OnTimer26000:
	disablenpc "bat_a01_rp1_b_warp";
	end;

OnTimer26500:
	donpcevent "Battle Therapist#a01_b::OnEnable";
	end;
}

bat_a01,43,17,0	script	bat_a01_rp1_b_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a01_rp1_b_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a01",353,52;
	end;
}

bat_a01,60,216,3	script	Fantôme::Valley Ghost#bat_a01_n	950,{
	specialeffect2 EF_HEAL;
	mes "[Fantôme de la Vallée]";
	mes "Bouh...Bouh...";
	close;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a01_rp1_n_warp";
	end;

OnTimer26000:
	disablenpc "bat_a01_rp1_n_warp";
	end;

OnTimer26500:
	donpcevent "Valley Ghost#bat_a01_n::OnEnable";
	end;
}

bat_a01,55,213,0	script	bat_a01_rp1_n_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a01_rp1_n_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a01",301,209;
	end;
}

bat_a01,194,267,0	script	barri_warp_up#bat_a01_a	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG1_id1)
		warp "bat_a01",194,261;
	end;
}

bat_a01,194,265,0	script	barri_warp_down#bat_a01a	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG1_id1) 
		warp "bat_a01",194,270;
	end;
}

bat_a01,177,130,0	script	barri_warp_up#bat_a01_b	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG1_id2)
		warp "bat_a01",178,125;
	end;
}

bat_a01,177,128,0	script	barri_warp_down#bat_a01b	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG1_id2)
		warp "bat_a01",178,134;
	end;
}

bat_a01,352,342,0	script	A_CODE#bat_a01	-1,5,5,{
OnTouch:
	set .@chk_bfquest,checkquest(2069);
	if ((.@chk_bfquest == 0) || (.@chk_bfquest == 1))
		setquest 2069;
	end;
}

bat_a01,353,52,0	script	B_CODE#bat_a01	-1,5,5,{
OnTouch:
	set .@chk_bfquest,checkquest(2069);
	if ((.@chk_bfquest == 0) || (.@chk_bfquest == 1))
		setquest 2069;
	end;
}

bat_a01,169,227,0	script	underladd#bat_a01_1	45,1,1,{
OnTouch_:
	warp "bat_a01",178,228;
	end;
}

bat_a01,208,164,0	script	underladd#bat_a01_2	45,1,1,{
OnTouch_:
	warp "bat_a01",200,171;
	end;
}

bat_a01,171,309,3	script	Camp de Guillaume::Guillaume Camp#flag1	973,{ end; }
bat_a01,149,310,3	script	Camp de Guillaume::Guillaume Camp#flag2	973,{ end; }
bat_a01,119,336,3	script	Camp de Guillaume::Guillaume Camp#flag3	973,{ end; }
bat_a01,118,357,3	script	Camp de Guillaume::Guillaume Camp#flag4	973,{ end; }
bat_a01,150,380,3	script	Camp de Guillaume::Guillaume Camp#flag5	973,{ end; }
bat_a01,173,380,3	script	Camp de Guillaume::Guillaume Camp#flag6	973,{ end; }
bat_a01,210,344,3	script	Camp de Guillaume::Guillaume Camp#flag7	973,{ end; }
bat_a01,350,325,3	script	Camp de Guillaume::Guillaume Camp#flag8	973,{ end; }
bat_a01,358,325,3	script	Camp de Guillaume::Guillaume Camp#flag9	973,{ end; }

bat_a01,138,12,3	script	Camp de Croix::Croix Camp#flag1	974,{ end; }
bat_a01,108,36,3	script	Camp de Croix::Croix Camp#flag2	974,{ end; }
bat_a01,108,63,3	script	Camp de Croix::Croix Camp#flag3	974,{ end; }
bat_a01,136,87,3	script	Camp de Croix::Croix Camp#flag4	974,{ end; }
bat_a01,167,86,3	script	Camp de Croix::Croix Camp#flag5	974,{ end; }
bat_a01,199,49,3	script	Camp de Croix::Croix Camp#flag6	974,{ end; }
bat_a01,168,16,3	script	Camp de Croix::Croix Camp#flag7	974,{ end; }
bat_a01,357,74,3	script	Camp de Croix::Croix Camp#flag8	974,{ end; }
bat_a01,348,74,3	script	Camp de Croix::Croix Camp#flag9	974,{ end; }

bat_a01,53,377,3	script	Aide de Camp (Guillaume)::Guillaume Vintenar#a01_a	419,{
	if (getcharid(4) == $@TierraBG1_id1) {
		if ($@TierraBG1_Victory == 1) {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Guillaume le Très Saint! Festoyons à notre glorieuse victoire!" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7828,3; //BF_Badge1
			}
			else {
				mes "[Axl Rose]";
				mes "Guillaume le Très Saint! Festoyons à notre glorieuse victoire!" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
		else {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 0) {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts. Prenez ceci en récompense.";
				close2;
				getitem 7828,1; //BF_Badge1
			}
			else {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts. Prenez ceci en récompense.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
	}
	else {
		if ($@TierraBG1_Victory == 2) {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 0) {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts. Prenez ceci en récompense.";
				close2;
				getitem 7828,1; //BF_Badge1
			}
			else {
				mes "[Axl Rose]";
				mes "Nous avons failli, mais vous avez su faire preuve de bravoure sur le champ de bataille. Voici de quoi récompenser votre dévotion envers Guillaume Marollo! Que cette défaite soit pour nous une leçon et nous rende plus forts. Prenez ceci en récompense.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
		else {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Guillaume le Très Saint! Festoyons à notre glorieuse victoire!" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7828,3; //BF_Badge1
			}
			else {
				mes "[Axl Rose]";
				mes "Guillaume le Très Saint! Festoyons à notre glorieuse victoire!" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Guillaume Vintenar#a01_a";
	end;
}

bat_a01,45,19,3	script	Aide de Camp (Croix)::Croix Vintenar#a01_b	415,{
	if (getcharid(4) == $@TierraBG1_id2) {
		if ($@TierraBG1_Victory == 2) {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Swandery]";
				mes "Croix le Très Saint! Festoyons à notre glorieuse victoire!" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7828,3; //BF_Badge1
			}
			else {
				mes "[Swandery]";
				mes "Croix le Très Saint! Festoyons à notre glorieuse victoire!" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
		else {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 0) {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7828,1; //BF_Badge1
			}
			else {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
	}
	else {
		if ($@TierraBG1_Victory == 1) {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 0) {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7828,1; //BF_Badge1
			}
			else {
				mes "[Swandery]";
				mes "Point de larmes ni de regrets, " + strcharinfo(0) + ". Nous avons failli, mais nous avons aussi su faire preuve de bravoure. Voici un présent royal de Croix. Puisse-t-il vous permettre de ne jamais oublier cette bataille. Que la prochaine soit celle de notre triomphe!";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
		else {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Swandery]";
				mes "Croix le Très Saint! Festoyons à notre glorieuse victoire!" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7828,3; //BF_Badge1
			}
			else {
				mes "[Swandery]";
				mes "Croix le Très Saint! Festoyons à notre glorieuse victoire!" + strcharinfo(0) + ", voici un symbole de notre triomphe.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Croix Vintenar#a01_b";
	end;
}

bat_a01,1,5,3	script	countdown#bat_a01	844,{
OnEnable:
	initnpctimer;
	end;

Onstop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_a01", "Aide de Camp de Guillaume Axl Rose : Chargez, fiers soldats, et réduisez les réserves de nourriture de Croix en cendres!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_a01", "Aide de Camp de Croix Swandery : Seigneurs du Walhalla! Donnez-nous la force de brandir le fer avec foi et courage!",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_a01", "Marollo VII : Guillaume Marollo, Croix Marollo! Partisans de Maroll!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_a01", "Marollo VII : Les deux camps ont fait preuve de vaillance, et aucun n'a failli! L'avenir de Maroll en ira de même!",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_a01", "Marollo VII : Nous ferions mieux de mettre fin à cette bataille. Appelons ça un match nul.",bc_map,"0x99CC00";
	end;

OnTimer1822000:
	mapannounce "bat_a01", "Marollo VII : Conservez votre ardeur et votre foi, braves guerriers, nous règlerons ce différent dans les Gorges de Tierra.",bc_map,"0x99CC00";
	end;

OnTimer1825000:
	mapannounce "bat_a01", "Axl Rose, Swandery : Oui, Sire!",bc_map,"0x99CC00";
	end;

OnTimer1830000:
	mapwarp "bat_a01","bat_room",154,150;
	donpcevent "countdown#bat_a01::Onstop";
	end;
}

/*
bat_a01,351,75,3	script	Soldat de Croix::Croix Camp Soldier#bat_a01_guide	934,{
	mes "Loading...";
	close;
}

bat_a01,356,326,3	script	Soldat de Guillaume::Guillaume Camp Soldier#bat_a01_guide	934,{
	mes "Loading...";
	close;
}
*/

/*
bat_a01,1,1,3	script	Release all#a01	81,{
	input .@input,0,2000;
	if (.@input == 0) {
		mes "Cancelled.";
		close;
	}
	else if (.@input == 1854) {
		mes "May I help you?";
		next;
		switch(select("Release all.:Cancel.")) {
		case 1:
			mes "Bye.";
			close2;
			mapwarp "bat_a01","bat_room",154,150;
			end;
		case 2:
			mes "Cancelled.";
		}
	}
}
*/

