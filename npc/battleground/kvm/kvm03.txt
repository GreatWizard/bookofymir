//===== eAthena Script ======================================= 
// BattleGround System - KvM 60-80
//===== By: ================================================== 
//= ???, L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: ========================================= 
//= [AEGIS Conversion]
//= Kreiger von Midgard Battleground for levels 60 to 80
//= - Winning Team: 2 points
//= - Losing Team: 1 point
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Updated using official Aegis files. [L0ne_W0lf]
//= 1.2 Upated some announces and dialogs from iRO.
//=     Changed how the scoreboard works slightly.
//=     Removed the areapercentheals, and minor things.
//= 1.3 Fixed wrong names for disablenpc/enablenpc. [Ai4rei]
//============================================================

// Waiting Room NPCs
//============================================================
bat_room,225,226,5	script	KVM Waiting Room#a3::KvM03R_Guillaume	418,{
	end;

OnInit:
	waitingroom "Combat à 5 Joueurs",5,"KvM03_BG::OnGuillaumeJoin",1;
	end;
	
OnEnterBG:
	set $@KvM03BG_id1, waitingroom2bg("bat_c03",52,129,"KvM03_BG::OnGuillaumeQuit","KvM03_BG::OnGuillaumeDie");
	end;
}

bat_room,225,220,0	warp	#kvm701	2,2,bat_room,154,150

bat_room,225,205,3	script	KVM Waiting Room#b3::KvM03R_Croix	414,{
	end;

OnInit:
	waitingroom "Combat à 5 Joueurs",5,"KvM03_BG::OnCroixJoin",1;
	end;

OnEnterBG:
	set $@KvM03BG_id2, waitingroom2bg("bat_c03",147,55,"KvM03_BG::OnCroixQuit","KvM03_BG::OnCroixDie");
	end;
}

bat_room,225,211,0	warp	#kvm702	2,2,bat_room,154,150

// Starting Line
//============================================================
-	script	KVM03::CellEffect3	-1,{
	end;

OnKvM03One: 
	misceffect EF_CHANGECOLD;
	end;

OnKvM03Two: 
	misceffect EF_CHANGEPOISON;
	end;
}

bat_c03,54,124,3	duplicate(CellEffect3)	#RedcellA1-3	139
bat_c03,55,124,3	duplicate(CellEffect3)	#RedcellA2-3	139
bat_c03,56,124,3	duplicate(CellEffect3)	#RedcellA3-3	139
bat_c03,57,124,3	duplicate(CellEffect3)	#RedcellA4-3	139
bat_c03,57,125,3	duplicate(CellEffect3)	#RedcellA5-3	139
bat_c03,57,126,3	duplicate(CellEffect3)	#RedcellA6-3	139
bat_c03,57,127,3	duplicate(CellEffect3)	#RedcellA7-3	139

bat_c03,145,59,3	duplicate(CellEffect3)	#RedcellB1-3	139
bat_c03,144,59,3	duplicate(CellEffect3)	#RedcellB2-3	139
bat_c03,143,59,3	duplicate(CellEffect3)	#RedcellB3-3	139
bat_c03,142,59,3	duplicate(CellEffect3)	#RedcellB4-3	139
bat_c03,142,56,3	duplicate(CellEffect3)	#RedcellB5-3	139
bat_c03,142,57,3	duplicate(CellEffect3)	#RedcellB6-3	139
bat_c03,142,58,3	duplicate(CellEffect3)	#RedcellB7-3	139

bat_c03,54,128,3	script	#A_camp_start03	139,4,4,{
	end;

OnInit:
	disablenpc "#A_camp_start03";
	end;

OnEnable:
	enablenpc "#A_camp_start03";
	end;

OnDisable:
	disablenpc "#A_camp_start03";
	end;

OnTouch:
	set Bat_Team,1;
	setquest 6025;
	end;
}

bat_c03,146,56,3	script	#B_camp_start03	139,4,4,{
	end;

OnInit:
	disablenpc "#B_camp_start03";
	end;

OnEnable:
	enablenpc "#B_camp_start03";
	end;

OnDisable:
	disablenpc "#B_camp_start03";
	end;

OnTouch:
	set Bat_Team,2;
	setquest 6025;
	end;
}

// Battleground Engine
//============================================================
-	script	KvM03_BG	-1,{
	end;

OnInit:
	setwall "bat_c03",54,122,6,7,0,"batc03wall_a";
	setwall "bat_c03",55,122,5,7,0,"batc03wall_b";
	setwall "bat_c03",140,56,6,7,0,"batc03wall_c";
	setwall "bat_c03",140,57,5,7,0,"batc03wall_d";
	disablenpc "KVM Officer#KVM03A";
	disablenpc "KVM Officer#KVM03B";
	end;

OnGuillaumeJoin:
OnCroixJoin:
	donpcevent "KvM03_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnGuillaumeDie:
	if( $@KvM03BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		bg_updatescore "bat_c03",.Guillaume_Count,.Croix_Count;
		if( .Guillaume_Count < 1 ) donpcevent "KvM03_BG::OnCroixWin";
		else {
			mapannounce "bat_c03", "Score des partisans de Guillaume: "+.Guillaume_Count+". Score des partisans de Croix: "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnCroixQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnCroixDie:
	if( $@KvM03BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		bg_updatescore "bat_c03",.Guillaume_Count,.Croix_Count;
		if( .Croix_Count < 1 ) donpcevent "KvM03_BG::OnGuillaumeWin";
		else {
			mapannounce "bat_c03", "Score des partisans de Guillaume: "+.Guillaume_Count+". Score des partisans de Croix: "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnReadyCheck:
	if( $@KvM03BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM03R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM03R_Croix");

	if( .@Guillaume < 5 || .@Croix < 5 )
		end;

	set $@KvM03BG, 1; // Starting
	donpcevent "KvM03R_Croix::OnEnterBG";
	donpcevent "KvM03R_Guillaume::OnEnterBG";
	donpcevent "KvM03_BG::OnStart";
	end;

OnStart:
	disablenpc "KVM Officer#KVM03A";
	disablenpc "KVM Officer#KVM03B";
	set $@KvM01BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM03BG_id1,"bat_c03",53,128;
	bg_warp $@KvM03BG_id2,"bat_c03",146,55;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c03", "Kreiger von Midgard commencera dans 1 minute!",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c03", "Les combats de Kreiger von Midgard sont limités à 5 minutes.",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c03", "Il est temps de vous préparer pour la bataille! Priez pour la victoire!",bc_map,"0x00ff00";
	donpcevent "#A_camp_start03::OnEnable";
	donpcevent "#B_camp_start03::OnEnable";
	end;

OnTimer13000:
	donpcevent "#A_camp_start03::OnDisable";
	donpcevent "#B_camp_start03::OnDisable";
	end;

OnTimer30000:
	mapannounce "bat_c03", "30 secondes restantes avant le début de la bataille de Kreiger von Midgard!",bc_map,"0x00ff00";
	end;

OnTimer45000:
	mapannounce "bat_c03", "15 secondes restantes avant le début de la bataille de Kreiger von Midgard!",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer50000:
	mapannounce "bat_c03", "10 secondes restantes avant le début de la bataille de Kreiger von Midgard!",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer55000:
	mapannounce "bat_c03", "5 secondes restantes avant le début de la bataille de Kreiger von Midgard!",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer59000:
	mapannounce "bat_c03", "Le début de la bataille de Kreiger von Midgard vient de commencer!",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer61000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM03BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM03BG_id2, 0);
	if( .Guillaume_Count < 5 || .Croix_Count < 5 )
	{
		set $@KvM03BG_Victory, 3;
		set $@KvM03BG, 3;
		mapannounce "bat_c03","Il n'y a pas assez de participants pour commencer la bataille!",1,0xC0C0C0;
		stopnpctimer;
		donpcevent "KvM03_BG::OnStop";
		end;
	}
	set $@KvM03BG, 2; // Playing
	bg_warp $@KvM03BG_id1,"bat_c03",62,119;
	bg_warp $@KvM03BG_id2,"bat_c03",137,64;
	end;

OnTimer300000:
	mapannounce "bat_c03", "1 minute restante avant la fin de la bataille de Kreiger von Midgard!",bc_map,"0x00ff00";
	end;

OnTimer330000:
	mapannounce "bat_c03", "30 secondes restantes avant la fin de la bataille de Kreiger von Midgard!",bc_map,"0x00ff00";
	end;

OnTimer345000:
	mapannounce "bat_c03", "15 secondes restantes avant la fin de la bataille de Kreiger von Midgard!",bc_map,"0x00ff00";
	end;

OnTimer350000:
	mapannounce "bat_c03", "10 secondes restantes avant la fin de la bataille de Kreiger von Midgard!",bc_map,"0x00ff00";
	end;

OnTimer355000:
	mapannounce "bat_c03", "5 secondes restantes avant la fin de la bataille de Kreiger von Midgard!",bc_map,"0x00ff00";
	end;

OnTimer360000:
	mapannounce "bat_c03", "KVM has ended.",bc_map,"0x00ff00";
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM03_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM03_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM03BG, 3;
		set $@KvM03BG_Victory, 3;
		mapannounce "bat_c03", "Score des partisans de Guillaume: "+.Guillaume_Count+". Score des partisans de Croix: "+.Croix_Count+". C'est un match nul!",bc_map,"0x00ff00";
		donpcevent "KvM03_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM03BG, 3;
	set $@KvM03BG_Victory, 1;
	mapannounce "bat_c03", "Guillaume remporte la victoire! Félicitation à ses partisans! Tout le monde va être reconduit vers le point de départ.",bc_map,"0x00ff00";
	donpcevent "KvM03_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM03BG, 3;
	set $@KvM03BG_Victory, 2;
	mapannounce "bat_c03", "Croix remporte la victoire! Félicitation à ses partisans! Tout le monde va être reconduit vers le point de départ.",bc_map,"0x00ff00";
	donpcevent "KvM03_BG::OnStop";
	end;

OnStop:
	stopnpctimer;
	enablenpc "KVM Officer#KVM03A";
	enablenpc "KVM Officer#KVM03B";
	// Warp Teams
	bg_warp $@KvM03BG_id1,"bat_c03",53,128;
	bg_warp $@KvM03BG_id2,"bat_c03",146,55;
	donpcevent "KvM03_BG_Out::OnBegin";
	end;

OnReset:
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set $@KvM03BG_Victory, 0;
	if( $@KvM03BG_id1 ) { bg_destroy $@KvM03BG_id1; set $@KvM03BG_id1, 0; }
	if( $@KvM03BG_id2 ) { bg_destroy $@KvM03BG_id2; set $@KvM03BG_id2, 0; }
	disablenpc "KVM Officer#KVM03A";
	disablenpc "KVM Officer#KVM03B";
	mapwarp "bat_c03","bat_room",154,150;
	maprespawnguildid "bat_c03",0,3; // Just in case someone else
	bg_updatescore "bat_c03",5,5;
	set $@KvM03BG, 0;
	donpcevent "KvM03_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM03_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c03", "Veuillez aller trouver l'Officier pour obtenir des Points de Kreiger von Midgard.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c03", "L'Officier pourra vous donner vos récompenses pendant 30 secondes. Il disparaîtra ensuite.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c03", "Faites bien attention, vous ne pourrez pas gagner de points si vous ne lui parlez pas.",bc_map,"0x00ff00";
	end;

OnTimer:55000:
	mapannounce "bat_c03", "Vous allez être reconduits à l'extérieur.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	donpcevent "KvM03_BG::OnReset";
	end;
}

// Battleground rewards
//============================================================
bat_c03,51,130,5	script	Officier::KVM Officer#KVM03A	419,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM03BG_Victory == Bat_Team )
		{ // Victory
			set kvm_point,kvm_point + 2;
			mes "[Officier de Kreiger von Midgard]";
			mes "Beau travail. Que la gloire de Kreiger von Midgard rejaillisse sur vous! Vous gagnez 2 points.";
			close2;
		}
		else
		{ // 
			set kvm_point,kvm_point + 1;
			mes "[Officier de Kreiger von Midgard]";
			mes "Je suis désolé. Vous aurez peut-être plus de chance la prochaine fois. Vous gagnez 1 point.";
			close2;
		}
		bg_leave;
		set Bat_Team,0;
		warp "bat_room",154,150;
		end;
	}
	end;
}

bat_c03,148,53,1	script	Officier::KVM Officer#KVM03B	415,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM03BG_Victory == Bat_Team )
		{ // Victory
			set kvm_point,kvm_point + 2;
			mes "[Officier de Kreiger von Midgard]";
			mes "Beau travail. Que la gloire de Kreiger von Midgard rejaillisse sur vous! Vous gagnez 2 points.";
			close2;
		}
		else
		{ // 
			set kvm_point,kvm_point + 1;
			mes "[Officier de Kreiger von Midgard]";
			mes "Je suis désolé. Vous aurez peut-être plus de chance la prochaine fois. Vous gagnez 1 point.";
			close2;
		}
		bg_leave;
		set Bat_Team,0;
		warp "bat_room",154,150;
		end;
	}
	end;
}
