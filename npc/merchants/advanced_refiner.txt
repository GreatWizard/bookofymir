//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script =======================================
//= Advanced Refiner
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= Eathena SVN
//===== Description: =========================================
//= [Aegis Conversion]
//= Refiner that uses Enriched ores to increase upgrade success.
//= After a conversation with Doddler, it's been established that
//= the advanced refiner works similar the the "Bubble Gum" item.
//= The success percentage is not "increased" however, if it fails
//= You get a second try. This tries twice at the same time,
//= effectively giving you a re-roll on your attempt.
//= - Dialog is only partly official to iRO.
//= - Uses the iRO position for this NPC.
//===== Additional Comments: =================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Fixed a weird carriage return. o_o [L0ne_W0lf]
//= 1.2 Optimizing refine method [Zephyrus]
//= 1.3 Typo fixes [Yommy]
//= 1.4 Removed unnecessary dialogs [Zephyrus]
//============================================================

payon,174,138,0	script	Suhnbi#cash	85,{
	mes "[Suhnbi]";
	mes "Bonjour. Mon nom est Suhnbi.";
	mes "Je suis forgeron, je peux raffiner tous types d'armes, armures et équipements. Quel équipement souhaiteriez-vous que je raffine?";
	next;
	
	setarray .@position$[1], "Tête","Corps","Main Gauche","Main Droite","Robe","Chaussures","Accessoire 1","Accessoire 2","Tête 2","Tête 3";
	set .@menu$,"";
	for( set .@i,1; .@i <= 10; set .@i,.@i+1 )
	{
		if( getequipisequiped(.@i) )
			set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";

		set .@menu$, .@menu$ + ":";
	}
	set .@part,select(.@menu$);

	if( !getequipisequiped(.@part) )
		close;

	if( !getequipisenableref(.@part) )
	{
		mes "[Suhnbi]";
		mes "Je ne peux pas travailler sur cet équipement...";
		close;
	}
	if( !getequipisidentify(.@part) )
	{
		mes "[Suhnbi]";
		mes "Vous devez d'abord identifier cet équipement.";
		close;
	}
	if( getequiprefinerycnt(.@part) >= 10 )
	{
		mes "[Suhnbi]";
		mes "Cet équipement est déjà à son maximum, je ne peux pas le raffiner plus.";
		close;
	}

	// Make sure you have the neccessary items and Zeny to refine your items
	// Determines chance of failure and verifies that you want to continue.
	switch( getequipweaponlv(.@part) )
	{
		case 1: callsub S_RefineValidate,1,7620,50,.@part; break;
		case 2: callsub S_RefineValidate,2,7620,200,.@part; break;
		case 3: callsub S_RefineValidate,3,7620,5000,.@part; break;
		case 4: callsub S_RefineValidate,4,7620,20000,.@part; break;
		default: callsub S_RefineValidate,0,7619,2000,.@part; break;
	}

	if( getequippercentrefinery(.@part) > rand(100) || getequippercentrefinery(.@part) > rand(100) )
	{
		mes "[Suhnbi]";
		mes "Cling! Clang! Clung!";
		SuccessRefItem .@part;
		next;
		Emotion e_no1;
		mes "[Suhnbi]";
		mes "HAHA, quel talent! On dirait que mes vieux os ne sont pas encore rouillés! Splendide... tout simplement splendide...";
		close;
	}
	else
	{
		mes "[Suhnbi]";
		mes "Cling! Clang! Clung!";
		FailedRefItem .@part;
		next;
		if (rand(5) == 1)
			Emotion e_cash;
		else 
			Emotion e_omg;
		mes "[Suhnbi]";
		mes "Aaahhh!! Oh non...!!";
		mes "He... Hem... Je suis désolé mais le raffinement a ^ff0000échoué^000000.";
		next;
		mes "[Suhnbi]";
		mes "J’ai vraiment honte de ce que j'ai fait... MAIS je vous avais prévenu qu'il y avait des risques!";
		close;
	}

S_RefineValidate:
	mes "[Suhnbi]";
	if (getarg(0))
		mes "Une arme de niveau " + getarg(0) + " ...";
	mes "Afin de raffiner cet équipement j'aurais besoin de minerais ^ff9999" + getitemname(getarg(1)) + "^000000 et de " + getarg(2) + " Zénys.";
	mes "Voulez-vous continuer?";
	next;
	if( select("Oui:Non") == 1 )
	{
		if( getequippercentrefinery(getarg(3)) < 100 )
		{
			mes "[Suhnbi]";
			mes "Hum... Attendez une minute! Cet équipement a déjà été raffiné un certain nombre de fois!";
			mes "Je dois vous prévenir, que si vous le raffinez ENCORE, il pourrait SE ^ff0000DETRUIRE^000000 durant la forge et devenir ^ff0000INUTILISABLE^000000!!";
			next;
			mes "[Suhnbi]";
			mes "Voulez-vous tout de même tenter de le raffiner? Si oui, je ne vous garantis pas le résultat...";
			next;
			if( select("Oui:Non") == 2 )
			{
				mes "[Suhnbi]";
				mes "Bon choix.";
				mes "Ah ça oui... un choix judicieux. Je serais terriblement embarrassé si je détruisais votre équipement par mégarde.";
				close;
			}
		}
		if( countitem(getarg(1)) > 0 && Zeny > getarg(2) )
		{
			delitem getarg(1), 1;
			set Zeny, Zeny - getarg(2);
			return;
		}
		else
		{
			mes "[Suhnbi]";
			mes "On dirait qu'il vous manque de l'argent ou bien des " + getitemname(getarg(1)) + "s...";
			mes "Allez en chercher. Je reste ici, de toutes façons...";
			close;
		}
	}
	else
	{
		mes "[Suhnbi]";
		mes "C'est vrai... Pas la peine de se presser.";
		mes "Prenez votre temps.";
		close;
	}
}
