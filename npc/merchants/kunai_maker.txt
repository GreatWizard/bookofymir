//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script =======================================
//= Kunai "Trader" @ que_ng
//===== By: ==================================================
//= eAthena dev team
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= eAthena 1.0
//===== Description: =========================================
//= NPC that trades you a few shurikens + ninja stones for 
//= elemental kunais.
//===== Additional Comments: =================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=	  id of the 2 required items plus the amount. Can trade
//= 	  up to 500 units (5,000 kunais) at once. [erKURITA]
//= 1.1 Officialized script [Playtester]
//= 1.2a Optimized/cleaned up a bit [ultramage]
//============================================================

que_ng,72,29,3	script	Kashin	83,{

	mes "[Kashin]";	
	if(BaseJob != Job_Ninja) {
		mes "Bonjour. Mon nom est Kashin du Mistral. Mon travail est de vendre et échanger des objets de l'Ombre...";
		next;
		mes "[Kashin]";
		mes "Mais vous n'avez pas l'air d'être un Ninja... Donc ça ne doit pas vraiment vous intéresser.";
		close;
	}
	mes "Bonjour. Mon nom est Kashin!";
	mes "Si vous êtes à court de Kunais, je suis la personne qu'il vous faut!";
	next;
	mes "[Kashin]";
	mes "Alors, besoin de quelque chose?";
	next;

	switch(select("Kunai Empoisonnés:Kunais de Glace:Kunais de Vent:Kunais de Terre:Kunais de Feu:Rien, merci.")) {
	//usage: callfunc "Kunai_Trade",itemreqid1,itemreqct1,itemreqid2,itemreqct2,itemidtrade;
	case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
	case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
	case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
	case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
	case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
	default:
		mes "[Kashin]";
		mes "Hum~ D'accord~";
		mes "Revenez quand vous voulez~";
		close;
	}
	close;
}

function	script	Kunai_Trade	{
	
	mes "[Kashin]";
	mes "Si vous me rapportez "+getarg(1)+" "+getitemname(getarg(0))+" et "+getarg(3)+" "+getitemname(getarg(2))+", je peux vous fournir un pack de 10 "+getitemname(getarg(4))+".";
	next;
	mes "[Kashin]";
	mes "Vous pouvez échanger jusqu'à 500 packs à la fois!";
	mes "Si vous souhaitez annuler l'échange, entrez 0.";
	next;
	input .@amount;

	mes "[Kashin]";
	if(.@amount < 1) {
		mes "Hum~ D'accord~";
		mes "Revenez quand vous voulez~";
		close;
	}
	if(.@amount > 500) {
		mes "Désolé, mais je vous ai dit que je ne pouvais pas en proposer plus de 500 unités à la fois.";
		close;
	}
	if(countitem(getarg(0)) < .@amount*getarg(1) || countitem(getarg(2)) < .@amount*getarg(3)) {
		mes "Désolé, mais il vous manque certains objets. Allez les chercher afin de poursuivre l'échange.";
		close;
 	}
	if(checkweight(getarg(4), .@amount*10) == 0) {
		mes "Désolé, mais vous portez déjà plus de 90% de votre charge maximale. Ce n'est pas bon pour votre dos...";
		close;
	}
	mes "Et voilà pour vous!";
	delitem getarg(0),getarg(1)*.@amount;
	delitem getarg(2),getarg(3)*.@amount;
	getitem getarg(4),10*.@amount;
	close;
}