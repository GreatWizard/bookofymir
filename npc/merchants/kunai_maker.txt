//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script =======================================
//= Kunai Merchant Kashin
//===== By: ==================================================
//= eAthena dev team
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= Trades a few shurikens + ninja stones for elemental kunai.
//===== Additional Comments: =================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=	  id of the 2 required items plus the amount. Can trade
//= 	  up to 500 units (5,000 kunais) at once. [erKURITA]
//= 1.1 Officialized script [Playtester]
//= 1.2a Optimized/cleaned up a bit [ultramage]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//============================================================

que_ng,72,29,3	script	Kashin	83,{
	if(BaseJob == Job_Ninja) {
		mes "[Kashin]";
		mes "Bonjour. Mon nom est Kashin!";
		mes "Si vous êtes à court de Kunais, je suis la personne qu'il vous faut!";
		mes "Alors, besoin de quelque chose?";
		next;
		switch(select("Kunai Empoisonnés:Kunais de Glace:Kunais de Vent:Kunais de Terre:Kunais de Feu:Rien, merci")) {
		case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
		case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
		case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
		case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
		case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
		case 6:
			mes "[Kashin]";
			mes "Eh bien merci d'avoir visité ma boutique. Revenez quand vous aurez besoin de Kunais. À bientôt~";
			close;
		}
	}
	mes "[Kashin]";
	mes "Bonjour. Mon nom est Kashin du Mistral. Mon travail est de vendre et échanger des objets de l'Ombre...";
	next;
	mes "[Kashin]";
	mes "Mais vous n'avez pas l'air d'être un Ninja... Donc ça ne doit pas vraiment vous intéresser.";
	close;
}

function	script	Kunai_Trade	{
	mes "[Kashin]";
	mes "Si vous me rapportez "+getarg(1)+" "+getitemname(getarg(0))+" et "+getarg(3)+" "+getitemname(getarg(2))+", je peux vous fournir un pack de 10 "+getitemname(getarg(4))+".";
	next;
	mes "[Kashin]";
	mes "Vous pouvez échanger jusqu'à 500 packs à la fois!";
	mes "Si vous souhaitez annuler l'échange, entrez 0.";
	next;
	input .@amount;
	if(.@amount < 1 || .@amount > 500) {
		mes "[Kashin]";
		mes "Veuillez entrer un nombre compris entre 1 et 500.";
		close;
	}
	if(countitem(getarg(0)) < .@amount*getarg(1) || countitem(getarg(2)) < .@amount*getarg(3)) {
		mes "[Kashin]";
		mes "Désolé, mais il vous manque certains objets. Allez les chercher afin de poursuivre l'échange.";
		close;
 	}
	if(checkweight(getarg(4), .@amount*10) == 0) {
		mes "[Kashin]";
		mes "Désolé, mais vous portez déjà plus de 90% de votre charge maximale. Ce n'est pas bon pour votre dos...";
		close;
	}
	mes "[Kashin]";
	mes "Et voilà pour vous!";
	mes "Je suis certain que vous serez très satisfait de ces Kunais!";
	delitem getarg(0),getarg(1)*.@amount;
	delitem getarg(2),getarg(3)*.@amount;
	getitem getarg(4),10*.@amount;
	close;
}
