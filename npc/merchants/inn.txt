//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Inn Npcs
//===== By: ================================================== 
//= Darkchild (1.1)
//= Playtester (1.2)
//===== Current Version: ===================================== 
//= 2.8
//===== Compatible With: ===================================== 
//= eAthena 1.0+
//===== Description: ========================================= 
//= Inn Npcs, Save and Heal
//============================================================
//= variables:
//= arg0 - name of npc
//= arg1 - name of the inn
//= arg2 - map to save at
//= arg3, arg4 - x and y cord. to save at
//= @cost - cost of renting a room
//===== Additional Comments: ================================= 
//= 1.1 Blind Effect By Kobra_k88 (Taken from his old script)
//= I added it to all of them though [Darkchild]
//= 1.1a Minor bug fixes and optimizations. Switched from @variables 
//= to arguments.[kobra_k88]
//= 1.2 Rewrote inn script [Playtester]
//= 1.3 Added (finally) Rachel Inn Maid. Official warp
//=	  and save points[erKURITA]
//= 1.4 Added Hugel inn [erKurita]
//= 1.5 Added "end;" after warping the player. [L0ne_W0lf]
//=	- Small corrections to the prontera Inns.
//=	- Removed the Cancel dialog.
//= 1.6 More fixes, changed progression of "Rest"ing.
//=	- Corrected Morroc NPCs. [L0ne_W0lf]
//= 1.7 Corrected Payon NPC. [L0ne_W0lf] 
//= 1.8 Corrected Geffen NPC. [L0ne_W0lf] 
//= 1.9 Added the Inn for Al De baran. [L0ne_W0lf]
//= 2.0 Added Inn for the Town of Veins. [L0ne_W0lf]
//= 2.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.1a Corrected a little Typo error. (bugreport:536 & 537) [Samuray22]
//= 2.2 Small update to Vein's Inn Master. [L0ne_W0lf]
//= 2.3 Added Lighthalzen Inn from Lighthalzen town file. [L0ne_W0lf]
//= 2.4 Fixed Inn Employee#Ahlma warping to same destination as save point, bugreport: 191.
//= 2.5 Added Moskovia Gostinitsa (Inn) from jA
//= 2.6 Updated Alberta inn to official. [L0ne_W0lf]
//= 2.7 Removed Morroc Inns as per episode 12.1. [L0ne_W0lf]
//= 2.8 Removed Moscovia Inn; Its in quests/quest_moscovia.txt [Kisuka]
//============================================================ 


//======================== Prontera ===================================
// West Side Inn -------------------
prt_in,244,135,2	script	Aubergiste#Sammy	53,{

	callfunc "F_InnMaid","[Sammy]","Auberge Nenkaras","prt_in",238,130;
	warp "prt_in",247,104; end;
}
// East Side Inn ------------------
prt_in,61,141,2	script	Aubergiste#Ahlma	53,{

	callfunc "F_InnMaid","[Ahlma]","Auberge Nenkaras","prt_in",64,136;
	warp "prt_in",60,166; end;
}

//======================== Alberta ====================================
alberta_in,32,142,3	script	Aubergiste#Jennie	53,{

	callfunc "F_InnMaid","[Jennie]","'Auberge du Pêcheur'!","alberta_in",26,142;
	warp "alberta_in",18,188; end;
}

//====================== Geffen ======================================
geffen_in,70,64,5	script	Aubergiste#Cena	53,{

	callfunc "F_InnMaid","[Cena]","Auberge Ifrit, la seule et l'unique Auberge de Geffen!","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}

//======================= Payon ======================================
payon_in01,132,62,5	script	Aubergiste#Ahee	53,{

	callfunc "F_InnMaid","[Ahee]","Auberge de Payon","payon_in01",136,61;
	warp "payon_in01",132,11; end;
}

//======================== Morocc ====================================
// North East --------------------------
/*
morocc_in,147,141,3	script	Aubergiste#Hasna	53,{

	callfunc "F_InnMaid","[Hasna]","Auberge de Morroc","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}
// South --------------------------------
morocc_in,80,100,5	script	Aubergiste#Manar	53,{

	callfunc "F_InnMaid","[Manar]","Auberge de Morroc","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}
*/

//====================== Aldebaran ===================================
aldeba_in,92,58,5	script	Aubergiste#Rilim	53,{

	callfunc "F_InnMaid","[Rilim]","Auberge d'Al De Baran","aldeba_in",92,50;
	warp "aldeba_in",92,112; end;
}

//======================== Rachel ====================================
ra_in01,376,69,4	script	Aubergiste#Annie	931,{

	callfunc "F_InnMaid","[Annie]","Auberge de Rachel","ra_in01",375,58;
	warp "ra_in01",384,128; end;
}

//====================== Lighthalzen =================================
lhz_in02,230,284,4	script	Employée de l'Hôtel#01	86,{

	mes "[Employée de l'Hôtel]";
	mes "Bienvenue à l'Hôtel Dragon Royal, où vous pourrez profiter des meilleurs services en matière d'hôtellerie.";
	next;
	menu "Sauvegarder",s_Save,"Louer une Chambre -> 5000 Zenys",s_Rest,"Rien, merci",-;

	mes "[Employée de l'Hôtel]";
	mes "Merci, passez une bonne journée.";
	close;

s_Save:
	savepoint "lhz_in02",209,275;
	mes "[Employée de l'Hôtel]";
	mes "Votre point de sauvegarde";
	mes "a été enregistré.";
	mes "Merci, je reste";
	mes "à votre disposition.";
	close;

s_Rest:
	if(Zeny < 5000)goto s_NEnoughZ;
	set Zeny,Zeny - 5000;
	mes "[Employée de l'Hôtel]";
	mes "Merci~";
	mes "J'espère que vous apprécierez votre séjour à l'Hôtel Dragon Royal.";
	close2;
	warp "lhz_in02",219,150;
	percentheal 100,100;
	sc_start SC_BLESSING,240000,10;
	skilleffect AL_BLESSING,0;
	end;

s_NEnoughZ:
	mes "[Employée de l'Hôtel]";
	mes "Je suis désolée, mais vous n'avez pas assez d'argent. Revenez quand vous en aurez!";
	close;
}

//======================== Hugel ====================================
hu_in01,246,107,3	script	Aubergiste#Receptio	53,{

	callfunc "F_InnMaid","[Réceptionniste]","Auberge d'Hugel","hu_in01",263,95;
	warp "hu_in01",267,5; end;
}

//======================== Veins ====================================
ve_in,157,219,5	script	Aubergiste#master	709,{
	mes "[Aubergiste]";
	mes "Bonjour~";
	mes "Bienvenue à l'auberge";
	mes "la plus confortable";
	mes "de la ville de Veins~";
	next;
	menu "Sauvegarder",Msave, "Louer une Chambre -> 5000 Zenys",Mrent, "Rien, merci",Mend;

	Msave:
		mes "[Aubergiste]";
		mes "Votre point de sauvegarde";
		mes "a été enregistré à Veins.";
		mes "Passez un agréable séjour en ville~";
		savepoint "ve_in",157,209;
		close;
	Mrent:
		mes "[Aubergiste]";
		if(Zeny < 5000){
			mes "Je suis désolé, mais vous n'avez pas assez d'argent pour louer une chambre.";
			mes "Le prix de ce service est de 5000 Zenys.";
			close;
		}
		mes "Profitez de votre séjour ici~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		warp "ve_in",184,228;

	Mend:
		close;
}

//======================= Inn Function ==============================
function	script	F_InnMaid	{
	mes getarg(0);
	mes "Bienvenue à";
	mes "l'"+getarg(1)+".";
	mes "Que puis-je pour vous?";
	next;
	menu "Sauvegarder",Msave, "Louer une Chambre -> 5000 Zenys",Mrent, "Rien, merci",Mend;

	Msave:
		mes getarg(0);
		mes "Votre point de sauvegarde";
		mes "a été enregistré.";
		mes "Merci, je reste";
		mes "à votre disposition.";
		savepoint getarg(2),getarg(3),getarg(4);
		close;
	Mrent:
		mes getarg(0);
		if(Zeny < 5000){
			mes "Je suis désolée, mais le coût de";
			mes "ce service est de 5000 Zenys.";
			mes "Veuillez vous assurer d'avoir assez";
			mes "d'argent sur vous la prochaine fois.";
			close;
		}
		mes "Merci.";
		mes "Passez une";
		mes "agréable nuit~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		return;

	Mend:
		close;
}
