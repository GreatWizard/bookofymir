//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//===== eAthena Script ======================================= 
//= Cash Hair Stylist
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Exchange cash item, New_Style_Coupon, for new hair styles.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//============================================================

itemmall,19,74,5	script	Styliste::Stylist#cash	91,{
	mes "[Styliste]";
	mes "Bonjour je m'appelle Kaniki. Je suis sur le point de lancer une mode avec mes nouvelles coupes de cheveux! Je pourrais vous en faire une en échange d'un ^FF0000New Style Coupon^000000~";
	next;
	if (Sex == 1) {
		mes "[Kaniki]";
		mes "J'ai deux coupes pour homme toutes particulières: la coupe 'Soins d'Urgence' et la coupe 'Épée d'Énergie'. Je vous les montre.";
		next;
		cutin "hair_m_24.bmp",4;
		mes "[Kaniki]";
		mes "Voici la première, la coupe 'Soins d'Urgence'. C'est un tout nouveau style que j'ai créé il y a peu. Qu'est-ce que vous en dites? Fun, non?";
		next;
		cutin "",255;
		cutin "hair_m_25.bmp",4;
		mes "[Kaniki]";
		mes "Et voici la coupe 'Épée d'Énergie'. Aviez-vous déjà vu quelque chose comme ça auparavant?";
		next;
		cutin "",255;
	}else{
		mes "[Kaniki]";
		mes "J'ai deux coupes pour femme toutes particulières: la coupe 'Assomption' et la coupe 'Échange d'Âmes'. Je vous les montre.";
		next;
		cutin "hair_f_24.bmp",4;
		mes "[Kaniki]";
		mes "Voici la première, la coupe 'Assomption'. Vous ne trouvez pas qu'elle vous irait parfaitement?";
		next;
		cutin "",255;
		cutin "hair_f_25.bmp",4;
		mes "[Kaniki]";
		mes "Et voici la coupe 'Échange d'Âmes'. Qu'en dites-vous? Plutôt réussie, non?";
		next;
		cutin "",255;
	}
	mes "[Kaniki]";
	mes "Bien, vous êtes-vous décidé? Voulez-vous que je vous fasse l'une de ces coupes?";
	next;
	if(select("Changer de coupe:Garder sa coupe actuelle") == 1) {
		if (countitem(7622) > 0) {
			mes "[Kaniki]";
			mes "Vous avez un New Style Coupon, magnifique! Bien, quelle coupe voudriez-vous que je vous fasse?";
			next;
			if (Sex == 1) {
				if(select("Soins d'Urgence:Épée d'Énergie") == 1) {
					delitem 7622,1;	// New_Style_Coupon
					setlook 1,24;
				}else{
					delitem 7622,1;	// New_Style_Coupon
					setlook 1,25;
				}
			}else{
				if(select("Assomption:Échange d'Âmes") == 1) {
					delitem 7622,1;	// New_Style_Coupon
					setlook 1,24;
				}else{
					delitem 7622,1;	// New_Style_Coupon
					setlook 1,25;
				}
			}
			mes "[Kaniki]";
			mes "Et voilà, c'est terminé! Alors, que pensez-vous de votre nouvelle coupe? J'espère vous revoir bientôt. Au revoir~";
			close;
		}else{
			mes "[Kaniki]";
			mes "Je suis désolée mais je n'offre mes services qu'à ceux qui possèdent un New Style Coupon. Revenez quand vous en aurez un, d'accord?";
			close;
		}
	}
	mes "[Kaniki]";
	mes "Vraiment? Comme c'est dommage: je suis sûre que vous auriez fait tourner des têtes avec l'une de mes coupes... Eh bien, revenez si jamais vous changez d'avis et décidez qu'il est temps de vous faire une nouvelle coupe, d'accord?";
	close;
}
