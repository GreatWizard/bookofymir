//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//===== eAthena Script =======================================
//= Hair Dresser
//===== By: ==================================================
//= Muad_Dib, Samuray22, Kisuka
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= [Aegis Conversion]
//= Allows you to change your hairstyle.
//===== Additional Comments: =================================
//= 1.2 Rescripted to the Aegis 10.3 Standards. [Samuray22]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.4 Added Lighthalzen Hair Dresser. [L0ne_W0lf]
//============================================================

// Hair Dresser Veronica
// ============================================================
alberta_in,55,142,7	script	Coiffeuse::Hair Dresser	91,{
	mes "[Veronica]";
	mes "Bienvenue dans le Salon de Coiffure de Veronica. En quoi puis-je vous aider?";
	next;
	switch(select("Voir les différentes coupes.:Changer de coupe.:Fin de la conversation.")) {
	case 1:
		mes "[Veronica]";
		mes "Nous avons un total de 19 coupes, numérotées de 1 à 19. Laquelle voudriez-vous voir (entrez '0' pour annuler)?";
		next;
		input .@style;
		if(.@style > 19) {
			mes "[Veronica]";
			mes "Oups, je suis désolée mais cette coupe n'est pas disponible. Vous vous rappelez? Il me faut un nombre compris entre 1 et 19.";
			next;
		} else if(.@style == 0) {
			mes "[Veronica]";
			mes "Alors, comment aimez-vous cette nouvelle coupe? N'hésitez pas à me poser des questions sur les autres coupes disponibles. Je me ferai un plaisir de vous relooker.";
			close;
		}
		else {
			callsub L_cutin,.@style;
			mes "[Veronica]";
			switch(.@style) {
			case 1:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Faire le Mort'. C'est une coupe de base mais très bien quand même. J'ai remarqué que c'est la préférée des gens un peu conservateurs.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Premiers Soins'. Les tresses qui arrivent aux épaules permettent d'avoir une certaine liberté de mouvement et les gens qui cherchent à joindre l'utile à l'agréable l'adorent. C'est aussi la préférée des Novices.";
					close2; cutin "",255; end;
			case 2:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Maîtrise de l'Épée à deux Mains'! Parfaite pour les Épéistes qui pourraient se décoiffer en maniant leur épée toute la journée.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Taugnée'. Faite pour les femmes d'autorité qui n'ont pas peur d'avoir du sang sur les mains, mais qui pourtant savent que leur coupe aura un look d'enfer quand elles feront tournoyer leur épée.";
					close2; cutin "",255; end;
			case 3:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Choc Psychique'. Une coupe unique, légèrement excentrique, avec néanmoins une certaine touche d'élégance.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Gel'. Les couettes donnent un air innocent à ces magiciennes qui d'habitude font fuir tous les hommes avec leurs sorts dévastateurs.";
					close2; cutin "",255; end;
			case 4:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Double Tir'. La disposition des cheveux permet une meilleure circulation de l'énergie statique, ce qui permet d'avoir naturellement les idées claires. Tout du moins c'est ce qu'on m'a racontée à l'école de coiffure...";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Volée de Flèches'. Elle est faire pour les archères qui ne veulent pas sacrifier leur concentration au profit de la mode. Bien plus séduisante toutefois que ces horribles chignons de grand-mères...";
					close2; cutin "",255; end;
			case 5:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Angélus'. Elle est faite autant pour les gens calmes et pieux que pour les sans-gênes du peuple.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Soin'. C'est la préférée des Prêtresses et des Acolytes, puisqu'elle convient autant pour les cérémonies que pour le combat.";
					close2; cutin "",255; end;
			case 6:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Permis Charrette'. Elle a été conçue à partir du dessin d'une charrette. Tout du moins c'est ce qu'on m'a appris à l'école de coiffure...";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Échoppe'. La coiffure de celles qui se font de l'argent! Et si je puis dire, elle est aussi économique...";
					close2; cutin "",255; end;
			case 7:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Empoisonnement'. Parfaite pour les Voleurs et les Assassins lorsqu'ils partent empoisonner des bêtes ou... des personnes. La mode des personnes violentes ou survoltées!";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Double Attaque'. La mignonne petite queue de cheval et la frange provocatrice en feront craquer plus d'un!";
					close2; cutin "",255; end;
			case 8:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Assaut'. Très populaire parmi les Chevaliers, cette coupe d'homme, de vrai, attire les jolies demoiselles et rend vraiment très bien sur les hommes au visage carré.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Gloire Divine'. Elle est très élégante et va magnifiquement avec l'habit des Grandes Prêtresses. En général, ce sont surtout des femmes qui ne combattent pas avec leurs mains qui la choisissent.";
					close2; cutin "",255; end;
			case 9:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Nuage Toxique'. Une coupe pour les rebelles et les marginaux qui pourtant attire irrésistiblement les belles demoiselles.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Régénération de Points d'Énergie'. Pour donner plus de volume à ces franges, j'utilise un spray qui vous donne l'impression de regagner vos Points d'Énergie. Malheureusement ce n'est qu'une impression...";
					close2; cutin "",255; end;
			case 10:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Vade Retro'. Très populaire parmi les Prêtre qui veulent se donner un air sérieux, avec tout de même une petite touche sauvage.";
//					mes "Definitely more attractive"; // Je n'ai pas traduit ça: quelqu'un connait le sens de 'mullet'? [No Healing]
//					mes "than the 'Lumière Divine' mullet.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Alchimie'. Les Alchimistes débutantes adorent les mèches partant vers l'extérieur - avec leurs expériences pour le moins explosives elles finiraient par avoir ce genre de coupe de toute manière...";
					close2; cutin "",255; end;
			case 11:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Dragonologie'. Une coupe au carré pour les gens studieux qui va très bien à ceux qui portent des lunettes. Elle est faite pour les intellectuels.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Grande Croix'. Très prisée par ces pieux Croisés qui ne veulent pas être décoiffés pendant la bataille, et qui doivent garder une coupe digne pour les cérémonies.";
					close2; cutin "",255; end;
			case 12:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Maîtrise de la Masse'. Croyez-moi, ça demande beaucoup de travail pour avoir ce résultat qui donne l'impression de dire 'Je me moque complètement de mon apparence'.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Intimidation'. Beaucoup de Maraudeuses adorent cette coupe, même si j'imagine que plus encore préfèrent quelque chose de plus 'sauvage' pour aller avec leurs bas...";
					close2; cutin "",255; end;
			case 13:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Tempête Foudroyante'. Une coupe en pétard flamboyante! Vous allez faire exploser les coeurs avec ce nouveau look.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Drain du Ki'. Une coupe qui donne un petit air de garçon manqué, tout en gardant une certaine part de féminité.";
					close2; cutin "",255; end;
			case 14:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Encore'. Les mèches élégantes vont très bien aux bardes ou les hommes qui apprécient la beauté masculine à sa juste valeur.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Baiser de la Chance'. Les Danseuses adorent cette coupe, même si à titre personnel je pense qu'elle convient mieux aux femmes portant des lunettes.";
					close2; cutin "",255; end;
			case 15:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Crocs des Ombres'. Une coupe désordonnée à la mode dans les cultures alternatives. À déconseiller pour les cérémonies officielles.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Contre-Attaque'. Une coupe qui donne un air intimidant. Faite pour les femmes qui veulent avoir l'air de dire: 'Tu me touches, tu t'en prends une!'. Elle fait ressortir le front et les pommettes.";
					close2; cutin "",255; end;
			case 16:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Attaque Éclair'. Cette coupe est faite pour les Chasseurs qui aiment avoir de longues mèches sans que ça ne leur gêne la vue.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Piège à Loup'. Une coupe faite pour les Chasseuses qui aiment avoir les cheveux au vent. Après tout, quel chasseur aimerait avoir les cheveux piégés?";
					close2; cutin "",255; end;
			case 17:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Connaissance des Minerais'. C'est une coupe pratique, économique, qu'adorent les Forgerons. Certains prétendent que cette coupe les aide à trouver des minerais, mais où est la science dans tout ça?";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Masse de Richter', pour les femmes qui ne veulent pas avoir leurs cheveux qui les gênent quand elles manient brutalement de lourds objets. Les Forgerons l'adorent.";
					close2; cutin "",255; end;
			case 18:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Colonne de Feu'. Il s'agit d'une coupe branchée avec laquelle un oeil est couvert pour donner un aspect mystérieux et intrigant. Mais le mystère est toujours à la mode, n'est-ce pas?";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Éclair Suprême'. Une coupe pour ceux qui ne veulent pas être embêtés par les noeuds ou les mèches rebelles. Elle rend très bien avec un Chapeau de Magicien";
					close2; cutin "",255; end;
			case 19:
				if(Sex == 1) {
					mes "Oh, ça c'est la coupe 'Frappe d'Asura'. Les mèches coiffées en arrière à la mode Ancien Empire montrent que pour vous les duels et les combats sont un mode vie... à moins que ce ne soit juste pour la frime.";
					close2; cutin "",255; end;
				}
					mes "Oh, ça c'est la coupe 'Coup Tourbillonnant'. L'une des préférées parmi les Sages studieuses. La natte fait en sorte que les cheveux ne se baladent pas lorsque vous lancez des sorts.";
					close2; cutin "",255; end;
			}
		}
	case 2:
		if(BaseLevel < 60) {
			mes "[Veronica]";
			mes "Oh, votre coupe actuelle vous va à ravir! Pourquoi ne vous mettriez-vous pas quelque chose dans les cheveux à la place si vous voulez changer de look, comme une broche ou un joli chapeau?";
			close;
		} else if((countitem(973) < 3) || (countitem(974) < 3) || (countitem(901) <100) || (countitem(1094) <100) || (countitem(1020) <100) || (countitem(1060) <100) || (countitem(7152) <100) || (Zeny < 99800)) {
			mes "[Veronica]";
			mes "Si vous souhaitez changer de coupe, il faudra me rapporter quelques objets. Vous devriez bien noter tout ça.";
			next;
			mes "[Veronica]";
			mes "3 Antiagents,";
			mes "3 Mixtures,";
			mes "100 Daenggies,";
			mes "100 Courtes Daenggies,";
			mes "100 mèches de Cheveux Noirs,";
			mes "100 mèches de Cheveux Dorés,";
			mes "100 mèches Cheveux Brillants, et enfin...";
			next;
			mes "[Veronica]";
			if (sex) set .@x$,"séduisant";
			else set .@x$,"séduisante";
			mes "Il faudra aussi que vous me payiez 99,800 zénys. Revenez quand vous aurez tout ça, et je m'occuperai de vous rendre aussi "+.@x$+" que possible. Hohohohoho~";
			close;
		}
		mes "[Veronica]";
		if (sex) set .@x$,"séduisant";
		else set .@x$,"séduisante";
		mes "Bien, maintenant, choisissez la coupe que vous voulez (1-19). Je ferai de mon mieux pour vous rendre aussi "+.@x$+" que possible.";
		next;
		input .@style;
		if(.@style > 19) {
			mes "[Veronica]";
			mes "Je suis désolée mais cette coupe n'existe pas. Vérifiez à nouveau le numéro.";
			close;
		} else if(.@style == 0) {
			mes "[Veronica]";
			mes "Très bien, comme vous voulez. Revenez me voir si vous changez d'avis.";
			close;
		} else if(getlook(1) == .@style) {
			mes "[Veronica]";
			mes "Je suis désolée mais vous avez déjà cette coupe! Choisissez-en une autre s'il vous plait.";
			close;
		}
			callsub L_cutin,.@style;
			mes "[Veronica]";
			mes "Vous avez choisi la coupe numéro (" + .@style + "). Bien, je vais m'y mettre, d'accord?";
			next;
			if(select("Allez-y.:Je veux en choisir une autre.") == 1) {
				if(getlook(6) == 0) {
					mes "[Veronica]";
					if (sex) set .@x$,"beau";
					else set .@x$,"belle";
					mes "Oh, mais... vous n'avez jamais teint vos cheveux! Vous auriez l'air encore plus "+.@x$+" avec une nouvelle couleur... Si vous voulez je pourrais vous les teindre gratuitement, qu'en dites-vous? Quelle teinte voudriez-vous que je vous fasse?";
					next;
					switch(select("Rouge.:Blond.:Violet.:Roux.:Vert.:Bleu.:Blanc.:Brun.:Annuler.")) {
					case 1:
						set .@pallete,8;
						break;
					case 2:
						set .@pallete,1;
						break;
					case 3:
						set .@pallete,2;
						break;
					case 4:
						set .@pallete,3;
						break;
					case 5:
						set .@pallete,4;
						break;
					case 6:
						set .@pallete,5;
						break;
					case 7:
						set .@pallete,6;
						break;
					case 8:
						set .@pallete,7;
						break;
					case 9:
						mes "[Veronica]";
						mes "Pourtant je n'allais pas vous faire payer pour ça... Eh bien, revenez me voir si vous changez d'avis. Il n'y a rien de mieux pour affirmer son look qu'une nouvelle teinture.";
						close;
					}
				}
				nude;
				mes "[Veronica]";
				mes "Bien, commençons. Essayez de ne pas bouger, s'il vous plaît. Je ne voudrais pas rater mon coup. Faites-moi confiance, je vais vous donner un look d'enfer~";
				next;
				mes "[Veronica]";
				mes "- *coupe coupe coupe* -";
				mes "- *bzzzzzzz bzzzzzzz* -";
				mes "- *coupe coupe coupe* -";
				mes "- *bzzzzzzz bzzzzzzz* -";
				next;
				set Zeny,Zeny-99800;
				delitem 973,3;	// Counteragent
				delitem 974,3;	// Mixture
				delitem 901,100;	// Danggie
				delitem 1094,100;	// Short_Daenggie
				delitem 1020,100;	// Long_Hair
				delitem 1060,100;	// Golden_Hair
				delitem 7152,100;	// Glossy_Hair
				setlook 1,.@style;
				setlook 6,.@pallete;
				cutin "",255;
				mes "[Veronica]";
				mes "Et voilà, c'est fait! J'espère que vous aimez cette coupe numéro (" + .@style + "). Revenez quand vous voulez si vous souhaitez à nouveau changer de coupe. Hohohohohoho~";
				setlook 1,.@style;
				setlook 6,.@pallete;
				close;
			}
			mes "[Veronica]";
			mes "Bien dans ce cas veuillez choisir une autre coupe. Je suis sûre que vous trouverez quelque chose qui vous va.";
			close;
	case 3:
		mes "[Veronica]";
		mes "Tout le monde a le droit de chercher à se faire le plus beau possible. J'espère que vous trouverez une coupe qui vous conviendra un jour.";
		close;
	}

L_cutin:
	if(Sex == 1) {
		if(getarg(0) < 10)
			cutin "hair_m_0"+getarg(0),4;
		else
			cutin "hair_m_"+getarg(0),4;
	} else {
		if(getarg(0) < 10)
			cutin "hair_f_0"+getarg(0),4;
		else
			cutin "hair_f_"+getarg(0),4;
	}
	return;
}

// Roving Hair Dresser
// ============================================================
alberta,33,141,7	script	Coiffeur Ambulant::Roving Hair Dresser	87,{
	mes "[Rui Vishop]";
	mes "Cette Veronica... Ah! La meilleure coiffeuse au monde? Laissez-moi rire! Elle n'est pas le meilleur coiffeur au monde, je le suis!";
	next;
	mes "[Rui Vishop]";
	mes "Moi, Rui Vishop, l'homme que toutes ces tignasses emmêlées attendent pour se transformer une oeuvres d'art grâce à son talent!";
	next;
	switch(select("Qui êtes-vous?:Coupez-moi les cheveux, s'il vous plait!:.....")) {
	case 1:
		mes "[Rui Vishop]";
		mes "Moi, Rui Vishop, maestro de la tondeuse, sculpteur des cheveux, je suis un artiste bien en avance sur mon temps. Mais j'imagine qu'un aventurier comme vous n'y comprend rien...";
		next;
		mes "[Rui Vishop]";
		mes "En tant que sculpteur des cheveux je prends plaisir à conférer à quelques gueux l'extrême faveur de m'occuper de leur tignasse ébouriffée pour une somme modique.";
		next;
		mes "[Rui Vishop]";
		mes "Cependant, depuis quelque temps je dois rivaliser avec une gourdasse qui ose se prétendre coiffeuse.";
		next;
		mes "[Rui Vishop]";
		mes "J'ai été voir par moi-même un jour, en prétendant être un client, et j'ai été scandalisé par toutes ces coupes mornes et sans vie qu'elle faisait pour tous ses clients...";
		next;
		mes "[Rui Vishop]";
		mes "Je me suis tout de suite rendu compte que ses compétences, pour autant qu'elle en ait, se résumaient à pouvoir massacrer une coupe en moins de temps qu'il n'en fallait que pour prendre une paire de ciseaux. C'est une honte de voir autant de déshonneur s'abattre sur cette si noble profession de coiffeur!";
		next;
		mes "[Rui Vishop]";
		mes "Mais le pire était qu'elle forçait ses clients à choisir leur coupe avant de commencer le travail!";
		next;
		mes "[Rui Vishop]";
		mes "Avez-vous déjà vu un coiffeur de talent agir de la sorte? Oh non, un bon coiffeur sait quelle coupe va convenir au client sans même avoir à leur demander!";
		next;
		mes "[Rui Vishop]";
		mes "Si jamais vous décidiez de changer de coupe, n'allez surtout pas la voir. Pensez plutôt à moi, je vous garantis que je suis biiieeeeen plus talentueux qu'elle.";
		next;
		mes "[Rui Vishop]";
		mes "Vous comprenez, n'est-ce pas? Je ne voudrais pas que vous vous retrouviez avec une coupe ratée. Autant laisser un Marchand vous refaire une coupe à coups de hache si c'est ce que vous cherchez~";
		close;
	case 2:
		if(BaseLevel < 60) {
			mes "[Rui Vishop]";
			mes "Hum, Je pense que votre style actuel est parfait. Croyez-moi, je sais ce que je dis.";
			close;
		} else if(Zeny < 199800) {
			mes "[Rui Vishop]";
			mes "Ah, je vois que vous savez reconnaître un génie quand vous en voyez un. Pour récompenser votre bon goût, je ne vous demanderai qu'une petite somme en échange de mes services.";
			next;
			mes "[Rui Vishop]";
			mes "Payez-moi simplement 199,800 zénys. Vous savez que je vous fais une grande faveur en ne vous demandant pas plus, n'est-ce pas? Mon art n'a pas de prix après tout...";
			close;
		}
		mes "[Rui Vishop]";
		mes "Bien, merci de me payer d'avance les 199,800 zénys pour mes services dans ce cas.";
		next;
		mes "[Rui Vishop]";
		mes "Si vous voulez encore attendre un peu, même si je comprendrais difficilement pourquoi, vous pourriez tout aussi bien demander à cette coiffeuse empotée et maladroite de faire le travail...";
		next;
		if(select("Non, coupez-moi les cheveux.:Hum, j'ai changé d'avis.") ==1 ) {
			mes "[Rui Vishop]";
			mes "Bien~";
			mes "Je m'y mets~";
			next;
			mes "[Rui Vishop]";
			mes "Wooooo~oooohhhh!! Toohhhhh~oooohhhh!!";
			next;
			mes "[Rui Vishop]";
			mes "Woooooo~aaaaaaahhhhh!!";
			next;
			mes "[Rui Vishop]";
			mes "Voilà!";
			next;
			mes "[Rui Vishop]";
			mes "Magnifique~ C'est magnifique! Un autre coup de maître de Vishop! Une fois de plus je me suis surpassé. Une coupe aussi unique et pourtant si raffinée... Oh ça oui, je suis vraiment le meilleur! Wooohahahahahaha!";
			set Zeny,Zeny-199800;
			setlook 1,rand(1,19);
			setlook 6,rand(1,8);
			close;
		}
		mes "[Rui Vishop]";
		mes "Bah! Très bien, c'est votre choix après tout. Mais ne venez pas vous plaindre quand vous réaliserez que vous avez une tête affreuse et vous regretterez de ne pas avoir un génie comme moi pour redonner de l'éclat à chacune de vos mèches rebelles.";
		close;
	case 3:
		mes "[Rui Vishop]";
		mes "Quoi? C'est une honte! Vous allez laisser ce charlatan massacrer votre coupe? Je suis sûr que les dieux sont en train de se morfondre devant l'incommensurable ignorance des hommes...";
		close;
	}
}


// Lighthalzen
lhz_in02,100,143,3	script	Coiffeur::Hair Dresser#li	122,{
	mes "[Prince Shammi]";
	mes "Bienvenue dans le Salon de Beauté du Prince Shammi, l'endroit incontournable pour avoir une chevelure faaaaabuleuse! Ne soyez pas timide, dites-moi exactement ce que vous voulez que je fasse pour vous rendre plus glamour~";
	next;
	switch(select("Consulter toutes les coupes:Changer de coupe:Annuler")) {
	case 1:
		mes "[Prince Shammi]";
		mes "Oh, vous voulez voir toutes les coupes branchées que j'ai à proposer?";
		next;
		mes "[Prince Shammi]";
		mes "Je vous en prie! Choisissez l'une des coupes dans mon catalogue, et je vous montrerai un aperçu.";
		next;
		switch(select("Anciennes coupes:Nouvelles coupes")) {
		case 1:
			if (Sex == 1) {
				switch(select("Coupe Petite:Coupe Exécuteur:"+ ( sex ? "Coupe du Prince" : "Coupe Princesse" ) +":Coupe Déviace:Annuler")) {
				case 1:
					cutin "hair_m_20",4;
					mes "[Prince Shammi]";
					mes "Voici la ^3131FFCoupe Petite^000000, qui adoucit l'apparence des hommes avec de longues nattes pour donner plus de volume.";
					break;
				case 2:
					cutin "hair_m_21",4;
					mes "[Prince Shammi]";
					mes "Oh, la ^3131FFCoupe Exécuteur^000000! C'est un style très brut et viril pour ces hommes rudes qui deviennent de plus en plus populaires. Et on sait que les femmes aiment ça, n'est-ce pas? ^333333*Tee hee~*^000000";
					break;
				case 3:
					cutin "hair_m_22",4;
					mes "[Prince Shammi]";
					mes "Vous avez certainement l'oeil pour reconnaître les belles choses! Oui, c'est la ^3131FF"+ ( sex ? "Coupe du Prince" : "Coupe Princesse" ) +"^000000, le summum de la sensualité et de la sophistication. Magnifique, non? Oui, choisissez celle-la! Choisissez celle-la!";
					break;
				case 4:
					cutin "hair_m_23",4;
					mes "[Prince Shammi]";
					mes "Ah-ah!~ La ^3131FFCoupe Déviace^000000! Elle ressemble à la Coupe du Prince, mais avec des cheveux coupés un peu plus courts dans le dos. Oh oui, c'est une coupe très \"propre sur soi\", très \"dandy\".";
					break;
				case 5:
					cutin "hair_f_01",255;
					mes "[Prince Shammi]";
					mes "Non? Vous ne vouliez rien voir? Vous êtes un aventurier! Je sais que vous savez être audacieux! Soyez aventureux en matière de mode~"; // NB: pas besoin de ( sex ? ) dans tout ce passage: il y a un if ( sex ) plus haut [NoH]
					emotion e_lv,"Hair Dresser#i";
					close;
				}
			}
			else {
				switch(select("Coupe Lapin Bondissant:Coupe Harpie:Coupe Méduse:Coupe Isis:Annuler")) {
				case 1:
					cutin "hair_f_20",4;
					mes "[Prince Shammi]";
					mes "Oh oui, la ^3131FFCoupe Lapin Bondissant^000000! La frange libre à l'avant ajoute un je ne sais quoi de chic, et un côté très taquin. Oui? Non? Oui? Non? Oh, oui!";
					break;
				case 2:
					cutin "hair_f_21",4;
					mes "[Prince Shammi]";
					mes "Oh, la ^3131FFCoupe Harpie^000000 vous intéresse? L'ondulation naturelle des cheveux et la queue de cheval en font quelque chose de très sophistiqué, et pourtant de très naturel et décontracté~";
					break;
				case 3:
					cutin "hair_f_22",4;
					mes "[Prince Shammi]";
					mes "Ah, la ^3131FFCoupe Méduse^000000~";
					mes "Ces mèches se baladant audacieusement crient \"pouvoir\" et \"domination\". C'est la coupe idéale pour les femmes de carrière qui désirent se rendre irrésistibles auprès des hommes~";
					break;
				case 4:
					cutin "hair_f_23",4;
					mes "[Prince Shammi]";
					mes "Oh, la ^3131FFCoupe Isis^000000~";
					mes "Oui, vous aurez l'air très mignonne avec des chignons de chaque côté. Ça vous irait comme un charme!";
					break;
				case 5:
					mes "[Prince Shammi]";
					mes "Non? Vous ne vouliez rien voir? Vous êtes une aventurière! Je sais que vous savez être audacieuse! Soyez aventureuse en matière de mode~";
					emotion e_lv,"Hair Dresser#li";
					close;
				}
			}
			break;
		case 2:
			if (Sex == 1) {
				switch(select("Permanente Soin Urgent:Coupe Aura d'Énergie:Coupe Hache Tournoyante:Coupe Rénovation:Annuler")) {
				case 1:
					cutin "hair_m_24",4;
					mes "[Prince Shammi]";
					mes "Il s'agit de la ^3131FFPermanente Soin Urgent^000000. Elle est très populaire auprès des guérisseurs.";
					break;
				case 2:
					cutin "hair_m_25",4;
					mes "[Prince Shammi]";
					mes "Vous devez être après une femme, n'est-ce pas? La ^3131FFCoupe Aura d'Énergie^000000 fera s'évanouir les dames, beau félin!";
					break;
				case 3:
					cutin "hair_m_26",4;
					mes "[Prince Shammi]";
					mes "Oh, vous êtes une brute!";
					mes "La ^3131FFCoupe Hache Tournoyante^000000 vous donne l'impression de gonfler vos muscles avec vos cheveux. C'est vraiment votre style.";
					break;
				case 4:
					cutin "hair_m_27",4;
					mes "[Prince Shammi]";
					mes "Ah! Je vois que vous êtes intéressé par les dernières tendances. Hé bien, ça vient de sortir, il s'agit de la ^3131FFCoupe Rénovation^000000!";
					break;
				case 5:
					mes "[Prince Shammi]";
					mes "Non? Vous ne vouliez rien voir? Vous êtes un aventurier! Je sais que vous savez être audacieux! Soyez aventureux en matière de mode~";
					emotion e_lv,"Hair Dresser#li";
					close;
				}
			}
			else {
				switch(select("Permanente Assomption:Coupe Échange d'Âmes:Coupe Hache Tornade:Coupe Oraison Funèbre:Annuler")) {
				case 1:
					cutin "hair_f_24",4;
					mes "[Prince Shammi]";
					mes "Il s'agit de la ^3131FFPermanente Assomption^000000. Une permanente plus courte pour un maximum d'efficacité quand vous lancez des sorts!";
					break;
				case 2:
					cutin "hair_f_25",4;
					mes "[Prince Shammi]";
					mes "Vous devez être une croqueuse d'hommes, non? La ^3131FFCoupe Échange d'Âmes^000000 donnera à tous les hommes envie de vous ouvrir leur porte-f... euh... leur coeur!";
					break;
				case 3:
					cutin "hair_f_26",4;
					mes "[Prince Shammi]";
					mes "Voici un style très à la mode: la ^3131FFCoupe Hache Tornade^000000!";
					mes "Elle est faite pour les femmes aventureuses qui aiment le changement.";
					break;
				case 4:
					cutin "hair_f_27",4;
					mes "[Prince Shammi]";
					mes "Ah! Je vois que les dernières tendances vous intéressent! Hé bien, ça vient de sortir, il s'agit de la ^3131FFCoupe Oraison Funèbre^000000!";
					mes "Vous ferez l'envie de tous vos amies avec cette coupe.";
					break;
				case 5:
					mes "[Prince Shammi]";
					mes "Non? Vous ne vouliez rien voir? Vous êtes une aventurière! Je sais que vous savez être audacieuse! Soyez aventureuse en matière de mode~";
					emotion e_lv,"Hair Dresser#li";
					close;
				}
			}
		}
		close2;
		cutin "",255;
		end;
	case 2:
		if (BaseLevel < 60) {
			mes "[Prince Shammi]";
			mes "Oh, je suis vraiment désolé, mais je ne peux proposer mes services qu'à des clients assez mûrs pour trouver leur véritable beauté intérieure. Mais revenez me voir quand vous y serez parvenu"+ ( sex ? "" : "e" ) +", d'accord?";
			close;
		}
		else if ((countitem(973) < 3) || (countitem(974) < 3) || (countitem(901) < 100) || (countitem(1094) < 100) || (countitem(1020) < 100) || (countitem(1060) < 100) || (countitem(7152) < 100) || (Zeny < 99800)) {
			mes "[Prince Shammi]";
			mes "Si vous savez déjà quelle coupe vous voulez, merci de préparer les matériaux dont j'ai besoin pour ce service, ainsi que la somme pour payer mes modestes émoluments. S'il vous plait, rapportez-moi...";
			next;
			mes "[Prince Shammi]";
			mes "^3355FF3 Antiagents^000000,";
			mes "^3355FF3 Mixtures^000000,";
			mes "^3355FF100 Daenggies^000000,";
			mes "^3355FF100 Courtes Daenggies^000000...";
			next;
			mes "[Prince Shammi]";
			mes "^3355FF100 mèches de Cheveux Noirs^000000,";
			mes "^3355FF100 mèches de Cheveux Dorés^000000,";
			mes "^3355FF100 mèches de Cheveux Brillants^000000";
			mes "et ^3355FF99,800 zénys^000000.";
			mes "Je ferai un miracle avec vos cheveux une fois que vous aurez tout ça!";
			close;
		}
		mes "[Prince Shammi]";
		mes "D'accord, choisissez la coupe que vous voulez en entrant un nombre entre 20 et 25.";
		mes "Voici la liste des correspondances au cas où vous en auriez besoin~";
		next;
		mes "[Prince Shammi]";
		if (Sex == 1) {
			mes "No. 20: Coupe Petite";
			mes "No. 21: Coupe Exécuteur";
			mes "No. 22: "+ ( sex ? "Coupe du Prince" : "Coupe Princesse" );
			mes "No. 23: Coupe Déviace";
			mes "No. 24: Permanente Soin Urgent";
			mes "No. 25: Coupe Aura d'Énergie";
			mes "No. 26: Coupe Hache Tournoyante et";
			mes "No. 27: Coupe Rénovation.";
		}
		else {
			mes "No. 20: Coupe Lapin Bondissant";
			mes "No. 21: Coupe Harpie";
			mes "No. 22: Coupe Méduse";
			mes "No. 23: Coupe Isis";
			mes "No. 24: Permanente Assomption";
			mes "No. 25: Coupe Échange d'Âmes";
			mes "No. 26: Coupe Hache Tornade et";
			mes "No. 27: Coupe Oraison Funèbre.";
		}
		next;
		input .@input;
		if (.@input == 0) {
			mes "[Prince Shammi]";
			mes "Oh...? Vous avez changé d'avis? Hé bien, j'imagine que vous savez ce qui est le mieux pour vous. Mais je suis tout de même déçu~";
			close;
		}
		else if ((.@input < 20) || (.@input > 27)) {
			mes "[Prince Shammi]";
			mes ( sex ? "Mon cher" : "Ma chère" ) +", vous devez indiquer un numéro de \"20\" à \"25\", d'accord? Je pourrai ensuite me mettre au travail et vous rendre irrrrrrrésistible!";
			close;
		}
		else if (getlook(VAR_HEAD) == .@input) {
			mes "[Prince Shammi]";
			mes ( sex ? "Oh, mon cher" : "Oh, ma chère" ) +", vous n'allez pas gaspiller votre argent pour que je vous refasse la même coupe, voyons! Il y a quelqu'un qui peut changer votre couleur de cheveux si c'est ce que vous voulez, vous savez?";
			close;
		}
		else {
			if (Sex == 1)
				cutin "hair_m_"+.@input+".BMP",4;
			else
				cutin "hair_f_"+.@input+".BMP",4;
			mes "[Prince Shammi]";
			mes "Oooh! Alors, est-ce que c'est la coupe que vous vouliez?";
			mes "Il s'agit de la coupe No. "+.@input+", au passage.";
			next;
			switch(select("Oui.:Non.")) {
			case 1:
				if (getlook(VAR_HEADPALETTE) == 0) {
					mes "[Prince Shammi]";
					mes "Oh, par Odin, j'allais presque oublier! Voudriez que je teigne vos cheveux par la même occasion? Je ne vous demanderai rien de plus, c'est compris dans le prix. Choisissez une nouvelle couleur~";
					next;
					switch(select("Rouge:Blond:Violet:Orange:Vert:Bleu:Blanc:Brun")) {
					case 1:
						set .@headpalette,8;
						break;
					case 2:
						set .@headpalette,1;
						break;
					case 3:
						set .@headpalette,2;
						break;
					case 4:
						set .@headpalette,3;
						break;
					case 5:
						set .@headpalette,4;
						break;
					case 6:
						set .@headpalette,5;
						break;
					case 7:
						set .@headpalette,6;
						break;
					case 8:
						set .@headpalette,7;
						break;
					}
				}
				mes "[Prince Shammi]";
				mes "D'accord, commençons vous voulez bien? Redressez la tête... Oui, comme ça...";
				next;
				mes "^3355FF*Coupe* *Coupe*";
				mes "*Rase* *Rase*";
				mes "*Clip clip clip clip*";
				mes "*Bzzzzzzzzzzzzzzzzzzzz*^000000";
				next;
				set zeny,zeny-99800;
				delitem 973,3; //Counteragent
				delitem 974,3; //Mixture
				delitem 901,100; //Danggie
				delitem 1094,100; //Short_Daenggie
				delitem 1020,100; //Long_Hair
				delitem 1060,100; //Golden_Hair
				delitem 7152,100; //Glossy_Hair
				setlook VAR_HEAD,.@input;
				setlook VAR_HEADPALETTE,.@headpalette;
				mes "[Prince Shammi]";
				mes "Et voilà, c'est terminé! Et oh mon... Vous avez l'air encore plus resplendissent"+ ( sex ? "" : "e" ) +" que je n'aurais osé l'espérer! Je n'arrive pas à croire à quel point vous êtes "+ ( sex ? "élégant, et pourtant toujours aussi viril et charmant" : "gracieuse et élégante! Absolument adorable" ) +"!";
				emotion e_kis,"Hair Dresser#li";
				next;
				mes "[Prince Shammi]";
				mes "Vous adorez votre nouvelle coupe, n'est-ce pas? Revenez quand vous voulez. Je ferai de vous la personne la plus attirante au monde!";
				emotion e_no1,"Hair Dresser#li";
				close;
			case 2:
				mes "[Prince Shammi]";
				mes "Oh, est-ce que vous avez oublié à quelles coupes correspondent ces numéros? Ne vous sentez pas gêné"+ ( sex ? "" : "e" ) +", vérifiez à nouveau! Trouvez la coupe qui vous ira à la perfection, d'accord?";
				close;
			}
		}
		break;
	case 3:
		mes "[Prince Shammi]";
		mes "Humm ? ";
		mes "Peut-être ne comprenez-vous rien à la sophistication de mes coupes... C'est la dernière tendance, vous savez? Hé bien, au revoir dans ce cas!";
		close;
	}
}

lhz_in02,91,155,5	script	Apprentie Esthéticienne::Assistant Beautician#li	862,{
	mes "[Apprentie Esthéticienne]";
	mes "Hein?! Oh, par Freya, vous m'avez fait peur! Qu'est-ce que vous faites là?!";
	emotion e_omg,"Assistant Beautician#li";
	next;
	mes "[Apprentie Esthéticienne]";
	mes "Oh! Vous êtes "+ ( sex ? "un client" : "une cliente" ) +"!";
	mes "B-b-b-b-b-"+ ( isday() ? "bonjour" : "bonsoir" ) +"! Est-ce que je peux vous aider?";
	next;
	switch(select("Qu'est-ce que vous faites?:Je voudrais changer de coupe.:Qui est le Prince Shammi?")) {
	case 1:
		mes "[Apprentie Esthéticienne]";
		mes "Oh! Moi...? Je suis une simple apprentie esthéticienne, mais je travaille dur pour devenir une véritable professionnelle un jour!";
		next;
		mes "[Apprentie Esthéticienne]";
		mes "Oui, je fais toutes sortes de travaux ingrats pour le patron pendant ma formation. Il me fait vraiment travailler dur parfois. En fait, je ferais mieux d'y retourner avant qu'il ne s'énerve après moi!";
		close;
	case 2:
		if (BaseLevel < 60) {
			mes "[Apprentie Esthéticienne]";
			mes "Moi...? Oh non, non, je ne peux pas! Je veux dire... J'adorerais, mais je suis toujours en formation, et je ne peux pas prendre la responsabilité de risquer de ruiner la coiffure de quelqu'un d'aussi jeune!";
			close;
		}
		else if ((Zeny < 250000)) {
			mes "[Apprentie Esthéticienne]";
			mes "Eh bien... Je suis juste une apprentie, mais j'étudie la coiffure après le travail. Si vous le voulez, vous n'aurez qu'à me rapporter 250,000 zénys et j'essaierai de faire de mon mieux avec votre coiffure~";
			next;
			mes "[Apprentie Esthéticienne]";
			mes "Je vous en serais vraiment reconnaissante si vous me laissiez ma chance! Le patron pense que je ne suis pas encore prête pour m'occuper de véritables clients, alors je n'ai pas beaucoup de pratique!";
			next;
			mes "[Apprentie Esthéticienne]";
			mes "Je sais juste que je ferais un super boulot avec vos cheveux! Enfin... Disons juste que même si je rate, ce ne sera pas si terrible, je vous le promets~";
			close;
		}
		else {
			mes "[Apprentie Esthéticienne]";
			mes "Vous allez vraiment me laisser ma chance? Oh, je vous aime tant! D'accord, il me faudra juste 250,000 zénys pour couvrir les frais en matériels. Est-ce que ça vous va?";
			next;
			switch(select("Bien sûr~:En y réfléchissant bien...")) {
			case 1:
				mes "[Apprentie Esthéticienne]";
				mes "Fantastique! Maintenant, veuillez choisir une coupe de \"1\" à \"23\". Oh, et si vous voulez annuler, entrez simplement \"0\".";
				next;
				input .@input;
				if (.@input == 0) {
					mes "[Apprentie Esthéticienne]";
					mes "Awwww...";
					mes "J'imagine que vous ne me faites pas vraiment confiance en fin de compte...";
					close;
				}
				else if ((.@input < 1) || (.@input > 23)) {
					mes "[Apprentie Esthéticienne]";
					mes "Hein? Je pensais vous avoir demandé de m'indiquer une coupe entre \"1\" et \"23\"! J'aurais encore fait quelque chose de travers? Hummm...";
					close;
				}
				else {
					mes "[Apprentie Esthéticienne]";
					mes "Alors c'est bien cette coupe que vous voulez que je vous fasse?";
					if (Sex == 1) {
						if (.@input < 10)
							cutin "hair_m_0"+.@input+".BMP",4;
						else
							cutin "hair_m_"+.@input+".BMP",4;
					}
					else {
						if (.@input < 10)
							cutin "hair_f_0"+.@input+".BMP",4;
						else
							cutin "hair_f_"+.@input+".BMP",4;
					}
					next;
					switch(select("Oui, c'est bien cela~:Annuler.")) {
					case 1:
						break;
					case 2:
						mes "[Apprentie Esthéticienne]";
						mes "Oooh, il doit bien y avoir une coupe que vous aimez, non? Hummm...";
						close;
					}
				}
				mes "[Apprentie Esthéticienne]";
				mes "Fantastique, vous avez enfin choisi une coupe!";
				mes "Attendez... C'était laquelle déjà? Ah, c'est bon! Je l'ai trouvée! Il n'y a pas de problème, ne vous en faites pas!";
				mes "Il est temps de m'y mettre!";
				next;
				nude;
				mes "[Apprentie Esthéticienne]";
				mes "Bwwwwaaaahhhh!";
				next;
				mes "[Apprentie Esthéticienne]";
				mes "Yap! Pwwwaaattt!";
				next;
				mes "[Apprentie Esthéticienne]";
				mes "Waaaah!";
				mes "Oh mince!";
				mes "Attendez, je peux...";
				mes "Je peux arranger ça!";
				next;
				set .@style_r,rand(1,23);
				set .@color_r,rand(1,8);
				mes "[Apprentie Esthéticienne]";
				mes "^333333*Souffle* *Souffle* *Souffle*^000000";
				next;
				set zeny,zeny-250000;
				setlook VAR_HEAD,.@style_r;
				setlook VAR_HEADPALETTE,.@color_r;
				mes "[Apprentie Esthéticienne]";
				mes "Bwahahaha! C'est réussi!";
				if (.@input == .@style_r) {
					mes "Alors... Comment aimez-vous votre nouvelle coupe? Moi, je l'adore!";
				}
				else {
					mes "Hein...? Ce n'est pas ce que vous vouliez? Oh, euh... Hum... Hé bien, je ferai un bien meilleur travail la prochaine fois, c'est promis!";
				}
				next;
				mes "[Apprentie Esthéticienne]";
				mes "Oh, vous êtes tellement chou de m'avoir aidée! Merci d'avoir utilisé mes services, et à très bientôt~";
				emotion e_kis,"Assistant Beautician#li";
				close;
			case 2:
				mes "[Apprentie Esthéticienne]";
				mes "Hein? Oh non, vous renoncez? Eh bien j'imagine que moi non plus je ne me ferais pas confiance pour mes cheveux... Vous... Vous avez raison.";
				close;
			}
		}
	case 3:
		mes "[Apprentie Esthéticienne]";
		mes "Le Prince Shammi? C'est un génie de la coiffure! Je ne suis que son apprentie, mais peut-être qu'un jour je serai moi aussi reconnue dans le monde de la mode!";
		close;
	}
}


//===== Old Changelog: =================================
//= 07/06/05 : Added 1st Version. [Muad_Dib]
//= Converted to eAthena format by Dr.Evil Fixed typos [Nexon]
//= 1.1 Removed Duplicates [Silent]
//============================================================
