//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script =======================================
//= Bullet Dealer Tony
//===== By ===================================================
//= Playtester, Paradox924X
//===== Version ==============================================
//= 1.4
//===== Compatible With ======================================
//= eAthena SVN
//===== Description ==========================================
//= Bullet trader.
//===== Comments =============================================
//= 1.0 First version [Playtester]
//= 1.1 Converted from Aegis [Paradox924X]
//= 1.2 More optimized conversion [Legionaire]
//= 1.2a Removed .GATs [Lupus]
//= 1.3 Fixed [Playtester] Optimized. Got rid of @vars [Lupus]
//= 1.4 Updated to match AEGIS script. [Kisuka]
//============================================================

que_ng,187,156,3	script	Tony	86,{
	if (BaseJob == Job_Gunslinger) {
		mes "[Tony]";
		mes "Bonjour, mon nom est Tony.";
		mes "Je vends des munitions pour les Gunslinger.";
		mes "Quand vous êtes à court de munitions, vous n'avez qu'à venir me voir et je vous dépannerai.";
		next;
		switch(select("Sphères de Poison:Sphères de Feu:Sphères de Vent:Sphères Aveuglantes:Sphères de Glace:Annuler")) {
		case 1: callfunc "Bullet_Trade",937,10,13205; break;
		case 2: callfunc "Bullet_Trade",7097,2,13203; break;
		case 3: callfunc "Bullet_Trade",7053,3,13204; break;
		case 4: callfunc "Bullet_Trade",1024,5,13206; break;
		case 5: callfunc "Bullet_Trade",7054,2,13207; break;
		case 6:
			mes "[Tony]";
			mes "Vous avez changé d'avis?";
			mes "Bien, si vous avez besoin de munitions un jour, vous savez où me trouver.";
			close;
		}
	}
	mes "[Tony]";
	mes "Bonjour, mon nom est Tony.";
	mes "Je vends des munitions pour les Gunslinger.";
	mes "Mais je pense que mes services ne vous seront pas très utiles, aventurier.";
	next;
	mes "[Tony]";
	mes "Si vous connaissez des Gunslingers, n'hésitez pas à leur recommander notre magasin!";
	close;
}

function	script	Bullet_Trade	{
	mes "[Tony]";
	mes "Pour chaque lot de";
	mes "30 "+getitemname(getarg(2))+"s,";
	mes "vous devez fournir";
	mes "1 Emveretarcon,";
	mes "1 Phracon, et";
	mes ""+getarg(1)+" "+getitemname(getarg(0))+".";
	next;
	mes "[Tony]";
	mes "Souvenez-vous que je ne peux pas vous donner plus de 500 lots de 30 balles à chaque échange. Veuillez entrer le nombre de lots que vous souhaitez. Si vous voulez annuler, entrez 0.";
	next;
	input .@amount;
	if (.@amount < 1 || .@amount > 500) {
		mes "[Tony]";
		mes "Hé, je ne peux pas vous donner tout ça d'un coup. Entrez un nombre compris entre 1 et 500.";
		close;
	}
	if (countitem(1010) >= .@amount && countitem(1011) >= .@amount && countitem(getarg(0)) >= (.@amount*getarg(1))) {
		if (checkweight(getarg(2),.@amount * 30) == 0) {
			mes "[Tony]";
			mes "Hé, vous portez déjà bien trop de choses. Vous n'aurez pas la place de porter mes packs en plus de tout votre barda, alors allez vous décharger et revenez me voir, ok?";
			close;
		}else{
			mes "[Tony]";
			mes "Ok, tout est en règle. Je récupère vos matériaux, et voici vos munitions. C'est un plaisir de faire affaires avec vous~";
			delitem 1010,.@amount;
			delitem 1011,.@amount;
			delitem getarg(0),.@amount * getarg(1);
			getitem getarg(2),.@amount * 30;
			close;
		}
	}else{
		mes "[Tony]";
		mes "Hum. On dirait qu'il vous manque certains matériaux. Revenez me voir quand vous aurez tout ce qu'il faut, ok?";
		close;
	}
}
