//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script =======================================
//= Bullet Dealer Tony
//===== By ===================================================
//= Playtester, Paradox924X
//===== Version ==============================================
//= 1.3
//===== Compatible With ======================================
//= eAthena SVN with jAthena scripting engine and . variables
//===== Description ==========================================
//= Bullet trader.
//===== Comments =============================================
//= 1.0 First version [Playtester]
//= 1.1 Converted from Aegis [Paradox924X]
//= 1.2 More optimized conversion [Legionaire]
//= 1.2a Removed .GATs [Lupus]
//= 1.3 Fixed [Playtester] Optimized. Got rid of @vars [Lupus]
//============================================================

que_ng,187,156,3	script	Tony	86,{

	mes "[Tony]";
	if (BaseJob == Job_Gunslinger) {
		mes "Bonjour, mon nom est Tony.";
		mes "Je vends des munitions pour les Gunslinger.";
		next;
		mes "[Tony]";
		mes "Quel type de munition voulez-vous?";
		next;
		switch(select("Poison Sphere:Flare Sphere:Lighting Sphere:Blind Sphere:Freezing Sphere:Annuler")) {
		case 1: callfunc "Bullet_Trade",937,10,13205; break;
		case 2: callfunc "Bullet_Trade",7097,2,13203; break;
		case 3: callfunc "Bullet_Trade",7053,3,13204; break;
		case 4: callfunc "Bullet_Trade",1024,5,13206; break;
		case 5: callfunc "Bullet_Trade",7054,2,13207; break;
		default:
			mes "[Tony]";
			mes "D'accord, pas de problème.";
			mes "Revenez quand vous voulez!";
			close;
		}
	}
	mes "Bonjour, mon nom est Tony.";
	mes "Je vends des munitions pour les Gunslinger.";
	next;
	mes "[Tony]";
	mes "Mais vous n'êtes pas Gunslinger alors ça ne vous intéressera sûrement pas...";
	close;
}

function	script	Bullet_Trade	{
	mes "[Tony]";
	mes "Combien en voulez-vous?";
	next;
	mes "[Tony]";
	mes "Vous pouvez avoir 30 "+getitemname(getarg(2))+"s contre";
	mes "1 Phracon,";
	mes "1 Emveretarcon,";
	mes "et "+getarg(1)+" "+getitemname(getarg(0))+".";
	next;
	mes "[Tony]";
	mes "Vous pouvez en échanger 500 au maximum.";
	mes "Entrez 0 si vous voulez annuler.";
	next;
	input .@amount;
	mes "[Tony]";
	if (.@amount < 1 || .@amount > 500) {
		mes "Ce n'est pas un nombre correct!";
		close;
	} else if (countitem(1010) >= .@amount && countitem(1011) >= .@amount && countitem(getarg(0)) >= (.@amount*getarg(1))) {
		if (checkweight(getarg(2),.@amount * 30) == 0) {
			mes "Vous portez déjà trop de choses...";
			close;
		} else {
			mes "Très bien!";
			mes "Voici pour vous!";
			delitem 1010,.@amount;
			delitem 1011,.@amount;
			delitem getarg(0),.@amount * getarg(1);
			getitem getarg(2),.@amount * 30;
			close;
		}
	} else {
		mes "Tss tss~";
		mes "Vous n'avez pas tous les matériaux nécessaires...";
		close;
	}
}