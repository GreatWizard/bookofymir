//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Renters
//===== By: ================================================== 
//= kobra_k88, mod by Lupus
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Knight and Crusader Peco Peco Breeders, Falcon Breeder scripts
//===== Additional Comments: ================================= 
//= Fully working
//= Added another Falcon Master into the Hunters Guild [Lupus]
//= 1.2: replaced checkoption(x) with checkriding,checkfalcon [Lupus]
//= 1.3: Added support Adv Classes + Baby Class [Lupus]
//= 1.4: Added different prices for normal, advanced and baby classes
//= but you could set them to the same [Lupus]
//= 1.5: Fixed spelling mistakes [Nexon]
//= 1.5a: Small fix (Falcon Taming -> Falcon Mastery) [Komurka]
//= 1.6: Moved the Falcon Master to Hugel [Poki#3]
//= 1.7 Updated to Aegis 10.3 standards. [L0ne_W0lf]
//============================================================ 

// PecoPeco Breeder (for Knights)-------------------------------------------
prontera,55,350,5	script	Eleveur de Peco#knt	105,{
	if(Upper==0) set .@price,2500; //Normal Peco - default price
	if(Upper==1) set .@price,2500; //Armored Peco
	if(Upper==2) set .@price,2500; //Baby Peco

	mes "[Eleveur de Peco Peco]";
	if (BaseJob == Job_Knight) {
		mes "Bienvenue, honorable Chevalier. Voudriez-vous chevaucher un Peco Peco? Le prix de location est de "+.@price+" Zenys.";
		next;
		switch(select("Louer un Peco Peco:Annuler")) {
		case 1:
			if (zeny < .@price) {
				mes "[Eleveur de Peco Peco]";
				mes "Comme je l'ai dit, le tarif est de "+.@price+" Zeny. Revenez quand vous aurez assez d'argent.";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Eleveur de Peco Peco]";
				mes  "Comme je l'ai dit, vous devez avoir la compétence ^3333FFRiding^000000 pour pouvoir louer un Peco Peco.";
				close;
			}
			else if(checkriding()) {
				mes "[Eleveur de Peco Peco]";
				mes "Hum... N'êtes-vous pas déjà sur un Peco Peco ?? ...";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[Eleveur de Peco Peco]";
			mes "D'accord. Passez une bonne journée.";
			close;
		}
	} else {
		mes "Je suis désolé, mais les Peco Peco sont réservés aux Chevaliers et Seigneurs Chevaliers.";
		close;
	}
}

// Grand PecoPeco Breeder (for Crusaders)-------------------------------------------
prontera,232,318,3	script	Eleveur de Grand Peco#cr	105,{
	if(Upper==0) set .@price,3500; //Normal Peco - default price
	if(Upper==1) set .@price,3500; //Armored Peco
	if(Upper==2) set .@price,3500; //Baby Peco

	mes "[Eleveur de Grand Peco Peco]";
	if (BaseJob == Job_Crusader) {
		if(Upper != 1 ) mes "Bienvenue, honorable Croisé."; 
		else mes "Bienvenue, honorable Paladin.";
		mes "Voudriez-vous chevaucher un Grand Peco Peco? Le prix de location est de "+.@price+" Zenys.";
		next;
		switch(select("Louer un Grand Peco Peco:Annuler")) {
		case 1:
			if (zeny < .@price) {
				mes "[Eleveur de Grand Peco Peco]";
				mes "Comme je l'ai dit, le tarif est de "+.@price+" Zeny. Revenez quand vous aurez assez d'argent.";
				close;
			}

			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Eleveur de Grand Peco Peco]";
				mes  "Comme je l'ai dit, vous devez avoir la compétence ^3333FFRiding^000000 pour pouvoir louer un Grand Peco Peco.";
				close;
			}
			else if(checkriding()) {
				mes "[Eleveur de Grand Peco Peco]";
				mes "Hum... N'êtes-vous pas déjà sur un Grand Peco Peco ?? ...";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[Eleveur de Grand Peco Peco]";
			mes "D'accord. Passez une bonne journée.";
			close;
		}
	} else {
		mes "Je suis désolé, mais les Grand Peco Peco sont réservés aux Croisés et aux Paladins.";
		close;
	}
}

// Falcon Master--------------------------------------------------------------
hu_in01,381,304,5	script	Eleveur de Faucon#hnt	105,{
	if(Upper==0) set .@price,2500; //Normal Falcon - default price
	if(Upper==1) set .@price,2500; //Scarf Falcon
	if(Upper==2) set .@price,2500; //Baby Falcon

	mes "[Eleveur de Faucon]";
	if (BaseJob == Job_Hunter) {
		mes "Je peux entraîner un Faucon pour vous, mais le tarif est de "+.@price+" Zenys.";
		next;
		switch(select("Louer un Faucon:Annuler")) {
		case 1:
			if (zeny < .@price) {
				mes "[Eleveur de Faucon]";
				mes "Comme je l'ai dit, le tarif est de "+.@price+" Zeny. Revenez quand vous aurez assez d'argent.";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[Eleveur de Faucon]";
				mes "Je suis désolé, mais vous ne possédez pas la compétence 'Falcon Mastery'. Revenez quand vous l'aurez apprise.";
				close;
			}
			else if(checkfalcon()) {
				mes "[Eleveur de Faucon]";
				mes "Vous ne pouvez vous occuper de plus d'un Faucon à la fois.";
				close;
			}
			set zeny,zeny-.@price;
			setfalcon;
			close;
		case 2:
			mes "[Eleveur de Faucon]";
			mes "D'accord. Passez une bonne journée.";
			close;
		}
	} else {
		mes "Seuls les Chasseurs peuvent s'occuper d'un Faucon. Les Faucons sont très capricieux quant à la nature de leur maître, vous savez.";
		close;
	}
}
