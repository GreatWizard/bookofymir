//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Renters
//===== By: ================================================== 
//= kobra_k88, mod by Lupus
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Knight and Crusader Peco Peco Breeders, Falcon Breeder scripts
//===== Additional Comments: ================================= 
//= Fully working
//= Added another Falcon Master into the Hunters Guild [Lupus]
//= 1.2: replaced checkoption(x) with checkriding,checkfalcon [Lupus]
//= 1.3: Added support Adv Classes + Baby Class [Lupus]
//= 1.4: Added different prices for normal, advanced and baby classes
//= but you could set them to the same [Lupus]
//= 1.5: Fixed spelling mistakes [Nexon]
//= 1.5a: Small fix (Falcon Taming -> Falcon Mastery) [Komurka]
//= 1.6: Moved the Falcon Master to Hugel [Poki#3]
//= 1.7 Updated to Aegis 10.3 standards. [L0ne_W0lf]
//============================================================ 

// PecoPeco Breeder (for Knights)-------------------------------------------
prontera,55,350,5	script	Éleveur de Péco#kn	105,{
	if(Upper==0) set .@price,2500; //Normal Peco - default price
	if(Upper==1) set .@price,2500; //Armored Peco
	if(Upper==2) set .@price,2500; //Baby Peco

	mes "[Éleveur de Péco Péco]";
	if (BaseJob == Job_Knight) {
		mes "Bienvenue, honorable Chevalier. Voudriez-vous chevaucher un Péco Péco? Le prix de location est de "+.@price+" Zénys.";
		next;
		switch(select("Louer un Péco Péco:Annuler")) {
		case 1:
			if (zeny < .@price) {
				mes "[Éleveur de Péco Péco]";
				mes "Comme je l'ai dit, le tarif est de "+.@price+" Zeny. Revenez quand vous aurez assez d'argent.";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Éleveur de Péco Péco]";
				mes "Comme je l'ai dit, vous devez avoir appris la compétence ^3333FFÉquitation^000000 pour pouvoir louer un Péco Péco.";
				close;
			}
			else if(checkriding()) {
				mes "[Éleveur de Péco Péco]";
				mes "Hum... N'êtes-vous pas déjà sur un Péco Péco ?? ...";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[Éleveur de Péco Péco]";
			mes "D'accord. Passez une bonne journée.";
			close;
		}
	} else {
		mes "Je suis désolé, mais les Péco Péco sont réservés aux Chevaliers et Seigneurs Chevaliers.";
		close;
	}
}

// Grand PecoPeco Breeder (for Crusaders)-------------------------------------------
prontera,232,318,3	script	Éleveur de Grands Péco#c	105,{
	if(Upper==0) set .@price,3500; //Normal Peco - default price
	if(Upper==1) set .@price,3500; //Armored Peco
	if(Upper==2) set .@price,3500; //Baby Peco

	mes "[Éleveur de Grands Péco Péco]";
	if (BaseJob == Job_Crusader) {
		if(Upper != 1 ) mes "Bienvenue, honorable Croisé."; 
		else mes "Bienvenue, honorable Paladin.";
		mes "Voudriez-vous chevaucher un Grand Péco Péco? Le prix de location est de "+.@price+" Zénys.";
		next;
		switch(select("Louer un Grand Péco Péco:Annuler")) {
		case 1:
			if (zeny < .@price) {
				mes "[Éleveur de Grands Péco Péco]";
				mes "Comme je l'ai dit, le tarif est de "+.@price+" Zeny. Revenez quand vous aurez assez d'argent.";
				close;
			}

			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Éleveur de Grands Péco Péco]";
				mes "Comme je l'ai dit, vous devez avoir appris la compétence ^3333FFÉquitation^000000 pour pouvoir louer un Grand Péco Péco.";
				close;
			}
			else if(checkriding()) {
				mes "[Éleveur de Grands Péco Péco]";
				mes "Hum... N'êtes-vous pas déjà sur un Grand Péco Péco ?? ...";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[Éleveur de Grands Péco Péco]";
			mes "D'accord. Passez une bonne journée.";
			close;
		}
	} else {
		mes "Je suis désolé, mais les Grands Péco Péco sont réservés aux Croisés et aux Paladins.";
		close;
	}
}

// Falcon Master--------------------------------------------------------------
hu_in01,381,304,5	script	Éleveur de Faucon#hnt	105,{
	if(Upper==0) set .@price,2500; //Normal Falcon - default price
	if(Upper==1) set .@price,2500; //Scarf Falcon
	if(Upper==2) set .@price,2500; //Baby Falcon

	mes "[Éleveur de Faucon]";
	if (BaseJob == Job_Hunter) {
		mes "Je peux entraîner un Faucon pour vous, mais le tarif est de "+.@price+" Zénys.";
		next;
		switch(select("Louer un Faucon:Annuler")) {
		case 1:
			if (zeny < .@price) {
				mes "[Éleveur de Faucon]";
				mes "Comme je l'ai dit, le tarif est de "+.@price+" Zeny. Revenez quand vous aurez assez d'argent.";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[Éleveur de Faucon]";
				mes "Je suis désolé, mais vous ne maîtrisez pas la compétence 'Domptage de Faucon'. Revenez quand vous l'aurez apprise.";
				close;
			}
			else if(checkfalcon()) {
				mes "[Éleveur de Faucon]";
				mes "Vous ne pouvez vous occuper de plus d'un Faucon à la fois.";
				close;
			}
			set zeny,zeny-.@price;
			setfalcon;
			close;
		case 2:
			mes "[Éleveur de Faucon]";
			mes "D'accord. Passez une bonne journée.";
			close;
		}
	} else {
		mes "Seuls les Chasseurs peuvent s'occuper d'un Faucon. Les Faucons sont très capricieux quant à la nature de leur maître, vous savez.";
		close;
	}
}
