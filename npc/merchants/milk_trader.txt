//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script =======================================
//= Milk Trader
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= [Aegis Conversion]
//= Trades bottles for milk
//===== Additional Comments: =================================
//= Fully working
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Raised the price to close zeny exploit [Lupus]
//= 1.2a Switched to Lupus's "loopless" technique.[kobra_k88]
//= 1.3 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.4 Implemented checkweight. [L0ne_W0lf]
//= 1.5 Fixed missed variable. (bugreport:1523) [L0ne_W0lf]
//============================================================

prontera,73,140,0	script	Laitier	86,{
	if (checkweight(1201,1) == 0) {
		mes "^3355FFAttendez!";
		mes "Je ne peux pas vous proposer mes services car vous portez actuellement trop d'objets sur vous. Allez en déposer dans votre Stock Kafra et revenez me voir~";
		close;
	}
	mes "[Laitier]";
	mes "Si vous m'apportez une Bouteille Vide et 15 Zénys, je vous donnerai une Bouteille de Lait en échange.";
	next;
	if (select("Donnez m'en le plus possible:Finalement, je n'ai besoin de rien") == 1) {
		if (countitem(713) <= 0) {
			mes "[Laitier]";
			mes "Ok, une bouteille vide s'il vous plaît! Une bouteille vide!";
			mes "... Oh... vous n'en avez pas...? Sans bouteille, pas de lait!";
			close;
		}
		set .@bottles,countitem(713);
		set .@total_weight,.@bottles * 50;
		set .@total_cost,.@bottles * 15;
		if (zeny < .@Total_cost) {
			mes "[Laitier]";
			mes "Oh, hé~!";
			mes "Désolé mais vous n'avez pas assez d'argent pour tout ça.";
			close;
		}
		if ((maxweight-weight) < .@total_weight) {
			mes "[Laitier]";
			mes "Hum...";
			mes "Vous me semblez un peu trop chargé. Faites un peu le ménage dans votre inventaire et revenez me voir après, ok?";
			close;
		}
		set zeny,zeny-.@total_cost;
		delitem 713,.@bottles; //Empty Bottles
		getitem 519,.@bottles; //Milk
		close;
	}
	close;
}
