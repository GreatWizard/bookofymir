//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Dye Maker
//===== By: ================================================== 
//= eAthena Dev Team
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//===== Additional Comments: ================================= 
//= 1.0 Fully working [kobra_k88]
//= 1.1 Spell Checked [Nexon] 
//= 1.2 Rescripted to Aegis 10.3 standard. [L0ne_W0lf]
//= 1.2b Fixed typos in variable names [ultramage]
//= 1.3 Made the checks more dynamic [ultramage]
//= 1.3b Fixed incorrect checking loop structure [ultramage]
//= 1.4 Added a checkweight. [L0ne_W0lf]
//============================================================ 

morocc_in,146,99,3	script	Java Dullihan	58,{
	if ((MaxWeight-Weight) < 200 || checkweight(1201,1) == 0) {
		mes "- Attendez! -";
		mes "- Vous portez déjà trop de -";
		mes "- choses sur vous. -";
		mes "- Allez vous décharger -";
		mes "- et revenez plus tard. -";
		close;
	}
	mes "[Teinturier Java Dullihan]";
	mes "Oh... Quelle belle journée nous avons aujourd'hui!";
	mes "Une journée idéale pour fabriquer des teintures...";
	next;
	switch(select("Parler:Fabriquer une Teinture:Annuler")) {
	case 1:
		mes "[Teinturier Java Dullihan]";
		mes "Je n'ai pas grand-chose à dire, vous savez...";
		next;
		mes "[Teinturier Java Dullihan]";
		mes "Mais si vous voulez connaître mon passé, je suis certain d'avoir quelques anecdotes à raconter.";
		next;
		mes "[Teinturier Java Dullihan]";
		mes "Voudriez-vous en écouter?";
		next;
		if (select("Bien sûr, pourquoi pas?:Peut-être une autre fois.") == 1) {
			mes "[Teinturier Java Dullihan]";
			mes "Mon père a toujours fabriqué des teintures, depuis ma plus tendre enfance...";
			mes "Je me rappelle encore de son visage, travaillant à la teinturerie toute la journée.";
			next;
			mes "[Teinturier Java Dullihan]";
			mes "Même pendant la maladie de ma mère, mon père a toujours continué à fabriquer des teintures sans jamais s'arrêter.";
			mes "Cela me mettait en colère! Comment pouvait-il travailler sans cesse sur ces fichues teintures?";
			next;
			mes "[Teinturier Java Dullihan]";
			mes "Je ne comprenais simplement pas sa manière de penser. Il me décevait beaucoup.";
			mes "L'acharnement de mon père m'a rendu allergique à la fabrication de teintures. J'ai fugué, et j'ai survécu en faisant des petits boulots.";
			next;
			mes "[Teinturier Java Dullihan]";
			mes "Et pourtant, comme vous pouvez le voir, j'ai finis par devenir moi-même un Fabricant de Teintures, tout comme mon père...";
			mes "Cela fait maintenant quinze ans que j'ai commencé à fabriquer ces satanées teintures...";
			next;
			mes "[Teinturier Java Dullihan]";
			mes "Maintenant que j'ai l'expérience du métier... je crois que je comprends enfin pourquoi mon père est tombé amoureux de son travail.";
			next;
			mes "[Teinturier Java Dullihan]";
			mes "Pourquoi il a tout sacrifié pour ce métier...";
			next;
			mes "[Teinturier Java Dullihan]";
			mes "Peut-être à cause de la beauté des couleurs, qui évoluent sans cesse...";
			mes "Peut-être parce qu'il voulait trouver sa couleur, une couleur parfaite...";
			next;
			mes "[Teinturier Java Dullihan]";
			mes "Je sais que j'ai la même intuition pour les couleurs... car... je travaille avec la même passion qui l'animait, quand il était encore en vie.";
			mes "Je suis capable de ressentir les couleurs à l'intérieur même de mon âme. Je sais que ce que je dis est difficile à appréhender, mais un jour vous comprendrez...";
			next;
			mes "[Teinturier Java Dullihan]";
			mes "Père, je fabrique ces teintures en pensant à toi...";
			close;
		}
		mes "[Teinturier Java Dullihan]";
		mes "Ahahahaha...";
			mes "Hahahaha... Je suppose que je ne peux pas vous en vouloir. Qui voudrait écouter la vie d'un vieux fabricant de teintures comme moi de toutes façons... Hahahaha...";
		close;

	case 2:
		mes "[Teinturier Java Dullihan]";
		mes "Très bien! Je fabriquerai la couleur exacte de vos rêves! Le tarif d'une teinture dépend de sa qualité.";
		mes "Mais ne soyez pas intimidé par les prix, toutes mes teintures sont des chefs d'oeuvre.";
		next;
		mes "[Teinturier Java Dullihan]";
		mes "Bref, quelle couleur souhaitez-vous?";
		next;
		switch(select("Teinture Rouge Écarlate:Teinture Jaune Citron:Teinture Bleu Cobalt:Teinture Vert Sapin:Teinture Orange:Teinture Violette:Teinture Blanche:Teinture Noire:Annuler")) {
		case 1: callsub S_MakeDye,1,1; break;
		case 2: callsub S_MakeDye,2,1; break;
		case 3: callsub S_MakeDye,3,2; break;
		case 4: callsub S_MakeDye,4,3; break;
		case 5: callsub S_MakeDye,5,4; break;
		case 6: callsub S_MakeDye,6,4; break;
		case 7: callsub S_MakeDye,7,4; break;
		case 8: callsub S_MakeDye,8,4; break;
		case 9:
			mes "[Teinturier Java Dullihan]";
			mes "Comment pouvez-vous changer d'avis aussi vite? Et beh...";
			close;
		}

	case 3:
		mes "[Teinturier Java Dullihan]";
		mes "Revenez quand vous voulez.";
		close;
	}

S_MakeDye:
	mes "[Teinturier Java Dullihan]";
	switch(getarg(0)) {
	case 1:
		mes "Hum... Pour fabriquer une Teinture Rouge Écarlate, j'aurais besoin de ^0000ff30 Herbes Rouges, 1 Antiagent et 1 Bouteille Vide^000000.";
		mes "Il y a également une participation financière de 3000 Zénys.";
		setarray .@item[0], 507, 973, 713;
		setarray .@count[0], 30,   1,   1;
		set .@cost,3000;
		set .@dyestuff,975;
		break;
	case 2:
		mes "Hum... Pour fabriquer une Teinture Jaune Citron, j'aurais besoin de ^0000ff30 Herbes Jaunes, 1 Antiagent, et 1 Bouteille Vide^000000.";
		mes "Il y a également une participation financière de 3000 Zénys.";
		setarray .@item[0], 508, 973, 713;
		setarray .@count[0], 30,   1,   1;
		set .@cost,3000;
		set .@dyestuff,976;
		break;
	case 3:
		mes "Hum... Pour fabriquer une Teinture Bleu Cobalt, j'aurais besoin de ^0000ff20 Herbes Bleues, 1 Antiagent, et 1 Bouteille Vide^000000.";
		mes "Les Herbes Bleues sont un peu plus difficiles à traiter, donc le tarif sera de 3500 Zénys.";
		setarray .@item[0], 510, 973, 713;
		setarray .@count[0], 20,   1,   1;
		set .@cost,3500;
		set .@dyestuff,978;
		break;
	case 4:
		mes "Hum... Pour fabriquer une Teinture Vert Sapin, j'aurais besoin de ^0000ff5 Herbes Bleues, 20 Herbes Vertes, 20 Herbes Jaunes, 1 Antiagent, 1 Mixture, et 1 Bouteille Vide^000000.";
		mes "Il y a beaucoup d'ingrédients, alors essayez de ne pas en oublier! Le tarif est de 5000 Zénys.";
		setarray .@item[0], 510, 511, 508, 974, 973, 713;
		setarray .@count[0],  5,  20,  20,   1,   1,   1;
		set .@cost,5000;
		set .@dyestuff,979;
		break;
	case 5:
		mes "Hum... Pour fabriquer une Teinture Orange, j'aurais besoin de ^0000ff20 Herbes Rouges, 20 Herbes Jaunes, 1 Antiagent, 1 Mixture, et 1 Bouteille Vide^000000.";
		mes "Le tarif est de 5000 Zénys.";
		setarray .@item[0], 507, 508, 974, 973, 713;
		setarray .@count[0], 20,  20,   1,   1,   1;
		set .@cost,5000;
		set .@dyestuff,980;
		break;
	case 6:
		mes "Hum... Pour fabriquer une Teinture Violette, j'aurais besoin de ^0000ff10 Herbes Bleues, 30 Herbes Rouges, 1 Antiagent, 1 Mixture, et 1 Bouteille Vide^000000.";
		mes "Le tarif est de 5000 Zénys.";
		setarray .@item[0], 510, 507, 974, 973, 713;
		setarray .@count[0], 10,  30,   1,   1,   1;
		set .@cost,5000;
		set .@dyestuff,981;
		break;
	case 7:
		mes "Hum... Pour fabriquer une Teinture Blanche, j'aurais besoin de ^0000ff30 Herbes Blanches, 1 Antiagent, et 1 Bouteille Vide^000000.";
		mes "Le prix est de 3000 Zénys.";
		setarray .@item[0], 509, 973, 713;
		setarray .@count[0], 30,   1,   1; 
		set .@cost,3000;
		set .@dyestuff,982;
		break;
	case 8:
		mes "Hum... Pour fabriquer une Teinture Noire, j'aurais besoin de ^0000ff30 Herbes Rouges, Jaunes et Vertes, 5 Herbes Bleues, 1 Antiagent, 1 Mixture, et 1 Bouteille Vide^000000.";
		mes "C'est la teinture la plus difficile et la plus longue à réaliser, donc le tarif est de 7000 Zénys.";
		setarray .@item[0], 507, 508, 511, 510, 974, 973, 713;
		setarray .@count[0], 30,  30,  30,   5,   1,   1,   1;
		set .@cost,7000;
		set .@dyestuff,983;
		break;
	}
	next;
	mes "[Teinturier Java Dullihan]";
	switch(getarg(1)) {
	case 1: mes "Voulez-vous que je commence la préparation?"; break;
	case 2: mes "Voulez-vous que je commence la préparation?"; break;
	case 3: mes "Voulez-vous que je commence la préparation?"; break;
	case 4: mes "Voulez-vous que je commence la préparation?"; break;
	}
	next;
	if (select("Fabriquer la Teinture:Annuler") == 1) {
		set .@size, getarraysize(.@item);
		// check requirements
		for( set .@i,0 ; .@i < .@size ; set .@i,.@i+1 )
			if( countitem(.@item[.@i]) < .@count[.@i] )
				break;
		if( .@i < .@size || Zeny < .@cost ) {
			mes "[Teinturier Java Dullihan]";
			mes "Hum... Vous n'avez pas tous les ingrédients ou l'argent requis pour la fabrication de la teinture. Revenez quand vous les aurez!";
			close;
		}
		// delete items
		for (set .@i, 0; .@i < .@size ; set .@i, .@i+1)
			delitem .@item[.@i], .@count[.@i];
		set zeny,zeny-.@cost;
		// get dyestuff
		getitem .@dyestuff,1;

		mes "[Teinturier Java Dullihan]";
		mes "Et voilà pour vous!";
		close;
	}
	mes "[Teinturier Java Dullihan]";
	mes "Comment pouvez-vous changer d'avis aussi vite? Et beh...";
	return;
}
