//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Alchemist Shop
//===== By: ================================================== 
//= eAthena Team
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Selling Alchemist Materials and Manuals
//===== Additional Comments: ================================= 
//= 1.1 fixed Medicine Bowl issue, thanx 2 MasterOfMuppets
//= 1.2 Reddozen's fixes of typos. added optional Elemental
//= Potion Guide. [Lupus]
//= 1.3 Deleted Elemental Potions Guide due to original quest [Lupus]
//= 1.4 Alchemists can now purchase 2000 Medicine Bowls at once. [SinSloth]
//= 1.5 Updated to Aegis 10.3 Standards. [L0ne_W0lf]
//= 1.6 Corrected canceling purchase. [L0ne_W0lf]
//============================================================ 

alde_alche,24,188,3	script	Gever Al Sharp	740,{
	if (checkweight(1201,1) == 0) {
		mes "- Attendez! -";
		mes "- Vous portez actuellement trop d'objets sur vous. -";
		mes "- Revenez après en avoir déposé dans votre Stock Kafra. -";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Bienvenue à la Guilde des Alchimistes.";
	mes "En quoi puis-je vous être utile?";
	next;
	switch(select("Acheter du matériel.:Acheter un manuel de fabrication.:Rien, merci.")) {
	case 1:
		mes "[Gever Al Sharp]";
		mes "De quoi avez-vous besoin?";
		next;
		if (select("Medicine Bowl - 8 Zenys:Rien, merci.") == 1) {
			mes "[Gever Al Sharp]";
			mes "Combien vous en faut-il?";
			mes "Dites '0' si vous ne souhaitez rien.";
			next;
			while(1) {
				input .@input,0,2001;
				if (.@input == 0) {
					mes "[Gever Al Sharp]";
					mes "D'accord, revenez quand vous voulez.";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					mes "[Gever Al Sharp]";
					mes "Le nombre entré doit être inférieur à 2000.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 8;
			set .@item_weight,.@input * 10;
			if (Zeny < .@sell) {
				mes "[Gever Al Sharp]";
				mes "Vous n'avez pas assez d'argent sur vous.";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				mes "[Gever Al Sharp]";
				mes "On dirait que vous ne pourrez pas porter tout ça! Faites un peu de place dans votre inventaire.";
				close;
			}
			set zeny,zeny-.@sell;
			getitem 7134,.@input; //Medicine_Bowl
			mes "[Gever Al Sharp]";
			mes "Merci.";
			mes "A bientôt.";
			close;
		}
		mes "[Gever Al Sharp]";
		mes "Très bien, revenez quand vous voulez!";
		close;
	case 2:
		mes "[Gever Al Sharp]";
		mes "Que cherchez-vous?";
		mes "Les manuels sont généralement à 100000 Zenys, sauf pour certains exemplaires assez rares.";
		next;
		switch(select("Manuel de Fabrication des Potions:Manuel de Fabrication d'Alcool:Manuel de Fabrication de Grenades:Manuel de Fabrication de Bouteilles d'Acides:Manuel de Fabrication de Plantes en Bouteilles:Manuel de Fabrication de Marine Sphere:Manuel de Fabrication de Glistening Coat:Manuel de Fabrication de Potions Concentrées:Rien, merci.")) {
		case 1:
			callsub S_SellManual,7144,100000; //Normal_Potion_Book
			break;
		case 2:
			callsub S_SellManual,7127,100000; //Alcol_Create_Book
			break;
		case 3:
			callsub S_SellManual,7128,100000; //FireBottle_Create_Book
			break;
		case 4:
			callsub S_SellManual,7129,100000; //Acid_Create_Book
			break;
		case 5:
			callsub S_SellManual,7130,100000; //Plant_Create_Book
			break;
		case 6:
			callsub S_SellManual,7131,100000; //Mine_Create_Book
			break;
		case 7:
			callsub S_SellManual,7132,100000; //Coating_Create_Book
			break;
		case 8:
			callsub S_SellManual,7133,240000; //Slim_Potion_Create_Book
			break;
		case 9:
			mes "[Gever Al Sharp]";
			mes "Revenez une prochaine fois!";
			close;
		}
	case 3:
		mes "[Gever Al Sharp]";
		mes "Très bien, alors bonne journée.";
		close;
	}

S_SellManual:
	if (Zeny < getarg(1)) {
		mes "[Gever Al Sharp]";
		mes "Vous n'avez pas assez d'argent.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Un "+getitemname(getarg(0))+"?";
	mes "Cela vous coûtera "+getarg(1)+" Zenys.";
	next;
	if (select("Acheter.:Annuler.") == 1) {
		set zeny,zeny-getarg(1);
		getitem getarg(0),1;
		mes "[Gever Al Sharp]";
		mes "Merci de votre achat!";
		mes "A bientôt.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Revenez une prochaine fois!";
	close;
}
