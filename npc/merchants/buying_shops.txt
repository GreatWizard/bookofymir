//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//===== eAthena Script =======================================
//= Buying Shop Licenses
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= Buying Shop Licenses
//===== Additional Comments: =================================
//= 1.0 First version. [Kisuka]
//============================================================ 

// Black Marketeer (Buy Licenses - Non-Merchant Classes)
//============================================================ 
que_job01,68,84,1	script	Trafiquant::Black Marketeer#Buying	881,{
	if(getskilllv("ALL_BUYING_STORE") == 1) {
		mes "[M. Jass]";
		mes "Hé! T'as déjà passé un contrat avec Hugh! Je n'ai rien à te dire, "+ ( sex ? "mec" : "ma grande" ) +"!";
		close;
	}else{
		mes "[M. Jass]";
		mes "Tu dois rechercher quelque chose à tout prix si tu es venu"+ ( sex ? "" : "e" ) +" me voir. Qu'est-ce que tu me veux?";
		next;
		switch(select("Une licence de magasin d'occasion:Qui êtes-vous?:Euh... Non, rien!")) {
		case 1:
			mes "[M. Jass]";
			mes "Je le savais! Bien sûr, je peux t'en faire une! Regarde, la mienne a l'air aussi vraie que celle que cet enfoiré de Hugh donne aux marchands!";
			next;
			mes "[M. Jass]";
			mes "Et ma licence et vachement mieux, parce qu'on n'a pas besoin d'apprendre de compétences à deux balles. T'en veux combien?";
			next;
			mes "[M. Jass]";
			mes "Je peux t'en faire en petites quantités, genre jusqu'à 10 à la fois. Comme ça t'es au courant. Ça t'en coûtera 500 zénys pièce. Alors, t'en veux combien?";
			next;
			while(1) {
				input .@input;
				mes "[M. Jass]";
				if(.@input == 0) {
					mes "Tu ne veux pas acheter de licence?";
					close;
				}
				else if(.@input > 10) {
					mes "Je viens de te dire que je ne peux en faire que 10 à la fois!";
					next;
				}
				else {
					mes "Bon, ça va t'en coûter "+(.@input*500)+" zénys.";
					next;
					mes "[M. Jass]";
					if(Zeny < (.@input*500)) {
						mes "Oh, mais tu n'as pas le fric!";
						next;
						mes "[M. Jass]";
						mes "Tout a un prix, tu sais? C'est les bases! Si tu veux quelque chose, il faut sortir le porte-feuille!";
					}else{
						mes "Ha... Ha ha ha!";
						mes "Hugh, je vais couler ton business avec les licences, tu vas voir!";
						mes "*Rigole*";
						getitem 12548,.@input;	// Shabby_Purchase_Street
						set Zeny,Zeny-(.@input*500);
					}
					close;
				}
			}

		case 2:
			mes "[M. Jass]";
			mes "Je me suis barré de chez moi il y a un bout de temps... Et puis, ça te sert à quoi de demander? Tout ce qui me reste, c'est les cicatrices que m'a laissé la vie.";
			next;
			mes "[M. Jass]";
			mes "...";
			mes "Hugh est un marchand sans morale qui ne connait rien au business. Mon seul but dans la vie, c'est de lui faire mordre la poussière!";
			next;
			mes "[M. Jass]";
			mes "Aaah, J'ai trop picolé... (*Hic*)";
			mes "C'est l'alcool qui parle. Oublie ce que je viens de dire.";
			close;
		case 3:
			mes "[M. Jass]";
			mes "Ok, ok, pas la peine de gueuler. Vas voir ailleurs si j'y suis si t'es pas là pour le business!";
			close;
		}
	}
}

// Purchasing Team (Learn Skill - Merchant Classes)
//============================================================ 
alberta_in,58,52,4	script	Service de Rachat::Purchasing Team#Buying	59,{
	if(BaseClass == Job_Merchant && getskilllv("MC_VENDING") >= 1) {
		if(getskilllv("ALL_BUYING_STORE") == 1) {
			mes "[M. Hugh]";
			mes "Je m'appelle Hugh, du service de rachat. Que puis-je faire pour vous aujourd'hui?";
			next;
			if(select("Licence de magasin d'occasions.:Quitter la conversation.") == 2) {
				mes "[M. Hugh]";
				mes "Bien, revenez me voir si vous avez besoin d'une licence pour ouvrir un magasin d'occasions. A une prochaine fois!~";
				close;
			}
			mes "[M. Hugh]";
			mes "Chaque licence coûte 200 zénys, et vous pouvez en acheter jusqu'à 50 à la fois. Combien vous en faut-il?";
			next;
			while(1) {
				input .@input;
				mes "[M. Hugh]";
				if(.@input == 0) {
					mes "Oh, vous n'en voulez pas finalement? Tant pis. Bonne "+ ( isday() ? "journée" : "soirée" ) +".";
					close;
				}
				if(.@input > 50) {
					mes "Je vous ai prévenu"+ ( sex ? "" : "e" ) +": je ne peux pas en vendre plus de 50 à la fois.";
					next;
				}
				else{
					mes .@input+" licences? Il vous en coûtera "+(.@input*200)+" zénys.";
					next;
					mes "[M. Hugh]";
					if(Zeny < (.@input*200)) {
						mes "Oh mais vous semblez ne pas avoir assez d'argent! Revenez me voir quand vous l'aurez.";
					}else{
						mes "Et voilà! Merci pour votre soutien!";
						getitem 6377,.@input;	// Buy_Stall_Permit
						set Zeny,Zeny-(.@input*200);
					}
					close;
				}
			}
		}else{
			mes "[M. Hugh]";
			mes "Je m'appelle Hugh. Je suis du Service de Rachat de la Guilde des Marchands d'Alberta.";
			mes "Vous êtes...";
			next;
			mes "[M. Hugh]";
			mes "Saviez-vous que notre guilde autorise maintenant ses membres à ouvrir des magasins pour racheter des objets aux autres?";
			next;
			select("Acheter n'a jamais été un problème...");
			mes "[M. Hugh]";
			mes "Vous avez raison, mais posez-vous la question: n'avez-vous jamais eu de problèmes pour racheter des objets en grande quantité? Vous savez... pour trouver et marchander avec tous ceux qui ont ce que vous vouliez racheter? Pas si facile, n'est-ce pas?";
			next;
			mes "[M. Hugh]";
			mes "Remplir nos stocks est quelque chose de primordial pour nous autres, les marchands. Mes dix années d'expérience dans le rachat de marchandises m'ont permis d'établir un plan pour la guilde.";
			next;
			mes "[M. Hugh]";
			mes "'Laisser chaque marchand ouvrir un magasin rachetant les occasions!'";
			mes "C'est le titre du mémo que je leur ai envoyé. Vous comprendrez en le lisant...";
			next;
			mes "[M. Hugh]";
			mes "...";
			mes "...(M. Hugh vous assomme avec tous les détails.)";
			next;
			select("Euh... Oui, et donc...?");
			mes "[M. Hugh]";
			mes "Oh, oui. Pour résumer, vous pourrez racheter certains objets en masse dans votre échoppe.";
			next;
			mes "[M. Hugh]";
			mes "Mais pour ça, il vous faudra une licence délivrée par la Guilde des Marchands.";
			next;
			mes "[M. Hugh]";
			mes "Il vous la faudra chaque fois que vous ouvrez votre échoppe. Nous nous attendons à augmenter de façon significative nos projets grâce à ce nouveau type de licence.";
			next;
			mes "[M. Hugh]";
			mes "Voilà à quoi se résume ma proposition! Notre Président était si heureux d'apprendre que ça nous permettrait d'engranger un bon paquet d'argent...";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Et donc, le but est...?";
			next;
			mes "[M. Hugh]";
			mes "Ne soyez pas si impatient"+ ( sex ? "" : "e" ) +", d'accord? En gros, nous pouvons vous laisser ouvrir un magasin de rachat si vous avez déjà une Échoppe.";
			next;
			mes "[M. Hugh]";
			mes "Bien sûr, nous devrons vous demander une participation de 10.000 zénys lors de votre inscription. Il vous faudra aussi une ^4A4AFFLicence de Magasin d'Occasion^00000 chaque fois que vous ouvrez une échoppe.";
			next;
			mes "[M. Hugh]";
			mes "Bon, est-ce que vous voulez vous inscrire maintenant? Si vous le faites, je vous montrerai comment ouvrir un magasin pour racheter des occasions.";
			next;
			if(select("S'inscrire.:Annuler.") == 2) {
				mes "[M. Hugh]";
				mes "Oh, je suis déçu! Ouvrir un magasin pour racheter des objets d'occasion ne pourrait qu'être profitable à votre commerce, mais c'est vous qui voyez. Je serai toujours là si vous changez d'avis.";
				close;
			}
			mes "[M. Hugh]";
			if(Zeny < 10000) {
				mes "Il y a 10.000 zénys de frais d'inscription. Assurez-vous de les avoir avant de tenter à nouveau de vous inscrire.";
				close;
			}else{
				mes "Vous avez fait le bon choix. Bon, donnez-moi l'argent et signez ici...";
				next;
				input .@name$;
				mes "[M. Hugh]";
				mes .@name$+"....";
				mes "J'aime bien votre écriture... Bon, vous êtes maintenant autorisé"+ ( sex ? "" : "e" ) +" à tenir une échoppe pour racheter des objets d'occasion.";
				set Zeny,Zeny-10000;
				getitem 6377,5;	// Buy_Stall_Permit
				skill "ALL_BUYING_STORE",1,0;
				next;
				mes "[M. Hugh]";
				mes "A l'heure actuelle, seuls les objets classiques peuvent être achetés, ^8C2121et non^000000 l'équipement, certaines potions, et les objets faits mains. Cela reste cependant très profitable si vous savez comment en tirer partie.";
				next;
				mes "[M. Hugh]";
				mes "Oh, et il vous faudra au moins un exemplaire de l'objet que vous voulez acheter dans votre inventaire, car vous devrez vous en servir pour le montrer aux autres dans votre échoppe.";
				next;
				mes "[M. Hugh]";
				mes "Vous devriez maintenant trouver la nouvelle compétence parmi les autres. Si vous n'arrivez pas à la trouver, il vous faudra certainement minimiser votre fenêtre de compétences, et regarder le volet des compétences des Classes 3.";
				next;
				mes "[M. Hugh]";
				mes "Je vous ai aussi donné 5 licences afin que vous puissiez essayer. Bon business!";
				close;
			}			
		}
	}else{
		mes "[M. Hugh]";
		mes "Je m'appelle Hugh. Je suis du Service de Rachat de la Guilde des Marchands d'Alberta.";
		mes "J'aime bavarder, mais je suis occupé en ce moment.";
		next;
		mes "[M. Hugh]";
		mes "Je consacre tout mon temps à mes clients, dans le commerce.";
		close;	
	}
}
