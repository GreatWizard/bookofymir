//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena, No Healing
//===== eAthena Script ======================================= 
//= Hair Dyer
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Hair Dyer with standard palletes
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Bugfix: fixed missing menu label LEnd->M_End [Lupus]
//= 1.2 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.3 Fixed problem what "freezes" the NPC. (bugreport:509) [Samuray22]
//= 1.4 Fixed getlook checking the wrong color. (bugreport:2392) [L0ne_W0lf]
//= 1.5 Added Lighthalzen Hair Dying NPC. [L0ne_W0lf]
//============================================================ 

prt_in,243,168,4	script	Jovovich	91,{
	mes "[Coiffeur-Teinturier Jovovich]";
	mes "Bienvenue dans mon salon de Coiffure & Teinture~!";
	mes "En quoi puis-je vous aider?";
	next;
	mes "[Coiffeur-Teinturier Jovovich]";
	if (sex) mes "Eh bien, dites!... vos cheveux n'ont pas l'air en très bon état. Ils ont sûrement besoin d'un soin spécial. Venez, asseyez-vous.";
	else {
		if (rand(20) > 11) {
			mes "Et ben dis donc... vos cheveux ressemblent à de la paille! Vous ne pouvez pas les laisser comme ça!";
			next;
			mes "[Coiffeur-Teinturier Jovovich]";
			mes "Ils ont sûrement besoin d'un soin spécial. Venez, asseyez-vous.";
		}
		else mes "Wahou! Vos cheveux ont une couleur... terne~ Il faudrait peut-être songer à les teindre?";
	}
	next;
	while(1) {
		switch(select("Teindre ses Cheveux:Conseils sur les Cheveux:Annuler")) {
		case 1:
			mes "[Coiffeur-Teinturier Jovovich]";
			mes "Hohoho~ C'est un très bon choix~~ Il est toujours bon d'apporter un peu de renouveau à ses cheveux grâce aux teintures !";
			next;
			mes "[Coiffeur-Teinturier Jovovich]";
			mes "Choisissez une couleur qui vous convient.";
			next;
			while(1) {
				if (.@choose_success == 1) {
					mes "[Coiffeur-Teinturier Jovovich]";
					mes "Qu'en pensez-vous? Souhaitez-vous choisir une autre couleur?";
					next;
					if (select("Oui:Non") == 1) {
						mes "[Coiffeur-Teinturier Jovovich]";
						mes "D'accord! Choisissez une couleur qui vous convient.";
						next;
					}
					else  {
						mes "[Coiffeur-Teinturier Jovovich]";
						mes "Hum, je suis un peu déçu, je ne vous le cache pas. J'espérais pouvoir vous donner un meilleur rendu. Mais promis, la prochaine fois je ferai encore mieux! Revenez quand vous voulez~";
						close;
					}
				}

				while(1) {
					switch(select("Rouge.:Jaune.:Violet.:Orange.:Vert.:Bleu.:Blanc.:Noir.:En fait, j'aime bien ma couleur actuelle.")) {
					case 1: set .@headpalette,8; break;
					case 2: set .@headpalette,1; break;
					case 3: set .@headpalette,2; break;
					case 4: set .@headpalette,3; break;
					case 5: set .@headpalette,4; break;
					case 6: set .@headpalette,5; break;
					case 7: set .@headpalette,6; break;
					case 8: set .@headpalette,7; break;
					case 9:
						if (.@choose_success != 0) {
							mes "[Coiffeur-Teinturier Jovovich]";
							mes "Vous devez vraiment aimer votre couleur actuelle~";
							close;
						}
						mes "[Coiffeur-Teinturier Jovovich]";
						mes "Hein~? Vous ne voulez pas teindre vos cheveux? Je suis déçu...";
						close;
					}

					if (.@headpalette == getlook(VAR_HEADPALETTE)) {
						mes "[Coiffeur-Teinturier Jovovich]";
						mes "Eh? Mais c'est déjà votre couleur, ça. Choisissez-en une autre s'il vous plaît.";
						next;
					}
					else {
						switch(.@headpalette) {
						case 1: callsub S_NoDye,976,"jaune"; break;
						case 2: callsub S_NoDye,978,"violet"; break;
						case 3: callsub S_NoDye,979,"orange"; break;
						case 4: callsub S_NoDye,980,"vert"; break;
						case 5: callsub S_NoDye,981,"bleu"; break;
						case 6: callsub S_NoDye,982,"blanc";  break;
						case 7: callsub S_NoDye,983,"noir"; break;
						Case 8: callsub S_NoDye,975,"rouge"; break;
						}
						if (zeny < 1000) {
							mes "[Coiffeur-Teinturier Jovovich]";
							mes "Le tarif est de 1000 Zénys. Vous n'avez pas assez d'argent...?";
							close;
						}

						switch(.@headpalette) {
						case 1: delitem 976,1; break; //Lemon_Dyestuffs
						case 2: delitem 978,1; break; //Violet_Dyestuffs
						case 3: delitem 979,1; break; //Orange_Dyestuffs
						case 4: delitem 980,1; break; //Darkgreen_Dyestuffs
						case 5: delitem 981,1; break; //Cobaltblue_Dyestuffs
						case 6: delitem 982,1; break; //White_Dyestuffs
						case 7: delitem 983,1; break; //Black_Dyestuffs
						case 8: delitem 975,1; break; //Scarlet_Dyestuffs
						}
						set Zeny,Zeny-1000;
						setlook VAR_HEADPALETTE,.@headpalette;
						set .@choose_success,1;
						break;
					}

				}

			}

		case 2:
			mes "[Coiffeur-Teinturier Jovovich]";
			mes "Quand vous avez le cafard, ou que vous êtes simplement lassé... si vous voulez avoir l'air sexy, ou juste avoir meilleure mine...";
			next;
			mes "[Coiffeur-Teinturier Jovovich]";
			mes "Changez de couleur de cheveux, et vous reprendrez confiance en vous, en apportant un peu de piment dans votre quotidien !!";
			next;
			mes "[Coiffeur-Teinturier Jovovich]";
			mes "Pour la modique somme de 1000 zeny et de la teinture appropriée, je peux teindre vos cheveux dans la couleur de vos rêves !";
			next;
			mes "[Coiffeur-Teinturier Jovovich]";
			mes "* Tarifs *";
			mes "- 1 Teinture de la couleur choisir.";
			mes "- 1000 Zénys.";
			break;

		case 3:
			mes "[Coiffeur-Teinturier Jovovich]";
			mes "Je pense que... tout être humain a le droit de vouloir s'embellir....";
			close;
		}

	}

S_NoDye:
	if (countitem(getarg(0)) == 0) {
		mes "[Coiffeur-Teinturier Jovovich]";
		mes "Hum... Vous devez fournir un "+getitemname(getarg(0))+" pour que je puisse teindre vos cheveux en "+getarg(1)+"...";
		close;
	}
	return;
}


// Lighthalzen
lhz_in02,100,134,3	script	Coiffeuse::Hair Dyer#lich	850,{
	mes "[Rossa]";
	if (Sex == 1) {
		mes "Bienvenue, entrez~";
		mes "Oh, je vois que vous prenez bien mieux soin de vos cheveux que la moyenne des hommes!";
		mes "Est-ce que vous voudriez les teindre en une autre couleur?";
	}
	else {
		mes "Oh, ouah~ Qui vous a fait cette coupe? J'adore! Mais... Ça vous irait mieux encore si jamais vous vous faisiez une couleur. Qu'en pensez-vous?";
	}
	while (.@choose_success != 2) {
		next;
		switch(select("Se teindre les cheveux:Informations sur les couleurs:Annuler")) {
		case 1:
			mes "[Rossa]";
			mes "Ho ho ho ho~";
			mes "Alors, vous voudriez essayer quelle couleur? Quelque chose de voyant ou de sombre? De sexy ou de mignon?";
			next;
			while (.@choose_success != 2) {
				if (.@choose_success == 1) {
					mes "[Rossa]";
					mes "Ooh, j'aime cette couleur. Mais voudriez-vous en essayer une autre?";
					next;
					switch(select("Oui:Non")) {
					case 1:
						mes "[Rossa]";
						mes "Choisissez une autre couleur s'il vous plaît~";
						next;
						break;
					case 2:
						mes "[Rossa]";
						mes "Un excellent choix~";
						mes "Hé bien, merci d'avoir utilisé mes services. J'espère que vous reviendrez!";
						close2;
						set .@choose_success,2;
						break;
					}
				}
				while (.@choose_success != 2) {
					switch(select("Rouge, s'il vous plaît.:Blond, s'il vous plaît.:Violet, s'il vous plaît.:Orange, s'il vous plaît.:Vert, s'il vous plaît.:Bleu, s'il vous plaît.:Blanc, s'il vous plaît.:Brun, s'il vous plaît.:J'aime ma couleur actuelle.")) {
					case 1:
						set .@headpalette,8;
						break;
					case 2:
						set .@headpalette,1;
						break;
					case 3:
						set .@headpalette,2;
						break;
					case 4:
						set .@headpalette,3;
						break;
					case 5:
						set .@headpalette,4;
						break;
					case 6:
						set .@headpalette,5;
						break;
					case 7:
						set .@headpalette,6;
						break;
					case 8:
						set .@headpalette,7;
						break;
					case 9:
						if (.@choose_success != 0) {
							mes "[Rossa]";
							mes "Êtes-vous sûr"+ ( sex ? "" : "e" ) +"? Bien. Vous savez ce qui vous va le mieux après tout, et à dire vrai, je suis d'accord avec votre choix~";
							close2;
							set .@choose_success,2;
						}
						else {
							mes "[Rossa]";
							mes "Oh, je vois. Mais je ne peux pas m'empêcher de me sentir déçue. Ça vous irait si bien si vous vous faisiez une couleur~";
							close2;
							set .@choose_success,2;
						}
						break;
					}
					if (getlook(VAR_HEADPALETTE) == .@headpalette) {
						mes "[Rossa]";
						mes "Hum, votre couleur tient toujours, il n'y a pas besoin de la refaire, si c'et ce que vous vous demandiez.";
						next;
					}
					else {
						if ((.@headpalette == 8) && (countitem(975) == 0)) {
							mes "[Rossa]";
							mes "Oh, je suis désolée, mais je ne vais pas pouvoir vous teindre les cheveux si vous ne m'avez pas apporté de Teinture Rouge Écarlate...";
							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 1) && (countitem(976) == 0)) {
							mes "[Rossa]";
							mes "Oh, je suis désolée, mais je ne vais pas pouvoir vous teindre les cheveux si vous ne m'avez pas apporté de Teinture Jaune Citron...";
							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 2) && (countitem(981) == 0)) {
							mes "[Rossa]";
							mes "Oh, je suis désolée, mais je ne vais pas pouvoir vous teindre les cheveux si vous ne m'avez pas apporté de Teinture Violette...";
							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 3) && (countitem(980) == 0)) {
							mes "[Rossa]";
							mes "Oh, je suis désolée, mais je ne vais pas pouvoir vous teindre les cheveux si vous ne m'avez pas apporté de Teinture Orange...";
							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 4) && (countitem(979) == 0)) {
							mes "[Rossa]";
							mes "Oh, je suis désolée, mais je ne vais pas pouvoir vous teindre les cheveux si vous ne m'avez pas apporté de Teinture Vert Sapin...";
							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 5) && (countitem(978) == 0)) {
							mes "[Rossa]";
							mes "Oh, je suis désolée, mais je ne vais pas pouvoir vous teindre les cheveux si vous ne m'avez pas apporté de Teinture Bleu Cobalt...";
							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 6) && (countitem(982) == 0)) {
							mes "[Rossa]";
							mes "Oh, je suis désolée, mais je ne vais pas pouvoir vous teindre les cheveux si vous ne m'avez pas apporté de Teinture Blanche...";
							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 7) && (countitem(983) == 0)) {
							mes "[Rossa]";
							mes "Oh, je suis désolée, mais je ne vais pas pouvoir vous teindre les cheveux si vous ne m'avez pas apporté de Teinture Noire...";
							close2;
							set .@choose_success,2;
							break;
						}
						if (Zeny < 1000) {
							mes "[Rossa]";
							mes "Oh, je suis désolée "+ ( sex ? "mon cher" : "ma chère" ) +", mais mes services coûtent 1.000 zénys. Est-ce que vous avez oublié de prendre l'argent avec vous?";
							close2;
							set .@choose_success,2;
							break;
						}
						if (.@headpalette == 8) delitem 975,1; //Scarlet_Dyestuffs
						else if (.@headpalette == 1) delitem 976,1; //Lemon_Dyestuffs
						else if (.@headpalette == 2) delitem 981,1; //Violet_Dyestuffs
						else if (.@headpalette == 3) delitem 980,1; //Orange_Dyestuffs
						else if (.@headpalette == 4) delitem 979,1; //Darkgreen_Dyestuffs
						else if (.@headpalette == 5) delitem 978,1; //Cobaltblue_Dyestuffs
						else if (.@headpalette == 6) delitem 982,1; //White_Dyestuffs
						else if (.@headpalette == 7) delitem 983,1; //Black_Dyestuffs
						set zeny,zeny-1000;
						setlook VAR_HEADPALETTE,.@headpalette;
						set .@choose_success,1;
						break;
					}
				}
			}
			break;
		case 2:
			mes "[Rossa]";
			mes "Lorsque vous avez une baisse de régime, lorsque vous voulez vous faire "+ ( sex ? "beau pour celui" : "beau pour celle" ) +" que vous aimez, ou tout simplement lorsque vous voulez changer de look, pourquoi ne pas vous faire une teinture?";
			next;
			mes "[Rossa]";
			mes "Tout ce dont vous avez besoin, c'est de Teinture de la couleur que vous voulez, et de 1.000 zénys pour payer mes services. Je suis là pour m'occuper de vous et vous rendre encore plus "+ ( sex ? "beau" : "belle" ) +"~";
			break;
		case 3:
			mes "[Rossa]";
			mes "Vous savez, en soignant un peu votre apparence, vous n'avez pas seulement l'air plus "+ ( sex ? "beau" : "belle" ) +", mais vous vous sentez aussi mieux. Ça se tient, non?";
			close;
		}
	}
	close;
}
