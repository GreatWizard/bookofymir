//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Geffen Guides
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.4a
//===== Compatible With: ===================================== 
//= eAthena  SVN
//===== Description: ========================================= 
//= [Aegis COnversion]
//= Guides for the City of Geffen
//===== Additional Comments: ================================= 
//= Fully working.  Added a guide at every exit.
//= v1.1 Now using duplicate command.
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.2a Small note that the BS guild moved to Einbroch [Poki#3]
//= 1.3 Removed Duplicates [Silent]
//= 1.4a Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.4 Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//============================================================

geffen,203,116,0	script	Guide#gef::GefGuide	705,{
	cutin "gef_soldier",2;
	mes "[Guide de Geffen]";
	mes "Bienvenue à Geffen, la Cité de la Magie.";
	next;
	mes "[Guide de Geffen]";
	mes "N'hésitez pas à me demander des renseignements si vous cherchez quelque chose de précis en ville, ou si vous voulez juste des informations touristiques!";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Guide de la Ville:Effacer les Informations de la Mini-Carte:Astuce:Annuler")) {
		case 1:
			mes "[Guide de Geffen]";
			mes "Sélectionnez un lieu spécifique dans le menu suivant.";
			if (compass_check == 0) {
				mes "Voulez-vous que je laisse des indications sur votre Mini-Carte?";
				next;
				if (select("Oui:Non") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
//				next;
				switch(select("^FF0000Académie de Magie^000000:Atelier des Forgerons:Armurerie:Boutique:Bar:Auberge:Tour de Geffen:Annuler")) {
				case 1:
					mes "[Guide de Geffen]";
					mes "'L'Académie de Magie est le lieu de formation des théoriciens de la magie ainsi que de nombreux magiciens.";
					if (.@compass_check)
						viewpoint 1,61,180,2,0xFF0000;
					break;
				case 2:
					mes "[Guide de Geffen]";
					mes "L'Atelier des Forgerons est situé au Sud Est de la Tour.";
					mes "Notez bien que le lieu de formation des Forgerons a déménagé depuis peu à Einbroch.";
					if (.@compass_check)
						viewpoint 1,182,59,3,0x00FF00;
					break;
				case 3:
					mes "[Guide de Geffen]";
					mes "Vous trouverez à l'Armurerie de nombreuses armes et armures pour votre usage personnel.";
					if (.@compass_check)
						viewpoint 1,99,140,4,0xFF00FF;
					break;
				case 4:
					mes "[Guide de Geffen]";
					mes "La Boutique offre toutes sortes de consommables.";
					if (.@compass_check)
						viewpoint 1,44,86,5,0xFF00FF;
					break;
				case 5:
					mes "[Guide de Geffen]";
					mes "Le Bar est le lieu idéal pour faire des rencontres et s'amuser!";
					if (.@compass_check)
						viewpoint 1,138,138,6,0xFF00FF;
					break;
				case 6:
					mes "[Guide de Geffen]";
					mes "Vous pourrez vous reposer à l'Auberge durant tout votre séjour à Geffen.";
					if (.@compass_check)
						viewpoint 1,172,174,7,0xFF00FF;
					break;
				case 7:
					mes "[Guide de Geffen]";
					mes "Les souterrains de la Tour de Geffen sont envahis de monstres...";
					mes "Mais les étages supérieurs sont sûrs, et abritent la ^0000FFGuilde des Sorciers^000000. Les Mages peuvent s'y rendre afin de devenir Sorciers.";
					if (.@compass_check)
						viewpoint 1,120,114,8,0x00FF00;
					break;
				case 8:
					mes "[Guide de Geffen]";
					mes "N'hésitez pas à me demander si vous voulez que j'efface les informations de votre Mini-Carte.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,2,0xFF0000;
			viewpoint 2,237,41,3,0x00FF00;
			viewpoint 2,46,345,4,0xFF00FF;
			viewpoint 2,175,220,5,0xFF00FF;
			viewpoint 2,134,221,6,0xFF00FF;
			viewpoint 2,204,214,7,0xFF00FF;
			viewpoint 2,204,214,8,0x00FF00;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Guide de Geffen]";
  			mes "Votre Mini-Carte est située en haut à droite de votre écran.";
			mes "Si vous ne la voyez pas, pressez Ctrl + Tab pour la rendre plus opaque.";
			next;
			mes "[Guide de Geffen]";
 			mes "Sur votre Mini-Carte, cliquez sur '+' ou '-' pour modifier le zoom.";
			mes "Nous espérons que vous apprécierez votre séjour à Geffen.";
			break;
		case 4:
			mes "[Guide de Geffen]";
			mes "Très bien, aventurier. J'espère que vos voyages seront agréables où que vous alliez.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "gef_soldier",255;
	end;
}

geffen,118,62,0	duplicate(GefGuide)	Guide#2gef	705