//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Einbroch Guides
//===== By: ================================================== 
//= Muad_dib; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Guides for the City of Einbroch
//===== Additional Comments: ================================= 
//= 1.0b missing tabs, fixed Guide coords [Lupus]
//= 1.1 added 1 more Guide NPC, thanx to Justin84 [Lupus]
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.4 Removed Duplicates [Silent]
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//============================================================

einbroch,72,202,4	script	Guide#ein::EinGuide	852,{
	cutin "ein_soldier",2;
	mes "[Guide d'Einbroch]";
	mes "Bienvenue à Einbroch, la Cité de l'Acier.";
	mes "N'hésitez pas à vous adresser à moi si vous avez des questions.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Guide de la Ville:Effacer les Informations de la Mini-Map:Astuce:Annuler")) {
		case 1:
			mes "[Guide d'Einbroch]";
			mes "Sélectionnez un lieu spécifique dans le menu suivant.";
			if (.@compass_check == 0) {
				mes "Voulez-vous que j'indique ces emplacements sur votre Mini-Map?";
				next;
				if (select("Oui:Non") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				switch(select("^FF0000Aéroport^000000:Gare Ferroviaire:Usine:Plaza:Hôtel:Armurerie:Laboratoire:Guilde des Forgerons:Tour d'Einbroch:Annuler")) {
				case 1:
					mes "[Guide d'Einbroch]";
					mes "L'^FF0000Aéroport^000000 est situé au Nord Ouest de la ville.";
					mes "Vous pourrez y contempler la fierté de notre ville, le Zeppelin.";
					mes "Attention, le trajet en Zeppelin est payant.";
					if (.@compass_check)
						viewpoint 1,63,228,2,0xFF0000;
					break;
				case 2:
					mes "[Guide d'Einbroch]";
					mes "La Gare Ferroviaire est située au Nord Est d'Einbroch.";
					mes "De là, vous pouvez prendre le train pour Einbech. Il y a des trajets tous les jours.";
					if (.@compass_check)
						viewpoint 1,236,279,3,0xFF00FF;
					break;
				case 3:
					mes "[Guide d'Einbroch]";
					mes "L'Usine est probablement le lieu le plus important d'Einbroch, et est située au Sud de la ville.";
					if (.@compass_check)
						viewpoint 1,158,78,4,0xFF00FF;
					break;
				case 4:
					mes "[Guide d'Einbroch]";
					mes "Le Plaza est notre plus grand centre commercial, et se situe un peu à l'Est du centre ville.";
					if (.@compass_check)
						viewpoint 1,232,190,5,0xFF00FF;
					break;
				case 5:
					mes "[Guide d'Einbroch]";
					mes "L'hôtel est situé à l'Est du Plaza, et offre de nombreux services.";
					mes "Vous pourrez bien évidement y demeurer pendant tout votre séjour à Einbroch.";
					if (.@compass_check)
						viewpoint 1,260,201,6,0x00FF00;
					break;
				case 6:
					mes "[Guide d'Einbroch]";
					mes "L'Armurerie est située au Nord du Plaza.";
					mes "Vous pourrez y acheter armes et armures pour votre usage personnel.";
					if (.@compass_check)
						viewpoint 1,215,221,7,0x00FF00;
					break;
				case 7:
					mes "[Guide d'Einbroch]";
					mes "Le Laboratoire est une annexe de l'Usine, et est situé au Sud Ouest d'Einbech";
					if (.@compass_check)
						viewpoint 1,36,49,8,0x00FF;
					break;
				case 8:
					mes "[Guide d'Einbroch]";
					mes "La Guilde des Forgerons (Blacksmith) est située au Sud Est d'Einbroch.";
					mes "Vous pourrez y faire raffiner votre équipement et accéder à différents services de forge.";
					if (.@compass_check)
						viewpoint 1,244,90,9,0x00FF00;
					break;
				case 9:
					mes "[Guide d'Einbroch]";
					mes "La Tour d'Einbroch est située au centre de la ville.";
					mes "Du haut de la tour, vous aurez un superbe panorama d'Einbroch.";
					if (.@compass_check)
						viewpoint 1,174,195,10,0xFFFF00;
					break;
				case 10:
					mes "[Guide d'Einbroch]";
					mes "N'hésitez pas à me demander si vous voulez que j'efface les informations de votre Mini-Map.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,63,228,2,0xFF0000;
			viewpoint 2,236,279,3,0xFF00FF;
			viewpoint 2,158,78,4,0xFF00FF;
			viewpoint 2,232,190,5,0xFF00FF;
			viewpoint 2,260,201,6,0x00FF00;
			viewpoint 2,215,221,7,0x00FF00;
			viewpoint 2,36,49,8,0x00FF00;
			viewpoint 2,244,90,9,0x00FF00;
			viewpoint 2,174,195,10,0xFFFF00;
			set .@compass_check,0;
			mes "[Guide d'Einbroch]";
			mes "Les informations ont été effacées de la Mini-Map.";
			mes "Si vous avez besoin d'informations concernant Einbech ou les environs d'Einbroch, n'hésitez-pas à vous adresser à moi ou à l'un de mes collègues Guides.";
			break;
		case 3:
			mes "[Guide d'Einbroch]";
  			mes "Votre Mini-Map est située dans le coin haut droit de votre écran.";
			mes "Si vous ne la voyez pas, pressez Ctrl + Tab ou cliquez sur le bouton 'Map' dans votre fenêtre d'informations.";
  			next;
  			mes "[Guide d'Einbroch]";
 			mes "Sur votre Mini-Map, cliquez sur '+' ou '-' pour modifier le zoom.";
			mes "Nous espérons que vous apprécierez votre séjour à Einbroch.";
			break;
		case 4:
			mes "[Guide d'Einbroch]";
			mes "Soyez prudent au cours de vos voyages, et n'hésitez-pas à vous adressez à moi si vous avez des questions à propos d'Einbroch. Ah, et faîtes attention aux alertes au Brouillard!";
			close2;
			set .@loop1,0;
		}
	}
	cutin "ein_soldier",255;
	end;
}

einbroch,155,43,4	duplicate(EinGuide)	Guide#2ein	852

einbroch,162,317,4	duplicate(EinGuide)	Guide#3ein	852
