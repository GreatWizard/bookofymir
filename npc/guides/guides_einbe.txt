//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Einbech Guides
//===== By: ================================================== 
//= Muad_dib; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Guides for the City of Einbroch
//===== Additional Comments: ================================= 
//= 1.0 Einbech guide by Muad_dib in prometheus format
//= 1.0a Conversion to eA format [MasterOfMuppets]
//= 1.1 missing tabs [Lupus]
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.4 Removed Duplicates [Silent]
//= 1.4a missing tabs [KarLaeda]
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//============================================================

einbech,67,37,4	script	Guide#4ein::EinGuide2	852,{
	cutin "ein_soldier",2;
	mes "[Guide d'Einbech]";
	mes "Bienvenue à Einbech, la Ville Minière.";
	mes "Je suis ici afin de renseigner les touristes, donc n'hésitez pas à me demander des renseignements.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Guide de la Ville:Effacer les Informations de la Mini-Carte:Astuce:Annuler")) {
		case 1:
			mes "[Guide d'Einbech]";
  			mes "Sélectionnez un lieu spécifique dans le menu suivant.";
			if (.@compass_check == 0) {
				mes "Voulez-vous que j'indique ces emplacements sur votre Mini-Carte?";
				next;
				if (select("Oui:Non") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				switch(select("Gare Ferroviaire:Taverne:Boutique:Guilde des Epéistes:Mine:Annuler")) {
				case 1:
					mes "[Guide d'Einbech]";
					mes "La Gare Ferroviaire est située dans les zones Nord Ouest et Nord Est d'Einbech.";
					mes "De là, vous pouvez prendre le train pour Einbroch. Il y a des trajets tous les jours.";
					if (.@compass_check)
						viewpoint 1,43,213,2,0xFF0000;
					break;
				case 2:
					mes "[Guide d'Einbech]";
					mes "La Taverne est située au Sud d'Einbech.";
					mes "C'est un endroit très sympathique où se relaxer après une longue journée de travail.";
					if (.@compass_check)
						viewpoint 1,142,112,3,0xFF00FF;
					break;
				case 3:
					mes "[Guide d'Einbech]";
					mes "La Boutique se situe au centre de la ville.";
					mes "Vous y trouverez de nombreux outils très utiles pour vos futurs voyages.";
					if (.@compass_check)
						viewpoint 1,176,136,4,0xFF00FF;
					break;
				case 4:
					mes "[Guide d'Einbech]";
					mes "La Guilde des Epéistes est située dans les quartiers Est d'Einbech.";
					mes "Elle est encore en construction, et les inscriptions ne sont pas encore ouvertes.";
					if (.@compass_check)
						viewpoint 1,250,110,5,0xFF00FF;
					break;
				case 5:
					mes "[Guide d'Einbech]";
					mes "La Mine est l'industrie majeure d'Einbech et est située au Nord de la ville.";
					mes "De cette mine sont extraits de nombreux minerais et pierres précieuses, bien qu'elle soit régulièrement envahie par les monstres.";
					if (.@compass_check)
						viewpoint 1,138,251,6,0x00FF00;
					break;
				case 6:
					mes "[Guide d'Einbech]";
				 	mes "Si vous ne souhaitez plus voir afficher les informations sur votre Mini-Carte, demandez-moi et je les effacerais.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,43,213,2,0xFF0000;
			viewpoint 2,142,112,3,0xFF00FF;
			viewpoint 2,176,136,4,0xFF00FF;
			viewpoint 2,250,110,5,0xFF00FF;
			viewpoint 2,138,251,6,0x00FF00;
			set .@compass_check,0;
			mes "[Guide d'Einbech]";
			mes "Les informations ont été effacées de la Mini-Carte.";
			mes "Si vous avez besoin d'informations concernant Einbech ou les environs d'Einbroch, n'hésitez-pas à vous adresser à moi ou à l'un de mes collègues Guides.";
			break;
		case 3:
			mes "[Guide d'Einbech]";
			mes "Grâce à la technologie,";
  			mes "votre Mini-Carte est située en haut à droite de votre écran.";
			mes "Si vous ne la voyez pas, pressez Ctrl + Tab pour la rendre plus opaque.";
  			next;
  			mes "[Guide d'Einbech]";
 			mes "Sur votre Mini-Carte, cliquez sur '+' ou '-' pour modifier le zoom.";
			mes "Nous espérons que vous apprécierez votre séjour à Einbech.";
			break;
		case 4:
			mes "[Guide d'Einbech]";
		 	mes "Soyez prudent au cours de vos voyages, et n'hésitez-pas à vous adressez à moi si vous avez des questions à propos d'Einbech.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "ein_soldier",255;
	end;
}

einbech,48,214,4	duplicate(EinGuide2)	Guide#5ein	852
