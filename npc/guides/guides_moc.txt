//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Morroc Guides
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.4a
//===== Compatible With: ===================================== 
//= eAthena  SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Guides for the City of Morroc
//===== Additional Comments: ================================= 
//= Fully working.  Added a guide at every exit.
//= v1.1 Now using duplicate command.
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Removed Duplicates [Silent]
//= 1.4 rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.4a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//============================================================

morocc,153,286,6	script	Guide#moc::MocGuide	707,{
	cutin "moc_soldier",2;
	mes "[Guide de Morroc]";
	mes "Bienvenue à Morroc, la Cité Désertique!";
	next;
	mes "[Soldat de Morroc]";
	mes "N'hésitez pas à me demander des renseignements si vous cherchez quelque chose de précis en ville, ou si vous voulez juste des informations touristiques!";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Guide de la Ville:Effacer les Informations de la Mini-Carte:Astuce:Annuler")) {
		case 1:
			mes "[Guide de Morroc]";
			mes "Sélectionnez un lieu spécifique dans le menu suivant.";
			if (.@compass_check == 0) {
				mes "Voulez-vous que je laisse des indications sur votre Mini-Carte?";
				next;
				if (select("Oui:Non") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000Guilde des Voleurs^000000:Armurerie:Auberge:Bar:Guilde des Mercenaires:Forge:Annuler")) {
				case 1:
					mes "[Guide de Morroc]";
					mes "La Guilde des Voleurs... J'ai entendu dire que c'était le lieu d'apprentissage pour tous les jeunes Voleurs, et qu'il se situerait à l'intérieur des pyramides.....";
					if (.@compass_check)
						viewpoint 1,24,297,2,0xFF0000;
					break;
				case 2:
					mes "[Guide de Morroc]";
					mes "Vous trouverez à l'Armurerie située au Sud-Est de Morroc de nombreuses armes et armures pour votre usage personnel.";
					if (.@compass_check)
						viewpoint 1,253,56,3,0xFF00FF;
					break;
				case 3:
					mes "[Guide de Morroc]";
					mes "Morroc compte deux Auberges très confortables, l'une au Sud et l'autre au Nord Est.";
					if (.@compass_check) {
						viewpoint 1,197,66,4,0xFF00FF;
						viewpoint 1,273,269,5,0xFF00FF;
					}
					break;
				case 4:
					mes "[Guide de Morroc]";
					mes "Le Bar est situé au Nord Ouest de la ville.";
					if (.@compass_check)
						viewpoint 1,52,259,6,0xFF00FF;
					break;
				case 5:
					mes "[Guide de Morroc]";
					mes "La Guilde des Mercenaires est située à l'extrémité Est de la ville.";
					if (.@compass_check)
						viewpoint 1,284,171,7,0x00FF00;
					break;
				case 6:
					mes "[Guide de Morroc]";
					mes "La Forge est située dans le quartier Sud-Ouest de la ville.";
					if (.@compass_check)
						viewpoint 1,47,47,7,0xFF00FF;
					break;
				case 7:
					mes "[Guide de Morroc]";
					mes "N'hésitez pas à me demander si vous voulez que j'efface les informations de votre Mini-Carte.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,2,0x00FF00;
			viewpoint 2,237,41,3,0x0000FF;
			viewpoint 2,46,345,4,0x00FF00;
			viewpoint 2,175,220,5,0xFF0000;
			viewpoint 2,175,220,6,0xFF0000;
			viewpoint 2,175,220,7,0xFF0000;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Guide de Morroc]";
  			mes "Votre Mini-Carte est située en haut à droite de votre écran.";
			mes "Si vous ne la voyez pas, pressez Ctrl + Tab pour la rendre plus opaque.";
			next;
			mes "[Guide de Morroc]";
 			mes "Sur votre Mini-Carte, cliquez sur '+' ou '-' pour modifier le zoom.";
			mes "Nous espérons que vous apprécierez votre séjour à Morroc.";
			break;
		case 4:
			mes "[Guide de Morroc]";
			mes "Très bien. Essayez de ne pas vous attirer des ennuis, aventurier.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "moc_soldier",255;
	end;
}

morocc,54,97,0	duplicate(MocGuide)	Guide#2moc	707