//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Prontera Guides
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.6a
//===== Compatible With: ===================================== 
//= eAthena  SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Guides for the City of Prontera
//===== Additional Comments: ================================= 
//= Fully working
//= v1.1 Now using duplicate command.
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Removed Duplicates [Silent]
//= 1.6 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.6a Fixed some errors with the loop. [Samuray22]
//	-Thanks to $ephiroth.
//= 1.6b Fixed a Problem with Unnecesary RGB Values. [Samuray22]
//	-Thanks to Brainstorm.
//============================================================

prontera,154,187,4	script	Guide#prt::PrtGuide	105,{
	cutin "prt_soldier",2;
	mes "[Guide de Prontera]";
	mes "Bienvenue à Prontera, la magnifique Capitale du Royaume de Rune-Midgard.";
	mes "N'hésitez pas à me demander des renseignements si vous cherchez quelque chose de précis en ville, ou si vous voulez juste des informations touristiques!";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Guide de la Ville:Effacer les Informations de la Mini-Carte:Astuce:Annuler")) {
		case 1:
			mes "[Guide de Prontera]";
			mes "Sélectionnez un lieu spécifique dans le menu suivant.";
			if (.@compass_check == 0) {
				mes "Voulez-vous que je laisse des indications sur votre Mini-Carte?";
				next;
				if (select("Oui:Non") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("Association des Epéistes:^0000FFSanctuaire^000000:Garde de Prontera:Armurerie:Boutique:Auberge:Poste:Bar:Bibliothèque:Agence d'Intérim:Château de Prontera:Mairie:Annuler")) {
				case 1:
					mes "[Guide de Prontera]";
					mes "L'Association des Epéistes a déménagé à Izlude.";
					mes "Le bâtiment est donc vide pour l'instant.";
					if (.@compass_check)
						viewpoint 1,237,41,4,0x00FF00;
					break;
				case 2:
					mes "[Guide de Prontera]";
					mes "Le Sanctuaire de Prontera est un saint lieu de prière, mais également le lieu de formation des Prêtres et des Acolytes. Il se situe au Nord Est de la ville.";
					if (.@compass_check)
						viewpoint 1,236,316,5,0xFF0000;
					break;
				case 3:
					mes "[Guide de Prontera]";
					mes "La Garde de Prontera est en charge de la sécurité dans la Capitale. Elle est située au Nord Ouest de la ville.";
					if (.@compass_check)
						viewpoint 1,46,345,6,0x00FF00;
					break;
				case 4:
					mes "[Guide de Prontera]";
					mes "L'Armurerie est située au Nord Est de la Fontaine Centrale.";
					if (.@compass_check)
						viewpoint 1,175,220,7,0xFF00FF;
					break;
				case 5:
					mes "[Guide de Prontera]";
					mes "La Boutique est située au Nord Ouest de la Fontaine Centrale.";
					if (.@compass_check)
						viewpoint 1,134,221,8,0xFF00FF;
					break;
				case 6:
					mes "[Guide de Prontera]";
					mes "Les Auberges de Prontera sont situées à l'Est et à l'Ouest de la Fontaine Centrale.";
					if (.@compass_check) {
						viewpoint 1,204,191,9,0xFF00FF;
						viewpoint 1,107,192,10,0xFF00FF;
					}
					break;
				case 7:
					mes "[Guide de Prontera]";
					mes "La Poste est située au Sud Est de la Fontaine Centrale.";
					if (.@compass_check)
						viewpoint 1,179,184,11,0x00FF00;
					break;
				case 8:
					mes "[Guide de Prontera]";
					mes "Le Bar est situé au Sud Est de la Fontaine, non loin de la Poste.";
					if (.@compass_check)
						viewpoint 1,208,154,12,0x00FF00;
					break;
				case 9:
					mes "[Guide de Prontera]";
					mes "Au Nord de la Fontaine Centrale, vous trouverez un petit chemin menant à la Bibliothèque de Prontera.";
					if (.@compass_check) {
						viewpoint 1,120,267,13,0x00FF00;
						viewpoint 1,192,267,14,0x00FF00;
					}
					break;
				case 10:
					mes "[Guide de Prontera]";
					mes "L'Agence d'Intérim est située au Sud Ouest de la Fontaine Centrale.";
					if (.@compass_check)
						viewpoint 1,133,183,15,0x00FF00;
					break;
				case 11:
					mes "[Guide de Prontera]";
					mes "Le Château de Prontera est situé au Nord de la ville.";
					mes "Vous pouvez rejoindre les régions au Nord de Prontera après avoir traversé le Château.";
					if (.@compass_check)
						viewpoint 1,156,360,16,0x00FF00;
					break;
				case 12:
					mes "[Guide de Prontera]";
					mes "La Mairie est situé au Sud Ouest de la ville.";
					if (.@compass_check)
						viewpoint 1,75,91,17,0x01FF01;
					break;
				case 13:
					mes "[Guide de Prontera]";
					mes "N'hésitez pas à me demander si vous voulez que j'efface les informations de votre Mini-Carte.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,4,0x00FF00;
			viewpoint 2,237,41,5,0x0000FF;
			viewpoint 2,46,345,6,0x00FF00;
			viewpoint 2,175,220,7,0xFF0000;
			viewpoint 2,134,221,8,0xFF0000;
			viewpoint 2,204,191,9,0xFF0000;
			viewpoint 2,107,192,10,0xFF0000;
			viewpoint 2,179,184,11,0x00FF00;
			viewpoint 2,208,154,12,0x00FF00;
			viewpoint 2,120,267,13,0x00FF00;
			viewpoint 2,192,267,14,0x00FF00;
			viewpoint 2,133,183,15,0x00FF00;
			viewpoint 2,156,360,16,0x00FF00;
			viewpoint 2,75,91,17,0x00FF00;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Guide de Prontera]";
  			mes "Votre Mini-Carte est située dans le coin haut droit de votre écran.";
			mes "Si vous ne la voyez pas, pressez Ctrl + Tab ou cliquez sur le bouton 'Map' dans votre fenêtre d'informations.";
			next;
			mes "[Guide de Prontera]";
 			mes "Sur votre Mini-Carte, cliquez sur '+' ou '-' pour modifier le zoom.";
			mes "Nous espérons que vous apprécierez votre séjour à Prontera.";
			break;
		case 4:
			mes "[Guide de Prontera]";
			mes "A bientôt, aventurier... J'espère que vos voyages en Rune-Midgard vous seront réjouissants et divertissants.";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}

prontera,282,208,2	duplicate(PrtGuide)	Guide de la Porte Est	105

prontera,29,200,6	duplicate(PrtGuide)	Guide de la Porte Ouest	105

prontera,160,29,0	duplicate(PrtGuide)	Guide de la Porte Sud	105

prontera,151,330,4	duplicate(PrtGuide)	Guide de la Porte Nord	105