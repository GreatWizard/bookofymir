//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Izlude Guides
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5a
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Guides for the City of Izlude
//===== Additional Comments: ================================= 
//= Fully working
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.5a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//============================================================

izlude,121,87,6	script	Guide#iz	105,{
	cutin "prt_soldier",2;
	mes "[Guide d'Izlude]";
	mes "Bienvenue à Izlude, la Cité Satellite de Prontera.";
	mes "Si vous avez besoin d'un guide sur les alentours d'Izlude, n'hésitez pas à vous adresser à moi.";
	set .@loop1,1;
	while(.@loop1) {
		next;
		switch(select("Guide de la Ville:Effacer les Informations de la Mini-Carte:Astuce:Annuler")) {
		case 1:
			mes "[Guide d'Izlude]";
			mes "Sélectionnez un lieu spécifique dans le menu suivant.";
			if (.@compass_check == 0) {
				mes "Voulez-vous que je laisse des indications sur votre Mini-Carte?";
				next;
				if (select("Oui:Non") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000Association des Epéistes^000000:Hall des Epéistes:Arène:Marina d'Izlude:Armurerie:Boutique:Annuler")) {
				case 1:
					mes "[Guide d'Izlude]";
					mes "L'Association des Epéistes est située sur île à l'Ouest d'Izlude.";
					mes "Si vous pensez à faire d'Epéiste votre métier, vous devriez vous y rendre.";
					if (.@compass_check)
						viewpoint 1,52,140,0,0xFF0000;
					break;
				case 2:
					mes "[Guide d'Izlude]";
					mes "Le Hall des Epéistes est situé sur l'île Est, connectée à Izlude.";
					if (.@compass_check)
						viewpoint 1,214,130,1,0x00FF00;
					break;
				case 3:
					mes "[Guide d'Izlude]";
					mes "La célèbre Arène d'Izlude est située à l'extrémité Nord de la ville.";
					if (.@compass_check)
						viewpoint 1,128,225,2,0x00FF00;
					break;
				case 4:
					mes "[Guide d'Izlude]";
					mes "La Marina se situe dans le quartier Nord Est d'Izlude.";
					mes "D'ici vous pourrez prendre le bateau pour Alberta ou pour l'Île de Byalan.";
					if (.@compass_check)
						viewpoint 1,200,180,3,0xFF0000;
					break;
				case 5:
					mes "[Guide d'Izlude]";
					mes "L'Armurerie est située au Nord Est d'Izlude.";
					if (.@compass_check)
						viewpoint 1,111,149,4,0xFF00FF;
					break;
				case 6:
					mes "[Guide d'Izlude]";
					mes "La Boutique est située au Nord Est d'Izlude.";
					if (.@compass_check)
						viewpoint 1,148,148,5,0xFF00FF;
					break;
				case 7:
					mes "[Guide d'Izlude]";
					mes "N'hésitez pas à me demander si vous voulez que j'efface les informations de votre Mini-Carte.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,0,0x00FF00;
			viewpoint 2,237,41,1,0x0000FF;
			viewpoint 2,46,345,2,0x00FF00;
			viewpoint 2,175,220,3,0xFF0000;
			viewpoint 2,134,221,4,0xFF0000;
			viewpoint 2,204,214,5,0xFF0000;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Guide d'Izlude]";
  			mes "Votre Mini-Carte est située dans le coin haut droit de votre écran.";
			mes "Si vous ne la voyez pas, pressez Ctrl + Tab ou cliquez sur le bouton 'Map' dans votre fenêtre d'informations.";
			next;
			mes "[Guide d'Izlude]";
 			mes "Sur votre Mini-Carte, cliquez sur '+' ou '-' pour modifier le zoom.";
			mes "Nous espérons que vous apprécierez votre séjour à Izlude.";
			break;
		case 4:
			mes "[Guide d'Izlude]";
			mes "D'accord, revenez me voir quand vous voulez si vous vous sentez un peu perdu dans Izlude, ok?";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}