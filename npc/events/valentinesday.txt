//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//===== eAthena Script =======================================
//= Valentine Event Script
//===== By: ==================================================
//= Muad_Dib(Prometheus Project)
//===== Current Version: =====================================
//= 1.01
//===== Compatible With: =====================================
//= Any Athena Version
//===== Description: =========================================
//
//===== Additional Comments: =================================
//= 07/06/05 : Added 1st Version. [Muad_Dib]
//= 08/28/05 : Ver. 1.01 Added a missing close; [Muad_Dib]
//= Converted to eAthena format by Dr.Evil
//============================================================


// Stephen - Valentine Event Chocolate seller ------------------
alberta,26,243,4	script	Stephen	58,{
	mes "[Stephen]";
	mes "Devinez ce que j'ai là? Une douceur très difficile à trouver dans Rune-Midgard...";
	next;
	mes "[Stephen]";
	mes "Du chocolat!";
	mes "Vous aimez le chocolat, n'est-ce pas? Moi j'adore! Et vous avez de la chance car j'en vend pour 5.000 zénys pièce!";
	next;
	menu "Je veux du chocolat!",M_CHOCO,"Non merci.",-;
	mes "[Stephen]";
	mes "Vous ne voulez pas de chocolat? Je vais vous dire quoi: je suis sûr que vous le regretterez! Vous feriez mieux de m'en acheter maintenant, l'opportunité ne se représentera peut-être pas de sitôt! Pensez-y et revenez me voir quand vous aurez changé d'avis.";
	close;

M_CHOCO:
	mes "[Stephen]";
	mes "Ah! Je le savais! Cependant, je ne peux pas en vendre plus de 5 à la fois. Mais vous pourrez toujours revenir si vous en voulez plus. Alors, combien en voulez-vous?";
	next;
	set @needmon,0;
	input @flag_num;
	if (@flag_num <= 0) goto L_NONE;
	if (@flag_num > 5) goto L_ERR;
	set @needmon,@flag_num*5000;
	if (Zeny < @needmon) goto L_NOTENO;
	set Zeny,Zeny - @needmon;
	getitem 558,@flag_num;
	mes "[Stephen]";
	mes "Et voilà pour vous! Vous pouvez l'offrir à quelqu'un, ou vous régaler avec.";
	mes "Hmm... Du chocolat...";
	mes "À bientôt!";
	close;

L_ERR:
	mes "[Stephen]";
	mes "Je suis désolé mais je ne peux pas vous en vendre autant.";
L_NONE:
	close;

L_NOTENO:
	mes "[Stephen]";
	mes "Je suis désolé mais on dirait que vous ne pouvez pas vous en offrir autant.";
	close;
}

// Jainie -- Gives information about Valentine Event ------------------------
alberta,29,243,4	script	Jainie	53,{
	mes "[Jainie]";
	mes "Devinez quoi! C'est moi qui confectionne les chocolats que vend mon petit ami. Je les fais moi-même.";
	next;
	mes "[Jainie]";
	mes "Vous savez, dans certains pays on a l'habitude d'offrir du chocolat à ceux que l'on aime un jour dans l'année. On appelle ce jour la ^3355FFSaint Valentin^000000.";
	next;
	mes "[Jainie]";
	mes "C'est pour ça que je lui ai donné mes délicieux chocolats. Mais après cela, il a voulu que j'en fasse d'autres, encore, et encore... Et maintenant il les vend à tout le monde. J'imagine qu'il les a vraiment aimés, mais ça me gêne quand même un peu de vendre ce que j'ai fait par amour...";
	next;
	mes "[Jainie]";
	mes "Vous devriez en acheter vous aussi... De toutes façons je suis partie pour en faire pendant un bon bout de temps.";
	close;
}

// Charles Orleans -- Valentine Event Chocolate maker ------------------
prt_castle,54,34,4	script	Charles d'Orléans::Charles Orleans	47,{
	mes "[Charles d'Orléans]";
	mes "Oui?";
	next;
	menu "Je voudrais du chocolat maison...",-,"Qu'est-ce que vous faites?",M_WAT;
     
	mes "[Charles d'Orléans]";
	mes "Eh bien, je vais peut-être pouvoir vous aider...";
	next;
	if (countitem(558) >=3) goto L_ENO;
	mes "[Charles d'Orléans]";
	mes "Oh, mais vous n'avez pas assez de Chocolat pour ça. Je ne peux rien faire, désolé.";
	close;

L_ENO:
	delitem 558,3;
	mes "[Charles d'Orléans]";
	mes "Vous avez 3 morceaux de chocolat pur cacao. Bien, donnez-les moi...";
	next;
	mes "[Charles d'Orléans]";
	mes "D'accord, je vais préparer mon célèbre chocolat, mais vous devez me promettre d'en faire bon usage, d'accord?";
	next;
	mes "[Charles d'Orléans]";
	mes "....Hum.....";
	mes "Ça y est presque...";
	next;
	mes "[Charles d'Orléans]";
	mes "Et voilà!";
	getitem 559,1;
	mes "J'espère que vous l'offrirez à quelqu'un qui vous est cher, car il s'agit de chocolat très spécial. Comme vous le savez, je suis le seul à pouvoir le préparer.";
	next;
	mes "[Charles d'Orléans]";
	mes "Bon appétit!";
	close;

M_WAT:
	mes "[Charles d'Orléans]";
	mes "Oh... Eh bien, si vous voulez que je vous prépare mes chocolats très spéciaux, il faudra que vous me rapportiez au moins 3 barres de ^0000FFChocolat^000000.";
	next;
	mes "[Charles d'Orléans]";
	mes "Vous avez bien entendu, seulement ^0000FF3 Chocolats^000000. Apportez-les moi, et je vous préparerai ce que vous êtes venu chercher.";
	next;
	mes "[Charles d'Orléans]";
	mes "À bientôt.";
	close;
}
