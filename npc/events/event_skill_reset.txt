//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//===== eAthena Script ======================================= 
//= Official Skill Resetter Event in Yuno (kRO & idRO)
//===== By: ================================================== 
//= $ephiroth
//===== Current Version: ===================================== 
//= 1.02
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: ========================================= 
//= 2006 - Edition Skill Reset NPC
//= 2006/11/16: 1.0 Release and fully working. [$ephiroth]
//= 1.01 Checked, fixed typos, now uses MISC_QUEST var [Lupus]
//= 1.02 Added missing message,comment,etc. [$ephiroth]
//===== Additional Comments: =================================
//= Skill Reset with payment, minimum BaseLv.60 to reset skill.
//= Each 1 BaseLv need 20.000 Zeny, so be careful to use skill.
//============================================================
yuno,138,187,4	script	Hypnotiseur::Hypnotist Teacher	124,{
	set @npcname$,"[^D5A500Hypnotiseur^000000]";
	mes @npcname$;

	if(MISC_QUEST & 1024){
		mes "Désolé mais comme je vous l'ai dit vous ne pouvez pas utiliser mes services à plusieurs reprises.";
		close;
	}
	if(BaseLevel < 60) {
		mes "Vous devriez revenir me voir quand votre Niveau de Base sera supérieur ou égal à 60.";
		close;
	}
	if(SkillPoint != 0) {
		mes "Il faut que vous utilisiez tous vos Points de Compétences avant de pouvoir continuer. À très bientôt!";
		close;
	}
	if(Weight || checkfalcon() || checkcart() || checkriding()) {
		mes "Vous voulez remettre à zéro vos compétences? Je suis désolé mais...";
		next;
		mes @npcname$;
		if(Weight) {
			mes "Vous ne pouvez pas le faire si vous avez des objets sur vous.";
		} else if(checkcart())
			mes "Vous devez d'abord vous débarrasser de votre charrette.";
		else if(checkfalcon())
			mes "Vous devez d'abord vous débarrasser de votre faucon.";
		else if(checkriding())
			mes "Vous devez d'abord vous débarrasser de votre monture.";
		next;
		mes @npcname$;
		mes "A très bientôt!";
		close;
	}
	mes "Bonjour. Je peux remettre à zéro vos Compétences. Votre nom est ^0080FF"+strcharinfo(0)+"^000000, n'est-ce pas? Que puis-je pour vous?";
	next;
	menu "^009500Informations à propos du service.^000000",L_Info,"^00B6FFRemettre à zéro les Compétences.^000000",L_Reset,"^000088Rien, merci.^000000",-;

	mes @npcname$;
	mes "Vous savez où me trouver si vous changez d'avis!"; 
	close;

L_Info:	
	mes @npcname$;
	mes "Ce service n'est pas gratuit. Il y a un prix à payer équivalent à ^D5A50020000 zénys x Niveau de Base^000000.";
	mes "Oui... La remise à zéro des compétences coûte 20000 zénys par niveau.";
	next;
	mes @npcname$;
	mes "Une dernière chose: vous devez vous débarrasser de votre charrette, de votre faucon, ou de votre monture avant de pouvoir remettre à zéro vos Compétences.";
	next;
	mes @npcname$;
	mes "Oh, et vous ne pourrez utiliser ce service qu'une seule fois, alors faites bien attention quand vous réutiliserez vos Points de Compétences.";
	close;
	
L_Reset:
	mes @npcname$;
	mes "Vous devez d'abord me dire votre Niveau de Base avant de pouvoir utiliser ce service."; 
	next;
	mes "^D5A500[" + strcharinfo(0) + "]^000000";
	mes "Mon Niveau de Base est de ^AA00AA" + BaseLevel + "^000000."; 
	next;
	set @zeny,BaseLevel*20000;
	mes @npcname$;
	mes "Bien. Ce service vous coûtera donc ^529DFF" + @zeny + " zénys^000000.";
	next;
	if(Zeny < @zeny) {
		mes @npcname$;
		mes "On dirait que vous n'avez pas assez d'argent. Ce n'est que partie remise!";
		emotion e_hmm;
		close;
	}
	set Zeny, Zeny-@zeny;
	set MISC_QUEST,MISC_QUEST | 1024;
	resetskill;
	mes @npcname$;
	mes "Merci. À bientôt!";
	emotion e_thx;
	logmes "SKILL RESET EVENT";
	close;
}
