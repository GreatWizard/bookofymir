//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing et Etna
//===== eAthena Script =======================================
//= 2005 ChristMas Event
//===== By: ==================================================
//= Brainstorm
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= [Aegis Conversion]
//= Event 1: Louise's Kim creates Louise's Santa Hat.
//= Event 2: Enjoy Happymerry Chistmas Event summons monsters.
//= Event 3: Oholy requires you to hunt Deviruchi NPCs.
//==========================================
//= Break down of arguments used in the function:
//=	arg(0) Originating NPC (disable)
//=	arg(1) Next NPC to be enabled
//=	arg(2) Map name of originating NPC
//=	arg(3) Coordinate X to be teleported to
//=	arg(4) Coordinate Y to be teleported to
//===== Additional Comments: =================================
//= 1.0 First semi-working conversion. [Brainstorm]
//= 1.1 Refined Brainstorm's conversion. [L0ne_W0lf]
//= 1.2 Replaced effect numerics with constants. [L0ne_W0lf]
//= 1.2a Small update to the checkweight. (bugreport:2553). [Paradox924X]
//============================================================

// Louise's Santa Hat (Event 1)
xmas_in,89,92,5	script	Louise Kim#designer	714,{
	mes "[Styliste Louise Kim]";
	mes "Les bonnets de Père Noël classiques sont trop ordinaires... Dans les années 80 ça passait encore, mais maintenant... Franchement!";
	mes "Haha~";
	next;
	mes "[Styliste Louise Kim]";
	mes "Si vous me donnez le vôtre, je vous le remplacerai par un modèle à la mode. Si vous voyez ce que je veux dire~";
	next;
	mes "[Styliste Louise Kim]";
	mes "Vous voyez à quoi ressemble le bonnet d'Antonio, n'est-ce pas? Eh bien devinez qui l'a dessiné!";
	mes "Comme vous le savez, Antonio est très dur à attraper. C'est tout simplement parce que son bonnet renferme un pouvoir caché. ";
	next;
	mes "[Styliste Louise Kim]";
	mes "Si vous n'aimez pas votre bonnet, apportez-le moi. Je le transformerai en un bonnet à la mode. Oui, un vrai bonnet de M'dame Louise!";
	mes "";
	emotion e_lv;
	next;
	if(countitem(2236) > 0) {
		if (select("Le voici.:Non, c'est bon.") == 1) {
			mes "[Styliste Louise Kim]";
			mes "Excellent!";
			mes "Alors, si je couds ça là et que je mets ça ici... Oui, ça sera merveilleux!";
			mes "Mais avant de pouvoir faire ça il me faudra quelques matériaux pour travailler.";
			next;
			if (select("De quoi avez-vous besoin?:Je m'en moque.") == 1) {
				mes "[Styliste Louise Kim]";
				mes "Oh, rien de très extraordinaire. Il vous faut un Bonnet de père Noël, bien sûr, et grâce à mes doigts de fée je pourrai en faire quelque chose de sensationnel. Enfin... Quelque chose d'un peu plus fonctionnel et d'un peu mieux dessiné. Haha...";
				next;
				mes "[Styliste Louise Kim]";
				mes "Bon, pour faire vite, il me faudra....";
				mes "^0000FF1 Bonnet de Père Noël^000000,";
				mes " ^0000FF1 Épine de Cactus ^000000 pour coudre, ";
				mes "^0000FF10 Bouteilles d'Eau Bénite ^000000 pour les bénédictions, et";
				mes "^0000FF1 Rosaire ^000000 pour la bonne fortune.";
				mes "Avec ça je pourrai vous faire un vrai bonnet de M'dame Louise!";
				next;
				if (countitem(952) > 0 && countitem(523) > 9 && countitem(2608) > 0) {
					select("Vous voilà.....");
					mes "[Styliste Louise Kim]";
					mes "Ouah~~!! Vous avez été rapide!!";
					mes "J'aime bien votre style, vous savez~.";
					mes "Bon! Puisque tout est prêt, il n'y a pas à hésiter. Je vais vous montrer le vrai talent de M'dame Louise Kim!";
					emotion e_no1;
					next;
					mes "-Elle pose le bonnet dans un seau rempli d'eau bénite. Elle le sort sans attendre et commence à le repriser en entonnant un hymne.-";
					next;
					mes "[Styliste Louise Kim]";
					mes "~Grâce à la styliste number one, Louise Kim, vous serez maintenant béni en permanence.~";
					emotion e_ho;
					misceffect EF_BLESSING;
					next;
					mes "-Elle plonge le Rosaire dans un liquide inconnu qui le dissout, avant de verser le mélange sur le bonnet et de le brosser pour qu'il s'imprègne du liquide.-";
					next;
					mes "[Styliste Louise Kim]";
					mes "~Grâce à la styliste number one, Louise Kim, la bonne fortune sera maintenant sur vous en permanence.~";
					emotion e_ho;
					misceffect EF_GLORIA;
					next;
					mes "-Elle commence à recoudre le bonnet avec l'Épine de Cactus et du fil.-";
					mes " ";
					next;
					mes "[Styliste Louise Kim]";
					mes "~Eh voilà, c'est le petit miracle de M'dame Kim.~";
					mes "~La plus talentueuse des stylistes,~";
					mes "~L_O_U_I_S_E K_I_M~";
					emotion e_lv;
					misceffect EF_BENEDICTIO;
					next;
					mes "[Styliste Louise Kim]";
					mes "Et voilà! C'est fini~";
					mes "Alors, qu'en pensez-vous? N'est-il pas merveilleux? Prenez-le, c'est un cadeau.";
					next;
					mes "[Styliste Louise Kim]";
					mes "Je pourrais en demander un bon prix, mais c'est Noël après tout! C'est mon petit cadeau: je ne vous demanderai rien en échange. Parlez-en aux autres!";
					emotion e_heh;
					next;
					mes "[Styliste Louise Kim]";
					mes "Où que vous alliez, quoi que vous fassiez, ne vous débarrassez jamais de ce bonnet. Vous ne pourrez jamais en trouver un autre, quel que soit le prix que vous seriez prêt à payer.";
					next;
					mes "[Styliste Louise Kim]";
					mes "Bon, vous pouvez maintenant aller frimer avec votre nouveau bonnet!";
					mes "~M'dame Louise, un petit moment de bonheur dans votre vie!";
					emotion e_ho;
					delitem 2236,1; //Santa's_Hat
					delitem 952,1; //Cactus_Needle
					delitem 523,10; //Holy_Water
					delitem 2608,1; //Rosary
					getitem 5136,1; //Santa's_Hat_
					close;
				}
				else {
					mes "[Styliste Louise Kim]";
					mes "Allez~ Vous n'avez qu'à m'apportez tous les matériaux: je ne vous demanderai pas d'argent. Revenez quand vous aurez tout.";
					emotion e_lv;
					close;
				}
			}
			mes "[Styliste Louise Kim]";
			mes "Oh, vous le regretterez! Pensez-y bien!";
			emotion e_heh;
			close;
		}
		mes "[Styliste Louise Kim]";
		mes "Bon, comme vous voulez~";
		mes "Après tout ce n'est pas moi qui passe à côté de cette opportunité formidable...";
		emotion e_heh;
		close;
	}
	else {
		mes "[Styliste Louise Kim]";
		mes "Peut-être pourrai-je vous en faire un plus tard. Pensez-y: je pourrai vous confectionner un bonnet de meilleure qualité et vous montrer l'étendue de mes talents.";
		emotion e_heh;
		close;
	}
}

// Enjoy Happymerry Chistmas Event (Event 2)
prontera,155,285,3	script	Enjoy#enjoy	753,{
	if (!christ_solo05){
		mes "[Enjoy]";
		mes "Oh~";
		mes "C'est déjà l'hiver~";
		mes "Avec ce temps glacial j'ai l'impression de geler sur place. Tout comme mon coeur. Qui a dit que Noël était la fête de l'amour? Oh, je me sens si seul~";
		next;
		mes "[Enjoy]";
		mes "Soeur indigne!";
		mes "Comment peut-elle me laisser seul le jour de Noël? Noël est quelque chose qui se fête en famille~";
		mes "Hum... Il faut que je me repose. Je suis si nerveux en ce moment...~";
		next;
		mes "[Enjoy]";
		mes "Bon, organisons une fête pour ceux qui sont seuls pour les fêtes. Qui est avec moi?!!";
		mes "!!!!!!!!!!!";
		mes "!!!!!!!!!!!!!";
		next;
		switch(select("Ouah!:Hum... Je ne suis pas intéressé.:J'ai de la famille, moi~")){
		case 1:
			mes "[Enjoy]";
			mes "Alors que la fête commence!";
			mes "Voilà mon plan: on va punir tous les couples qui sont si excités à propos de Noël. Je ne veux pas les voir heureux.";
			next;
			mes "[Enjoy]";
			mes "Il nous faudra plus de troupes pour le mettre en action. Bon! Allez contacter nos recrues dans les différentes villes pour leur dire de venir ici. C'est compris? Alors c'est parti!";
			next;
			mes "[Enjoy]";
			mes "Ils devraient tous avoir les coordonnées de quelqu'un. Nous devons garder tout cela secret jusqu'à ce que nous entrions en action.";
			mes "En fait...";
			mes "J'ai les coordonnées de ^0000FFHappymerry^000000.";
			next;
			mes "[Enjoy]";
			mes "Commencez par aller voir ^0000FFHappymerry^000000 et parlez-lui de notre plan. Enrôlez autant de recrues que vous pouvez sur le chemin du retour.";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "Bien, M'sieur!";
			next;
			mes "[Enjoy]";
			mes "Oh, et pendant que vous y êtes, rapportez-moi 5 Branches Mortes. On en aura besoin pour attaquer la ville~";
			mes "Hahahaha~~";
			emotion e_gg;
			next;
			mes "[Enjoy]";
			mes "Vous avez bien compris? Alors que l'heure de notre châtiment arrive!";
			mes "Nous ne sommes pas jaloux, nous voulons juste les remettre dans le droit chemin!";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "Châtions-les!!!";
			set christ_solo05,1;
			close;
		case 2:
			mes "[Enjoy]";
			mes "Si vous n'êtes pas avec moi, vous êtes contre moi! Hors de ma vue!";
			mes "";
			close;
		case 3:
			mes "[Enjoy]";
			mes "......";
			specialeffect EF_BLESSING;
			next;
			mes "[Enjoy]";
			mes ".............";
			specialeffect EF_INCAGIDEX;
			next;
			mes "[Enjoy]";
			mes "........................";
			specialeffect EF_STEELBODY;
			next;
			mes "[Enjoy]";
			mes "........................";
			mes "Dégage, ordure!!!!!!";
			specialeffect EF_BEGINASURA;
			specialeffect2 EF_HIT2;
			set Hp,Hp/2;
			set Hp,Hp/2;
			close2;
			warp "prontera",155,230;
			end;
		}
	}
	else if((christ_solo05 > 0) && (christ_solo05 < 5)){
		mes "[Enjoy]";
		mes "Ils devraient tous avoir les coordonnées de quelqu'un. Nous devons garder tout cela secret jusqu'à ce que nous entrions en action.";
		mes "En fait...";
		mes "Je connais les coordonnées de ^0000FFHappymerry^000000.";
		next;
		mes "[Enjoy]";
		mes "Commencez par aller voir ^0000FFHappymerry^000000 et parlez-lui de notre plan. Enrôlez autant de recrues que vous pouvez sur le chemin du retour.";
		next;
		mes "[Enjoy]";
		mes "Oh, et pendant que vous y êtes, rapportez-moi 5 Branches Mortes. On en aura besoin pour attaquer la ville~";
		close;
	}
	else if (christ_solo05 == 5) {
		if (countitem(604) > 4){
			mes "[Enjoy]";
			mes "Avez-vous fait comme je vous l'avais demandé? Est-ce que vous avez les branches mortes?";
			next;
			mes "[Enjoy]";
			mes "Voyons ça...";
			mes "Oui, vous avez tout!";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "Attendez!";
			mes "Où sont les autres?";
			next;
			mes "[Enjoy]";
			mes "De quoi est-ce que vous parlez? Ils sont déjà là...";
			next;
			mes "[Enjoy]";
			mes "Vous ne les voyez pas? Est-ce que ça va?!";
			donpcevent "Happymerry#happymerry02::OnCommandOn";
			donpcevent "Christ#christ02::OnCommandOn";
			donpcevent "Mas#mas02::OnCommandOn";
			donpcevent "Event#event02::OnCommandOn";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "Oh... Oui!!!";
			next;
			mes "[Enjoy]";
			mes "Bienvenue, mes chères recrues! Être seul n'est pas un crime. Pourquoi devrions-nous nous cacher? Je hais les couples! Pauvres célibataires...";
			next;
// Si quelqu'un comprend ça, qu'il me le dise - je supprime pour l'instant [No Healing]
/*			mes "[Enjoy]";
			mes "For those of who have friend who just met his/her mate,";
			mes "or who had to turn his/her back from kissing couples!!";
			mes "What are you waiting for!!";
			mes "Why do we have to be the victim!!";
			mes "";
			next; */
			mes "[Enjoy]";
			mes "La saison des fêtes est de nouveau là!";
			mes "";
			next;
			mes "[Enjoy]";
			mes "Nous sommes l'armée invincible des célibataires, fiers et bien entraînés! Nous avons sauté d'un zeppelin en plein vol, nous avons exterminé des monstres à mains nues, et nous sommes maintenant prêts! Haha~";
			next;
			mes "[Enjoy]";
			mes "Chassez de votre coeur la mélancolie qui vous envahi à la vue de groupes d'aventuriers! Point de jalousie à avoir! Ce Noël sera celui des solitaires! ";
			mes "Yahoo~";
			mes "";
			next;
			mes "[Tous Ensemble]";
			mes "Le Noël des hommes seuls!";
			mes "Fi du chagrin! Fi des regrets! Fi de la colère!";
			mes "Le Noël des hommes seuls!";
			next;
			mes "-Enjoy prend les branches mortes et les tient fermement.-";
			next;
			mes "[Enjoy]";
			mes "Châtions ces couples menant une vie de débauche!";
			mes " ";
			mes "[Tous Ensemble]";
			mes "Châtions-les!!!";
			emotion e_go;
			emotion e_go,1;
			donpcevent "Happymerry#happymerry02::OnCommandEmotion";
			donpcevent "Christ#christ02::OnCommandEmotion";
			donpcevent "Mas#mas02::OnCommandEmotion";
			donpcevent "Event#event02::OnCommandEmotion";
			misceffect 376; //_HITLINE2
			delitem 604,5; //Branch_of_Dead_Tree
			set christ_solo05,6;
			donpcevent "Happymerry#happymerry02::OnCommandOff";
			donpcevent "Christ#christ02::OnCommandOff";
			donpcevent "Mas#mas02::OnCommandOff";
			donpcevent "Event#event02::OnCommandOff";
			close2;
// [No Healing] Je suis désolé mais la suite est stupide au niveau du script: on construit un nom de mob, qui est limité à 23 caractères,
// à partir du nom du perso (lui-même limité à 23 ou 24 caractères) auquel on ajoute une string. Bref il y a tout à parier que le résultat
// fera plus de 23 caractères et beuguera donc. Celui qui traduira ça fera comme il veut mais je pense qu'il faudrait carrément virer la partie
// strcharinfo(0) +"'s, qui ne sert pas à grand chose en fin de compte.
			mapannounce "prontera", "Les soldats du chagrin de "+ strcharinfo(0) +" se déploient sur la ville!",bc_map,0x66FFCC;
			monster "prontera",155,300,"Angoisse",1062,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",156,300,"Jalousie",1062,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",157,300,"Désespoir",1062,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",158,300,"Frustration",1062,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",154,300,"Attentat",1062,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",158,299,"Rancune",1246,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",157,299,"Malédiction",1246,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",156,299,"Colère",1246,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",155,299,"Reproche",1246,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",154,299,"Haine",1246,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",158,298,"Amertume du Solitaire",1245,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",157,298,"Terreur des Couples",1245,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",156,298,"Solitude",1245,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",155,298,"Chagrin",1245,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",154,298,"Ténèbres",1245,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",158,297,"Dépression",1244,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",157,297,"Étranglement",1244,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",156,297,"Cauchemar",1244,1,"Enjoy#enjoy::OnMyMobDead"; // trad: cauchemar
			monster "prontera",155,297,"Gémissement",1244,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",154,297,"Murmure",1244,1,"Enjoy#enjoy::OnMyMobDead"; // Murmure
			monster "prontera",158,296,"Regret",1588,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",157,296,"Ombre",1588,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",156,296,"Briseur de Couples",1588,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",155,296,"Tristesse",1588,1,"Enjoy#enjoy::OnMyMobDead";
			monster "prontera",154,296,"Coeur Brisé",1588,1,"Enjoy#enjoy::OnMyMobDead";
			donpcevent "Enjoy#enjoy::OnCommandGo";
			end;
		}
		else {
			mes "[Enjoy]";
			mes "Nous n'avons plus beaucoup de temps! Il n'y a pas à hésiter! Voulez-vous voir tous ces couples répugnants célébrer Noël dans la joie et l'allégresse?";
			mes "Courage! Nous réussirons! Rapportez-moi ^0000FF5 Branches Mortes^000000.";
			close;
		}
	}
	else {
		mes "[Enjoy]";
		mes "Hum.... ";
		mes "Se faire des reproches ne sert à rien! Nous, solitaires, pouvons bâtir notre propre futur et le rendre radieux! Allons, mes troupes, allons!";
		next;
		mes "[Enjoy]";
		mes "En ce jour nous serons tous heureux~";
		mes "Allons-y! Allons-y!";
		mes "Allez, tous ensemble!!!";
		mes "Cassons tout pour être heureux!~";
		set christ_solo05,0;
		close;
	}

OnMyMobDead:
	end;

OnCommandGo:
	disablenpc "Enjoy#enjoy";
	initnpctimer;
	end;

OnCommandStop:
	enablenpc "Enjoy#enjoy";
	killmonster "prontera","Enjoy#enjoy::OnMyMobDead";
	stopnpctimer;
	end;

OnTimer3000:
	mapannounce "prontera", "Vous devez vivre et mourir seul!",bc_map,0x66FFCC;
	end;

OnTimer5000:
	mapannounce "prontera", "Créer une équipe pour un donjon? Pfft.. Quelle perte de temps!",bc_map,0x66FFCC;
	end;

OnTimer7000:
	mapannounce "prontera", "Eww... Quelqu'un est en train de me draguer!",bc_map,0x66FFCC;
	end;

OnTimer9000:
	mapannounce "prontera", "Je suis seul depuis le jour où je suis né!",bc_map,0x66FFCC;
	end;

OnTimer11000:
	mapannounce "prontera", "Nous arracherons les mauvaises herbes même le jour de Noël!",bc_map,0x66FFCC; // Pas sûr de la trad ("we dig herbs")
	end;

OnTimer13000:
	mapannounce "prontera", "...Nous sommes la fière armée des solitaires!",bc_map,0x66FFCC;
	end;

OnTimer180000:
	mapannounce "prontera", "Que tous nos fiers soldats passent un joyeux Noël!",bc_map,0x66FFCC;
	donpcevent "Enjoy#enjoy::OnCommandStop";
	end;
}

prontera,150,286,5	script	Happymerry#happymerry02	869,{
	end;

OnInit:
	disablenpc "Happymerry#happymerry02";
	stopnpctimer;
	end;

OnCommandOn:
	initnpctimer;
	enablenpc "Happymerry#happymerry02";
OnCommandEmotion:
	emotion e_go;
	end;

OnCommandOff:
	disablenpc "Happymerry#happymerry02";
	stopnpctimer;
	end;

OnTimer60000:
	donpcevent "Happymerry#happymerry02::OnCommandOff";
	donpcevent "Christ#christ02::OnCommandOff";
	donpcevent "Mas#mas02::OnCommandOff";
	donpcevent "Event#event02::OnCommandOff";
	stopnpctimer;
	end;
}

prontera,161,286,4	script	Christ#christ02	875,{
	end;
OnInit:
	disablenpc "Christ#christ02";
	end;

OnCommandOn:
	enablenpc "Christ#christ02";
OnCommandEmotion:
	emotion e_go;
	end;

OnCommandOff:
	disablenpc "Christ#christ02";
	end;
}

prontera,161,281,1	script	Mas#mas02	748,{
	end;
OnInit:
	disablenpc "Mas#mas02";
	end;

OnCommandOn:
	enablenpc "Mas#mas02";
OnCommandEmotion:
	emotion e_go;
	end;

OnCommandOff:
	disablenpc "Mas#mas02";
	end;
}

prontera,150,281,7	script	Event#event02	881,{
	end;
OnInit:
	disablenpc "Event#event02";
	end;

OnCommandOn:
	enablenpc "Event#event02";
OnCommandEmotion:
	emotion e_go;
	end;

OnCommandOff:
	disablenpc "Event#event02";
	end;
}

prontera,188,177,4	script	Happymerry#happymerry	869,{
	if (christ_solo05==1) {
		mes "[Happymerry]";
		mes "Holgren~~!!";
		mes "Je ne pensais pas que tu me trahirais! Je ne veux pas qu'on raffine des objets pour moi! Je déteste Noël~";
		emotion e_sob;
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Êtes-vous...?";
		next;
		mes "[Happymerry]";
		mes "Pourquoi vous riez? Uh~!";
		mes "Au moins je n'ai jamais eu besoin que d'autres raffinent mes armes pour moi! Je suis un brave, moi!";
		mes "Bénédiction? Gloire Divine?~";
		mes "Tous ces couples~ Duh~!!!!!!";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "On dirait que c'est vous que je cherchais!";
		mes "Enjoy vous attend, allons-y!";
		next;
		mes "[Happymerry]";
		mes "...!";
		mes "Alors ce jour est venu?";
		mes "Il m'a aidé l'année dernière après que je n'aie pas réussi à raffiner mes objets. Oh, soit béni, Enjoy~";
		next;
		mes "[Happymerry]";
		mes "Voilà un an que j'attends. Je suis prêt à tout casser pour Noël!";
		mes "Alors, où est Enjoy? Où est-il?!";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Il vous attend. Allez-y~";
		mes "Je vous rejoindrai avec les autres.";
		next;
		mes "[Happymerry]";
		mes "D'accord!";
		mes "J'étais sensé appeler ^0000FFChrist^000000, allez le chercher pour moi! Je vais préparer mes affaires et j'y vais!";
		set christ_solo05,2;
		close;
	}
	else if (christ_solo05 > 1) {
		mes "[Happymerry]";
		mes "Hum... J'ai plus de choses à prendre que je ne le pensais. Après tout, ça fait un an que je mets tout ça de côté...";
		mes "N'oubliez pas de prévenir ^0000FFChrist^000000!";
		mes "À bientôt!";
		close;
	}
	else {
		mes "[Happymerry]";
		mes "Holgren~~!!";
		mes "Je ne pensais pas que tu me trahirais! Je ne veux pas qu'on raffine des objets pour moi! Je déteste Noël~";
		emotion e_sob;
		next;
		mes "[Happymerry]";
		mes "Pourquoi vous riez? Uh~!";
		mes "Au moins je n'ai jamais eu besoin que d'autres raffinent mes armes pour moi! Je suis un brave, moi!";
		mes "Bénédiction? Gloire Divine?~";
		mes "Tous ces couples~ Duh~!!!!!!";
		close;
	}
}

prontera,62,339,3	script	Christ#christ	875,{
	if (christ_solo05 == 2) {
		mes "[Christ]";
		mes "Enfin!!!";
		mes "Je tiens maintenant sur un Péco Péco~";
		mes "Pourquoi devrais-je créer une équipe avec un Prêtre? Je n'en ai pas besoin! Là seule chose dont j'ai besoin, c'est de mon bon gros Péco~";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Hum...";
		next;
		mes "[Christ]";
		mes "Qui êtes-vous?";
		mes "Hé! Ne tournez pas autour de mon Péco! Oh, mon pauvre Péco, tu as eu peur? Ce n'est rien mon bichon~";
		mes "Enjoy est seul depuis le jour où il est né, mais pas moi... J'ai mon fidèle Péco avec moi!";
		next;
		mes "[Christ]";
		mes "Je vais faire une fête avec mon Péco! On se partagera une bûche et puis...";
		mes "Je ne serai pas seul, moi!";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "En fait Enjoy m'a dit que...";
		next;
		mes "[Christ]";
		mes "Uh? Qu'est-ce que vous avez dit? Enjoy? Vous le connaissez? Alors vous devez être envoyé par Happymerry!";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Oui~ Happymerry m'envoie...";
		next;
		mes "[Christ]";
		mes "Alors le jour est enfin venu! Tu as entendu mon Péco? On a un plan! Ce Noël sera inoubliable!";
		mes "Sus aux couples!";
		next;
		mes "[Christ]";
		mes "Bien! Je vais retrouver Enjoy sur mon fidèle Péco~!";
		mes "Allez parler de tout ça à ^0000FFMas^000000!";
		mes "À bientôt~";
		set christ_solo05,3;
		close;
	}
	else if(christ_solo05 > 2) {
		mes "[Christ]";
		mes "On se retrouve là-bas, alors! Je vais voir Enjoy. N'oubliez pas de prévenir ^0000FFMas^000000.";
		mes "À bientôt~";
		close;
	}
	else {
		mes "[Christ]";
		mes "Enfin!!!";
		mes "Je tiens maintenant sur un Péco Péco~";
		mes "Pourquoi devrais-je créer une équipe avec un Prêtre? Je n'en ai pas besoin! Là seule chose dont j'ai besoin, c'est de mon bon gros Péco~";
		next;
		mes "[Christ]";
		mes "Qui êtes-vous?";
		mes "Hé! Ne tournez pas autour de mon Péco! Oh, mon pauvre Péco, tu as eu peur? Ce n'est rien mon bichon~";
		mes "Enjoy est seul depuis le jour où il est né, mais pas moi... J'ai mon fidèle Péco avec moi!";
		next;
		mes "[Christ]";
		mes "Je vais faire une fête avec mon Péco! On se partagera une bûche et puis...";
		mes "Je ne serai pas seul, moi!";
		close;
	}
}

prontera,163,66,7	script	Mas#mas	748,{
	if(christ_solo05 == 3) {
		mes "[Mas]";
		mes "Aaaah!!!!! Quelqu'un sait combien de temps ça prend à ces herbes pour pousser? Je vais les planter et faire des potions pour les vendre aux célibataires!";
		mes "Hahahaha!!";
		next;
		mes "[Mas]";
		mes "Ah~~";
		mes "Pourquoi est-ce que je me sens si vide à l'intérieur?~";
		mes "Non!~";
		mes "Il n'y a pas de temps à perdre~";
		mes "Plantons ces herbes.......";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Hé~ Est-ce que ça va? Vous êtes Mas, c'est ça?";
		next;
		mes "[Mas]";
		mes "Qui... Qui êtes-vous?!!!";
		mes "Ça fait si longtemps que je n'ai pas parlé à un étranger...";
		mes "Hum...";
		mes "Je sens comme si il y avait quelque chose de chaud dans mon coeur...";
		mes "Oubliez ça! À quoi est-ce que je pense?!";
		mes "Allez-vous en~ Je dois planter ces herbes~";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Mas!!";
		mes "Est-ce que ça va?!";
		mes "Christ m'envoie.";
		next;
		mes "[Mas]";
		mes "Quoi?! Déjà?!";
		mes "Oh, d'accord~";
		mes "Dans ce cas je n'ai pas besoin de planter ces herbes!";
		mes "Si Enjoy a lancé le mouvement, je ferais mieux de le rejoindre au plus vite~";
		next;
		mes "[Mas]";
		mes "...Vous venez avec moi?";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Non~";
		mes "Il me reste encore quelque chose à faire. Je dois en parler aux autres.";
		next;
		mes "[Mas]";
		mes "Oh oui, c'est vrai!";
		mes "Bonne chance avec ^0000FFEvent^000000.";
		mes "Bien, au revoir dans ce cas~.";
		mes "Je vous verrai là-bas. ";
		set christ_solo05,4;
		close;
	}
	else if(christ_solo05 > 3) {
		mes "[Mas]";
		mes "Excusez-moi, je dois aller rejoindre Enjoy.";
		mes "Oh, et n'oubliez pas de parler de tout ça à ^0000FFEvent^000000.";
		mes "Ce Noël va être exceptionnel!";
		mes "Hahahaha~";
		close;
	}
	else {
		mes "[Mas]";
		mes "Aaaah!!!!! Quelqu'un sait combien de temps ça prend à ces herbes pour pousser? Je vais les planter et faire des potions pour les vendre aux célibataires!";
		mes "Hahahaha!!";
		next;
		mes "[Mas]";
		mes "Ah~~";
		mes "Pourquoi est-ce que je me sens si vide à l'intérieur?~";
		mes "Non!~";
		mes "Il n'y a pas de temps à perdre~";
		mes "Plantons ces herbes.......";
		close;
	}
}

prontera,35,209,5	script	Event#event	881,{
	if(christ_solo05 == 4) {
		mes "[Event]";
		mes "........................";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "...Est-ce que... vous...?";
		next;
		mes "[Event]";
		mes "...Moi aussi je vous aime!~";
		emotion e_omg,1;
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "(Oh, mon..)";
		next;
		mes "[Event]";
		mes "Ça va, p'tite abeille, je ne suis pas tout seul. J'ai Miss Bathory et M'dame Orc avec moi.";
		mes "Hahahaha~~~";
		mes "";
		next;
		mes "-Il rit en parlant à sa main droite.-";
		mes "";
		//Emotion "Event#event" ET_KIK
		emotion e_kis;
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Mas m'envoie. Vous connaissez son plan, pas vrai?";
		mes "...Est-ce que vous m'écoutez?";
		next;
		mes "[Event]";
		mes "...T'entends?? P'tite abeille?";
		mes "C'est le grand jour!";
		mes "Hahaha~";
		next;
		mes "[Event]";
		mes "Attends-moi, mam'zelle Kafra~~~";
		mes "Arrête de te sentir seule~";
		mes "Je vais rendre ce Noël vraiment inoubliable.";
		mes "Allez, viens, p'tite abeille.";
		mes "";
		next;
		mes "-Il continue à parler à sa main droite, range ses affaires, et fait la révérence à la Kafra.-";
		mes "";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Enfin! J'ai prévenu tout le monde. Maintenant, je devrais préparer mes ^0000FFBranches Mortes^000000 et assouvir ma vengeance sur ces satanés couples!";
		mes "Hahahaha..";
		set christ_solo05,5;
		close;
	}
	else if(christ_solo05 > 4) {
		mes "[Event]";
		mes "Attends-moi, mam'zelle Kafra~~~";
		mes "Arrête de te sentir seule~";
		mes "Je vais rendre ce Noël vraiment inoubliable.";
		mes "Allez, viens, p'tite abeille.";
		mes "";
		next;
		mes "-Il continue à parler à sa main droite, range ses affaires, et fait la révérence à la Kafra.-";
		mes "";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Enfin! J'ai prévenu tout le monde. Maintenant, je devrais préparer mes ^0000FFBranches Mortes^000000 et assouvir ma vengeance sur ces satanés couples!";
		mes "Hahahaha..";
		close;
	}
	else {
		mes "[Event]";
		mes "........................";
		next;
		mes "[Event]";
		mes "...Moi aussi je t'aime!~";
		emotion e_omg,1;
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "(Oh mon~)";
		next;
		mes "[Event]";
		mes "Ça va, p'tite abeille, je ne suis pas tout seul. J'ai Miss Bathory et M'dame Orc avec moi.";
		mes "Hahahaha~";
		mes "";
		next;
		mes "-Il rit en parlant à sa main droite.-";
		mes "";
		emotion e_kis;
		close;
	}
}

// Oholy (Event 3)
prontera,156,242,0	script	Oholy#pron::OholyDup	79,{
	if (!christ_carol05) {
		mes "[Oholy]";
		mes "Joie sur ce monde!";
		mes "Le Seigneur est revenu!";
		specialeffect EF_GLORIA;
		next;
		if (Sex) {
			mes "[Oholy]";
			mes "Joyeux Noël!";
			mes "Cher frère, quelle est la première chose qui vous vient à l'esprit quand vous pensez à Noël?";
			next;
		}
		else {
			mes "[Oholy]";
			mes "Joyeux Noël!";
			mes "Chère soeur, quelle est la première chose qui vous vient à l'esprit quand vous pensez à Noël?";
			next;
		}
		switch(select("Le Père Noël:Les cadeaux de Noël:Les Chants de Noël:Le Bonnet de Noël:Je déteste les fêtes familiales")) {
		case 1:
			mes "[Oholy]";
			mes "Le Père Noël?!";
			if (sex) mes "Vous avez gardé votre innocence d'enfant, jeune homme!";
			else mes "Vous avez gardé votre innocence d'enfant, jeune fille!";
			mes "Hohoho.";
			next;
			mes "[Oholy]";
			mes "On raconte que le Père Noël vivrait dans une ville où Noël ne finit jamais. Mais c'est entre vous et moi, c'est bien compris?";
			next;
			mes "[Oholy]";
			mes "Eh oui! Je vous donne les dernières nouvelles! N'est-ce pas génial?";
			emotion e_heh;
			close;
		case 2:
			mes "[Oholy]";
			mes "Des cadeaux! Oh, j'imagine que vous trouvez ça excitant quand vous vous réveillez et trouvez des cadeaux au pied de votre lit, n'est-ce pas?";
			next;
			mes "[Oholy]";
			mes "Avez-vous entendu parler de ce faux Père Noël qui sévit à chaque Noël et dérobe les cadeaux des enfants sages?";
			next;
			mes "[Oholy]";
			mes "Le vrai père Noël de Lutie offre une récompense à ceux qui captureront le faux Père Noël, Antonio.";
			next;
			mes "[Oholy]";
			mes "C'était les dernières nouvelles de Oholy! Qu'en dites-vous?~";
			emotion e_heh;
			close;
		case 3:
			mes "[Oholy]";
			mes "Oh, c'est vrai! On ne peut se passer des chants de Noël! Je les avais en tête pendant toute la période des fêtes quand j'étais jeune.";
			next;
			mes "[Oholy]";
			mes "Mais il n'y a plus beaucoup de gens qui en chantent de nos jours. On a du mal à se rendre compte que Noël approche.";
			next;
			mes "[Oholy]";
			mes "C'est pour ça que j'ai décidé de souhaiter un joyeux noël à tout le monde en chantant, et en offrant des cadeaux aux petits garçons. Malheureusement, je me suis faite voler mon recueil des chants de Noël!";
			next;
			mes "[Oholy]";
			mes "Il me reste encore beaucoup de maisons à visiter. Je me sens triste pour ces enfants qui n'entendront pas mes chants.";
			next;
			menu "...Est-ce que je peux vous aider?",-;
			mes "[Oholy]";
			mes "Mon Dieu! Êtes-vous sérieux? Tous les enfants vous remercieront de votre gentillesse.";
			emotion e_heh;
			mes "N'ayez pas peur. Je ne vais pas vous demander de composer un nouveau chant de noël.";
			next;
			mes "[Oholy]";
			mes "Si vous avez une volonté, nous avons peu de temps à perdre. Passons à autre chose pour donner espoir aux enfants!";
			next;
			mes "[Oholy]";
			mes "Bien, veuillez me rapporter le livre sur les chants de Noël, "+ strcharinfo(0) +". Ce petit diable devrait encore être en ville, étant donné que c'est arrivé il y a seulement quelques minutes.";
			next;
			mes "[Oholy]";
			mes "S'il vous plaît, soyez prudent car vous avez affaire au diable. Eh bien, je vous souhaite bonne chance!";
			emotion e_no1;
			set christ_carol05,1;
			close;
		case 4:
			mes "[Oholy]";
			mes "Un Bonnet de Père Noël!! Vous savez que le vrai chapeau du Père Noël est totalement différent de celui que portent les monstres?! J'ai entendu dire qu'un styliste anonyme, à Lutie, fait tous les chapeaux du Père Noël.";
			next;
			mes "[Oholy]";
			mes "Et aussi, il y a une autre rumeur qui court à propos d'un faux Père Noël, Antonio. Il a été chassé par de nombreux aventuriers, mais n'a jamais été attrapé à cause de son Costume de Père Noël.";
			next;
			mes "[Oholy]";
			mes "Peut-être que son chapeau et ses vêtements ont des fonctions spéciales d'intégrées... Peut-être que ce styliste anonyme vit toujours à Lutie. Pourquoi n'allez-vous pas lui rendre visite et lui demander de vous faire un nouveau Chapeau de Père Noël?";
			next;
			mes "[Oholy]";
			mes "C'était le dernier scoop d'Oholy! N'était-ce pas incroyable?";
			emotion e_heh;
			close;
		case 5:
			mes "[Oholy]";
			mes "Ah, mon cher. Je pensais la même chose... Oups, ho.. hoho. En fait, ce n'est pas seulement moi. Beaucoup de gens pensent pareil de Noël.";
			next;
			mes "[Oholy]";
			mes "J'ai entendu un bruit étrange courir à propos de personnes qui comploteraient quelque chose pour ce Noël... espérons que tout se passera bien (*murmure*)";
			next;
			mes "[Oholy]";
			mes "C'était le dernier scoop d'Oholy! N'était-ce pas étonnant?";
			emotion e_heh;
			close;
		}
	}
	else if(christ_carol05 == 1) {
		mes "[Oholy]";
		mes "Il ne devrait pas pouvoir s'échapper de la ville. Veuillez trouver ce diable et rapportez-moi mon recueil de chants de Noël. Châtiez le méchant démon qui ruine Noël!";
		close;
	}
	else if(christ_carol05 == 2) {
		if(!checkweight(1201,1)) {
			mes "^3355FFAttendez une seconde! Vous portez trop de choses sur vous là! Veuillez revenir après avoir stocké quelques-uns de vos articles auprès d'une Kafra.^000000";
			close;
		}
		set .@now_weight,MaxWeight-Weight;
		if(.@now_weight < 2000) {
			mes "^3355FFAttendez une seconde! Vous portez trop de choses sur vous là! Veuillez revenir après avoir stocké quelques-uns de vos articles auprès d'une Kafra.^000000";
			close;
		}
		if (countitem(1097) > 0) {
			mes "[Oholy]";
			mes "Oh, mon Dieu! Vous m'avez rapporté le livre! Le diable ne vous a pas trop tourmenté? Je suis heureuse que vous soyez revenu sans encombre.";
			next;
			mes "[Oholy]";
			mes "Pour vous remercier, je vais chanter un chant de noël de mon recueil.";
			next;
			mes "[Oholy]";
			mes "Hum! Huum!!";
			mes "~Dormez, petits enfants,~";
			mes "~où que vous soyez;~";
			mes "~Demain c'est Noël~";
			mes "~sous tous les cieux.~";
			specialeffect EF_GLORIA;
			next;
			mes "-Votre esprit est submergé par son chant.-";
			mes "-Vous commencez à fredonner puis,-";
			mes "-vous vous surprenez à chanter la suite de la chanson-";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "~Bientôt les flocons tomberont~";
			mes "~et demain, vous verrez~";
			mes "~les souhaits de chacun,~";
			mes "~seront exaucés sous le sapin.~";
			specialeffect2 EF_GLORIA;
			next;
			mes "[Oholy]";
			mes "Oh, ça alors! Vous avez une voix magnifique!! D'accord!!!!";
			mes "J'allais les donner aux enfants, mais, puisque vous avez trouvé mon livre de musique et que vous m'avez chanté un cantique de Noël!!";
			next;
			mes "-Elle a apporté un grand sac-";
			mes "-et l'a ouvert devant vous-";
			next;
			mes "[Oholy]";
			mes "Bien! Ne regardez pas à l'intérieur. Plongez-y seulement vos mains et sortez-en ce que vous voulez.";
			next;
			switch(rand(1,15)) {
			case 1:
				mes "[Oholy]";
				mes "Des Sacs de Cookies!";
				mes "Je les ai emballés individuellement. Il y a beaucoup de sucreries à l'intérieur. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 12130,7;//Cookie_Bag
				close;
			case 2:
				mes "[Oholy]";
				mes "Des Bonbons!";
				mes "Ils ont été faits par le Chef en titre. Très doux et délicieux. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 529,20;//Candy
				close;
			case 3:
				mes "[Oholy]";
				mes "Des Sucres d'Orge!";
				mes "Ils ont été faits par le Chef en titre. Très doux et délicieux. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 530,15;//Candy_Striper
				close;
			case 4:
				mes "[Oholy]";
				mes "Des Parts de Gâteau!";
				mes "Elles ont été cuites par l'Acolyte en chef. Très douces et délicieuses. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 539,5;//Piece_of_Cake
				close;
			case 5:
				mes "[Oholy]";
				mes "Des Cookies Cuits à Point!";
				mes "Ils ont été cuits par l'Acolyte en chef. Très croustillants et délicieux. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 538,10;//Well_baked_Cookie
				close;
			case 6:
				mes "[Oholy]";
				mes "Un Spore en Peluche!";
				mes "Il a été fait minutieusement par l'Évêque, Tomas. Très mignon. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 743,1;//Spore_Doll
				close;
			case 7:
				mes "[Oholy]";
				mes "Un Baphomet en Peluche!";
				mes "..........?!..........";
				mes "Comment est-il arrivé ici..?! Oups, hé bien. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 750,1;//Baphomet_Doll
				close;
			case 8:
				mes "[Oholy]";
				mes "Un Osiris en Peluche!";
				mes "..........?!..........";
				mes "Comment est-il arrivé ici..?! Oups, hé bien. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 751,1;//Osiris_Doll
				close;
			case 9:
				mes "[Oholy]";
				mes "Un Rocker en Peluche!";
				mes "Ceci a été offert par un chevalier nommé Lighten. C'est très gentil de sa part. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 752,1;//Grasshopper_Doll
				close;

			case 10:
				mes "[Oholy]";
				mes "Un Yoyo en Peluche!";
				mes "Il a été offert par un assassin nommé Marzia. C'est très gentil à lui. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 753,1;//Monkey_Doll
				close;
			case 11:
				mes "[Oholy]";
				mes "Un Raton-Laveur en Peluche!";
				mes "Un chasseur nommé Raiden Kurs en a fait don. C'est très gentil à lui. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 754,1;//Raccoondog_Doll
				close;
			case 12:
				mes "[Oholy]";
				mes "Un Chat Noir en Peluche!";
				mes "Soeur Magareta l'a trouvé sur le monstre Loli Ruri. C'est très gentil à elle. J'espère qu'elle se porte bien. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 7206,1;//Black_Kitty_Doll
				close;
			case 13:
				mes "[Oholy]";
				mes "Une Poupée de Pendu!";
				mes "J'ai fait cette poupée. N'est-elle pas adorable?! Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 7212,1;//Hanging_Doll
				close;
			case 14:
				mes "[Oholy]";
				mes "Une Munak en Peluche!";
				mes "Elle vient d'un pays de l'autre côté de l'océan. Un artisan a fait cette poupée avec passion. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 7277,1;//Munak_Doll
				close;
			case 15:
				mes "[Oholy]";
				mes "Un Bonnet de Père Noël!";
				mes "Il est uniquement fabriqué en période de Noël. Ce n'est pas un chapeau commun. Joyeux Noël!";
				emotion e_heh;
				delitem 1097,1;//Worn_Out_Page
				set christ_carol05,3;
				getitem 2236,1;//Santa's_Hat
				close;
			}
		}
		else {
			mes "[Oholy]";
			mes "Je suis contente que vous soyez de retour!!! Vous avez bonne mine! 2 bras et 2 jambes, tout va bien. Mais, où est mon livre de musique?!";
			close;
		}
	}
	else {
		mes "[Oholy]";
		mes "Merci beaucoup. Les gens et même les Démons sont tout excités le jour de Noël, ainsi personne ne sait ce qui va arriver. Vous allez donc m'aider, n'est-ce pas? S'il vous plaît~";
		next;
		mes "[Oholy]";
		mes "Pensons à nos prochains et faisons une bonne action à Noël!";
		set christ_carol05,0;
		close;
	}
}

// Deviruchi functions
function	script	F_carol_devi	{
	if (christ_carol05 == 1) {
		mes "[Deviruchi]";
		mes "Héhé, humain!! T'veux pas vendre ton âme et passer un bon pacte avec moi?";
		emotion e_kis;
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Hé, toi! Deviruchi!!!";
		mes "Sale morveux!!! Je te tiens!!";
		next;
		mes "-Vous saisissez rapidement la nuque du Deviruchi-";
		next;
		switch(rand(1,4)) {
		case 1:
			mes "[Deviruchi]";
			mes "Que fais-tu!?";
			mes "Humain?";
			mes "Lâche moi... tout de suite!";
			emotion e_omg;
			emotion e_omg,1;
			next;
			mes "-Le Deviruchi prend rapidement la fuite,-";
			mes "-^4d4dfflaissant derrière lui un papier usé.^000000-";
			mes "-Vous lisez le papier.-";
			enablenpc getarg(1);
			disablenpc getarg(0);
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "Rudolph... Si... Brumeux... Traîneau... Rennes... Hmm, je crois que j'ai mis la main sur ce que je cherche. Retournons voir Mme Oholy.";
			set christ_carol05,2;
			getitem 1097,1; //Worn_Out_Page
			close;
		case 2:
			mes "[Deviruchi]";
			mes "Que fais-tu!?";
			mes "Humain?";
			mes "Lâche moi... tout de suite!";
			emotion e_omg;
			emotion e_omg,1;
			next;
			mes "-Le Deviruchi s'enfuit rapidement en marmonnant quelque chose.-";
			enablenpc getarg(1);
			disablenpc getarg(0);
			next;
			mes "-Vous êtes maintenant maudits!!!-";
			sc_start SC_Curse,5000,0;
			emotion e_omg,1;
			close;
		case 3:
			mes "[Deviruchi]";
			mes "Que fais-tu!?";
			mes "Humain?";
			mes "Lâche moi... tout de suite!";
			emotion e_omg;
			emotion e_omg,1;
			next;
			mes "-Le Deviruchi s'enfuit rapidement en marmonnant quelque chose.-";
			enablenpc getarg(1);
			disablenpc getarg(0);
			next;
			mes "-Vous êtes aveuglé!-";
			sc_start SC_Blind,5000,0;
			emotion e_omg,1;
			close;
		case 4:
			mes "[Deviruchi]";
			mes "Que fais-tu!?";
			mes "Humain?";
			mes "Lâche moi... tout de suite!";
			emotion e_omg;
			emotion e_omg,1;
			next;
			mes "-Le Deviruchi s'enfuit rapidement en marmonnant quelque chose.-";
			enablenpc getarg(1);
			disablenpc getarg(0);
			next;
			mes "-Vous êtes empoisonnés!!!-";
			sc_start SC_Poison,5000,0;
			emotion e_omg,1;
			close;
		}
	}
	else {
		mes "[Deviruchi]";
		mes "Héhé, humain!! T'veux pas vendre ton âme et passer un bon pacte avec moi?";
		emotion e_omg;
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Hé, toi! Deviruchi!!!";
		mes "Sale morveux!!! Je te tiens!!";
		next;
		mes "-Vous saisissez rapidement la nuque du Deviruchi-";
		next;
		mes "[Deviruchi]";
		mes "Que fais-tu!?";
		mes "Humain?";
		mes "Lâche moi... tout de suite!";
		emotion e_omg;
		emotion e_omg,1;
		next;
		mes "-Le Deviruchi s'enfuit à toute allure en marmonnant quelque chose.-";
  		mes "-Votre corps se met soudainement à flotter.-";
		close2;
		warp getarg(2),getarg(3),getarg(4);
		end;
	}
}

function	script	F_carol_devi2	{
	mes "[Deviruchi]";
	mes "Héhé, humain!! T'veux pas vendre ton âme et passer un bon pacte avec moi?";
	emotion e_omg;
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Hé, toi! Deviruchi!!!";
	mes "Sale morveux!!! Je te tiens!!";
	next;
	mes "-Vous saisissez rapidement la nuque du Deviruchi-";
	next;
	mes "[Deviruchi]";
	mes "Que fais-tu!?";
	mes "Humain?";
	mes "Lâche moi... tout de suite!";
	emotion e_omg;
	emotion e_omg,1;
	next;
	mes "-Le Deviruchi s'enfuit à toute allure en marmonnant quelque chose.-";
	mes "-Votre corps se met soudainement à flotter.-";
	close2;
	warp getarg(0),getarg(1),getarg(2);
	end;
}

prontera,94,297,3	script	Deviruchi#pron_01	738,3,3,{
	callfunc("F_carol_devi2","prontera",155,230);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#pron_01","Deviruchi#pron_02","prontera",155,230);
	end;
}

prontera,205,242,3	script	Deviruchi#pron_02	738,3,3,{
	callfunc("F_carol_devi2","prontera",155,230);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#pron_02","Deviruchi#pron_03","prontera",155,230);
	end;
}

prontera,126,118,3	script	Deviruchi#pron_03	738,3,3,{
	callfunc("F_carol_devi2","prontera",155,230);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#pron_03","Deviruchi#pron_01","prontera",155,230);
	end;
}

// Payon
payon,165,153,3	duplicate(OholyDup)	Oholy#payon	79
payon,142,203,3	script	Deviruchi#payon_01	738,3,3,{
	callfunc("F_carol_devi2","payon",166,60);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#payon_01","Deviruchi#payon_02","payon",166,60);
	end;
}

payon,248,239,3	script	Deviruchi#payon_02	738,3,3,{
	callfunc("F_carol_devi2","payon",166,60);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#payon_02","Deviruchi#payon_03","payon",166,60);
	end;
}

payon,140,56,3	script	Deviruchi#payon_03	738,3,3,{
	callfunc("F_carol_devi2","payon",166,60);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#payon_03","Deviruchi#payon_01","payon",166,60);
	end;
}

morocc,161,85,0	duplicate(OholyDup)	Oholy#morocc	79
morocc,52,134,3	script	Deviruchi#morocc_01	738,3,3,{
	callfunc("F_carol_devi2","morocc",160,51);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#morocc_01","Deviruchi#morocc_02","morocc",160,51);
	end;
}

morocc,210,264,3	script	Deviruchi#morocc_02	738,3,3,{
	callfunc("F_carol_devi2","morocc",160,51);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#morocc_02","Deviruchi#morocc_03","morocc",160,51);
	end;
}

morocc,223,74,3	script	Deviruchi#morocc_03	738,3,3,{
	callfunc("F_carol_devi2","morocc",160,51);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#morocc_03","Deviruchi#morocc_01","morocc",160,51);
	end;
}

// Geffen
geffen,120,188,0	duplicate(OholyDup)	Oholy#geffen	79
geffen,173,163,3	script	Deviruchi#geffen_01	738,3,3,{
	callfunc("F_carol_devi2","geffen",120,34);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#geffen_01","Deviruchi#geffen_02","geffen",120,34);
	end;
}

geffen,49,95,3	script	Deviruchi#geffen_02	738,3,3,{
	callfunc("F_carol_devi2","geffen",120,34);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#geffen_02","Deviruchi#geffen_03","geffen",120,34);
	end;
}

geffen,111,101,3	script	Deviruchi#geffen_03	738,3,3,{
	callfunc("F_carol_devi2","geffen",120,34);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#geffen_03","Deviruchi#geffen_01","geffen",120,34);
	end;
}

// Alberta
alberta,54,237,0	duplicate(OholyDup)	Oholy#alberta	79
alberta,97,86,3	script	Deviruchi#alberta_01	738,3,3,{
	callfunc("F_carol_devi2","alberta",28,235);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#alberta_01","Deviruchi#alberta_02","alberta",28,235);
	end;
}

alberta,180,48,3	script	Deviruchi#alberta_02	738,3,3,{
	callfunc("F_carol_devi2","alberta",28,235);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#alberta_02","Deviruchi#alberta_03","alberta",28,235);
	end;
}

alberta,54,132,3	script	Deviruchi#alberta_03	738,3,3,{
	callfunc("F_carol_devi2","alberta",28,235);
	end;

OnTouch:
	callfunc("F_carol_devi","Deviruchi#alberta_03","Deviruchi#alberta_01","alberta",28,235);
	end;
}
