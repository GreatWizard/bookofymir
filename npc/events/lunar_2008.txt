//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//===== eAthena Script =======================================
//= Lunar New Year Event (2008)
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= iRO Lunar New Year event; Year of the Rat. (2008)
//= Kill Cramps, Taruus, and Matrins to retrieve "Rice Pouches"
//= in return for various rice-themed foods, envelopes, and
//= a "New year Doll" egg. Rates are guessed.
//=
//= Cramp, Tarou, and Matrin drops altered, Uncomment in
//= mob_db2 to enable. [required]
//=
//= Miss Lunar NPC uses the sprite 4_f_nacoruri, which isn't
//= standard in kRO's data files. If you're having problems
//= or are unsure if your client supports that sprite, an
//= alternate NPC header has been included.
//===== Additional Comments: =================================
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 Added missing checkweights. [L0ne_W0lf]
//============================================================

payon_in01,193,30,4	script	Grand-Mère de la Rizerie::Rice Mill Grandma#rat	78,{
	mes "[Grand-Mère de la Rizerie]";
	if (!lunar_rat) {
		mes "Saleté de bestioles! Toujours à trottiner dans les parages et à me dérober des choses! Si seulement elles n'étaient pas aussi rapides...";
		next;
		select("Qu'est-ce qui ne va pas?");
		mes "[Grand-Mère de la Rizerie]";
		mes "Oh, j'étais en train de préparer des gâteaux de riz et des pâtisseries pour célébrer la nouvelle année, mais ces sales bêtes ont pillé les sacs de riz que j'avais préparés.";
		next;
		select("Des sacs de riz?");
		mes "[Grand-Mère de la Rizerie]";
		mes "Oui, mon fils en a besoin. Sans ça il ne peut pas réduire le riz en poudre pour que je puisse faire mes pâtisseries. Comment je ferais si je n'ai même pas de riz? Pensez-vous que vous pourriez m'aider?";
		next;
		if (select("Bien sûr.:Je ne vous connais même pas.") == 1) {
			set lunar_rat,1;
			mes "[" + strcharinfo(0)+"]";
			mes "Bien sûr.";
			mes "Que dois-je faire?";
			next;
			mes "[Grand-Mère de la Rizerie]";
			mes "Eh bien... Est-ce que vous pensez pouvoir attraper les bêtes qui ont volé mes Sacs de Riz? Ce sont ces rats blancs et bleuâtres, et ces vicieuses petites taupes.";
			next;
			select("Ce n'est pas un problème.");
			mes "[" + strcharinfo(0)+"]";
			mes "Aucun problème. Je vais les attraper et vous ramener autant de sac de riz que je trouverai.";
			next;
			mes "[Grand-Mère de la Rizerie]";
			mes "Oh, merci!";
			mes "Bonne chance avec cette vermine~";
			close;
		}
		mes "[Grand-Mère de la Rizerie]";
		mes "Je... Eh bien...";
		mes "C'est vrai, mais je comptais vous donner un petit quelque chose en retour. Je sais que vous n'êtes pas mauvais au fond de vous, alors...";
		close;
	}
	else if ((lunar_rat >= 1 && lunar_rat <= 2) || lunar_rat == 4) {
		if (countitem(7770) >= 1 && lunar_rat == 2) {
			mes "Oh, c'est le Riz Doux que mon fils a préparé? Puis-je l'avoir?";
			mes "Attendez un moment: je vous prépare un gâteau~";
			delitem 7770,1; //Sweet_rice
			set lunar_rat,3;
			close;
		}
		else if (lunar_rat == 4) {
			if (countitem(7770) >= 1) {
				mes "Oh, c'est le Riz Doux que mon fils a préparé? Puis-je l'avoir?";
				mes "Attendez un moment: je vous prépare un gâteau~";
				delitem 7770,1; //Sweet_rice
				set lunar_rat,5;
				close;
			}
			mes "Oh, où sont tous les sacs de riz? J'en ai besoin pour préparer mes pâtisseries...";
			close;
		}
		else if (countitem(7869) < 1 && lunar_rat <= 2) {
			mes "Oh, ces rats blancs et bleuâtres ont dérobé mes sacs de riz. Et ces saletés de taupes se sont sans doute aussi servies. Quelle vermine, n'est-ce pas?";
			close;
		}
		mes "Oh, c'est l'un de mes sacs de riz? Je suis désolée, mais j'ai mal aux reins. Voudriez-vous être un ange et l'apporter à mon fils pour moi?";
		set lunar_rat,2;
		close;
	}
	else if (lunar_rat == 3 || lunar_rat == 5) {
		set .@reward,rand(1,100);
		if (.@reward <= 5) {
			if (lunar_rat == 3) {
				getitem 9038,1; // New_Year_Doll_Egg
				set lunar_rat,4;
			}
			else if (lunar_rat == 5) {
				getitem 668,1; // Red_Envelope
			}
		}
		else if (.@reward >= 6 && .@reward <= 10) getitem 12198,2; // Lucky_Rice_Cake
		else if (.@reward >= 11 && .@reward <= 30) getitem 12195,3; // Plain_Rice_Cake
		else if (.@reward >= 31 && .@reward <= 40) getitem 12196,2; // Hearty_Rice_Cake
		else if (.@reward >= 41 && .@reward <= 55) getitem 12123,2; // Honey_Pastry
		else if (.@reward >= 56 && .@reward <= 70) getitem 12122,2; // Sesame_Pastry
		else if (.@reward >= 71 && .@reward <= 80) getitem 12124,2; // Rainbow_Cake
		else if (.@reward >= 81 && .@reward <= 90) getitem 12198,2; // Korean_Rice_Cake
		else if (.@reward >= 91 && .@reward <= 100) getitem 12197,3; // Salty_Rice_Cake
		// After getting the egg there is a chance to get an envelope with each turn in
		if (rand(1,20) <= 5 && lunar_rat == 4) getitem 668,1; // Red_Envelope
		// Set the quest accordingly for repeat turn-ins.
		if (lunar_rat == 3) set lunar_rat,2;
		else if (lunar_rat == 5) set lunar_rat,4;
		mes "Ce n'est rien comparé au service que vous m'avez rendu, mais je voudrais que vous preniez ce gâteau que je viens de préparer. Régalez-vous~";
		next;
		mes "[Grand-Mère de la Rizerie]";
		mes "Merci pour le Riz Doux! Je vous préparerai quelque chose de délicieux si vous m'en apportez d'autres, d'accord?";
		close;
	}
}

payon,215,127,4	script	Homme de la Rizerie::Rice Mill Man#rat	54,{
	mes "[Homme de la Rizerie]";
	if (!lunar_rat) {
		mes "Ça me brise le cœur...";
		mes "Ma mère passe tout son temps à préparer des gâteaux de riz pour le nouvel an, mais des vermines ont volé tout son riz!";
		close;
	}
	else if (lunar_rat == 1) {
		mes "Ma mère m'a dit que vous l'aidiez à retrouver son riz. C'est une brave vieille femme, n'est-ce pas?";
		mes "Si vous trouvez des Sacs de Riz, vous devriez aller lui montrer.";
		close;
	}
	else if (lunar_rat >= 2) {
		if (countitem(7869) < 1) {
			mes "Si vous m'apportez des Sacs de Riz, je pourrai en faire du Riz Doux. Vous voulez aider ma mère, pas vrai?";
			mes "J'apprécie vraiment, merci.";
			close;
		}
		mes "Oh, vous m'avez apporté des sacs de riz de ma mère?";
		mes "Laissez-moi un instant, je vais le broyer pour en faire de la pâte en un rien de temps.";
		next;
		mes "[Homme de la Rizerie]";
		mes "Hoo! Haa! Hi-yah!";
		mes "Woosha! Whoosha!";
		mes "Ka-taaaaaaaaaa!";
		mes "WOOOSHA!";
		next;
		mes "[Homme de la Rizerie]";
		mes "Voilà, c'est fait. Ce Riz Doux est prêt à être utilisé pour faire des gâteaux. Voudriez-vous bien l'apporter à ma mère?";
		delitem 7869,1; //Rice_Pouch
		getitem 7770,1; //Sweet_Rice
		close;
	}
}

//payon,126,113,4	script	Madame Luna::Miss Lunar#rat	862,{
payon,126,113,4	script	Madame Luna::Miss Lunar#rat	719,{
	mes "[Madame Luna]";
	if ((MaxWeight-Weight) < 3000 || checkweight(1201,1) == 0) {
		mes "Oh, je suis désolée, mais je ne peux rien pour vous si vous êtes aussi surchargé. Vous devriez déposer quelques objets dans votre Stock Kafra.";
		close;
	}
	if (lunar_rat < 4) {
		mes "Je voudrais gagner assez d'argent pour pouvoir me payer l'une de ces grandes broches en argent en forme de croissant de lune. Je dois vendre autant de Mochi que possible!";
		close;
	}
	else if (lunar_rat >= 4) {
		mes "Oh, " + ( isday() ? "bonjour" : "bonsoir" ) + "! Je vends des gâteaux de riz spéciaux faits à partir de riz doux. Vous pourrez le manger ou nourrir votre Poupée du Nouvel An avec, si vous en avez une comme Animal de Compagnie.";
		next;
		mes "[Madame Luna]";
		mes "Bref, je fais les 10 Mochis à 3,000 zénys. En voulez-vous quelques uns?";
		next;
		if (select("Oui:Non") == 1) {
			if (Zeny >= 3000) {
				set Zeny,Zeny-3000;
				getitem 554,10; // Mojji
				mes "[Madame Luna]";
				mes "Merci. J'espère que vous passerez une bonne fête du nouvel an!";
				mes "Revenez quand vous voulez~";
				close;
			}
			mes "[Madame Luna]";
			mes "Oh, je suis désolée, mais vous n'avez pas assez d'argent sur vous en ce moment....";
			close;
		}
		mes "[Madame Luna]";
		mes "Oh, d'accord. Je reste ici, au cas où vous changiez d'avis, ou que l'un de vos amis veuille des Mochis, d'accord? À bientôt~";
		close;
	}
}
