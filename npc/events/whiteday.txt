//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//===== eAthena Script =======================================
//= White Day Event Script
//===== By: ==================================================
//= 1.0a Muad_Dib (Prometheus Project)
//===== Current Version: =====================================
//= 1.0a
//===== Compatible With: =====================================
//= Any Athena Version; RO Episode 6+
//===== Description: =========================================
//= Sells candy, candy cane and well baked cookie.
//===== Additional Comments: =================================
//= 07/06/05 : Added 1st Version. [Muad_Dib]
//= Converted to eAthena format by Dr.Evil
//============================================================


alberta,188,64,4	script	Sucre	91,{
	set @maplenum,0;
	set @mapleItemID,0;
	set @maplePrice,0;
	set @maplePriceT,0;
	mes "[Sucre]";
	mes "Bienvenue! Alors, vous aimez les sucreries? Je suis l'élève du plus grand confiseur d'Alberta! Je peux préparer les sucreries les plus complexes.";
	next;
	mes "[Sucre]";
	mes "On avait autrefois l'habitude de donner des sucreries à son bien-aimé en retour lorsqu'il nous offrait un cadeau. J'imagine que c'était pour avoir dans la bouche la douceur que l'on avait dans le coeur...";
	next;
	menu "J'en veux!",-,"Ça n'est pas pour moi.",M_END,"Le plus grand confiseur?",M_L1;

	mes "[Sucre]";
	mes "Oh, je peux vous en vendre si vous voulez. Choisissez ce qui vous intéresse dans la liste ci-dessous. Attention, vous ne pourrez pas en acheter plus de ^ff00005^000000 à la fois.";
	next;
	menu "Bonbons",-,"Sucres d'Orge",L0_2,"Cookies",L0_3;

	set @maplePrice,3000;
	set @mapleItemID,529;
	mes "[Sucre]";
	mes "Je vends mes Bonbons 3000 zénys pièce. Combien en voulez-vous?";
	next;
	goto L_INPUT;
L0_2:
	set @maplePrice,4000;
	set @mapleItemID,530;
	mes "[Sucre]";
	mes "Je vends mes Sucres d'Orge 4000 zénys pièce. Combien en voulez-vous?";
	next;
	goto L_INPUT;
L0_3:
	set @maplePrice,2000;
	set @mapleItemID,538; 
	mes "[Sucre]";
	mes "Je vends mes Cookies 2000 zénys pièce. Combien en voulez-vous?";
	next;

L_INPUT:
	input @maplenum;
	if (@maplenum > 5) goto L_ERROR;
	if (@maplenum == 0) goto M_END;
	set @maplePriceT,@maplePrice*@maplenum;
	if (Zeny < @maplePriceT) goto L_ERROR2;
	set Zeny,Zeny- @maplePriceT;
	getitem @mapleItemID,@maplenum;
	mes "[Sucre]";
	mes "Merci! Vous verrez, vous allez adorer mes sucreries. J'ai appris auprès du plus grand confiseur au monde! Attention tout de même à ne pas trop en manger... Vous ne voulez pas grossir, si? À bientôt!";
	close;

M_L1:
	mes "[Sucre]";
	mes "Oui, M. Kuberu m'a tout appris. Nous avons fait ces sucreries tous les deux - ça m'a permis de progresser.";
	next;
	mes "[Sucre]";
	mes "Cependant je me demande... Pourquoi est-ce qu'il n'est pas là à les vendre avec moi, puisqu'on se partage le travail?";
	close;

L_ERROR:
	mes "[Sucre]";
	mes "Vous ne m'aviez pas entendu? Je ne peux pas en vendre plus de ^ff00005^000000 à la fois.";
	next;
	goto L_INPUT;

L_ERROR2:
	mes "[Sucre]";
	mes "Hoho! On dirait que vous n'avez pas assez d'argent pour m'acheter tout ça. Vous devriez d'abord vérifier combien vous avez sur vous.";
	close;

M_END:
	mes "[Sucre]";
	mes "Quel dommage... Je suis sûr que vous le regretterez plus tard. Je reste là, si jamais vous changez d'avis, d'accord? Passez une bonne journée!";
	close;
}
