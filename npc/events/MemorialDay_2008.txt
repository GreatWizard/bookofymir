//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par NoH
// Bon, j'avoue, je me suis lâché sur cette trad, et j'en ai rajouté des tonnes. Ce n'est vraiment pas du littéral.
// Mais faire traduire ça à quelqu'un que la chose militaire répugne, aussi...
//===== eAthena Script =======================================
//= In Memory of Heroes (iRO Memorial Day 2008)
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//
//===== Additional Comments: =================================
//= 1.0 First version. [Kisuka]
//= 1.1 Many Many Fixes. [Kisuka]
//= 1.2 Some changes. [Kisuka]
//============================================================

// ================== Lauds ======================
// ===============================================
prontera,182,214,4	script	Lauds#Memorial	58,{
	if(Memorial08 < 1) {
		mes "[Sago Lauds le Fou]";
		mes "Hé, salut! Qu'est-ce que vous faites là?!";
		next;
		mes "[Sago Lauds le Fou]";
		mes "Vous ne savez pas quel jour on est? Mais voyons, c'est une date des plus importantes! Une date dont tout le monde devrait se souvenir!";
		next;
		mes "[Sago Lauds le Fou]";
		mes "Mais... Qu'est-ce que c'est que cela?!";
		emotion 0;
		next;
		mes "[Sago Lauds le Fou]";
		mes "Où est votre serviette?!";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "Ma quoi?";
		next;
		mes "[Sago Lauds le Fou]";
		mes "Et comment est-ce que vous pensez que vous allez accomplir cette quête sans serviette?!";
		next;
		mes "[Sago Lauds le Fou]";
		mes "Si vous êtes l'un"+ ( sex ? "" : "e" ) +" de ces stupides hippies pacifistes, je ne vais pas perdre mon temps avec vous! Mais si jamais vous êtes un"+ ( sex ? "" : "e" ) +" véritable patriote, "+ ( sex ? "fier" : "fière" ) +" de son pays et des braves qui ont donné leur vie pour notre glorieuse nation, je vous aiderai à vous préparer.";
		next;
		if (select("J'imagine...:Vous êtes complètement taré, non?") == 2) {
			mes "[Sago Lauds le Fou]";
			mes "Vous ne voulez pas vous préparer, très bien!";
			next;
			mes "[Sago Lauds le Fou]";
			mes "^FF0000VOUS ALLEZ LE REGRETTER AMÈREMENT!^000000";
			close;
		}
		mes "[Sago Lauds le Fou]";
		mes "Bien! Alors, écoutez-moi attentivement.";
		next;
		mes "[Sago Lauds le Fou]";
		mes "Avant de pouvoir rendre hommage à tous nos braves, tombés au combat pour défendre nos idéaux et forcer tous ces barbares à connaître les joies de la liberté, vous devez vous préparer. Sans serviette, vous serez perdu"+ ( sex ? "" : "e" ) +"!";
		mes "Si vous me rapportez les matériaux, je pourrai vous en fabriquer une.";
		next;
		mes "[Sago Lauds le Fou]";
		mes "Écoutez-moi bien.";
		mes "Rapportez-moi ^FF000030 morceaux de Tissu et 20 Duvets^000000.";
		set Memorial08,1;
		close;
	}

	else if(Memorial08 == 1) {
		if (countitem(1059) < 30 || countitem(914) < 20) {
			mes "[Sago Lauds le Fou]";
			mes "Qu'est-ce que c'est que ça? Ce n'est pas assez!";
			mes "*Soupire* Je dois vraiment tout vous redire?!";
			mes "Rapportez-moi ^FF000030 morceaux de Tissu et 20 Duvets^000000.";
			next;
			mes "[Sago Lauds le Fou]";
			mes "Dites-moi: vous n'essayez pas de saborder cette fête au moins, parce que sinon... SINON...";
			close;
		}else{
			mes "*** Sago Lauds le Fou semble marmonner quelque chose dans sa barbe... ***";
			next;
			mes "[Sago Lauds le Fou]";
			mes "Alors, vous m'avez rapporté de quoi confectionner une serviette?";
			next;
			mes "[Sago Lauds le Fou]";
			mes "Excellent, vous m'avez tout rapporté. Je vais maintenant comme promis vous faire une Serviette du Souvenir en hommage à notre divine nation!";
			mes "Laissez-moi un moment.";
			next;
			mes "[Sago Lauds le Fou]";
			mes "Et voilà pour vous!";
			delitem 1059,30;
			delitem 914,20;
			getitem 6025,1;
			getnameditem 6025," + strcharinfo(0) + ";
			set Memorial08,2;
			close;
		}
	}
	
	else if(Memorial08 == 2) {
		mes "[Sago Lauds le Fou]";
		mes "Ah, vous avez une nouvelle serviette toute douce maintenant. J'ai même brodé votre nom dessus.";
		next;
		mes "[Sago Lauds le Fou]";
		mes "Dites, est-ce que vous seriez tenté"+ ( sex ? "" : "e" ) +" par une petite quête pour rendre hommage à nos braves?";
		next;
		if (select("Bien sûr.:Non, je suis occupé"+ ( sex ? "" : "e" ) +".") == 2) {
			mes "[Sago Lauds le Fou]";
			mes "Hé bien ne venez pas pleurer si quelqu'un décide de construire une route à travers votre maison!";
			next;
			mes "[Sago Lauds le Fou, dans sa barbe]";
			mes "Tous ces drogués et ces hippies débiles... Les gens comme eux haïssent la démocratie - euh... notre bien-aimée monarchie! Même pas foutus de rendre hommage à ceux qui se battent pour leur droit à ce que notre bon roi décide des choses à leur place...";
			next;
			mes "[Sago Lauds le Fou]";
			mes "Et ils vont encore trouver le moyen de se plaindre après ça!";
			close;
		}
		mes "[Sago Lauds le Fou]";
		mes "Bien. Au fait, vous savez à quoi cette serviette va servir au moins?";
		next;
		mes "[Sago Lauds le Fou]";
		mes "On dirait que vous la portez sans comprendre ce qu'elle représente. Quelle honte! Vous devriez aller parler à ^FF0000Grast à Prontera^000000.";
		set Memorial08,3;
		close;
	}

	else if(Memorial08 == 3) {
		mes "[Sago Lauds le Fou]";
		mes "Qu'est-ce que faite encore là? Je vous ai dit d'aller parler à Grast à Prontera! Vous ne seriez pas l'un"+ ( sex ? "" : "e" ) +" de ces pacifistes qui serait venu"+ ( sex ? "" : "e" ) +" m'espionner par hasard?!";
		close;
	}

	else if(Memorial08 == 4 || Memorial08 == 5 || Memorial08 == 6) {
		mes "[Sago Lauds le Fou]";
		mes "Allez aider Grast, et revenez me voir ensuite.";
		close;
	}

	else if(Memorial08 == 7) {
		mes "[Sago Lauds le Fou]";
		mes "Oh, vous m'avez tout rapporté!";
		mes "Hahaha!";
		mes "HAHAHAHAHAHA!!!! *Tousse* *Tousse*";
		next;
		mes "[Sago Lauds le Fou]";
		mes "Donnez-moi tout ça, s'il vous plaît. Merci pour votre dur labeur, au fait. Dites-moi... Est-ce que vous avez appris quelque chose dans votre quête?";
		mes "Allez maintenant voir la plaque commémorative au Nord de Prontera.";
		next;
		mes "[Sago Lauds le Fou]";
		mes "Comment ça, c'est de là que vous venez?";
		next;
		mes "[Sago Lauds le Fou]";
		mes "Hein? Et pourquoi est-ce que vous m'avez rapporté le fond de vos poubelles? Qu'est-ce que je pourrais bien faire de tout ça, moi? Hein?! DITES-MOI?!";
		next;
		mes "[Sago Lauds le Fou]";
		mes "Allez voir cette satanée plaque maintenant! À moins que vous ne soyez l'un"+ ( sex ? "" : "e" ) +" de ces traitres à la nation?! L'un"+ ( sex ? "" : "e" ) +" de ces *pacifistes*?!";
		set Memorial08,8;
		close;
	}

	else if(Memorial08 == 8) {
		mes "[Sago Lauds le Fou]";
		mes "Mais arrêtez d'essayer de me refourguer le fond de vos poubelles, enfin! Allez vois la plaque au Nord de Prontera! MAINTENANT!";
		close;
	}
	
	else if(Memorial08 >= 9) {
		set .@RandomMsg, rand(1,5);
		if(.@RandomMsg == 1) {
			mes "[Sago Lauds le Fou]";
			mes "N'ayez pas peur! Je ne mords pas. À part peut-être ces abrutis de pac... Oh, oubliez ça.";
			emotion 0;
			close;	
		}
		else if(.@RandomMsg == 2) {
			mes "[Sago Lauds le Fou]";
			mes "Ma maman disait toujours que la vie c'est comme un pamplemousse."; // J'ai pas pu m'empêcher de rajouter la référence à Forest Gump en début de phrase... [NoH]
			mes "C'est orange et mou, il y a quelques pépins à l'intérieur, et certains en mangent la moitié d'un au petit déjeuner.";
			close;
		}
		else if(.@RandomMsg == 3) {
			mes "[Sago Lauds le Fou]";
			mes "Il y avait une morale à cette histoire, mais j'avoue que pour le coup, je n'arrive plus à m'en rappeler...";
			close;
		}
		else if(.@RandomMsg == 4) {
			mes "[Sago Lauds le Fou]";
			mes "42!";
			next;
			mes "[Sago Lauds le Fou]";
			mes "Comment ça vous ne savez pas ce qui s'est passé en 42?! Vous ne savez pas ce qui s'est passé en 42? VOUS NE SAVEZ PAS CE QUI...";
			close;
		}
		else if(.@RandomMsg == 5) {
			mes "[Sago Lauds le Fou]";
			mes "Vous pensez qu'on va régler tous les problèmes dans le monde avec de simples patates, vous aussi? Vraiment? Les pacifistes sont tous des idiots, quand ce ne sont pas des traitres à la nation!";
			close;
		}
	}
}
		
// ============== Memorial Plaque ================
// ===============================================
prontera,153,286,4	script	Plaque Commémorative::Memorial Plaque#Memorial	857,{
	if(Memorial08 < 8) {
		mes "[" + strcharinfo(0) + "]";
		mes "- Il s'agit d'une vielle plaque poussiéreuse.-";
		close;
	}
	
	else if(Memorial08 == 8) {
		mes "[" + strcharinfo(0) + "]";
		mes "- Il s'agit d'une vielle plaque poussiéreuse.-";
		mes "'Ce doit être de ça que Lauds parlait.'";
		mes "'Je vais la nettoyer avec cette stupide serviette!'";
		next;
		set Memorial08,9;
		getexp 93750,43750;
		goto L_CleanPlaque;
	}
	
	else if(Memorial08 >= 9) {
		goto L_CleanPlaque;
	}
	
	L_CleanPlaque:
		mes "- Vous pouvez lire un message une fois la plaque nettoyée.-";
		next;
		mes "- Même si de nulle sculpture de marbre ils ne devraient se rappeler,-";
		mes "- Pas plus que de la pierre sur laquelle leurs actes héroïques sont gravés,-";
		mes "- Le souvenir de leur hauts faits perdurera autant que cette terre qu'ils ont chérie, et protégée.-";
		mes "- Daniel Webster -";
		if(Memorial08 == 9) {
			getexp 93750,43750;
		}
		next;
		mes "-Il y a un message.-";
		next;
		mes "- Je ne suis peut-être pas allé où je voulais aller,-";
		mes "- Mais je pense avoir terminé là où je devais être.-";
		mes "- Douglas Adams -";
		if(Memorial08 == 9) { 
			getexp 93750,43750;
		}
		next;
		mes "- Il s'agit du dernier message.-";
		next;
		mes "- L'héroïsme véritable est très sobre, bien loin du sensationnalisme. -";
		mes "- Il ne s'agit pas d'en faire plus que tous les autres à tout prix, -";
		mes "- Il s'agit de servir les autres quel qu'en soit le prix. -";
		mes "- Arthur Ashe -";
		if(Memorial08 == 9) { 
			getexp 93750,43750;
			set Memorial08,10;
		}
		close;
}


// ==================== Grast ====================
// ===============================================
prontera,150,270,4	script	Grast#Memorial	900,{
	if(Memorial08 < 3) {
		mes "[Grast]";
		mes "Tous ces imbéciles qui ne se souviennent de rien... Même pas foutus de respecter un jour comme aujourd'hui! Tous ces braves soldats morts pour leur roi bien-aimé...";
		close;
	}
	
	else if(Memorial08 == 3) {
		mes "[Grast]";
		mes "Oh, mais... C'est une Serviette du Souvenir, non?! Je suis tellement content de rencontrer enfin quelqu'un qui comprenne le sens de ce jour sacré!";
		next;
		mes "[Grast]";
		mes "Tout le monde devrait savoir ce que l'on célèbre aujourd'hui, vous ne pensez pas? Il faut éduquer tous ces incultes!";
		next;
		mes "[Grast]";
		mes "Si vous voulez vous joindre à ma croisade, vous devriez me rapporter quelques matériaux.";
		next;
		mes "[Grast]";
		mes "Il me faudrait ^FF0000une Potion Rouge, une Potion Verte, une Potion d'Éveil, et une Aile de Papillon^000000. Je vous attendrai.";
		set Memorial08,4;
		close;
	}

	else if(Memorial08 == 4) {
		if (countitem(501) < 1 || countitem(506) < 1 || countitem(656) < 1 || countitem(602) < 1) {
			mes "[Grast]";
			mes "Oh, mais vous ne m'avez pas rapporté tous les matériaux. Il faudrait vraiment que vous m'apportiez ^FF0000une Potion Rouge, une Potion Verte, une Potion d'Éveil, et une Aile de Papillon^000000.";
			next;
			mes "[Grast]";
			mes "C'est juste le début, il me faudra d'autres choses ensuite. Mais si vous êtes débordé"+ ( sex ? "" : "e" ) +", on peut arrêter là...";
			next;
			if (select("Je ramènerai le reste.:J'arrête là.") == 2) {
				goto L_GiveUp;
			}
			goto L_Continue;
		}else{
			mes "[Grast]";
			mes "Oh, vous m'avez rapporté tous les matériaux...";
			next;
			mes "[Grast]";
			mes "Hum, il va encore me falloir quelques petites choses...";
			next;
			mes "[Grast]";
			mes "S'il vous plaît, rapportez-moi ^FF0000un Piège, une Feuille de l'Yggdrasil, une Gemme Bleue, un Miroir de Cristal, de la Viande, et une Carotte^000000.";
			next;
			mes "[Grast]";
			mes "Pourquoi vous me regardez comme ça? Je suis sérieux!";
			set Memorial08,5;
			close;
		}
	}
	
	else if(Memorial08 == 5) {
		if (countitem(1065) < 1 || countitem(610) < 1 || countitem(717) < 1 || countitem(747) < 1 || countitem(517) < 1 || countitem(515) < 1) {
			mes "[Grast]";
			mes "Oh, mais vous ne m'avez pas rapporté tous les matériaux. Il faudrait vraiment que vous m'apportiez ^FF0000un Piège, une Feuille de l'Yggdrasil, une Gemme Bleue, un Miroir de Cristal, de la Viande, et une Carotte^000000.";
			next;
			mes "[Grast]";
			mes "C'est juste le début, il me faudra d'autres choses ensuite. Mais si vous êtes débordé"+ ( sex ? "" : "e" ) +", on peut arrêter là...";
			next;
			if (select("Je ramènerai le reste.:J'arrête là.") == 2) {
				goto L_GiveUp;
			}
			goto L_Continue;
		}else{
			mes "[Grast]";
			mes "Oh, vous m'avez rapporté tous les matériaux...";
			next;
			mes "[Grast]";
			mes "Hum, il va encore me falloir quelques petites choses...";
			next;
			mes "[Grast]";
			mes "S'il vous plaît, rapportez-moi ^FF0000une Couveuse, un Pétard, un Poring en Peluche, et un Bouquet^000000.";
			set Memorial08,6;
			close;
		}
	}
	
	else if(Memorial08 == 6) {
		if (countitem(643) < 1 || countitem(12018) < 1 || countitem(741) < 1 || countitem(745) < 1) {
			mes "[Grast]";
			mes "Oh, vous n'avez pas rapporté tous les matériaux. S'il vous plaît, rapportez-moi ^FF0000une Couveuse, un Pétard, un Poring en Peluche, et un Bouquet^000000.";
			next;
			mes "[Grast]";
			mes "Ce sont les dernières choses dont j'ai besoin. Mais si vous êtes débordé"+ ( sex ? "" : "e" ) +", on peut arrêter là...";
			next;
			mes "[Grast]";
			mes "Mais si vous le faites et que je vous donne tout, je ne pourrai pas vos remercier pour vos efforts. C'est vous qui choisissez, " + strcharinfo(0) + ".";
			next;
			if (select("Je ramènerai le reste.:J'arrête là.") == 2) {
				goto L_GiveUp;
			}
			goto L_Continue;
		}else{
			mes "[Grast]";
			mes "Est-ce que vous m'avez tout rapporté? Ah, merci pour toute votre peine. On sent là l'âme d'un"+ ( sex ? "" : "e" ) +" véritable patriote!";
			next;
			mes "[Grast]";
			mes "J'espère que vous aurez appris une leçon en collectant tout ceci.";
			mes "J'aimerais vous donner un petit présent pour tous vos efforts.";
			next;
			mes "[Grast]";
			mes "Vous voyez, j'ai un cadeau dans chaque main. Le sais que vous voudriez que je vous donne les deux, mais... Qu'est-ce que vous diriez de tester votre chance, " + strcharinfo(0) + "?";
			next;
			mes "[Grast]";
			mes "D'accord, quelle main choisissez-vous?";
			next;
			if (select("Main gauche.:Main droite.") == 2) {
				mes "[Grast]";
				mes "Ma main droite? Voilà votre cadeau!";
				mes "Allez maintenant apporter tout cela à Lauds, vous voulez bien?";
				set Memorial08, 7;
				getitem 617,1;
				close;
			}
			mes "[Grast]";
			mes "Ma main gauche? Voilà votre cadeau!";
			mes "Allez maintenant apporter tout cela à Lauds, vous voulez bien?";
			set Memorial08, 7;
			getitem 12109,1;
			close;
		}
	}
	
	else if(Memorial08 == 7) {
		mes "[Grast]";
		mes "Vous avez été voir Lauds?";
		mes "J'espère que vous vous souviendrez qu'il n'y aurait pas de foin pour vos montures si de preux soldats n'avaient donné leur vie pour conquérir les terres de ces impies adorateurs de Freya, et que vous ne seriez pas libre d'être gouverné"+ ( sex ? "" : "e" ) +" par notre bon roi sans le sang versé des braves!";
		close;
	}

	else if(Memorial08 >= 8) {
		mes "[Grast]";
		mes "Une serviette est de loin l'accessoire le plus utile qu'"+ ( sex ? "un aventurier" : "une aventurière" ) +" puisse avoir en sa possession."; // Pas crai: le chèche c'est vachement mieux, ça fait tout: couvre-chef, écharpe, serviette, bandage, PQ... D'accord, j'arrête [NoH]
		close;
	}

	L_GiveUp:
		mes "[Grast]";
		mes "Oh, je vois. J'imagine que vous êtes plutôt occupé"+ ( sex ? "" : "e" ) +"... Aucun problème, je vais tout vous donner.";
		next;
		mes "[Grast]";
		mes "Voilà pour vous. J'ai tout compressé pour rendre cela beaucoup plus compact pour vous. Vous me remercierez plus tard...";
		mes "Apportez tout ceci à Lauds s'il vous plaît.";
		set Memorial08,7;
		getitem 7126,1;
		next;
		mes "[Grast]";
		mes "Au fait, il a eu un malheureux accident, et le pauvre n'a plus toute sa tête. Ne vous inquiétez pas s'il commence à divaguer...";
		close;
	
	L_Continue:
		mes "[Grast]";
		mes "C'est une sage décision. J'attendrai que vous reveniez.";
		close;
}
