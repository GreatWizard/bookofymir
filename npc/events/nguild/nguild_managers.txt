//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script =======================================
//= War of Emperium Managers for N Guild Castles
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= eAthena 0.1+; RO Episode 4+
//===== Description: =========================================
//=
//==============================================
//= Break down of arguments used in the function:
//=   arg(0): name of Castle Manager
//=   arg(1): name of guild castle.
//=   arg(2): x1 coordinate for warp to treasure room
//=   arg(3): y1 coordinate for warp to treasure room
//=   arg(4): guild script suffix for kafra, etc.
//===== Additional Comments: =================================
//= 1.0 Based off existing guild scripts.  Do not know if it is accurate.[kobra_k88]
//= 1.1 Added modified guild manager function. [L0ne_W0lf]
//= 	Removed guardian summoning.
//= 1.2 Corrected function name. (bugreport:1728) [L0ne_W0lf]
//============================================================

//==============================================
function	script	F_GldManager	{

	set @GID, GetCastleData(getarg(1),1);
	mes "[ "+getarg(0)+" ]";
	if (@GID == 0){
		mes "J'attends les ordres de mon Maître. Brave aventurier, suis ta destinée!";
		return;
	}
	if (getcharid(2) != @GID){
		mes "Je suis employé ici afin de suivre les ordres de Maître ^5533FF" + getguildmaster(@GID) + "^000000! Hé, vous ne faîtes même pas partie de la Guilde!!";
		mes "Gardiens! Saisissez cet intrus!";
		return;
	}
	if (strcharinfo(0) != getguildmaster(@GID)){
		mes "Vous n'êtes pas ^5533FF" + getguildmaster(@GID) + "^000000! Je suis employé ici afin de suivre les ordres de Maître ^5533FF" + getguildmaster(@GID) + "^000000.";
		return 0;
	}

	mes "Bienvenue, Maître ^5533FF" + getguildmaster(@GID) + "^000000 ! Je vais vous assister du mieux possible!";
	next;

	menu "Emploi ou Renvoi d'une Kafra",M_Kaf, "Entrer dans la Salle des Coffres",M_Treas, "Annuler",M_End;

	//===========================
	M_Kaf:
	//======
		mes "[ "+getarg(0)+" ]";
		if (GetCastleData(getarg(1),9) == 1) goto L_Dismiss;
		if (getgdskilllv(@GID,10001) == 0){
			mes "Maître, vous n'avez pas de contrat avec la Corporation Kafra.";
			mes "Afin de pouvoir employer une Kafra, votre guilde doit d'abord apprendre la compétence ^ff0000Contrat Kafra^000000.";
			return;
		}

		L_Hire:
			mes "Voulez-vous contacter la Corporation Kafra pour engager une Employée dans votre Château?";
			mes "^ff0000 10 000 Zenys sont nécessaires pour leurs services. ";
			next;
			menu "Engager.",-,"Annuler.",sM_KafEnd;

				mes "[ "+getarg(0)+" ]";
				if (Zeny < 10000){
					mes "Maître, vous n'avez pas assez d'argent. L'emploi d'une Kafra a été annulé.";
					return;
				}
				set Zeny,Zeny-10000;
				enablenpc "Employée Kafra#"+getarg(4);
				SetCastleData getarg(1),9,1;
				mes "Vous avez passé un contrat avec la Corporation Kafra.";
				next;
				cutin "kafra_01",2;
				mes "[ Employée Kafra ]";
				mes "C'est un honneur de pouvoir vous proposer mes services. Je vous aiderai au mieux.";
				next;
				cutin "kafra_01",255;
				mes "[ "+getarg(0)+" ]";
				//mes "Your employment contract lasts ^5533FF1 month^000000.  After this term is over you will have to create a new contract.";
				mes "Je pense que l'Employée Kafra sera très bénéfique pour les membres de la Guilde.";
				return;

			sM_KafEnd:
				mes "[ "+getarg(0)+" ]";
				mes "Très bien, Maître. Mais je vous suggère d'employer une Kafra très bientôt!";
				return;

		L_Dismiss:
			mes "Nous employons actuellement une Employée Kafra... Voulez vous renvoyer l'actuelle Employée Kafra?";
			next;
			menu "Renvoyer.",-,"Annuler.",sM_KafEnd2;

				cutin "kafra_01",2;
				mes "[ Employée Kafra ]";
				mes "Si je ne vous ai pas servi au mieux, je vous présente toutes mes excuses.";
				next;
				menu "Renvoyer.",-,"Annuler.",ssM_KafEnd2;

					mes "[ Employée Kafra ]";
					mes "Je suis confuse de ne pas avoir réussi à servir votre Guilde comme j'aurais dû....";
					next;
					disablenpc "Employée Kafra#"+getarg(4);
					SetCastleData getarg(1),9,0;
					cutin "kafra_01",255;
					mes "[ "+getarg(0)+" ]";
					mes "....";
					mes "J'ai renvoyé l'Employée Kafra... Mais... Êtes-vous satisfait de votre décision?";
					return;
				ssM_KafEnd2:
					mes "[ Employée Kafra ]";
					mes "Je travaillerai dur pour vous... Merci!";
					cutin "kafra_01",255;
					return;
			sM_KafEnd2:
				mes "[ "+getarg(0)+" ]";
				mes "Elle est très travailleuse, vous avez raison de la garder à votre service.";
				return;

	//=========================
	M_Treas:
	//========
		mes "[ "+getarg(0)+" ]";
		mes "Voulez-vous vous rendre dans la Salle des Coffres? Seul vous, le Chef de Guilde, êtes autorisé à entrer dans cette salle.";
		next;
		menu "Entrer.",-,"Annuler",sM_TresEnd;

			mes "[ "+getarg(0)+" ]";
			mes "Suivez-moi, je vais vous montrer le passage secret pour y accéder.";
			mes "Vous devrez activer le petit levier dans la salle afin d'en ressortir.";
			next;
			warp getarg(1),getarg(2),getarg(3);
			return;
		sM_TresEnd:
			mes "[ "+getarg(0)+" ]";
			mes "Nous recevons des coffres chaque jour.";
			mes "Vous devriez en prendre possession chaque fois que vous le pouvez.";
			return;


	//==========================
	M_End:
	//=======
		mes "[ "+getarg(0)+" ]";
		mes "Très bien, maître.";
		return;
}

// Castle 1 ==================================================================================
nguild_alde,218,175,0	script	Frolo	55,{
	callfunc "F_GldManager","Frolo","nguild_alde",119,223,"N01";
	close;
}


// Castle 2 ==================================================================================
nguild_gef,40,48,5	script	Leiber	55,{
	callfunc "F_GldManager","Leiber","nguild_gef",155,112,"N02";
	close;
}


// Castle 3 ==================================================================================
nguild_pay,120,58,4	script	Dundar	55,{
	callfunc "F_GldManager","Dundar","nguild_pay",290,7,"N03";
	close;
}


// Castle 4 ==================================================================================
nguild_prt,112,181,0	script	Thefton	55,{
	callfunc "F_GldManager","Thefton","nguild_prt",15,209,"N04";
	close;
}
