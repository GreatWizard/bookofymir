//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing, corrigé par Icaros
//===== eAthena Script =======================================
//= iRO Halloween (2008)
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= iRO Halloween (2008)
//= Run away from the zombies for tickets.
//= Redeem tickets for prizes.
//===== Notes: ===============================================
//= You must enable the event monsters in mob_db2.
//===== Additional Comments: =================================
//= 1.0 First version. [Kisuka]
//============================================================

payon,162,176,4	script	Magicien d'Halloween::Halloween Magician#iRO08	704,{
	
	if(Hallow08 < 1) {
			mes "[Magicien d'Halloween]";
			mes "Kkkkkkkkk!";
			mes "J'organise un évènement spécial pour Halloween afin de tester votre chance et votre agilité. Voulez-vous participer?";
			next;
			mes "[Magicien d'Halloween]";
			mes "Allez! Ne soyez pas une mauviette! Si vous collectez assez de tickets, vous pourrez obtenir un prix. Alors, qu'est-ce que vous en dites?";
			next;
			goto MainMenu;
	}

	if (Hallow08Kill == 1) {
		mes "[Magicien d'Halloween]";
		mes "Une poule mouillée comme vous veut à nouveau tenter sa chance? Vous connaissez les règles, au moins?";
		next;
		set Hallow08Kill,0;
		if(select("Oui, je les connais.:Non, je ne les connais pas.") == 2) {
			goto Rules;
		}
		goto Participate;
	}

	if (Hallow08Kill == 2) {
		mes "[Magicien d'Halloween]";
		mes "Oh, vous avez réussi! Alors, ça s'est bien passé avec les zombies? Vous savez, eux aussi sont des gens...";
		mes "Ha!";
		mes "Kkkkkkk.";
		next;
		getitem 7941,1;
		set Hallow08Kill,0;
		mes "[Magicien d'Halloween]";
		mes "Comme je vous l'avais dit, vous pouvez échanger vos Tickets contre de belles récompenses.";
		close;
	}

	if(Hallow08 > 0) {
		mes "[Magicien d'Halloween]";
		mes "Voulez-vous à nouveau entendre les règles ou simplement y retourner?";
		next;
		goto MainMenu2;
	}
	
		Rules:
			mes "[Magicien d'Halloween]";
			mes "Ce village ressemble à Payon. Il y a des Goules et des Zombies un peu partout, et trois sorties au Sud, mais seule l'une d'entre elle peut être empruntée. Ce sera à vous de trouver laquelle.";
			next;
			mes "[Magicien d'Halloween]";
			mes "Vous ne pourrez pas utiliser de compétence pour tuer les Goules et les Zombies. Oh, et il y a autre chose que vous devrez garder à l'esprit...";
			next;
			mes "[Magicien d'Halloween]";
			mes "Les participants ne doivent rien porter sur eux: pas d'arme, pas d'armure, rien! Vous devrez revenir quand votre Poids sera à 0.";
			next;
			mes "[Magicien d'Halloween]";
			mes "Une autre chose encore: vous ne pourrez pas non plus avoir de monture ou de Charrette. Si c'est le cas, je vous les prendrai avant de vous envoyer là-bas. C'est bien compris?";
			next;
			mes "[Magicien d'Halloween]";
			mes "Rappelez-vous bien: il y a trois sorties, mais seule l'une d'entre elle peut être empruntée. Les zombies et les goules ne peuvent pas être tués, et vous ne devez rien porter sur vous.";
			next;
			if (Hallow08 > 0) {
				mes "[Magicien d'Halloween]";
				mes "Eh... Revenez quand vous serez prêt.";
				close;
			}else{
				goto MainMenu;
			}
			
		Participate:
			mes "[Magicien d'Halloween]";
			mes "D'accord, vous êtes prêt. Laissez-moi vérifier que vous ne cachez pas d'objets sur vous...";
			next;
			if (Weight > 0) {
				mes "[Magicien d'Halloween]";
				mes "Haha! Qu'est-ce que c'est que ça?! Je vous avais dit: aucun objet!";
				next;
				mes "[Magicien d'Halloween]";
				mes "Vous pensiez que je n'allais pas le voir, c'est ça?";
				close;
			}else{
				mes "[Magicien d'Halloween]";
				mes "On dirait que vous êtes prêt à partir: vous n'avez rien sur vous.";
				next;
				mes "[Magicien d'Halloween]";
				mes "Oh, j'ai trop de travail: je suis épuisé.";
				next;
				mes "[Magicien d'Halloween]";
				mes "Parfois j'oublie d'envoyer les gens là-bas...";
				next;
				mes "[Magicien d'Halloween]";
				mes "Bref... J'espère que vous reviendrez entier!";
				close2;
				if (Hallow08 < 1) {
					set Hallow08,1;
				}
				set Hallow08Kill,1;
				set @Hallow08Warp, rand(1,3);
				percentheal -98,0;
				setriding 0;
				setcart 0;
				warp "evt_zombie",155,246;
				end;
			}
			
		TicketExchange:
			mes "[Magicien d'Halloween]";
			mes "Vous voulez échanger vos Tickets contre des prix? Kkkkkk!";
			next;
			mes "[Magicien d'Halloween]";
			mes "Laissez-moi vous donner la liste de ce que vous pouvez obtenir.";
			next;
			mes "[Magicien d'Halloween]";
			mes "5 Tickets contre une Tarte à la Citrouille.";
			mes "20 Tickets contre une Tête de Citrouille.";
			mes "50 Tickets contre une Vieille Boîte Bleue.";
			mes "70 Tickets contre une Vieille Boîte Mauve.";
			mes "200 Tickets contre un Vieil Album de Cartes.";
			next;
			mes "[Magicien d'Halloween]";
			mes "Alors, qu'est-ce qui vous intéresse?";
			next;
			switch(select("Tarte à la Citrouille:Tête de Citrouille:Vieille Boîte Bleue:Vieille Boîte Mauve:Vieil Album de Cartes")) {
				case 1:
					if(countitem(7941) < 5) {
						goto NotEnough;
					}else{
						delitem 7941,5;
						getitem 12192,1;
						goto Enough;
					}
					break;
				case 2:
					if(countitem(7941) < 20) {
						goto NotEnough;
					}else{
						delitem 7941,20;
						getitem 5134,1;
						goto Enough;
					}
					break;
				case 3:
					if(countitem(7941) < 50) {
						goto NotEnough;
					}else{
						delitem 7941,50;
						getitem 603,1;
						goto Enough;
					}
					break;
				case 4:
					if(countitem(7941) < 70) {
						goto NotEnough;
					}else{
						delitem 7941,70;
						getitem 617,1;
						goto Enough;
					}
					break;			
				case 5:
					if(countitem(7941) < 200) {
						goto NotEnough;
					}else{
						delitem 7941,200;
						getitem 616,1;
						goto Enough;
					}
			}
					
		NextTime:
			mes "[Magicien d'Halloween]";
			mes "Bien, ce sera pour une prochaine fois, alors.";
			mes "Kkkkkkkk.";
			close;

	MainMenu:
		menu "Expliquez-moi les règles.",Rules,"Je veux participer.",Participate,"Je veux échanger mes Tickets contre des prix.",TicketExchange,"Je reviendrai plus tard.",NextTime;

	MainMenu2:
		menu "Je veux y aller maintenant!",Participate,"Quelles sont les règles",Rules,"Je veux échanger mes Tickets contre des prix.",TicketExchange,"Je reviendrai plus tard.",NextTime;

	NotEnough:
		mes "[Magicien d'Halloween]";
		mes "Mais vous n'avez pas assez de Tickets voyons! Vous ne savez même pas compter? Revenez quand vous aurez le bon nombre!";
		close;

	Enough:
		mes "[Magicien d'Halloween]";
		mes "Et voilà. Voulez-vous autre chose?";
		next;
		if (Hallow08 == 1) {
			goto MainMenu2;
		}else{
			goto MainMenu;
		}
}
	
// --------------- MapFlags ---------------


evt_zombie,16,142,1	script	zombiewarp001	45,2,2,{
	OnTouch:
	if (@Hallow08Warp == 1) {
		set Hallow08Kill,2;
		specialeffect 16;
		warp "payon",28,142;
	}
	end;
}

evt_zombie,122,27,1	script	zombiewarp002	45,2,2,{
	OnTouch:
	if (@Hallow08Warp == 2) {
		set Hallow08Kill,2;
		specialeffect 16;
		warp "payon",121,40;
	}
	end;
}

evt_zombie,267,89,1	script	zombiewarp003	45,2,2,{
	OnTouch:
	if (@Hallow08Warp == 3) {
		set Hallow08Kill,2;
		specialeffect 16;
		warp "payon",253,95;
	}
	end;
}

evt_zombie,0,0,0,0	monster	--ja--	3000,150,0,0,0
evt_zombie,0,0,0,0	monster	--ja--	3001,80,0,0,0
evt_zombie,0,0,0,0	monster	--ja--	3002,20,0,0,0
