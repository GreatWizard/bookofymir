//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Etna, corrigé par Myllena
//===== eAthena Script =======================================
//= Easter Event (2008)
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= iRO Easter Event. (2008)
//= Create Holy Eggs
//= Help a suspicious man.
//===== Additional Comments: =================================
//= 1.0 First version. [Kisuka]
//= 1.1 Corrected a small bug. [L0ne_W0lf]
//============================================================

// Prontera
prontera,111,99,5	script	Vendeur d'Œufs#prt::EggVendor	98,{
	mes "[Vendeur d'Œufs]";
	mes "^FF0000Œufs de Poule^000000! Venez acheter autant d'Œufs de Poule que vous voulez, quand vous voulez! Hé l'ami"+( sex ? "" : "e" )+", vous voulez jeter un oeil aux Œufs de Poule que je vends?";
	next;
	while(1) {
		switch(select("Que faites-vous?:Pourquoi des Œufs de Poule?:Oui, je vais en acheter quelques uns.:Non non, c'est bon!")) {
		case 1:
			mes "[Vendeur d'Œufs]";
			mes "Qu'est-ce que je...? Je vends des Œufs de Poule! En cette période de Pâques, je vends autant d'Œufs de poule que vous voulez pour seulement 500 zénys chacun!";
			next;
			mes "[Vendeur d'Œufs]";
			mes "Garçons, filles, amis, famille, personnes âgées, jeunes, euh, peut-être pas la plupart des végétariens ou les purs amateurs de bonne bouffe, mais presque tout le monde aime les Œufs!";
			next;
			mes "[Vendeur d'Œufs]";
			mes "Pouvez-vous imaginer un monde sans Œufs? Il n'y aurait pas de pain, pâtisseries, gâteaux ou glaçage... La glace serait beaucoup plus liquide... Et les gags ne seraient pas aussi amusants...";
			next;
			break;
		case 2:
			mes "[Vendeur d'Œufs]";
			mes "Je ne sais pas vraiment pourquoi, mais les Œufs de Poule sont très appréciés en cette période de l'année. Ils se vendent comme des petits pains en ce moment et je compte bien en profiter!";
			next;
			break;
		case 3:
			mes "[Vendeur d'Œufs]";
			mes "Super! Combien en voulez-vous? N'oubliez pas que chaque Œuf de Poule coûte 500 zénys. Et pensez bien que vous ne pourrez pas toujours acheter des Œufs de Poule si facilement!";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Vendeur d'Œufs]";
					mes "Hé, si vous n'allez rien acheter, alors est-ce que ça vous ennuierait de vous éloigner afin que je puisse servir les autres clients?";
					close;
				}
				else if (.@input < 0 || .@input > 1000) {
					mes "[Vendeur d'Œufs]";
					mes "Hé, ça vous dérangerait de me demander une quantité raisonnable? Je ne peux pas vous vendre plus de 1000 Œufs de Poule à la fois, vous savez. Allez, dites-moi combien vous en voulez vraiment maintenant.";
					next;
				}
				else {
					set .@egg_zeny,.@input*500;
					if (zeny < .@egg_zeny) {
						mes "[Vendeur d'Œufs]";
						mes "Vous n'avez pas assez de zénys.";
						next;
						mes "[Vendeur d'Œufs]";
						mes "Pas de ristourne, allez chercher plus de zénys.";
						close;
					}
					else {
						if (checkweight(7605,.@input) == 0) {
							mes "[Vendeur d'Œufs]";
							mes "Hé, je sais que vous voulez vraiment acheter quelques Œufs de Poule, mais vous ne pourrez pas en porter autant pour le moment. Pourquoi ne pas commencer par faire un peu de place dans votre inventaire?";
							close;
						}
						else {
							mes "[Vendeur d'Œufs]";
							mes "Voici vos Œufs de Poule! Je crois qu'il y a une fête où vous en aurez besoin, mais je ne sais rien à ce sujet. Eh bien, passer un bon moment, et à la prochaine!";
							set zeny,zeny-.@egg_zeny;
							getitem 7605,.@input; //Chicken_Egg
							close;
						}
					}
				}
			}
		case 4:
			mes "[Vendeur d'Œufs]";
			mes "Pas intéressé par l'achat d'Œufs de Poule, hein? Bien, mais la rumeur circule qu'ils tomberaient à point nommé pour une fête... Pouvez-vous vraiment laisser passer une telle affaire?";
			close;
		}
	}
}

prontera,113,98,0	script	Matrone mal fagotée#prt::EggMatron	701,{
	mes "[Matrone mal fagotée]";
	mes "Cette fête va être tellement amusante! Ooh! Je devrais acheter des Œufs de Poule pour faire quelques décorations.";
	next;
	mes "[Matrone mal fagotée]";
	mes "En parlant de ça, j'ai entendu dire que la nonne qui peut faire des Œufs Sacrés est de retour à Prontera. Quoique, je pense qu'elle utilise des Œufs de Poule plutôt que d'ordinaires pour les faire.";
	close;
}

prontera,108,96,5	script	Bonhomme Revêche#prt::EggGeezer	709,{
	mes "[Bonhomme Revêche]";
	mes "Hé, savez-vous pourquoi les Œufs de Poule sont devenus si chers? Je veux dire, 500 zénys c'est beaucoup! Je pense que c'est parce qu'un type à Morroc essaye d'amasser une tonne d'Œufs Sacrés!";
	next;
	mes "[Bonhomme Revêche]";
	mes "Je ne sais pas ce qu'il offre pour ces Œufs Sacrés, mais les aventuriers lui apportent des Œufs Sacrés en masse. Je me demande ce qu'il leur donne en retour?";
	close2;
	if (easter2008 < 1) {
		set easter2008,1;
	}
	end;
}

// Payon
payon,172,173,5	duplicate(EggVendor)	Vendeur d'Œufs#pay	98
payon,175,171,1	duplicate(EggMatron)	Matrone mal fagotée#pay	701
payon,170,171,5	duplicate(EggGeezer)	Bonhomme Revêche#pay	709

// Geffen
geffen,136,64,5	duplicate(EggVendor)	Vendeur d'Œufs#gef	98
geffen,138,63,1	duplicate(EggMatron)	Matrone mal fagotée#gef	701
geffen,133,63,5	duplicate(EggGeezer)	Bonhomme Revêche#gef	709

// Al De Baran
aldebaran,133,119,5	duplicate(EggVendor)	Vendeur d'Œufs#alde	98
aldebaran,135,117,1	duplicate(EggMatron)	Matrone mal fagotée#ald	701
aldebaran,132,116,5	duplicate(EggGeezer)	Bonhomme Revêche#ald	709

// Alberta
alberta,90,55,5	duplicate(EggVendor)	Vendeur d'Œufs#alb	98
alberta,92,53,1	duplicate(EggMatron)	Matrone mal fagotée#alb	701
alberta,88,52,5	duplicate(EggGeezer)	Bonhomme Revêche#alb	709

// Create Holy Eggs
prontera,230,312,3	script	Nerlen#es07	79,{
	mes "[Nerlen]";
	mes "Bonjour! Si vous le souhaitez, je peux prendre vos Œufs de Poule et en faire des Œufs Sacrés. C'est quelque chose que je fais pour d'autres, en célébration de cette période sainte.";
	next;
	mes "[Nerlen]";
	mes "Les Œufs Sacrés représentent la renaissance et, justement, peuvent être utilisés pour redonner vie à vos amis. Voudriez-vous que je vous en fasse quelques-uns?";
	next;
	switch(select("Que faut-il pour faire des Œufs Sacrés?:Je veux faire des Œufs Sacrés.:Peut-être une autre fois.")) {
	case 1:
		mes "[Nerlen]";
		mes "Pour chaque Œuf Sacré que vous souhaitez avoir, vous devez m'apporter...";
		next;
		mes "[Nerlen]";
		mes "^4D4DFF1 Herbe Blanche^000000,";
		mes "^4D4DFF1 Herbe Verte^000000,";
		mes "^4D4DFF1 Herbe Jaune^000000,";
		mes "^4D4DFF1 Eau Bénite^000000, et";
		mes "^4D4DFF1 Œuf de Poule^000000.";
		close;
	case 2:
		mes "[Nerlen]";
		mes "Bien, combien d'Œufs Sacrés voulez-vous que je vous fasse? Je ne peux pas en faire plus de dix à la fois. Entrez '0' pour annuler.";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Nerlen]";
				mes "Oh, vous avez changé d'avis? N'hésitez pas à venir me voir chaque fois que vous voudrez que je vous fasse des Œufs Sacrés~";
				close;
			}
			else if (.@input < 0 || .@input > 10) {
				mes "[Nerlen]";
				mes "Je suis désolée, mais je peux seulement faire jusqu'à 10 Œufs Sacrés à la fois. Si vous voulez annuler, alors il suffit d'entrer '0'.";
				next;
			}
			else {
				if ((countitem(509) < .@input) || (countitem(511) < .@input) || (countitem(508) < .@input) || (countitem(523) < .@input) || (countitem(7605) < .@input)) {
					mes "[Nerlen]";
					mes "Je suis désolée, mais vous n'avez pas tout ce qu'il faut pour faire des Œufs Sacrés. Pour chaque Œuf Sacré que vous voulez que je vous créé, vous avez besoin des choses suivantes.";
					next;
					mes "[Nerlen]";
					mes "^4D4DFF1 Herbe Blanche^000000,";
					mes "^4D4DFF1 Herbe Verte^000000,";
					mes "^4D4DFF1 Herbe Jaune^000000,";
					mes "^4D4DFF1 Eau Bénite^000000, et";
					mes "^4D4DFF1 Œuf de Poule^000000.";
					close;
				}
				else {
					break;
				}
			}
		}
		if (checkweight(12019,.@input) == 0) {
			mes "[Nerlen]";
			mes "Je suis désolée, mais vous ne pourrez pas porter autant d'Œufs Sacrés pour le moment... Vous feriez mieux de commencer par faire un peu de place dans votre inventaire.";
			close;
		}
		else {
			delitem 509,.@input; //White_Herb
			delitem 511,.@input; //Green_Herb
			delitem 508,.@input; //Yellow_Herb
			delitem 523,.@input; //Holy_Water
			delitem 7605,.@input; //Egg
			getitem 12019,.@input; //Holy_Egg
			mes "[Nerlen]";
			mes "Merci! J'espère que vous apprécierez ces Œufs Sacrés~";
			mes "Que Dieu veille sur vous et vos proches.";
			close;
		}
	case 3:
		mes "[Nerlen]";
		mes "Bien, je comprends.";
		mes "Que Dieu vous bénisse!";
		close;
	}
}

// Easter Quest
morocc,131,26,5	script	Organisation Secrète#egg	880,{
	if (easter2008 == 1) {
		mes "[Membre de l'Organisation]";
		mes "Comment allez-vous?";
		mes "La mort... C'est inéluctable. Même vous, vous allez mourir un jour. Mais...";
		next;
		if (countitem(12019) > 0) {
			mes "[Membre de l'Organisation]";
			mes "Oh, oui! Voilà! Vous l'avez! L'oeuf que vous tenez a un pouvoir secret. Je pense que... Oui. Quelqu'un comme vous nous comprendrait, et s'intégrerait parfaitement dans notre groupe.";
			next;
			mes "[Membre de l'Organisation]";
			mes "Pourquoi ne pas... rejoindre notre organisation?";
			next;
			switch(select("Pour quoi faire?:Hum... Mais vous avez l'air suspect!")) {
			case 1:
				mes "[Membre de l'Organisation]";
				mes "Je vois que j'ai attiré votre attention! Eh bien, si vous souhaitez vous joindre à nous et en apprendre davantage, alors pourquoi ne pas m'apporter 10 Œufs Sacrés? C'est ma condition.";
				set easter2008,2;
				close;
			case 2:
				mes "[Membre de l'Organisation]";
				mes "Vraiment? Eh bien, je suppose que je n'y peux rien. Laissez-moi vous assurer que notre organisation travaille pour le bien de tous.";
				close;
			}
		}
		else {
			mes "[Membre de l'Organisation]";
			mes "Nous devrions tous profiter de la vie tant qu'il en est encore temps. Qui sait? Peut-être qu'il y aura d'autres vies à vivre après celle-ci. Peut-être, peut-être....";
			close;
		}
	}
	else if (easter2008 == 2) {
		mes "[Membre de l'Organisation]";
		mes "Ah, vous revoilà! Désirez-vous rejoindre nos rangs? Si oui, j'espère que vous avez apporté les 10 Œufs Sacrés que j'exige.";
		next;
		switch(select("Donner des Œufs Sacrés:Ne pas donner d'Œufs Sacrés")) {
		case 1:
			mes "[Membre de l'Organisation]";
			mes "Donc vous souhaitez vous joindre à notre organisation. Parfait! Mais d'abord, laissez-moi vérifier si vous avez apporté les Œufs Sacrés...";
			next;
			if (countitem(12019) > 9) {
				mes "[Membre de l'Organisation]";
				mes "Très bien. En échange de ces Œufs Sacrés, et en guise de cadeau de bienvenue, laissez-moi vous donner un petit présent. Maintenant, je peux vous dire ce que nous faisons.";
				delitem 12019,10; //Holy_Egg
				set easter2008,3;
				getitem 644,2; //Gift_Box
				next;
				mes "[Membre de l'Organisation]";
				mes "^333333*Tousse* *Tousse*^000000";
				mes "Excusez-moi, j'ai...";
				mes "^333333*Tousse!*^000000 Un... Un peu mal à la gorge. Donnez-moi une seconde...";
				close;
			}
			else {
				mes "[Membre de l'Organisation]";
				mes "Hum! Vous n'avez pas assez d'Œufs Sacrés pour être accepté en tant que membre dans notre organisation. Les avez-vous laissés dans votre Stock?";
				close;
			}
		case 2:
			mes "[Membre de l'Organisation]";
			mes "Hum? Je suppose que je ne peux pas vous blâmer de vous méfier un peu de nous. Laissez-moi vous assurer que nôtre mission est sainte.";
			close;
		}
	}
	else if (easter2008 == 3) {
		mes "[Membre de l'Organisation]";
		mes "Nous sommes une organisation qui se consacre à... Ressusciter les héros qui ont perdu la vie!";
		next;
		mes "[Membre de l'Organisation]";
		mes "Ces Œufs Sacrés que vous avez apporté ont le pouvoir de redonner vie aux morts. En grande quantité... Ils peuvent même faire revivre d'anciens héros tombés au combat!";
		next;
		mes "[Membre de l'Organisation]";
		mes "Lorsque vous m'apportez 33 Œufs Sacrés, je les enchante afin que vous puissiez les utiliser sur des lieux où d'anciens héros sont morts.";
		next;
		mes "[Membre de l'Organisation]";
		mes "Allez me chercher 33 Œufs Sacrés! J'attendrai votre retour ici...";
		set easter2008,4;
		close;
	}
	else if (easter2008 == 4) {
		mes "[Membre de l'Organisation]";
		mes "Ah, vous revoilà! Alors, m'avez-vous apporté 33 Œufs Sacrés pour accomplir votre tâche?";
		next;
		switch(select("Donner des Œufs Sacrés:Ne pas donner d'Œufs Sacrés")) {
		case 1:
			if (countitem(12019) > 32) {
				mes "[Membre de l'Organisation]";
				mes "Ah, ça fera l'affaire! Maintenant, notez la destination que je décris pendant que j'enchante ces Œufs Sacrés.";
				next;
				if (BaseLevel < 41) {
					mes "[Membre de l'Organisation]";
					mes "Vous devez aller à l'entrée des Grottes de Payon, où un agile héros est tombé. Sacrifiez les Œufs Sacrés là-bas, et revenez me voir. À présent... Partez, et ramenez-le à la vie!";
					set easter2008,5;
					close;
				}
				else if ((BaseLevel > 40) && (BaseLevel < 61)) {
					mes "[Membre de l'Organisation]";
					mes "Allez au 3ème niveau de l'Antre Sous-Marine... Un héros qui a cherché la sagesse a péri à l'entrée du 4ème niveau. Sacrifiez les Œufs Sacrés là-bas et ramenez-le à la vie!";
					set easter2008,6;
					close;
				}
				else if ((BaseLevel > 60) && (BaseLevel < 81)) {
					mes "[Membre de l'Organisation]";
					mes "Allez à la Mine de Charbon abandonnée où un héros courageux est mort pour protéger ses camarades. Sacrifiez les Œufs Sacrés dès que vous arrivez au 3ème étage. Allez-y, et ramenez-le à la vie!";
					set easter2008,7;
					close;
				}
				else {
					mes "[Membre de l'Organisation]";
					mes "Allez à Glast Heim et sacrifiez les Œufs Sacrés dés que vous arrivez au 2ème étage. Un héros a péri là, mais vous allez le ramener à la vie!";
					set easter2008,8;
					close;
				}
			}
			else {
				mes "[Membre de l'Organisation]";
				mes "Hum? Il n'y a pas 33 Œufs Sacrés. En avez-vous laissé dans votre Stock?";
				close;
			}
		case 2:
			mes "[Membre de l'Organisation]";
			mes "Ne comprenez-vous pas la droiture de notre cause? Le retour des héros tombés apportera la paix à nos terres beaucoup plus rapidement!";
			close;
		}
	}
	else if (easter2008 >= 5 && easter2008 <= 8) {
		mes "[Membre de l'Organisation]";
		mes "Avez-vous oublié où vous devez aller? Laissez-moi vous le rappeler afin que vous ne vous égariez pas...";
		next;
		mes "[Membre de l'Organisation]";
		switch(easter2008) {
		case 5:
			mes "[Membre de l'Organisation]";
			mes "Vous devez aller à l'entrée des Grottes de Payon, où un agile héros est tombé. Sacrifiez les Œufs Sacrés là-bas, et revenez me voir. À présent... Partez, et ramenez-le à la vie!";
			close;
		case 6:
			mes "[Membre de l'Organisation]";
			mes "Allez au 3ème niveau de l'Antre Sous-Marine... Un héros qui a cherché la sagesse a péri à l'entrée du 4ème niveau. Sacrifiez les Œufs Sacrés là-bas et ramenez-le à la vie!";
			close;
		case 7:
			mes "[Membre de l'Organisation]";
			mes "Allez à la Mine de Charbon abandonnée où un héros courageux est mort pour protéger ses camarades. Sacrifiez les Œufs Sacrés dès que vous arrivez au 3ème étage. Allez-y, et ramenez-le à la vie!";
			close;
		case 8:
			mes "Allez à Glast Heim et sacrifiez les Œufs Sacrés dés que vous arrivez au 2ème étage. Un héros a péri là, mais vous allez le ramener à la vie!";
			close;
		}
	}
	else if (easter2008 == 9) {
		mes "[Membre de l'Organisation]";
		mes "Ah, j'espère que vous êtes revenus avec de bonnes nouvelles. Alors, est-ce que ça a marché?";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "Pour le moment...";
		mes "Il ne s'est rien passé....";
		next;
		mes "[Membre de l'Organisation]";
		mes "Pardon? Ça ne se peut! La magie... Ça aurait dû être assez puissant... Eh bien, c'est dommage que nous ayons échoué. Néanmoins, je suis reconnaissant pour toute l'aide que vous nous avez apportée...";
		next;
		mes "[Membre de l'Organisation]";
		mes "Nous regarderons ce qui a pû mal tourner et nous réessayerons l'année suivante. Adieu mon ami"+( sex ? "" : "e" )+", et j'espère que vous nous aiderez à nouveau.";
		set easter2008,10;
		if (BaseLevel < 41) {
			getexp 5000,0;
			getexp 5000,0;
		}
		else if ((BaseLevel > 40) && (BaseLevel < 61)) {
			getexp 50000,0;
			getexp 50000,0;
		}
		else if ((BaseLevel > 60) && (BaseLevel < 81)) {
			getexp 500000,0;
		}
		else if ((BaseLevel > 80) && (BaseLevel < 99)) {
			getexp 1000000,0;
		}
		else {
			if (Upper == 2) {
				getexp 0,10000000;
			}
			else {
				getexp 2000000,0;
			}
		}
		close;
	}
	else {
		mes "[Membre de l'Organisation]";
		mes "Chut! Ne dites à personne que je suis ici.";
		close;
	}
}

pay_dun00,22,127,0	script	Trace#egg1	844,{
	if (easter2008 == 5) {
		mes "[" + strcharinfo(0) + "]";
		mes "Eh bien... C'est ici que le membre de l'organisation m'a demandé de trouver... Il est temps d'utiliser tous ces Œufs Sacrés.";
		next;
		if (countitem(12019) > 32) {
			mes "[" + strcharinfo(0) + "]";
			mes "C'est parti... Est-ce que ceci ramènera vraiment cet ancien héros à la vie? Je suis... Je suis un peu effrayé"+( sex ? "" : "e" )+"!";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "...............................";
			mes "...............................";
			mes "...............................";
			mes "...............................";
			mes "...............................";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "^333333*Ouf!*^000000 Rien ne se passe! Je ferai mieux de retourner voir ce membre de l'organisation et lui faire savoir que ça ne marche pas.";
			close2;
			delitem 12019,33; //Holy_Egg
			set easter2008,9;
			end;
		}
		else {
			mes "[" + strcharinfo(0) + "]";
			mes "Hein? Où ai-je mis ces 33 Œufs Sacrés dont j'ai besoin pour ça?";
			close;
		}
	}
	else {
		mes "[" + strcharinfo(0) + "]";
		mes "Il y avait quelque chose ici, mais je ne peux pas dire avec certitude ce que c'était...";
		close;
	}
}

iz_dun02,340,346,0	script	Trace#egg	844,{
	if (easter2008 == 6) {
		mes "[" + strcharinfo(0) + "]";
		mes "Eh bien... C'est ici que le membre de l'organisation m'a demandé de trouver... Il est temps d'utiliser tous ces Œufs Sacrés.";
		next;
		if (countitem(12019) > 32) {
			mes "[" + strcharinfo(0) + "]";
			mes "C'est parti... Est-ce que ceci ramènera vraiment cet ancien héros à la vie? Je suis... Je suis un peu effrayé"+( sex ? "" : "e" )+"!";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "...............................";
			mes "...............................";
			mes "...............................";
			mes "...............................";
			mes "...............................";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "^333333*Ouf!*^000000 Rien ne se passe! Je ferai mieux de retourner voir ce membre de l'organisation et lui faire savoir que ça ne marche pas.";
			close2;
			delitem 12019,33; //Holy_Egg
			set easter2008,9;
			end;
		}
		else {
			mes "[" + strcharinfo(0) + "]";
			mes "Hein? Où ai-je mis ces 33 Œufs Sacrés dont j'ai besoin pour ça?";
			close;
		}
	}
	else {
		mes "[" + strcharinfo(0) + "]";
		mes "Il y avait quelque chose ici, mais je ne peux pas dire avec certitude ce que c'était...";
		close;
	}
}

mjo_dun03,308,256,0	script	Trace#egg2	844,{
	if (easter2008 == 7) {
		mes "[" + strcharinfo(0) + "]";
		mes "Eh bien... C'est ici que le membre de l'organisation m'a demandé de trouver... Il est temps d'utiliser tous ces Œufs Sacrés.";
		next;
		if (countitem(12019) > 32) {
			mes "[" + strcharinfo(0) + "]";
			mes "C'est parti... Est-ce que ceci ramènera vraiment cet ancien héros à la vie? Je suis... Je suis un peu effrayé"+( sex ? "" : "e" )+"!";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "...............................";
			mes "...............................";
			mes "...............................";
			mes "...............................";
			mes "...............................";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "^333333*Ouf!*^000000 Rien ne se passe! Je ferai mieux de retourner voir ce membre de l'organisation et lui faire savoir que ça ne marche pas.";
			close2;
			delitem 12019,33; //Holy_Egg
			set easter2008,9;
			end;
		}
		else {
			mes "[" + strcharinfo(0) + "]";
			mes "Hein? Où ai-je mis ces 33 Œufs Sacrés dont j'ai besoin pour ça?";
			close;
		}
	}
	else {
		mes "[" + strcharinfo(0) + "]";
		mes "Il y avait quelque chose ici, mais je ne peux pas dire avec certitude ce que c'était...";
		close;
	}
}

gl_prison,157,176,0	script	Trace#egg3	844,{
	if (easter2008 == 8) {
		mes "[" + strcharinfo(0) + "]";
		mes "Eh bien... C'est ici que le membre de l'organisation m'a demandé de trouver... Il est temps d'utiliser tous ces Œufs Sacrés.";
		next;
		if (countitem(12019) > 32) {
			mes "[" + strcharinfo(0) + "]";
			mes "C'est parti... Est-ce que ceci ramènera vraiment cet ancien héros à la vie? Je suis... Je suis un peu effrayé"+( sex ? "" : "e" )+"!";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "...............................";
			mes "...............................";
			mes "...............................";
			mes "...............................";
			mes "...............................";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "^333333*Ouf!*^000000 Rien ne se passe! Je ferai mieux de retourner voir ce membre de l'organisation et lui faire savoir que ça ne marche pas.";
			close2;
			delitem 12019,33; //Holy_Egg
			set easter2008,9;
			end;
		}
		else {
			mes "[" + strcharinfo(0) + "]";
			mes "Hein? Où ai-je mis ces 33 Œufs Sacrés dont j'ai besoin pour ça?";
			close;
		}
	}
	else {
		mes "[" + strcharinfo(0) + "]";
		mes "Il y avait quelque chose ici, mais je ne peux pas dire avec certitude ce que c'était...";
		close;
	}
}
