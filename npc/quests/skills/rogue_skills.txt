//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//Balises sex ok
//===== eAthena Script ======================================= 
//= Rogue Skills Quests
//===== By: ================================================== 
//= Lupus, Reddozen
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Quests for skills: CLose Confine
//===== Additional Comments: ================================= 
//= 1.4 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//= 1.5 Fixed bad NPC header data to comply with rev. 11603. [L0ne_W0lf]
//= 1.6 Bug fixes as pointed out by Brainstorm in bugreport:351 [L0ne_W0lf]
//= 1.6a Fixed(?) antoher problem (NPCs not disabling) hopefully. [L0ne_W0lf]
//= 1.6b Corrected a Typo error with "rouge". (bugreport:909) [Samuray22]
//= 1.7 Replaced effect numerics with constants. [L0ne_W0lf]
//============================================================

in_rogue,359,177,0	script	#killershow01	-1,{
OnInit:
	disablenpc "#killershow01";
	end;

OnEnable:
	enablenpc "#killershow01";
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	disablenpc "#killershow01";
	end;

OnTimer1000:
	enablenpc "Killer#Rogueguild";
	end;

OnTimer120000:
	disablenpc "Killer#Rogueguild";
	end;

OnTimer150000:
	disablenpc "Killer#Rogueguild";
	disablenpc "#killershow01";
	end;
}

in_rogue,357,174,2	script	Meurtrier::Killer#Rogueguild	705,1,1,{
OnInit:
	disablenpc "Killer#Rogueguild";
	end;

OnTouch:
	if (ROG_SK == 10) {
		specialeffect2 EF_CHANGECOLD;
		specialeffect EF_CHANGEWIND;
		specialeffect2 EF_LIGHTSPHERE;
		set ROG_SK,11;
		mes "[Meurtrier]";
		mes "Qu'est-ce que vous m'avez fait?! Je.. Je ne peux pas bouger!";
		next;
		mes "[Haijara Greg]";
		mes "Oh? Je ne m'attendais pas à ce que vous sachiez utiliser un Emprisonnement aussi rapidement! Fantastique, vraiment fantastique! C'est une bonne opportunité pour que vous appreniez à maîtriser cette compétence...";
		next;
		mes "[Haijara Greg]";
		mes "Faites bien attention. Je veux que vous vous entrainiez à prédire les mouvements de votre adversaire avec ce meurtrier. Vous devriez être capable de voir dans quelle direction il compte aller grâce à son aura.";
		next;
		mes "[Haijara Greg]";
		mes "S'il a l'intention d'aller sur la gauche, son aura sera blanche. S'il veut bouger sur la droite, elle sera jaune. S'il a dans l'idée de reculer, elle sera rouge pâle.";
		next;
		while(1) {
			if (.@lim_1 == 10) {
				break;
			}
			else {
				set .@lim_1,.@lim_1 + 1;
				set .@move_1,rand(1,3);
				if (.@move_1 == 1) {
					specialeffect EF_CHANGECOLD;
					if (select("Le bloquer sur la gauche.:Le bloquer sur la droite.:Bloquer sa retraite.") == 1) {
						mes "^3355FFLe meurtrier ne peut plus bouger et a l'air perdu. Vous venez d'utiliser l'Emprisonnement à la perfection!^000000";
						specialeffect EF_POTION1;
						specialeffect2 EF_POTION7;
						next;
					}
					else {
						mes "^3355FFAttendez!";
						mes "Pour une raison inconnue, vous sentez que le meurtrier ne va pas aller dans cette direction. Vous vous corrigez naturellement.^000000";
						next;
					}
				}
				else if (.@move_1 == 2) {
					specialeffect EF_CHANGEWIND;
					if (select("Le bloquer sur la gauche.:Le bloquer sur la droite.:Bloquer sa retraite.") == 2) {
						mes "^3355FFLe meurtrier ne peut plus bouger et a l'air perdu. Vous venez d'utiliser l'Emprisonnement à la perfection!^000000";
						specialeffect EF_POTION1;
						specialeffect2 EF_POTION7;
						next;
					}
					else {
						mes "^3355FFAttendez!";
						mes "Pour une raison inconnue, vous sentez que le meurtrier ne va pas aller dans cette direction. Vous vous corrigez naturellement.^000000";
						next;
					}
				}
				else if (.@move_1 == 3) {
					specialeffect EF_CHANGEEARTH;
					if (select("Le bloquer sur la gauche.:Le bloquer sur la droite.:Bloquer sa retraite.") == 3) {
						mes "^3355FFLe meurtrier ne peut plus bouger et a l'air perdu. Vous venez d'utiliser l'Emprisonnement à la perfection!^000000";
						specialeffect EF_POTION1;
						specialeffect2 EF_POTION7;
						next;
					}
					else {
						mes "^3355FFAttendez!";
						mes "Pour une raison inconnue, vous sentez que le meurtrier ne va pas aller dans cette direction. Vous vous corrigez naturellement.^000000";
						next;
					}
				}
			}
		}
		mes "^3355FFVous réussissez à récupérer l'inestimable livre sur les compétences du légendaire Chae Takbae.^000000";
		close2;
		disablenpc "Killer#Rogueguild";
		donpcevent "#killershow01::OnDisable";
		end;
	}
	else if (ROG_SK == 11) {
		mes "[Meurtrier]";
		mes "Grrrrr...";
		mes "Encore...";
		mes "P-pas... Bouger!";
		close2;
		disablenpc "Killer#Rogueguild";
		donpcevent "#killershow01::OnDisable";
		end;
	}
	end;
}

in_rogue,355,179,0	script	Haijara Greg#rogueguild	46,{
	if (BaseJob == Job_Rogue) {
		if (ROG_SK == 12 && Upper == 1) {
			mes "[Haijara Greg]";
			mes "Hum? Ah, l'amnésie provoquée par la transcendance, hein? Hé bien, je vais vous enseigner une nouvelle fois comment utiliser la compétence d'Emprisonnement.";
			specialeffect2 EF_LIGHTSPHERE;
			skill 1005,1,0;
			set ROG_SK,13;
			close;
		}
		else if (ROG_SK == 13 && Upper == 1) {
			mes "[Haijara Greg]";
			mes ( sex ? "Un Rôdeur" : "Une Rôdeuse" ) + ", hein? Assurez-vous d'utiliser vos techniques sur des adversaires sans foi, méritant d'être pris par derrière. Je vous souhaite bonne chance, "+ ( sex ? "aventurier" : "aventurière" ) +"!";
			close;
		}
		else {
			if (ROG_SK < 1) {
				mes "[Haijara Greg]";
				mes "Comment est-ce que vous avez trouvé cet endroit? Je pensais que cette chambre sécurisée était inviolable, même par des Maraudeurs!";
				next;
				mes "[Haijara Greg]";
				mes "Ce n'est pas bon du tout! Ce n'est qu'une question de temps avant qu'ils ne me trouvent...";
				close;
			}
			else if (ROG_SK == 1) {
				mes "[Haijara Greg]";
				mes "Qui êtes-vous, et comment est-ce que vous êtes arrivé"+ ( sex ? "" : "e" ) +" ici? Identifiez-vous!";
				next;
				if (select("Je ne vous veux aucun mal!:Donnez-nous ce que nous voulons!") == 1) {
					mes "[Haijara Greg]";
					mes "Non? Hé bien... Dans ce cas... Pourquoi êtes-vous là? Vous ne m'avez pas trouvé par accident, n'est-ce pas?";
					next;
					if (select("Je viens vous aider.:Oh, ce n'est pas vous que je cherchais.") == 1) {
						mes "[Haijara Greg]";
						mes "Hum... Hé bien, j'imagine que je peux faire confiance à "+ ( Upper == 1 ? ( sex ? "un Rôdeur" : "une Rôdeuse" ) : ( sex ? "un Maraudeur" : "une Maraudeuse" ) ) +" comme moi pour ce genre de choses. Le sens de l'honneur entre brigands, tout ça... Bon. Est-ce que vous connaissez la légende de Chae Takbae?";
						next;
						if (select("Non:Oui") == 1) {
							mes "[Haijara Greg]";
							mes "Il y a un siècle, Chae Takbae fut le tout premier à transcender ses limites. C'était aussi le premier à choisir un chemin différent, plutôt que de suivre la voie du métier qu'il exerçait auparavant.";
							next;
							mes "[Haijara Greg]";
							mes "Il était Moine à l'origine, mais après sa transcendance, il devint Maraudeur. Grâce à ses connaissance des techniques de base des Moines, il décida de les adapter pour leur faire servir les desseins des Maraudeurs.";
							next;
							mes "[Haijara Greg]";
							mes "Chae Takbae a consigné les bases de cette nouvelle technique pour les Maraudeurs, et j'ai eu la chance de mettre la main dessus. Mais le chantage a commencé dès que je l'ai eu en ma possession...";
							next;
							mes "[Haijara Greg]";
							mes "Il y a des gens sans scrupules qui sont prêts à employer tous les moyens nécessaires pour me prendre cet ouvrage. Je peux le comprendre: mes fils et moi avons pu apprendre de nouvelles techniques grâce aux écrits de Takbae.";
							next;
							mes "[Haijara Greg]";
							mes "Mais je manque de temps et ces hommes finiront inévitablement par me trouver. Est-ce que nous aiderez à nous sauver, moi et mes fils?";
							next;
							if (select("Bien sûr!:Je suis désolé"+ ( sex ? "" : "e" ) +", je suis occupé"+ ( sex ? "" : "e" ) +".") == 1) {
								mes "[Haijara Greg]";
								mes "Oh, merci beaucoup! Écoutez, je ne peux pas prendre le risque qu'on me trouve, alors pourriez-vous apporter cette lettre à mon plus jeune fils, ^FF0000Louis Greg^000000? Dépêchez-vous, il ne nous reste plus beaucoup de temps!";
								set ROG_SK,2;
								close;
							}
							mes "[Haijara Greg]";
							mes "Je... Je vois. Mais comprenez-moi bien: si je ne peux pas vous faire confiance, à qui le pourrai-je?";
							close;
						}
						mes "[Haijara Greg]";
						mes "Alors vous connaissez la valeur de ce traité qu'il a écrit, et qui se trouve être en ma possession. Cependant, je suis pourchassé par des hommes sans foi qui seraient prêts à tout pour s'en emparer. Est-ce que vous m'aiderez?";
						next;
						if (select("Bien sûr!:Je suis désolé"+ ( sex ? "" : "e" ) +", je suis occupé"+ ( sex ? "" : "e" ) +".") == 1) {
							mes "[Haijara Greg]";
							mes "Oh, merci beaucoup! Écoutez, je ne peux pas prendre le risque qu'on me trouve, alors pourriez-vous apporter cette lettre à mon plus jeune fils, ^FF0000Louis Greg^000000? Dépêchez-vous, il ne nous reste plus beaucoup de temps!";
							set ROG_SK,2;
							close;
						}
						mes "[Haijara Greg]";
						mes "Je... Je vois. Mais comprenez-moi bien: si je ne peux pas vous faire confiance, à qui le pourrai-je?";
						close;
					}
					mes "[Haijara Greg]";
					mes "Hum...?";
					mes "C'est vraiment dur à croire, mais je ferais mieux de vous accorder le bénéfice du doute. Hé bien dans ce cas, j'espère que vous retrouverez la sortie.";
					close;
				}
				mes "[Haijara Greg]";
				mes "Je vois. Alors qu'il en soit ainsi. Je vais vous faire la démonstration de ces compétences que vous convoitez tant...";
				mes "^FF0000Emprisonnement^000000!";
				specialeffect EF_CHANGECOLD;
				specialeffect2 EF_CHANGEWIND;
				next;
				mes "[Haijara Greg]";
				mes "Humpf. Vous voilà maintenant sans défenses, ce qui me permet de faire ceci...";
				mes "^FF0000Attaque Sournoise^000000!";
				specialeffect2 EF_COMBOATTACK5;
				percentheal -95,0;
				next;
				mes "[Haijara Greg]";
				mes "Hors d'ici maintenant! Je ne veux plus jamais revoir votre face pleine de cupidité! Estimez-vous "+ ( sex ? "heureux" : "heureuse" ) +" que je ne vous laisse la vie sauve!";
				set ROG_SK,1;
				close;
			}
			else if (ROG_SK == 2) {
				mes "[Haijara Greg]";
				mes "Nous n'avons plus beaucoup de temps devant nous... S'il vous plaît, apportez cette lettre à mon cadet, ^FF0000Louis^000000, dès que possible! Vous le trouverez dans la Guilde des Maraudeurs. Il passe souvent du temps avec ^FF0000Hollgrehenn Junior^000000.";
				close;
			}
			else if (ROG_SK == 3) {
				mes "[Haijara Greg]";
				mes "Louis vous a envoyé"+ ( sex ? "" : "e" ) +" chercher ^FF0000Thor^000000? Il devrait se trouver dans l'enceinte de la Guilde des Maraudeurs. Il traine souvent avec ^FF0000Hermanthorn Junior.^000000 je crois. Je suis désolé que vous ayez à rendre visite à mes fils un par un... Je sais, ça n'est pas très pratique.";
				close;
			}
			else if (ROG_SK == 4) {
				mes "[Haijara Greg]";
				mes "Ah, vous cherchez ^FF0000Jay^000000, hein? Il passe souvent du temps avec ^FF0000Antonio Jr^000000 ici, dans la Guilde des Maraudeurs. En général il se trouve avec lui.";
				close;
			}
			else if (ROG_SK == 5) {
				mes "[Haijara Greg]";
				mes "Oh, vous revoilà! Et vous m'avez rapporté une lettre de Jay. Bien, bien. Laissez-moi lire ce qu'il me raconte...";
				set ROG_SK,6;
				next;
				mes "[Haijara Greg]";
				mes "Ah, ce sont de très bonnes nouvelles! Il a contacté la Guilde des Maraudeurs pour demander une protection supplémentaire, et pour alerter leurs gardes. Je peux enfin me détendre un minimum maintenant. Personne ne défie les Maraudeurs!";
				next;
				mes "[Haijara Greg]";
				mes "Merci beaucoup pour votre aide. Je serais toujours à me terrer dans mon coin sans vous. En retour, laissez-moi vous offrir l'opportunité de vous apprendre la technique secrète de Chae Takbae.";
				next;
				mes "[Haijara Greg]";
				mes "Allez trouver ^FF0000Thor^000000 s'il vous plaît, et dites-lui que je vous ai permis d'apprendre la compétence que j'ai enseignée à tous mes enfants. Il s'exécutera, j'en suis sûr.";
				close;
			}
			else if (ROG_SK == 6) {
				mes "[Haijara Greg]";
				mes "S'il vous plaît, allez demander à Thor de vous enseigner la technique secrète de Chae Takbae. Comprenez bien que je ne peux pas le faire moi-même avec le peu d'équipements qu'il y a là.";
				close;
			}
			else if (ROG_SK == 7) {
				mes "[Haijara Greg]";
				mes "Hein? N'êtes-vous pas censé"+ ( sex ? "" : "e" ) +" être en train de vous entrainer pour apprendre cette nouvelle technique? Allez parler à Thor pour terminer votre entrainement s'il vous plaît.";
				close;
			}
			else if (ROG_SK == 8) {
				mes "[Haijara Greg]";
				mes "Vous avez terminé l'entraînement? Ah, c'est exceptionnel! Mais vous devriez peut-être d'abord aller parler à Thor...";
				close;
			}
			else if (ROG_SK == 9) {
				mes "[Haijara Greg]";
				mes "Oh, vous revoilà! J'ai appris que vous aviez terminé l'entraînement et que vous étiez prêt"+ ( sex ? "" : "e" ) +" à apprendre comment on pratique un Emprisonnement.";
				next;
				mes "[Haijara Greg]";
				mes "Quoi! Qu'est-ce q--?!";
				mes "Arrêtez! S'il vous plaît... Arrêtez cet homme! Nous ne pouvons pas le laisser voler ce livre!";
				specialeffect EF_COMBOATTACK5;
				donpcevent "Killer#Rogueguild::OnEnable";
				set ROG_SK,10;
				close;
			}
			else if (ROG_SK == 10) {
				mes "[Haijara Greg]";
				mes "Ce type vient de voler l'ouvrage sur l'Emprisonnement! Je vous en prie! Ne le laissez pas s'échapper! Ce livre est inestimable!";
				donpcevent "#killershow01::OnEnable";
				close;
			}
			else if (ROG_SK == 11) {
				donpcevent "Killer#Rogueguild::OnDisable";
				donpcevent "#killershow01::OnDisable";
				mes "^3355FFVous rendez à Haijara le livre de Chae Takbae.^000000";
				next;
				mes "[Haijara Greg]";
				mes "Merci beaucoup pour votre aide. Maintenant, je pourrais vous parler plus en détail de l'Emprisonnement si c'est ce que vous souhaitez.";
				next;
				if (select("Oui, s'il vous plaît.:Non merci.") == 1) {
					mes "[Haijara Greg]";
					mes "Comme vous le savez, l'Emprisonnement immobilise un ennemi proche. Cependant, cette compétence varie quelque peu selon si elle est utilisée sur un humain ou sur un monstre.";
					next;
					mes "[Haijara Greg]";
					mes "L'Emprisonnement est similaire à la compétence Enracinement des Moines, mais elle n'empêche l'ennemi que de bouger, pas d'attaquer.";
					next;
					mes "[Haijara Greg]";
					mes "Il n'y a que l'Attaque Sournoise que nous ne pouvons utiliser qu'une fois pendant un Emprisonnement. Il n'y a pas de limite pour les autres compétences.";
					next;
					mes "[Haijara Greg]";
					mes "Les ennemis emprisonnés peuvent s'échapper en utilisant un sort ou un objet de Téléportation, ou en faisant une Disparition. Cependant, seule cette dernière peut être utilisée pendant les Guerres de l'Empérium.";
					next;
					mes "[Haijara Greg]";
					mes "L'Emprisonnement peut par conséquent être très utile lors des sièges des Guerres de Guildes, tant que la cible ne s'échappe pas avec une Disparition.";
					next;
					mes "[Haijara Greg]";
					mes "L'Emprisonnement immobilisera les monstres pendant 10 secondes, à l'exception des boss, offrant de nouvelles perspectives aux Maraudeurs dans les parties de chasse.";
					next;
					mes "[Haijara Greg]";
					mes "Hé bien, c'est tout ce que je peux vous dire à propos de l'Emprisonnement. J'espère que vous en apprendrez plus par la pratique, et que vous deviendrez une légende aussi grande que Chae Takbae.";
					set ROG_SK,12;
					skill 1005,1,0;
					specialeffect2 EF_LIGHTSPHERE;
					close;
				}
				mes "[Haijara Greg]";
				mes "Très bien, dans ce cas... J'espère que vous pourrez en apprendre plus sur cette compétence par la pratique. Bonne chance, et encore merci pour votre aide.";
				set ROG_SK,12;
				skill 1005,1,0;
				specialeffect2 EF_LIGHTSPHERE;
				close;
			}
			mes "[Haijara Greg]";
			mes "Merci de m'avoir aidé à protéger cet ouvrage inestimable. J'espère que nous serons alliés sur le champ de bataille la prochaine fois que nous nous reverrons...";
			close;
		}
	}
	else {
		mes "[Haijara Greg]";
		mes "Comment est-ce que vous avez trouvé cet endroit? Je pensais que personne ne pouvait entrer ici, même pas les Maraudeurs!";
		next;
		mes "[Haijara Greg]";
		mes "Ce n'est pas bon du tout! Ce n'est qu'une question de temps avant qu'ils ne me trouvent...";
		next;
		mes "[Haijara Greg]";
		mes "S'il vous plaît... Trouvez-moi un Maraudeur ou un Rôdeur en qui je puisse avoir confiance, et envoyez-le m'aider! Je n'ai plus beaucoup de temps devant moi...";
		close;
	}
}

in_rogue,152,29,0	script	Louis Greg#rogueguild	84,{
	if (BaseJob == Job_Rogue || Class == Job_Thief_High) {
		if (ROG_SK < 1) {
			mes "[Louis Greg]";
			mes "Mon père a... Il a tout simplement disparu! Il nous enseignait à moi et mes frères une nouvelle compétence, mais depuis quelques temps, il se mettait à agir comme un paranoïaque pour une raison inconnue...";
			next;
			mes "[Louis Greg]";
			mes "Peut-être s'est-il caché? Je sais qu'il y a une pièce sécurisée dans la Guilde des Maraudeurs, mais je ne sais même pas où elle se trouve exactement. J'espère que tout va bien...";
			set ROG_SK,1;
			close;
		}
		else if (ROG_SK == 1) {
			mes "[Louis Greg]";
			mes "Père nous avait prévenus que des hommes pourraient être après lui s'ils apprenaient au sujet de la nouvelle compétence qu'il nous enseignait... Peut-être s'est-il caché dans la pièce sécurisée de la Guilde...";
			close;
		}
		else if (ROG_SK == 2) {
			mes "[Louis Greg]";
			mes "Qu'est-ce que c'est? Une lettre de mon père? Oh, alors il doit être sain et sauf! Vite, laissez-moi la lire!";
			set ROG_SK,3;
			next;
			mes "[Louis Greg]";
			mes "Oh non, il est peut-être sain et sauf pour l'instant, mais des types dangereux sont à sa recherche. Mon frère Thor voudra être tenu au courant. Laissez-moi lui écrire une lettre rapidement...";
			next;
			mes "[Louis Greg]";
			mes "Je sais que je ne suis pas en position de vous demander le moindre service, mais j'imagine que mon père vous fait confiance. Est-ce que vous voudriez bien apporter cette lettre à mon grand frère, Thor?";
			next;
			mes "[Louis Greg]";
			mes "Vous devriez le trouver près d'Hermanthorn Junior, dans l'enceinte de la Guilde. J'apprécierais vraiment si vous pouviez aider ma famille.";
			close;
		}
		else if (ROG_SK == 3) {
			mes "[Louis Greg]";
			mes "Mon grand frère, ^FF0000Thor^000000, doit être tenu au courant sans attendre! Apportez-lui cette lettre de ma part, s'il vous plaît. Vous devriez le trouver près d'^FF0000Hermanthorn Junior^000000 dans l'enceinte de la Guilde.";
			close;
		}
		else if (ROG_SK == 4) {
			mes "[Louis Greg]";
			mes "Vous cherchez mon frère Jay? En général il traine avec Antonio Jr dans la Guilde des Maraudeurs.";
			close;
		}
		else if (ROG_SK == 5) {
			mes "[Louis Greg]";
			mes "Si jamais vous avez besoin de reparler à mon père, il devrait toujours se trouver dans la chambre sécurisée de la Guilde. Mais je ne sais toujours pas où elle se trouve exactement...";
			close;
		}
		else if (ROG_SK == 6) {
			mes "[Louis Greg]";
			mes "Vous voulez apprendre la technique secrète des Maraudeurs? Oh, vous devriez probablement aller en parler à ^FF0000Thor^000000. Je... Je ne suis pas doué pour expliquer les choses aux gens. Vraiment pas.";
			close;
		}
		else if (ROG_SK == 7) {
			mes "[Louis Greg]";
			mes "Hein? Vous êtes en train d'apprendre la technique secrète des Maraudeurs, n'est-ce pas? Oh, dans ce cas, vous ne devriez pas être ici. Retournez parler à ^FF0000Thor^000000, d'accord?";
			close;
		}
		else if (ROG_SK == 8) {
			mes "[Louis Greg]";
			mes "Oh, oh! Vous en avez terminé avec l'entraînement? Ah, dans ce cas, vous devriez aller reparler à Thor!";
			close;
		}
		else if (ROG_SK == 9) {
			mes "[Louis Greg]";
			mes "Vous vouliez en apprendre plus au sujet de l'Emprisonnement? Ugh, dans ce cas, vous feriez mieux d'aller parler à mon père. Je viens moi-même tout juste d'apprendre cette technique, vous savez?";
			close;
		}
		else if (ROG_SK == 11) {
			mes "[Louis Greg]";
			mes "Mes frères viennent de me dire que Chae Takbae avait créé l'Emprisonnement pour battre à mort ses ennemis en les empêchant de s'enfuir. Est-ce que c'est vrai?";
			close;
		}
		mes "[Louis Greg]";
		mes "Oh, vous êtes vraiment fantastique. Je voudrais être aussi fort que vous... Je déteste être un enfant!";
		close;
	}
	mes "[Louis Greg]";
	mes "Pourquoi je veux devenir Maraudeur? J'imagine que c'est parce que j'aime les coups fourrés. Et puis, l'ordre, la morale... Toutes ces choses là et moi, ça fait deux!";
	close;
}

in_rogue,268,125,2	script	Thor Greg#rogueguild	86,{
	if (BaseJob == Job_Rogue || Class == Job_Thief_High) {
		if (ROG_SK < 1) {
			mes "[Thor Greg]";
			mes "Où est-ce que Père est allé? J'espère que ces hommes bizarres ne lui ont pas mis la main dessus. Avec un peu de chance, il se sera caché dans la chambre sécurisée, mais quand même...";
			set ROG_SK,1;
			close;
		}
		else if (ROG_SK == 1) {
			mes "[Thor Greg]";
			mes "Hum... Mon père se cache peut-être dans la salle sécurisée. Je n'ai jamais pu la trouver, mais Markie dit que l'entrée est habilement dissimulée à sa gauche. Hummm...";
			close;
		}
		else if (ROG_SK == 2) {
			mes "[Thor Greg]";
			mes "Est-ce que vous cherchez mon petit frère ^FF0000Louis^000000? Oh, il traine toujours avec ce ^FF0000Hollgrehenn Junior^000000 dans la Guilde. De quoi est-ce que vous vouliez lui parler?";
			close;
		}
		else if (ROG_SK == 3) {
			mes "[Thor Greg]";
			mes "Qu'est-ce que vous me donnez là? Une lettre de Louis? Voyons...";
			set ROG_SK,4;
			next;
			mes "[Thor Greg]";
			mes "Oh, par tous les dieux! Père a de graves ennuis! Je ferais mieux d'en parler à mon frère Jay au plus vite! Il n'y a pas une seconde à perdre! Allez trouver ^FF0000Jay^000000 près d'^FF0000Antonio Jr^000000 dans l'enceinte de la Guilde! Dépêchez-vous s'il vous plaît!";
			close;
		}
		else if (ROG_SK == 4) {
			mes "[Thor Greg]";
			mes "Vous trouverez Jay à traîner autour d'Antonio Jr dans la Guilde. S'il vous plaît, apportez-lui cette lettre aussi vite que possible!";
			close;
		}
		else if (ROG_SK == 5) {
			mes "[Thor Greg]";
			mes "J'espère que Père n'a pas quitté la salle sécurisée. C'est peut-être le seul endroit où il peut être en sécurité...";
			close;
		}
		else if (ROG_SK == 6) {
			mes "[Thor Greg]";
			mes "Comment cela? Père voulait que je vous apprenne la technique secrète des Maraudeurs? Bien, je vais vous entrainer de la même façon que lui m'a entrainé. Est-ce que vous voudriez d'abord avoir des explications?";
			next;
			mes "[Thor Greg]";
			mes "Sans cela, je vous enverrai directement sur le terrain d'entraînement pour que vous puissiez apprendre à maîtriser l'^FF0000Emprisonnement^000000 au plus vite.";
			next;
			if (select("Écouter les explications:Se rendre sur le terrain d'entraînement") == 1) {
				mes "[Thor Greg]";
				mes "Bien, la première étape pour apprendre à maîtriser l'^FF0000Emprisonnement^000000, c'est d'apprendre à bloquer les mouvements de son adversaire. Je vais vous envoyer sur un terrain d'entraînement spécial où vous pourrez vous exercer.";
				next;
				mes "[Thor Greg]";
				mes "Vous y trouverez notre partenaire pour l'entraînement. Approchez-vous d'elle au plus près, et assurez-vous de bien prédire ses mouvements et de les bloquer. Préparez-vous, je vous y conduis sans attendre...";
				close2;
				warp "in_rogue",89,114;
				end;
			}
			warp "in_rogue",89,114;
			end;
		}
		else if (ROG_SK == 7) {
			mes "[Thor Greg]";
			mes "Bon, je vais vous laisser le bénéfice du doute et supposer que vous avez été"+ ( sex ? "" : "e" ) +" déconnecté"+ ( sex ? "" : "e" ) +" du jeu. Est-ce que vous voulez retourner sur le terrain d'entraînement pour apprendre les techniques d'^FF0000Emprisonnement^000000?";
			next;
			if (select("Oui, s'il vous plaît.:Non merci.") == 1) {
				mes "[Thor Greg]";
				mes "Bon, la première étape pour apprendre à maîtriser l'^FF0000CEmprisonnement^000000, c'est d'apprendre à bloquer les mouvements de votre adversaire. Je vais vous envoyer sur un terrain d'entraînement spécial où vous pourrez vous exercer.";
				next;
				mes "[Thor Greg]";
				mes "Vous y trouverez notre partenaire pour l'entraînement. Approchez-vous d'elle au plus près, et assurez-vous de bien prédire ses mouvements et de les bloquer. Préparez-vous, je vous y conduis sans attendre...";
				set ROG_SK,6;
				close2;
				warp "in_rogue",89,114;
				end;
			}
			mes "[Thor Greg]";
			mes "D'accord, d'accord. Dites-le moi quand vous serez prêt"+ ( sex ? "" : "e" ) +" à reprendre l'entraînement.";
			close;

		}
		else if (ROG_SK == 8) {
			mes "[Thor Greg]";
			mes "Ah, Kienna m'a dit que vous aviez terminé l'entrainement. Félicitations!";
			mes "Allez maintenant parler à mon père pour qu'il puisse vous expliquer toutes les subtilités de l'Emprisonnement.";
			set ROG_SK,9;
			close;
		}
		else if (ROG_SK == 9) {
			mes "[Thor Greg]";
			mes "S'il vous plaît, allez parler à mon père pour qu'il puisse vous expliquer toutes les subtilités de l'Emprisonnement. Il devrait toujours être dans la salle sécurisée de la Guilde.";
			close;
		}
		else if (ROG_SK == 11) {
			mes "[Thor Greg]";
			mes "Vous savez, Chae Takbae dirait...";
			mes "\"Je suis Chae Takbae. Et vous êtes...?\" à ses adversaires, et avant qu'ils n'aient eu le temps de répondre, il les aurait réduits en morceaux.";
			next;
			mes "[Thor Greg]";
			mes "C'est vraiment un héros de légendes parmi les Maraudeurs!";
			close;
		}
		mes "[Thor Greg]";
		mes "Hum...?";
		mes "On dirait que vous êtes même encre plus fort" + ( sex ? "" : "e" ) + " que moi. Il n'y a probablement pas beaucoup de gens qui osent vous chercher des noises...";
		close;
	}
	mes "[Thor Greg]";
	mes "Hum, faire les poches de ce type, faire les poches de celui-là... *Soupire* C'est ma plus grande honte: je dépense les zénys presque aussi vite que je n'en vole. C'est irresponsable...";
	close;
}

in_rogue,181,114,3	script	Jay Greg#rogueguild	85,{
	if (BaseJob == Job_Rogue || Class == Job_Thief_High) {
		if (ROG_SK < 1) {
			mes "[Jay Greg]";
			mes "Mon père doit se cacher dans la salle sécurisée de la Guilde des Maraudeurs. Mais pour une raison inconnue, nous ne pouvons jamais la trouver avec mes frères, et nous ne savons pas s'il est en sécurité...";
			set ROG_SK,1;
			next;
			mes "[Jay Greg]";
			mes "Depuis peu, il est pourchassé par ces hommes étranges qui menacent notre famille. Enfin... Il vient de nous enseigner une nouvelle technique, alors j'imagine qu'il peut s'en servir pour se défendre...";
			close;
		}
		else if (ROG_SK == 1) {
			mes "[Jay Greg]";
			mes "Mon père doit se cacher dans la salle sécurisée de la Guilde des Maraudeurs. Pour une raison inconnue, nous ne la trouvons jamais avec mes frères, mais on suppose que l'entrée est dissimulée près de là où se trouve Markie.";
			close;
		}
		else if (ROG_SK == 2) {
			mes "[Jay Greg]";
			mes "Vous cherchez Louis, mon petit frère? Il est dans l'enceinte de la Guilde des Maraudeurs, alors il ne devrait pas être très dur à trouver. Il doit surement trainer avec Hollgrehenn Junior.";
			close;
		}
		else if (ROG_SK == 3) {
			mes "[Jay Greg]";
			mes "Thor? Il devrait se trouver dans l'enceinte de la Guilde. Vous avez regardé s'il n'est pas avec Hermanthorn Junior.?";
			close;
		}
		else if (ROG_SK == 4) {
			mes "[Jay Greg]";
			mes "Une lettre pour moi? Ah, elle est de la part de Thor, alors j'imagine que je ferais mieux de la lire sans attendre. Hummm...";
			set ROG_SK,5;
			next;
			mes "[Jay Greg]";
			mes "Je vois... Je dois signaler tout ceci à la Guilde sans attendre, et envoyer une réponse à mon père. Hum... Laissez-moi le temps de lui écrire un mot s'il vous plaît.";
			next;
			mes "[Jay Greg]";
			mes "Voilà, c'est fait. Apportez ceci en toute hâte à mon père s'il vous plaît. Je sais que j'en demande beaucoup, mais je n'arrive pas à trouver cette chambre sécurisée moi-même...";
			close;
		}
		else if (ROG_SK == 5) {
			mes "[Jay Greg]";
			mes "S'il vous plaît, apportez ma lettre à mon père aussi vite que vous le pouvez. Il se cache toujours dans la salle sécurisée de la Guilde des Maraudeurs. Enfin... Si nos ennemis ne l'ont pas déjà trouvé.";
			close;
		}
		else if (ROG_SK == 6) {
			mes "[Jay Greg]";
			mes "Vous devez vous entraîner pour apprendre à maîtriser les Emprisonnements? Je pense que seul Thor a accès au terrain d'entraînement, alors vous devriez d'abord aller lui parler.";
			close;
		}
		else if (ROG_SK == 7) {
			mes "[Jay Greg]";
			mes "Vous devez vous entraîner pour apprendre à maîtriser les Emprisonnements? Je pense que seul Thor a accès au terrain d'entraînement, alors vous devriez d'abord aller lui parler.";
			close;
		}
		else if (ROG_SK == 8) {
			mes "[Jay Greg]";
			mes "Ah, vous avez fini de vous entraîner pour apprendre les techniques d'Emprisonnement, n'est-ce pas? Fantastique! Allez maintenant en parler à mon frère Thor.";
			close;
		}
		else if (ROG_SK == 9) {
			mes "[Jay Greg]";
			mes "Hum... Vous devriez certainement aller parler à mon père pour apprendre toutes les subtilités des Emprisonnements. Vous l'avez vu dans la chambre sécurisée de la Guilde des Maraudeurs?";
			close;
		}
		else if (ROG_SK == 12) {
			mes "[Jay Greg]";
			mes "Chae Takbae avait peut-être l'air d'être brutal et borné, mais il devait être sacrément ingénieux pour inventer ses propres techniques.";
			close;
		}
		mes "[Jay Greg]";
		mes "J'ai le sentiment que vous allez devenir "+ ( sex ? "l'un des meilleurs Maraudeurs" : "l'une des meilleures Maraudeuses" ) +" de la région, si ce n'est pas déjà le cas.";
		close;
	}
	mes "[Jay Greg]";
	mes "Depuis que j'ai appris les Intimidations, je suis devenu de plus en plus agressif. J'ai peut-être même parfois un petit côté masochiste. Je veux dire... S'ils utilisent une compétence sur vous, vous pouvez leur renvoyer en pleine face!";
	close;
}

prt_are01,150,150,0	script	#1strecog	-1,10,10,{
OnTouch:
	donpcevent "#1st5min::OnEnable";
	donpcevent "#1stmove::OnEnable";
	disablenpc "#1strecog";
	end;
}

prt_are01,135,135,0	script	#1st5min	-1,{
OnInit:
	disablenpc "#1st5min";
	end;

OnEnable:
	enablenpc "#1st5min";
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	disablenpc "#1st5min";
	end;

OnTimer1000:
	mapannounce "prt_are01","Bienvenue sur le Terrain d'Entraînement à l'Emprisonnement! Vous serez automatiquement téléporté à l'extérieur dans 5 minutes.",bc_map,"0x00ff00"; // NB: Pas de RID attaché, donc pas de check sur le sexe
	end;

OnTimer290000:
	mapannounce "prt_are01","Vous allez être téléporté à l'extérieur dans 20 secondes.",bc_map,"0x00ff00";
	end;

OnTimer310000:
	mapannounce "prt_are01","Vous allez être téléporté à l'extérieur dans 5 secondes.",bc_map,"0x00ff00";
	end;

OnTimer315000:
	mapannounce "prt_are01","Vous allez maintenant être téléporté à l'extérieur.",bc_map,"0x00ff00";
	disablenpc "Kienna#1st";
	disablenpc "Kienna#2nd";
	disablenpc "Kienna#3rd";
	disablenpc "Kienna#4th";
	disablenpc "Kienna#5th";
	disablenpc "Kienna#6th";
	disablenpc "Kienna#7th";
	disablenpc "Kienna#8th";
	donpcevent "#1stmove::OnDisable";
	enablenpc "#1strecog";
	donpcevent "Waiting Room#rogue10::OnEnable";
	stopnpctimer;
	mapwarp "prt_are01","in_rogue",264,124;
	end;
}

prt_are01,135,135,0	script	#1stmove	-1,{
OnInit:
	disablenpc "#1stmove";
	end;

OnEnable:
	enablenpc "#1stmove";
	initnpctimer;
	end;

OnTimer3000:
	mapannounce "prt_are01","Kienna va apparaître dans 1 seconde. Veuillez vous approcher d'elle autant que possible.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	switch(rand(1,8)) {
	case 1:
		enablenpc "Kienna#1st";
		break;
	case  2:
		enablenpc "Kienna#2nd";
		break;
	case  3:
		enablenpc "Kienna#3rd";
		break;
	case  4:
		enablenpc "Kienna#4th";
		break;
	case  5:
		enablenpc "Kienna#5th";
		break;
	case  6:
		enablenpc "Kienna#6th";
		break;
	case  7:
		enablenpc "Kienna#7th";
		break;
	case 8:
		enablenpc "Kienna#8th";
	}
	end;

OnTimer8000:
	disablenpc "Kienna#1st";
	disablenpc "Kienna#2nd";
	disablenpc "Kienna#3rd";
	disablenpc "Kienna#4th";
	disablenpc "Kienna#5th";
	disablenpc "Kienna#6th";
	disablenpc "Kienna#7th";
	disablenpc "Kienna#8th";
	end;

OnTimer9000:
	stopnpctimer;
	donpcevent "#1stmove::OnEnable";
	mapwarp "prt_are01","prt_are01",150,150;
	end;

OnDisable:
	stopnpctimer;
	disablenpc "#1stmove";
	end;
}

prt_are01,149,162,0	script	Kienna#1st	700,1,1,{
	end;
OnTouch:
	callfunc "F_Kienna","1st";
	end;
}

prt_are01,136,150,0	script	Kienna#2nd	700,1,1,{
	end;
OnTouch:
	callfunc "F_Kienna","2nd";
	end;
}

prt_are01,150,138,0	script	Kienna#3rd	700,1,1,{
	end;
OnTouch:
	callfunc "F_Kienna","3rd";
	end;
}

prt_are01,163,147,0	script	Kienna#4th	700,1,1,{
	end;
OnTouch:
	callfunc "F_Kienna","4th";
	end;
}

prt_are01,134,140,0	script	Kienna#5th	700,1,1,{
	end;
OnTouch:
	callfunc "F_Kienna","5th";
	end;
}

prt_are01,161,140,0	script	Kienna#6th	700,1,1,{
	end;
OnTouch:
	callfunc "F_Kienna","6th";
	end;
}

prt_are01,161,160,0	script	Kienna#7th	700,1,1,{
	end;
OnTouch:
	callfunc "F_Kienna","7th";
	end;
}

prt_are01,138,159,0	script	Kienna#8th	700,1,1,{
	end;
OnTouch:
	callfunc "F_Kienna","8th";
	end;
}

function	script	F_Kienna	{
	if (ROG_SK == 6) {
		donpcevent "#1stmove::OnDisable";
		mes "[Kienna]";
		mes "Bien, dans cet exercice, vous allez devoir prédire dans quelle direction je vais me déplacer, et m'empêcher de bouger en me bloquant - vous allez m'immobiliser en gros. Préparez-vous~";
		set ROG_SK,7;
		next;
		while(1) {
			if (.@lim_1 == 10) {
				break;
			}
			else {
				set .@lim_1,.@lim_1 + 1;
				set .@move_1,rand(1,3);
				if (.@move_1 == 1) {
					if (select("La bloquer sur la gauche:La bloquer sur la droite:Bloquer sa retraite") == 1) {
						mes "[Kienna]";
						mes "Hein? Vous avez réussi à me bloquer! Très beau travail~";
						specialeffect 204; //EF_POTION1
						specialeffect2 210; // EF_POTION7
						set .@suc_1,.@suc_1 + 1;
						next;
					}
					else {
						mes "[Kienna]";
						mes "Désolé, mais je n'allais pas dans cette direction. Vous n'avez pas réussi à me bloquer...";
						next;
					}
				}
				else if (.@move_1 == 2) {
					if (select("La bloquer sur la gauche:La bloquer sur la droite:Bloquer sa retraite") == 2) {
						mes "[Kienna]";
						mes "Hein? Vous avez réussi à me bloquer! Très beau travail~";
						specialeffect 204; //EF_POTION1
						specialeffect2 210; // EF_POTION7
						set .@suc_1,.@suc_1 + 1;
						next;
					}
					else {
						mes "[Kienna]";
						mes "Désolé, mais je n'allais pas dans cette direction. Vous n'avez pas réussi à me bloquer...";
						next;
					}
				}
				else if (.@move_1 == 3) {
					if (select("La bloquer sur la gauche:La bloquer sur la droite:Bloquer sa retraite") == 3) {
						mes "[Kienna]";
						mes "Hein? Vous avez réussi à me bloquer! Très beau travail~";
						specialeffect 204; //EF_POTION1
						specialeffect2 210; // EF_POTION7
						set .@suc_1,.@suc_1 + 1;
						next;
					}
					else {
						mes "[Kienna]";
						mes "Désolé, mais je n'allais pas dans cette direction. Vous n'avez pas réussi à me bloquer...";
						next;
					}
				}
			}
		}
		mes "[Kienna]";
		mes "Bien, nous en avons terminé. Vous avez obtenu la note...";
		switch(.@suc_1) { 
		case 10:
			mes "\"^0000FFS^000000.\"";
			mes "C'est un score parfait!";
			break;
		case 9:
			mes "\"^0000FFA^000000.\"";
			mes "Vous êtes vraiment doué"+ ( sex ? "" : "e" ) +" pour ça!";
			break;
		case 8:
			mes "\"^0000FFB+^000000.\"";
			mes "C'est une note très honorable!";
			break;
		case 7:
			mes "\"^0000FFB^000000.\"";
			mes "C'est du bon travail~";
			break;
		case 6:
			mes "\"^0000FFC+^000000.\"";
			mes "Pas trop mal, mais vous passez de justesse!";
			break;
		case 5:
			mes "\"^FF0000C^000000.\"";
			break;
		case 4:
			mes "\"^FF0000D+^000000.\"";
			break;
		case 3:
			mes "\"^FF0000D^000000.\"";
			break;
		case 2:
			mes "\"^FF0000F^000000.\"";
			break;
		case 1:
			mes "\"^FF0000F-^000000.\"";
			break;
		case 0:
			next;
			mes "[Kienna]";
			mes "En fait, je n'arrive pas à calculer votre note. Qu'est-ce qui a bien pu se passer?!";
		}
		next;
		if (.@suc_1 > 5) {
			mes "[Kienna]";
			mes "Je suis contente que vous ayez terminé votre entraînement! Laissez-moi maintenant vous reconduire auprès de Thor Greg pour que vous puissiez terminer d'apprendre à maîtriser les ^FF0000Emprisonnements^000000.";
			set ROG_SK,8;
			disablenpc "Kienna#"+getarg(0);
			donpcevent "#1st5min::OnDisable";
			enablenpc "#1strecog";
			close2;
			donpcevent "Waiting Room#rogue10::OnEnable";
			warp "in_rogue",264,124;
			end;
		}
		else {
			mes "[Kienna]";
			mes "Hum... Je ne pense pas que vous soyez prêt"+ ( sex ? "" : "e" ) +" pour la suite"+ ( .@suc_1 ? " avec une telle note" : "" ) +". Est-ce que vous voudriez refaire cet exercice avec moi?";
			next;
			if (select("Oui!:Non merci.") == 1) {
				disablenpc "Kienna#"+getarg(0);
				donpcevent "#1stmove::OnEnable";
				set ROG_SK,6;
				warp "prt_are01",150,150;
				end;
			}
			disablenpc "Kienna#"+getarg(0);
			donpcevent "#1st5min::OnDisable";
			enablenpc "#1strecog";
			set ROG_SK,6;
			donpcevent "Waiting Room#rogue10::OnEnable";
			warp "in_rogue",264,124;
			end;
		}
	}
	else if (ROG_SK == 7) {
		mes "[Kienna]";
		mes "Vous devez avoir annulé votre entraînement au milieu de l'exercice. Je suis désolée, mais il vous faudra recommencer depuis le début.";
		set ROG_SK,6;
		close2;
		disablenpc "Kienna#"+getarg(0);
		donpcevent "#1stmove::OnEnable";
		warp "prt_are01",150,150;
		end;
	}
	else if (ROG_SK == 8) {
		mes "[Kienna]";
		mes "Vous avez déjà accompli les exercices pour apprendre à maîtriser les Emprisonnements. Vous n'avez désormais plus besoin de mon aide.";
		close2;
		donpcevent "Waiting Room#rogue10::OnEnable";
		warp "in_rogue",264,124;
		end;
	}
	else {
		mes "[Kienna]";
		mes "Je suis désolée, mais je ne pense pas que vous soyez ici à votre place. Laissez-moi vous reconduire à la Guilde des Maraudeurs si vous vous êtes perdu"+ ( sex ? "" : "e" ) +"...";
		close2;
		donpcevent "Waiting Room#rogue10::OnEnable";
		warp "in_rogue",264,124;
		end;
	}

OnInit:
	disablenpc "Kienna#1st";
	disablenpc "Kienna#2nd";
	disablenpc "Kienna#3rd";
	disablenpc "Kienna#4th";
	disablenpc "Kienna#5th";
	disablenpc "Kienna#6th";
	disablenpc "Kienna#7th";
	disablenpc "Kienna#8th";
	end;
}

in_rogue,88,119,3	script	Salle d'Attente::Waiting Room#rogue10	828,{
OnEnable:
	enablenpc "Waiting Room#rogue10";
	enablewaitingroomevent "Waiting Room#rogue10";
	end;

OnInit:
	waitingroom "Terrain d'Entraînement",10,"Waiting Room#rogue10::OnStartArena",1;
	enablewaitingroomevent "Waiting Room#rogue10";
	end;

OnStartArena:
	warpwaitingpc "prt_are01",150,150;
	disablewaitingroomevent "Waiting Room#rogue10";
	end;
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 for fully working skills only [Lupus]
//= 1.1 Added more new skill quests for more classes [Lupus]
//= Somehow eA engine doesn't let you keep learn't skill V_V'
//= 1.2 Added to correct locations, correct NPC's, fixed
//= some of the items required and made them into real
//= quests. [Reddozen] 
//= 1.3 Fixed bugs and minor typos. Optimized [Lupus]
//= 1.3a fixed an item ID typo, thx 2Spiritual Kid
//= 1.3b Splitted into different files [DracoRPG]
//============================================================
