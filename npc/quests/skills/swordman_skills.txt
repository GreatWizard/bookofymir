//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//===== eAthena Script =======================================
//= Swordsman Skills Quests
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.6
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= [Aegis Conversion]
//= Quests for skills: Fatal Blow, Mobile HP Recovery,
//  Auto-Berserk
//===== Additional Comments: =================================
//= 1.6 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//============================================================

izlude_in,175,130,2	script	Chevalier De Thomas::Knight De Thomas#qsk_sm	98,{
	if (BaseClass == Job_Swordman) {
		if (SWORD_SK < 1 && getskilllv("SM_MOVINGRECOVERY") == 0) {
			mes "[De Thomas]";
			mes "Oh, non! Vous êtes blessé! Est-ce que ça va? Vous avez dû mener un vaillant combat pour avoir de telles blessures! Il est parfois difficile d'être un Épéiste: cela implique de nombreuses responsabilités et sacrifices.";
			next;
			mes "[De Thomas]";
			mes "Mais il existe une compétence extraordinaire pour les Épéistes et les Chevaliers"+ ( sex ? "" : ", jeune damoiselle" ) +", j'ai nommée: la Récupération Physique Active!";
			mes "Grâce à certains exercices physiques, vous pourrez regagner votre force (vos Points de Vie) en vous déplaçant!";
			next;
			mes "[De Thomas]";
			mes "Nous sommes encore en train de la développer, alors vous ne regagnerez pas tant que force que cela, mais cela aide tout de même. Qu'en dites-vous? Voudriez-vous apprendre cette compétence?";
			next;
			if (select("Oui je veux l'apprendre!:Non merci.") == 1) {
				mes "[De Thomas]";
				mes "Très bien. Mais pour apprendre cette compétence, il faut que votre Niveau de Métier soit au moins de ^00880035^000000. Il vous faudra aussi ^008800200 Bouteilles Vides^000000. Pourquoi? Tout simplement parce qu'avoir utilisé autant de potions me prouvera que vous avez combattu vaillamment.";
				next;
				mes "[De Thomas]";
				mes "Il me faudra aussi l'armure que vous avez utilisé durant vos nombreuses batailles, qui sera une autre preuve de votre expérience – il n'y a rien de tel que l'armure d'un Épéiste pour cela! Et le dernier mais non le moindre... Apportez-moi une paire d'^008800Ailes de Papillon de Nuit^000000.";
				next;
				select("Hein? Vous avez vraiment besoin de ça aussi?");
				mes "[De Thomas]";
				mes "Non. Enfin... Je n'en ai pas vraiment BESOIN. C'est juste que ma petite nièce doit chasser les insectes pendant les vacances pour l'école, et je n'ai pas le temps de l'aider avec tout mon travail... ";
				next;
				mes "[De Thomas]";
				mes "Ne trouvez-vous pas ça dommage que je doive rester dans cet endroit chaque jour sans pouvoir en sortir? Allez, trouvez-m'en une... *snif*";
				mes "Ou sinon... Vous n'aurez rien du tout! Muahahaha.";
				set SWORD_SK,1;

				close;
			}
			mes "[De Thomas]";
			mes "...";
			close;
		}
		else if (SWORD_SK == 1 && getskilllv("SM_MOVINGRECOVERY") == 0) {
			mes "[De Thomas]";
			mes "Ah, vous revoilà... Êtes-vous prêt"+ ( sex ? "": "e" ) +" à apprendre à maîtriser la Récupération Physique Active?";
			next;
			switch(select("Oui.:Non, je ne suis pas encore prêt"+ ( sex ? "": "e" ) +".")) {
			case 1:
				if (JobLevel > 34 && countitem(713) > 199 && countitem(1058) > 0) {
					mes "[De Thomas]";
					mes "Voyons.....";
					next;
					mes "[De Thomas]";
					mes "D'accord! Je vais maintenant vous apprendre les exercices physiques permettant de restaurer vos Points de Vie en vous déplaçant.";
					next;
					delitem 713,200; //Empty_Bottle
					delitem 1058,1; //Wing_Of_Moth
					skill "SM_MOVINGRECOVERY",1,0;
					set SWORD_SK,0;
					mes "[De Thomas]";
					mes "Voilà, comme ça! Allez, essayez! Mais n'en faites pas trop, hein, d'accord?";
					next;
					mes "[De Thomas]";
					mes "Oh et finalement je n'aurai pas besoin de votre armure. Vous pouvez la garder. Bonne chance!";
					close;
				}
				else if (JobLevel < 35) {
					mes "[De Thomas]";
					mes "Oh, mais une petite minute! Vous n'avez pas un Niveau de Métier de ^00880035^000000 ou plus! Revenez quand ce sera le cas.";
					close;
				}
				else if (countitem(713) < 200 || countitem(1058) < 1) {
					mes "[De Thomas]";
					mes "Vous n'avez pas tout ce que je vous avais demandé d'apporter.";
					next;
					mes "[De Thomas]";
					mes "Souvenez-vous, j'ai besoin de ^008800200 Bouteilles Vides^000000, de votre armure, et d'une ^008800Aile de Papillon de Nuit^000000. Revenez lorsque vous aurez tout cela.";
					close;
				}
				else {
					mes "[De Thomas]";
					mes "Vous n'avez pas tout ce que je vous avais demandé d'apporter.";
					next;
					mes "[De Thomas]";
					mes "Souvenez-vous, j'ai besoin de ^008800200 Bouteilles Vides^000000, de votre armure, et d'une ^008800Aile de Papillon de Nuit^000000. Revenez lorsque vous aurez tout cela.";
					close;
				}
			case 2:
				mes "[De Thomas]";
				mes "Ah oui? Eh bien revenez quand vous serez prêt"+ ( sex ? "": "e" ) +".";
				close;
			}
		}
		else {
			mes "[De Thomas]";
			mes "Oh, c'est vous?! Cela faisait longtemps depuis la dernière fois. Vous avez l'air d'être en bien meilleure forme qu'auparavant! Hahahaha! Prenez bien soin de vous et à bientôt.";
			close;
		}
	}
	else {
		mes "[De Thomas]";
		mes "Je m'appelle De Thomas Carlos, Chevalier du 3ème Régiment de Cavalerie de Prontera. Je suis actuellement en mission. *Ahem* Dois-je en dire plus?";
		close;
	}
}

prt_in,75,88,5	script	Leon Von Frich#qsk_sm	85,3,3,{
	if ((BaseJob == Job_Swordman && JobLevel >= 25) || BaseJob == Job_Knight || BaseJob == Job_Crusader) {
		if (getskilllv("SM_FATALBLOW") == 1) {
			mes "[Leon]";
			mes "Eh? Je me demandais qui venait. Mais c'est vous! Comment allez vous? Faites bien attention! Hahaha!";
			close;
		}
		else if (countitem(1752) > 9 && countitem(1751) > 9 && countitem(532) > 0 && countitem(962) > 29 && countitem(526) > 4) {
			mes "[Leon]";
			mes "Oh! Vous, vous êtes plus que prêt"+ ( sex ? "": "e" ) +" pour apprendre à maîtriser les Coups Fatals. Qu'en dîtes-vous? Est-ce que ça vous tente?";
			next;
			switch(select("Oui!:Non, sans façons.:Mais qu'est-ce qu'un Coup Fatal?")) {
			case 1:
				mes "[Leon]";
				mes "Bien, commençons!";
				next;
				delitem 1752,10; //Fire_Arrow
				delitem 1751,10; //Silver_Arrow
				delitem 532,1; //Banana_Juice
				delitem 962,30; //Tentacle
				delitem 526,5; //Royal_Jelly
				skill "SM_FATALBLOW",1,0;
				mes "[Leon]";
				mes "Et voilà! Allez exploiter tout le potentiel de votre nouvelle compétence. Hahahahahahahaha!";
				close;
			case 2:
				mes "[Leon]";
				mes "Pfft! Vous le regretterez!";
				close;
			case 3:
				mes "[Leon]";
				mes "J'ai créé cette compétence il y a peu. Elle permet d'étourdir un adversaire en lui infligeant une Taugnée. Vous maîtrisez la Taugnée, n'est-ce pas?";
				next;
				mes "[Leon]";
				mes "Qu'en dites-vous? L'étourdissement est une technique très utile. N'auriez-vous pas envie de maîtriser cette compétence? Si c'est le cas, revenez me voir!";
				close;
			}
		}
		else {
			mes "[Leon]";
			mes "Oh! Un"+ ( sex ? "": "e" ) +" jeune Épéiste!";
			next;
			mes "[Leon]";
			mes "Ouah, d'après l'état de votre bras vous devez adorer la Taugnée, n'est-ce pas?";
			next;
			select("Euh, C'est juste... que...");
			mes "[Leon]";
			if (Sex == 0) {
				mes "Oh, ne soyez pas surpris. Vous devez bien sûr avoir des bras puissants pour manier une épée.";
			}
			else {
				mes "Oh, vous n'avez pas à vous sentir gênée. Même les femmes ont besoin d'avoir des bras puissants pour manier une épée!";
			}
			mes "Je suis content de rencontrer quelqu'un qui se laisse pas aller en ces temps où la jeunesse est si paresseuse.";
			next;
			mes "[Leon]";
			mes "Oui, je voudrais aider un"+ ( sex ? "" : "e" ) +" Épéiste tel"+ ( sex ? "" : "le" ) +" que vous.";
			next;
			switch(select("Comment ça?:Pas la peine.")) {
			case 1:
				mes "[Leon]";
				mes "Oh ce n'est pas grand-chose... Juste une technique pour porter un coup sur les points vitaux.";
				next;
				break;
			case 2:
				mes "[Leon]";
				mes "... Oh ce n'est pas grand-chose... Juste une technique pour porter un coup sur les points vitaux.";
				next;
				break;
			}
			mes "[Leon]";
			mes "Il s'agit d'une compétence développée récemment. Elle permet d'étourdir ses ennemis en utilisant la Taugnée. Vous maîtrisez la Taugnée, n'est-ce pas?";
			next;
			mes "[Leon]";
			mes "Quand j'étais un Épéiste comme vous, j'adorais utiliser la Taugnée, mais à chaque fois je me disais que l'attaque pourrait être encore plus puissante si je pouvais en même temps étourdir mon adversaire.";
			next;
			mes "[Leon]";
			mes "Je me suis donc retiré du champ de bataille pour faire quelques recherches, et j'ai au point cette compétence extraordinaire! Voudriez-vous l'apprendre?";
			next;
			switch(select("Oui.:Non.:Vous savez comment manger les sushis?")) {
			case 1:
				mes "[Leon]";
				mes "D'accord. Je vais vous donner la liste des prérequis. Tout d'abord, il vous faudra maîtriser la Taugnée au niveau 5. Ensuite, il faudra m'apporter 10 Flèches de Feu, 10 Flèches d'Argent, 1 bouteille de Jus de Banane, 30 Tentacules, et 5 Gelées Royales. C'est.. comme qui dirait quelques ingrédients.";
				next;
				mes "[Leon]";
				mes "Revenez me voir lorsque vous aurez tout cela.";
				close;
			case 2:
				mes "[Leon]";
				mes "Hahahahahahahahahaha!";
				mes "... ";
				mes "... ";
				mes "...";
				mes "En fait je ne sais pas quoi dire...";
				close;
			case 3:
				mes "[Leon, le Roi des Sushis]";
				mes "Le meilleur moyen est de le manger avec les mains. Ça c'est la base. Ensuite, il faut le tremper dans la sauce soja, mais pas le riz.";
				next;
				mes "[Leon, le Roi des Sushis]";
				mes "C'est ce qui va enrichir son goût. Oh, et ne mangez que du poisson de saison. Commencez toujours par le poisson à chair blanche, puis celui à chair rose ensuite. Ça ne rendra le goût que meilleur. Hum! J'adore les sushis~";
				close;
			}
		}
		close;
	}
	else {
		mes "[Leon]";
		mes "Oh, non! Je n'ai rien à vous offrir! Je ne peux rien pour quelqu'un qui n'est pas Épéiste! On se reverra dans l'autre monde!";
		close;
	}

OnTouch:
	mes "[Leon]";
	mes "Hahahahahahaha!";
	mes "Hahahahahahaha!";
	close;
}

prt_in,94,57,3	script	Juan#qsk_sm	85,{
	if (BaseClass == Job_Swordman) {
		if (getskilllv("SM_AUTOBERSERK") == 1) {
			mes "[Juan]";
			mes "Hum? Ah, ça faisait longtemps! Comment allez-vous? On dirait que vous êtes devenu encore plus fort que la dernière fois. Beaucoup attendent de grandes choses de vous. Vous y arriverez!";
			close;
		}
		else if (BaseJob == Job_Swordman && JobLevel < 10) {
			mes "[?]";
			mes "Qui vous êtes, vous? Ah, vous êtes "+ ( sex ? "un petit débutant" : "une petite débutante" ) +". Je suis occupé, alors entraînez-vous un peu avant de revenir me voir.";
			close;
		}
		else if (BaseJob == Job_Swordman && JobLevel > 10 && JobLevel < 30) {
			mes "[Juan]";
			mes "Oh, ravi de vous rencontrer mais... Vous pouvez passer votre chemin. :)";
			close;
		}
		else if (countitem(924) > 34 && countitem(958) > 9 && countitem(957) > 9 && countitem(518) > 9) {
			if ((BaseJob == Job_Swordman && JobLevel >= 30) || BaseJob == Job_Knight || BaseJob == Job_Crusader) {
				mes "[Juan]";
				mes "Oh, un"+ ( sex ? "" : "e" ) +" jeune Épéiste! Êtes-vous prêt"+ ( sex ? "" : "e" ) +" à apprendre la dernière compétence que nous avons développée, le 'Dernier Recours'?";
				next;
				switch(select("Oh, je voudrais l'apprendre maintenant.:Qu'est-ce que c'est?")) {
				case 1:
					mes "[Juan]";
					mes "Bien, d'accord...";
					next;
					delitem 924,35; //Powder_Of_Butterfly
					delitem 958,10; //Horrendous_Mouth
					delitem 957,10; //Decayed_Nail
					delitem 518,10; //Honey
					skill "SM_AUTOBERSERK",1,0;
					mes "[Juan]";
					mes "Et voilà, vous pouvez maintenant utiliser le Dernier Recours! Vous êtes maintenant capable d'accomplir de grandes choses.";
					next;
					mes "[Juan]";
					mes "Bonne chance!";
					next;
					mes "[Juan]";
					mes "....................Oh, j'allais oublier.";
					mes "Il y a deux choses dont vous devrez vous souvenir.";
					next;
					mes "[Juan]";
					mes "Une fois que vous aurez récupéré vos Points de Vie, l'effet de cette compétence disparaîtra. De plus, même si elle n'a pas de limite au niveau de sa temps, son effet s'estompera aussi si votre Énergie tombe à zéro.";
					next;
					mes "[Juan]";
					mes "Si vous ne gardez pas ça à l'esprit, vous pourriez avoir des problèmes sur le champ de bataille lorsque l'effet de cette compétence disparaîttra soudainement.";
					next;
					mes "[Juan]";
					mes "Bien, alors cette fois c'est un vrai au revoir~";
					close;
				case 2:
					mes "[Juan]";
					mes "Le Dernier Recours? C'est une compétence cruciale sur le champ de bataille. Lorsque vous êtes grièvement blessé, elle permet à vos pouvoirs cachés de resurgir et de vous aider pendant le combat.";
					next;
					mes "[Juan]";
					mes "Elle est parfaite pour ceux qui combattent avec rage sur le champ de bataille. Vous avez le niveau pour l'apprendre. Il faudra simplement me rapporter certains objets.";
					next;
					mes "[Juan]";
					mes "Il vous faudra 35 tas de Poudre de Papillon, dont l'énergie vous aidera à rassembler vos forces, 10 Rictus Épouvantables, 10 Ongles Putrides, et enfin 10 pots de Miel.";
					next;
					mes "[Juan]";
					mes "Est-ce que vous avez tout noté? Bien, revenez quand vous serez prêt"+ ( sex ? "e" : "" ) +". Je vous attendrai.";
					close;
				}
			}
		}
		mes "[Juan]";
		mes "Oh vous avez plus de cicatrices que la dernière fois que je vous ai vu. On dirait que vous vous poussez vous-même à bout.";
		next;
		mes "[Juan]";
		mes "Quelle que soit votre force, vous ne pourrez pas faire grand-chose si vous atteignez vos limites, alors ne vous surestimez pas. Cependant, vous pourriez utiliser une compétence que nous avons développée pour repousser ces limites.";
		next;
		switch(select("De quoi est-ce que vous parlez?:Une telle compétence ne peut pas exister!:Keuuuuuuuh!")) {
		case 1:
			mes "[Juan]";
			mes "Cette compétence s'appelle le Recours Ultime. On la considère comme le fleuron du champ de bataille. Lorsque vous êtes grièvement blessé, elle permet à vos pouvoirs cachés de resurgir et de vous aider pendant le combat.";
			next;
			mes "[Juan]";
			mes "Elle est parfaite pour ceux qui combattent avec rage sur le champ de bataille. Vous avez le niveau pour l'apprendre. Il faudra simplement me rapporter certains objets.";
			next;
			mes "[Juan]";
			mes "Il vous faudra 35 tas de Poudre de Papillon, dont l'énergie vous aidera à rassembler vos forces, 10 Rictus Épouvantables, 10 Ongles Putrides, et enfin 10 pots de Miel.";
			next;
			mes "[Juan]";
			mes "Est-ce que vous avez tout noté? Bien, revenez quand vous serez prêt"+ ( sex ? "e" : "" ) +". Je vous attendrai.";
			close;
		case 2:
			mes "[Juan]";
			mes "C'est ce que vous croyez, hein? Si c'est le cas, vous feriez mieux de partir sans attendre.";
			close;
		case 3:
			mes "[Juan]";
			mes "Keuuuuuuuuuuuuuh!";
			mes "Ooowwwwwuuuuuuuuuuuuuhhh!";
			mes "Keuaaaaaaaaaaah!";
			close;
		}
	}
	else {
		mes "[Juan]";
		mes "Est-ce que vous aimez voyager? J'espère que de beaux jours vous attendent. Ah oui, je suis le preux chevalier Juan. Ne faites pas attention à moi. Hahaha...";
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= Fully working
//= 1.0a Now using functions found in "Global_Functions.txt"
//= for class checks.
//= 1.1 Added missing delitem [Lupus]
//= 1.2 Added Baby Class Support [Lupus]
//= 1.3 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 1.4 Updated the NPC to allow subclasses of swordsman to learn the skills, [MasterOfMuppets]
//=	1.5 Fixed exploits [Lupus]
//= 1.5a Fixed some typos [IVBela]
//============================================================
