//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par NoH
//===== eAthena Script =======================================
//= Merchant Skills Quests
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= [Aegis Conversion]
//= Quests for skills: Crazy Uproar, Change Cart, Cart
//  Revolution
//===== Additional Comments: =================================
//= 1.5 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//============================================================

alberta,83,96,5	script	Necko#qsk_mc	98,7,7,{
	if (BaseClass == Job_Merchant) {
		if (getskilllv("MC_LOUD") == 1) {
			mes "[Necko]";
			mes "Je suis désolée. Le magasin de Necko est... Oh? Qui va là? Pukakakakakakakakakaka!!!";
			mes "Ça faisait un bail! Comment est-ce que vous allez? Ça fait plaisir de vous revoir! Alors... quoi de neuf? Wouh~ Je suis si contente de vous revoir! J'avais presque oublié à quoi vous ressembliez! Pukakakakakakakakaka!!!";
			mes "Vraiment, ça fait très plaisir de vous revoir! Je suis contente de vous voir comme ça! Vous devriez venir à Alberta plu souvent! Ça ferait d'une pierre deux coups! Je vous verrais, et je pourrais aussi pratiquer mes cris de guerre! Jusqu'à quel point est-ce que je peux les améliorer...?";
			mes "Keuhahahahahahahaha!!";
			mes "Pukakakakakakakakakaka!!!";
			mes "Ppyakikakikakakakakakakaka!!";
			close;
		}
		else if (BaseJob == Job_Merchant && JobLevel < 15) {
			mes "[Necko]";
			mes "Oh, vous êtes là parce que vous vous êtes entiché"+ ( sex ? "" : "e" ) +" de ma voix? J'ai une voix très forte, n'est-ce pas? Vous pouvez m'entendre de là où vous êtes, et puis...";
			mes "*Regarde autour*";
			next;
			mes "[Necko]";
			mes "Ce n'est pas qu'une question de puissance! Je vous donnerai mon petit secret quand vous serez niveau 15. Revenez me voir quand ce sera le cas!";
			next;
			mes "[Necko]";
			mes "Uahahahahahahahahahahahaha!";
			mes "Kyukwakakakakakakakakakaka!";
			close;
		}
		else if (countitem(722) > 6 && countitem(532) > 0 && countitem(921) > 49) {
			mes "[Necko]";
			mes "Oh! Vous! On dirait maintenant que je vais pouvoir vous montrer comment pousser des cris de combat. Est-ce que vous voudriez que je vous enseigne comment pousser un Cri de Guerre?";
			next;
			switch(select("Oui!!!:Non.:De quoi s'agit-il?")) {
			case 1:
				mes "[Necko]";
				mes "Ahahahahaha!! Bien!!!";
				mes "C'est ça l'esprit!";
				mes "On y va!";
				next;
				delitem 722,7; //Scarlet_Jewel
				delitem 532,1; //Banana_Juice
				delitem 921,50; //Mushroom_Spore
				skill "MC_LOUD",1,0;
				mes "[Necko]";
				mes "Voilà, vous savez maintenant comment pousser des Cris de Guerre. Ne vous retenez pas, et criez autant que vous voulez! Hahahahahahaha!";
				close;
			case 2:
				mes "[Necko]";
				mes "Ehh... Ok.";
				mes "Pooooh...";
				close;
			case 3:
				mes "[Necko]";
				mes "Vous pouvez renforcer votre esprit guerrier grâce aux Cris de Guerre. Ça vous en coûtera 8 Points d'Énergie, mais ça augmentera votre Force de 4 points pour 5 minutes!";
				next;
				mes "[Necko]";
				mes "Vous pouvez mener votre vie de tous les jours comme n'importe qui. Cette compétence vous permettra de faire appel à votre esprit guerrier quand vous en aurez besoin. C'est très utile, je dois dire!";
				close;
			}
		}
		mes "[Necko]";
		mes "Oh, vous êtes là parce que vous vous êtes entiché"+ ( sex ? "" : "e" ) +" de ma voix? J'ai une voix très forte, n'est-ce pas? Vous pouvez m'entendre de là où vous êtes, et puis...";
		mes "*Regarde autour*";
		next;
		mes "[Necko]";
		mes "Mais ma voix n'est pas juste puissante. J'ai appris une compétence qui me permet de me motiver grâce à mes cris, les Cris de Guerre!";
		next;
		mes "[Necko]";
		mes "Les Cris de Guerre reposent sur une voix puissante afin de stimuler votre esprit guerrier. Il en coûte 8 Points d'Énergie pour augmenter la Force de 4 points pendant 5 minutes!";
		next;
		mes "[Necko]";
		mes "Vous pouvez mener votre vie de tous les jours comme n'importe qui. Cette compétence vous permettra de faire appel à votre esprit guerrier quand vous en aurez besoin. Mais il vous faudra d'abord apprendre les bases si vous voulez maîtriser cette technique!";
		next;
		mes "[Necko]";
		mes "Avant il y avait un chanteur à Payon qui pouvait vous apprendre à affermir votre voix..";
		next;
		mes "[Necko]";
		mes "Mais malheureusement il est mort il y a peu, alors ce n'est plus possible!";
		mes "Hahahahahahahaha!!!!";
		mes "..............................";
		mes "...Aah vous avez l'air déçu"+( sex ? "" : "e" )+"! Vous vouliez apprendre à pousser des Cris de Guerre?";
		next;
		mes "[Necko]";
		mes "Hum... Est-ce que je devrais vous l'apprendre? Si vous y tenez vraiment, je veux dire... Bon, tout d'abord, il va falloir échauffer vos cordes vocales. Il me faudra quelques petites choses pour cela.";
		next;
		mes "[Necko]";
		mes "7 Perles, 1 bouteille de Jus de Banane, et 50 Spores de Champignon. Avec tout cela on va pouvoir commencer à échauffer ces cordes vocales!";
		next;
		mes "[Necko]";
		mes "Revenez me voir quand vous serez prêt"+ ( sex ? "" : "e" ) +", je vais m'échauffer la gorge en attendant!";
		mes "Bboowuuuuuuuuuuuuuuuuh~~~";
		close;
	}
	else {
		mes "[Necko]";
		mes "Le magasin de Necko est fermé en ce moment. Revenez plus tard. Héhé~";
		close;
	}

OnTouch:
	mes "[!?]";
	mes "Muahahahahahahahahahahahaha!!";
	mes "Pukakakakakakakakakakakakaka!";
	close;

}

alberta,119,221,6	script	Charlron#qsk_mc	107,{
	if (BaseClass == Job_Merchant) {
		if (getskilllv("MC_CHANGECART") == 1) {
			mes "[Charlron]";
			mes "Hum? Je me demandais qui faisait tout ce boucan avec sa charrette! Alors, c'était vous. Oui, je me souviens de vous... Qu'en dites-vous? Cette nouvelle charrette... Ça va?";
			next;
			mes "[Charlron]";
			mes "Profitez de la vie. La vie, ce n'est pas juste d'avancer, c'est aussi prendre son temps, et s'arrêter en chemin pour profiter des choses. C'est ce que les Marchands apprennent en tirant leur lourde charrette.";
			next;
			mes "[Charlron]";
			mes "A la prochaine fois. Portez-vous bien~";
			close;
		}
		else if ((BaseJob == Job_Merchant && JobLevel >= 30) || BaseJob == Job_Blacksmith || BaseJob == Job_Alchemist) {
			mes "[Charlron]";
			mes "Ça fait plaisir de voir un autre Marchand. Je crois bien que vous êtes connu"+ ( sex ? "" : "e" ) +", non?";
			next;
			mes "[Charlron]";
			mes "Hum... On dirait que votre charrette n'est pas à la mesure de votre renommée. Quel dommage...";
			next;
			switch(select("Je suis là pour çà.:Quel est le problème avec ma charrette?:Deux paires, ça ne va pas?")) {
			case 1:
				if (countitem(1019) > 49 && countitem(998) > 9 && countitem(919) > 19) {
					mes "[Charlron]";
					mes "Haha! Ok, ok! Avec ce que vous m'apportez, je vais pouvoir changer votre charrette.";
					next;
					delitem 1019,50; //Wooden_Block
					delitem 998,10; //Iron
					delitem 919,20; //Animal's_Skin
					skill "MC_CHANGECART",1,0;
					mes "[Charlron]";
					mes "Félicitations. Vous pouvez maintenant choisir une charrette en fonction de vos goûts. Vous n'avez même plus besoin de mon aide maintenant. Tout ce qu'il vous fait, c'est quelques Points de Mana.";
					next;
					mes "[Charlron]";
					mes "Pratique, n'est-ce pas? Oh, et encore une chose! Vous pourrez utiliser une nouvelle charrette quand vous serez d'un niveau plus élevé, alors travaillez dur!";
					next;
					mes "[Charlron]";
					mes "Cela dit, ça vous va très bien.";
					mes "Hahaha! Passez une bonne "+ ( isday() ? "journée" : "soirée" ) +"!";
					close;
				}
				else {
					mes "[Charlron]";
					mes "Hum... Oui, je peux modifier les charrettes, c'est vrai, mais... Il me faut quelques petites choses pour ce faire.";
					next;
					mes "[Charlron]";
					mes "50 Troncs.";
					mes "10 morceaux de Fer.";
					mes "20 Peaux de Bête.";
					mes "Voilà ce qu'il me faut pour modifier votre charrette.";
					next;
					mes "[Charlron]";
					mes "Revenez me voir lorsque vous aurez tout cela.";
					close;
				}
			case 2:
				mes "[Charlron]";
				mes "Oh, vous n'êtes pas au courant? (change de ton)";
				mes "Il y a une nouvelle façon de montrer votre ego de Marchand! C'est une nouvelle compétence, Décoration, qui permet aux Marchands de sortir du lot!";
				next;
				mes "[Charlron]";
				mes "Cette technique permet de changer l'aspect de sa charrette en fonction de votre niveau, et pour seulement 40 Points d'Énergie!";
				next;
				mes "[Charlron]";
				mes "C'est une compétence de base pour tous les Marchands. Je vais vous montrer comment redécorer une charrette. Faites bien attention, et faites comme moi à l'avenir!";
				next;
				mes "[Charlron]";
				mes "Mais pour ça, il me faudra quelques matières premières.";
				mes "50 Troncs.";
				mes "10 morceaux de Fer.";
				mes "20 Peaux de Bête.";
				mes "Apportez-moi tout ça s'il vous plaît!";
				next;
				mes "[Charlron]";
				mes "Avec tout cela, on pourra modifier votre charrette à volonté, alors revenez quand vous aurez tout, d'accord?";
				close;
			case 3:
				mes "[Charlron]";
				mes "... On n'est pas en train de jouer aux 'CARTES'!";
				close;
			}
		}
		else {
			mes "[Charlron]";
			mes ( isday() ? "Bonjour" : "Bonsoir" ) + ", "+ ( sex ? "jeunot" : "jeunette" ) +". Vous aimez le commerce? Je suis la Marchand Charlron. J'ai pas mal travaillé sur les charrettes récemment.";
			next;
			mes "[Charlron]";
			mes "N'en avez-vous pas marre de cette charrette? Revenez me voir si jamais vous voulez que je la modifie. Je ferai du bon travail. Mais il y a quelques conditions.";
			next;
			mes "[Charlron]";
			mes "Je suis un Marchand assez connu, alors je ne travaille pas avec les débutant"+( sex ? "" : "e" )+"s. On pourra peut-être en reparler lorsque vous aurez atteint le niveau 30.";
			close;
		}
	}
	else {
		mes "[Charlron]";
		mes "Je fais le commerce de pas mal de choses. Je m'appelle Charlron. Si jamais vous trouvez quelque chose d'intéressant, revenez me voir, on discutera du prix.";
		close;
	}
}

alberta,232,106,6	script	Gershaun#qsk_mc	57,{
	if (BaseClass == Job_Merchant) {
		if (getskilllv("MC_CARTREVOLUTION") == 1) {
			mes "[Gershaun]";
			mes "Ah, ça faisait longtemps! Oh, ces marques rouges sur votre charrette... Ça doit être les marques de 'ce' coup. On dirait que vous le maîtrisez maintenant!";
			next;
			mes "[Gershaun]";
			mes "On dirait qu'on est fait du même bois! Hum... Je suis content!";
			mes "*Se gratte le dos*";
			mes "Là, voilà!";
			close;
		}
		else if ((BaseJob == Job_Merchant && JobLevel >= 35) || BaseJob == Job_Blacksmith || BaseJob == Job_Alchemist) {
			mes "[Gershaun]";
			mes "Oh. Vous avez un corps bien ferme pour "+ ( sex ? "un Marchand" : "une Marchande" ) +". Vous devez être doué"+ ( sex ? "" : "e" ) +" pour ce qui est de transporter des choses?! Hum... Vous devriez pouvoir utiliser des Coups de Charrette.";
			next;
			switch(select("Hé~ C'est pour ça que je suis là.:Qu'est-ce que c'est?")) {
			case 1:
				switch(rand(1,3)) {
				case 1: setarray .@Quantity[0],2,20,30,20,5; break;
				case 2: setarray .@Quantity[0],2,15,25,15,5; break;
				case 3: setarray .@Quantity[0],2,23,32,23,6; break;
				}
				if (countitem(533) >= .@Quantity[0] && countitem(998) >= .@Quantity[1] && countitem(938) >= .@Quantity[2] && countitem(601) >= .@Quantity[3] && countitem(962) >= .@Quantity[4]) {
					mes "[Gershaun]";
					mes "Hoho! Compris! Je vais vous enseigner le secret des Coups de Charrette!";
					next;
					mes "[Gershaun]";
					mes "Ah, c'est un si beau jour! Je devrais récompenser ceux qui manient ainsi leur charrette!";
					next;
					delitem 533,.@Quantity[0]; //Grape_Juice
					delitem 998,.@Quantity[1]; //Iron
					delitem 938,.@Quantity[2]; //Sticky_Mucus
					delitem 601,.@Quantity[3]; //Wing_Of_Fly
					delitem 962,.@Quantity[4]; //Tentacle
					skill "MC_CARTREVOLUTION",1,0;
					mes "[Gershaun]";
					mes "Vous pouvez maintenant envoyer des Coups de Charrette! Je compte sur vous pour rendre les Marchands célèbres pour cet incroyable pouvoir de destruction!";
					next;
					mes "[Gershaun]";
					mes "Passez une très bonne "+ ( isday() ? "journée" : "soirée" ) +"!";
					close;
				}
				else if (countitem(532) > 0) {
					mes "[Gershaun]";
					mes "Oh! Du jus de banane! Merci beaucoup!";
					mes "...............Kaah!";
					delitem 532,1; //Banana_Juice
					mes "Très rafraîchissant! Merci! Au revoir!";
					next;
					mes "[Gershaun]";
					mes "Hein? Vous êtes encore là?! Ah, vous voulez toujours apprendre à utiliser les Coups de Charrette! Bien, je vais vous montrer.";
					next;
					mes "[Gershaun]";
					mes "Il n' y a rien de difficile à ça, mais faut quand même que je fasse quelques préparatifs.";
					next;
					// Do not "Correct" this dialog. It IS correct already.
					// You're not suposed to know the exact amount to bring.
					mes "[Gershaun]";
					mes "Tout d'abord, il me faudrait une vingtaine morceaux de Fer pour renforcer la charrette. Ensuite, j'aurai besoin d'une trentaine de morceaux de Morve pour absorber les chocs, et d'environ 20 Ailes de Mouche, et 5 Tentacules. Oh, et aussi 2 bouteilles de Jus de Raisin pour moi!";
					next;
					mes "[Gershaun]";
					mes "Enfin... Ramenez-moi ce que vous pouvez. Je n'aurai pas besoin d'exactement 20 de ceci ou 30 de ça, c'est juste un ordre d'idée. On verra ça quand vous aurez tout! Je vous attendrai. Au revoir.~";
					close;
				}
				else {
					mes "[Gershaun]";
					mes "Hum! Je ne peux rien faire sans une boisson fraîche! Qu'est-ce que vous diriez de me rapporter une bouteille de Jus de Banane si vous voulez discuter?";
					close;
				}
			case 2:
				mes "[Gershaun]";
				mes "... On dirait que vous n'avez jamais entendu parler des Coups de Charrette. Bien, je vais vous raconter quelque chose que je n'ai encore jamais dit à personne. Écoutez bien...";
				next;
				mes "[Gershaun]";
				mes "J'ai travaillé sur un moyen d'utiliser autrement les charrettes. J'ai essayé de les utiliser pour faire cuire du ramen, j'ai même essayé d'en faire des balançoires, mais rien de tout ça ne m'a convaincu."; // corde à sauter dans le texte, pas balançoire - enfin... ça reste un passage cucul...
				next;
				mes "[Gershaun]";
				mes "Je désespérais. Je me promenais chaque jour à la campagne avec ma Charrette. Mais un jour, un terrifiant monstre m'a sauté dessus. Il en allait de ma vie! Pourquoi est-ce que ça m'arrivait à moi, un Marchant sans arme ni armure?!";
				next;
				mes "[Gershaun]";
				mes "J'étais mort de peur. Je réfléchissais et j'espérais que ce n'était pas ma dernière fois qu'on entendait parler de moi. Sans grand espoir, j'ai tenté la dernière chose que j'aurais pu faire: frapper le monstre à l'aide de ma charrette!";
				next;
				mes "[Gershaun]";
				mes "Le monstre est mort du premier coup. C'est à ce moment-là que j'ai réalisé que les charrettes pouvaient elles-aussi devenir des armes à l'occasion, et que l'on pouvait s'en servir pour attaquer!";
				next;
				mes "[Gershaun]";
				mes "Le Coup de Charrette! C'est la nouvelle technique d'attaque que j'ai développée. Avec 12 Points d'Énergie, vous pouvez soulever votre charrette et l'écraser en plein sur le crâne de votre adversaire!";
				next;
				mes "[Gershaun]";
				mes "Plus votre charrette est lourde, et plus dur vous frappez. En un coup, vous pourrez blesser votre adversaire et le forcer à s'enfuir! C'est ça, le Coup de Charrette.";
				close;
			}
		}
		else {
			mes "[Gershaun]";
			mes "Hum, "+ ( sex ? "un jeune Marchand" : "une jeune Marchande" ) +". Vous utiliser votre Charrette pour transporter vos marchandises, mais est-ce bien tout ce que vous pouvez faire avec?";
			next;
			mes "[Gershaun]";
			mes "J'ai travaillé sur un moyen d'utiliser autrement les charrettes. J'ai essayé de les utiliser pour faire cuire du ramen, j'ai même essayé d'en faire des balançoires, mais rien de tout ça ne m'a convaincu.";
			next;
			if (select("Je sais, je sais.:...?") == 2) {
				mes "[Gershaun]";
				mes "Je désespérais. Je me promenais chaque jour à la campagne avec ma Charrette. Mais un jour, un terrifiant monstre m'a sauté dessus. Il en allait de ma vie! Pourquoi est-ce que ça m'arrivait à moi, un Marchant sans arme ni armure?!";
				next;
				mes "[Gershaun]";
				mes "J'étais mort de peur. Je réfléchissais et j'espérais que ce n'était pas ma dernière fois qu'on entendait parler de moi. Sans grand espoir, j'ai tenté la dernière chose que j'aurais pu faire: frapper le monstre à l'aide de ma charrette!";
				next;
				mes "[Gershaun]";
				mes "Le monstre est mort du premier coup. C'est à ce moment-là que j'ai réalisé que les charrettes pouvaient elles-aussi devenir des armes à l'occasion, et que l'on pouvait s'en servir pour attaquer!";
				next;
				mes "[Gershaun]";
				mes "Le Coup de Charrette! C'est la nouvelle technique d'attaque que j'ai développée. Avec 12 Points d'Énergie, vous pouvez soulever votre charrette et l'écraser en plein sur le crâne de votre adversaire!";
				next;
				mes "[Gershaun]";
				mes "Plus votre charrette est lourde, et plus dur vous frappez. En un coup, vous pourrez blesser votre adversaire et le forcer à s'enfuir! C'est ça, le Coup de Charrette.";
				next;
				mes "[Gershaun]";
				mes "Voudriez-vous apprendre cette technique?";
				next;
				switch(select("Oui!!:Je veux apprendre à couper des sushis!")) {
				case 1:
					mes "[Gershaun]";
					mes "D'accord! Je vais vous apprendre ce coup. Mais avant cela, il faut que vous vous entraîniez un peu tout"+ ( sex ? "" : "e" ) +" seul"+ ( sex ? "" : "e" ) +". Revenez quand vous serez niveau 35. On en reparlera ensuite.";
					close;
				case 2:
					mes "[Gershaun, le Roi des Sushis]";
					mes ( sex ? "Petit idiot" : "Petite idiote" ) + "! Tu n'es pas près d'avoir la trempe pour manipuler les sushis!";
					close;
				}
			}
			mes "[Gershaun]";
			mes "... Non, ce n'est rien. Ne faites pas attention.";
			close;
		}

	}
	else {
		mes "[Gershaun]";
		mes "Si ça ne marche pas, il faut le faire marcher. Si ça ne marche pas, il faut le faire marcher. Si ça ne marche pas, il faut le faire marcher.";
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= Fully working
//= v1.1 Bug fixes
//= v1.a Now using functions found in "Global_Functions.txt"
//= for class checks.[kobra_k88]
//= 1.2 Added Baby Class Support [Lupus]
//= 1.3 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 1.4 Fixed exploits [Lupus]
//= 1.4a Fixed some typos [IVBela] 1.4b  Gershaun -> Gershuan
//= 	changed perm. variables to temp ones [Lupus]
//= 1.4b Added missing 1 Banana Juice requirement to Cart Revolution [DracoRPG]
//============================================================
