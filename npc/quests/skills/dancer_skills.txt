//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//===== eAthena Script ======================================= 
//= Dancer Skills Quests
//===== By: ================================================== 
//= Lupus, Reddozen
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Quests for skills: Charming Wink
//===== Additional Comments: ================================= 
//= 1.4 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//= 1.4a Changed NPC location [Yommy]
//= 1.5 Replaced effect numerics with constants. [L0ne_W0lf]
//============================================================

comodo,204,172,5	script	Cannelle::Canell#qsk_dan01	724,{
	if (BaseJob == Job_Dancer && JobLevel > 39) {
		if (DANCER_SK == 0) {
			mes "[Cannelle]";
			mes "Tout le monde sait que nous nous devons d'être belles pour captiver notre audience. Mais à moins de vouloir gâcher nos talents, nous ne devons pas nous reposer uniquement sur cela.";
			next;
			mes "[Cannelle]";
			mes "Oui, je vois bien que vous ne comprenez pas encore tout à fait que le charme est un élément essentiel, et qu'il ne s'agit pas beauté pure et simple. Mais dites-moi... Êtes-vous à l'aise sur la piste de danse?";
			next;
			if (select("Bien sûr, vielle bique!:Je... Je ne sais pas...") == 1) {
				mes "[Cannelle]";
				mes "Ho-ho~ Je crois que vous confondez confiance et arrogance! Seules les naïves ne comprennent pas qu'il y a une distinction fondamentale entre les deux! Et maintenant, préparez-vous pour votre punition!";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes "Aïe! Le... Le fouet! Ça... Ça fait maaaaaaal!";
				specialeffect2 EF_HIT2;
				percentheal -5,0;
				close;
			}
			mes "[Cannelle]";
			mes "Être consciente de ses faiblesses et défauts est primordial si vous voulez vous perfectionner. Cependant... La façon dont vous gâchez votre don naturel pour la danse... Je vais devoir vous punir!";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "Qu'est-ce que...? Aïe! Arrêtez de me... de me fouetter! Ça piiiiiique!";
			specialeffect2 EF_HIT2;
			percentheal -10,0;
			next;
			mes "[Cannelle]";
			mes "Ho-ho!~ Heureusement, je peux vous montrer comment dépasser vos limites actuelles. Qu'en j'en aurai fini avec vous, ni ange ni démon ne pourra vous résister!~";
			next;
			if (select("Vous ne m'enseignerez rien du tout!:Voyons ce que vous pouvez m'apprendre.") == 1) {
				mes "[Cannelle]";
				mes "Alors... Vous ne maîtrisez pas encore l'art subtil de mélanger charme et humilité! Pour cet affront évident envers votre profession, il y aura des conséquences! Préparez-vous!";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes "Nooooon--!";
				mes "Pas encore le fouet! Ça fait si mal!";
				specialeffect2 EF_HIT2;
				percentheal -10,0;
				close;
			}
			mes "[Cannelle]";
			mes "Ho-ho~ C'est une bonne chose que vous reconnaissiez que vous avez beaucoup à apprendre. Bien, ne perdons pas une minute. La première leçon portera sur votre image et sur l'image que vous vous faites de vous. Mais pour ça...";
			next;
			mes "[Cannelle]";
			mes "...Vous devrez me rapporter ^FF00001 Miroir de Cristal^000000! Il m'en faut impérativement un pour la leçon, alors rapportez-m'en un au plus vite!";
			set DANCER_SK,1;
			close;
		}
		else if (DANCER_SK == 1) {
			if (countitem(747) > 0) {
				mes "[Cannelle]";
				mes "Ah, vous revoilà enfin avec le miroir! Maintenant, regardez attentivement votre reflet. Examinez tous les petits défauts, et tous les traits gracieux de votre visage.";
				next;
				mes "[Cannelle]";
				mes "Aucun visage n'est jamais parfait, mais c'est le vôtre, et ce premier atout en matière de beauté ne peut appartenir à personne d'autre.";
				mes "Répétez maintenant ces truismes après-moi...";
				next;
				mes "[Cannelle]";
				mes "\"Je suis belle...";
				mes "Je suis irrésistible...\"";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes "\"Je suis belle...";
				mes "Je suis irrésistible...\"";
				next;
				mes "[Cannelle, exaspérée]";
				mes "Plus fort!";
				mes "Mettez-y plus de coeur!";
				mes "\"Je suis belle...";
				mes "Je suis irrésistible...";
				mes "Je suis la plus belle femme du monde!\"";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes "\"Je suis belle...";
				mes "Je suis irrésistible...";
				mes "Je suis la plus belle femme du monde!\"";
				specialeffect2 EF_FLASHER;
				next;
				mes "^3355FFVotre estime de vous-même vient de remonter en flèche. Heureusement, vous n'en devenez pas pour autant une prima donna.^000000";
				delitem 747,1; //Crystal_Mirror
				set DANCER_SK,2;
				close;
			}
			else {
				mes "[Cannelle, frustrée]";
				mes "Je vous dit de revenir sans avoir de ^FF0000Miroir de Cristal^000000? Allez m'en chercher un et revenez avant que je n'aie trouvé une bonne raison de vous punir!";
				specialeffect EF_CLAYMORE;
				close;
			}
		}
		else if (DANCER_SK == 2) {
			mes "[Cannelle]";
			mes "Le premier pas vers la beauté véritable, et c'est aussi le plus important, est de prendre conscience de vos charmes. D'abord la confiance de soi, ensuite la beauté. Jamais l'inverse.";
			next;
			mes "[Cannelle]";
			mes "Maintenant que vous avez le bon état d'esprit, il est temps pour vous d'apprendre une technique de séduction avancée. Je connais une experte que je pourrai vous recommander.";
			next;
			mes "[Cannelle]";
			mes "Allez parler à ma soeur, à l'Église de Prontera. Elle vous enseignera ce que vous avez besoin d'apprendre si vous mentionnez mon nom. Je m'en chargerais bien moi-même, mais je veux profiter de mon nouveau Miroir de Cristal~";
			next;
			mes "^3355FFOn dirait qu'elle a l'intention de garder le miroir...^000000";
			Emotion e_ag,1;
			set DANCER_SK,3;
			close;
		}
		else if (DANCER_SK > 2 && DANCER_SK < 7) {
			mes "[Cannelle]";
			mes "Ah, quel beau visage! Quelle belle tenue! Ma beauté est incompara--";
			mes "Oh! C'est vous. Alors, est-ce que ma soeur à l'église de Prontera vous a appris quelque chose?";
			Emotion e_go;
			close;
		}
		else if (DANCER_SK == 7) {
			mes "[Cannelle]";
			mes "Ah, alors vous avez trouvé Aelle? Elle a pu vous apprendre quelque chose?";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "En fait, elle m'a juste appris à faire des clins d'oeil... Enfin... Elle m'en a parlé. Elle avait un coup dans le nez je crois et... Ce n'était pas très clair.";
			next;
			mes "[Cannelle]";
			mes "Oh, j'aurais dû me douter que ça aller arriver. Eh bien, je pense qu'on ne peut plus rien y faire. Mais vous devriez savoir qu'enivrer ma soeur est un crime...";
			next;
			mes "[Cannelle]";
			mes "Bam!";
			specialeffect2 EF_HIT2;
			percentheal -5,0;
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "?!";
			mes "C'était quoi ça?";
			next;
			mes "[Cannelle]";
			mes "Une véritable Danseuse devrait savoir d'elle-même quel est le moment opportun pour un clin d'oeil. Mais puisque je suis si gentille, je vais vous l'expliquer. Ho-ho~";
			next;
			mes "^3355FFSi gentille...? Elle vient juste de vous frapper! Pas fort, bien sûr, mais tout de même!^000000";
			set DANCER_SK,8;
			close;
		}
		else if (DANCER_SK == 8) {
			mes "[Cannelle]";
			mes "Bon, le secret derrière les clins d'oeil, c'est de laisser sa paupière bouger à un rythme naturel. Fermez les yeux un instant, et rouvrez-les quand je vous le dirai.";
			next;
			mes "[Cannelle]";
			mes "Un, deux, trois~"; // En français dans le texte
			mes "Regardez-moi... Comme ça. Un, deux, trois~";
			mes "Allez-y, essayez.";
			Emotion e_no1;
			next;
			switch(select("Un deux trois~:Un, deux trois~:Un, deux, trois~")) {// Un, doux, trois dans le texte... No comment.
			default:
				mes "[Cannelle]";
				mes "Non, non!";
				mes "Pas comme ça! Ne me dites pas que vous n'y arrivez pas!";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes "Ooooh! S'il vous plaîîîît... Pas le fouet!";
				specialeffect2 EF_HIT2;
				percentheal -5,0;
				close;
			case 3:
				mes "["+ strcharinfo(0) +"]";
				mes "Un, deux, trois~";
				Emotion e_no1,1;
				specialeffect2 EF_FLASHER;
				next;
				mes "[Cannelle]";
				mes "Oui, c'est exactement comme ça qu'on le fait! N'oubliez pas: le rythme et le naturel sont essentiels pour que ça soit efficace.";
				Emotion e_kis;
				next;
				mes "[Cannelle]";
				mes "Maintenant que vous avez maîtrisé l'art subtil des clins d'oeil, vous devez prendre conscience du fait qu'ils ne marcheront pas sur tout le monde. Certains sont plus forts que vous.";
				next;
				mes "[Cannelle]";
				mes "Bien sûr, mes clins d'oeil sont sans défauts, mais vous n'en êtes pas encore là. Quoi qu'il en soit, il est temps de nous séparer. Ho-ho~";
				mes "J'espère que vous deviendrez encore plus glamour dans les jours à venir!";
				Emotion e_heh;
				next;
				mes "^3355FFVous avez enfin appris à maîtriser les Clins d'Oeil Charmeurs.^000000";
				set DANCER_SK,9;
				specialeffect2 EF_ABSORBSPIRITS;
				skill "DC_WINKCHARM",1,0;
				close;
			}
		}
		else if (DANCER_SK > 8 && getskilllv("DC_WINKCHARM")) {
			mes "[Cannelle]";
			mes "Hum...?";
			mes "Vous vouliez me demander quelque chose d'autre? Ah, vous devez avoir été hypnotisée par ma beauté, c'est ça?";
			Emotion e_pif;
			next;
			mes "[Cannelle]";
			mes "Oh, être aussi belle doit être un pêché! Je devrais travailler à l'église de Prontera, comme ma soeur, et prier pour ma repentance~";
			close;
		}

		else if (DANCER_SK == 9 && getskilllv("DC_WINKCHARM") == 0) {
			mes "[Cannelle]";
			mes "Hum? Oh, je me souviens de vous! Je vois que vous êtes maintenant une Bohémienne~ Ce look vous va bien. Mais je suis sûre que vous avez oublié quelques petites choses depuis votre renaissance, j'ai tort? Regardez bien ce clin d'oeil...";
			next;
			mes "[Cannelle]";
			mes "Vous vous souvenez maintenant? Tout est dans le naturel, la séduction, et dans le rythme du mouvement de vos paupières. J'espère que vous permettrez à votre beauté intérieur de resplendir grâce à votre Clin d'Oeil Charmeur~";
			next;
			mes "^3355FFVous venez d'apprendre à maîtriser les Clins d'Oeil Charmeurs.^000000";
			specialeffect2 EF_ABSORBSPIRITS;
			skill "DC_WINKCHARM",1,0;
			close;
		}
	}
	else if (BaseJob == Job_Dancer && JobLevel < 40) {
		mes "[Cannelle]";
		mes "Oh, vous êtes si mignonne! Ah, je suis désolée, mais vous devriez revenir me voir quand vous aurez atteint le Niveau de Métier 40 - je pourrai vous apprendre quelques petites trucs de Danseuses~";
		close;
	}

	else if (BaseJob != Job_Dancer) {
		if (Sex == 0) {
			mes "[Cannelle]";
			mes "Oh, quelle jolie "+ callfunc("ster") +"! Regardez-moi ces formes! Si mignon~";
			close;
		}
		else {
			mes "[Cannelle]";
			mes "Salut, beau gosse~";
			mes "^333333*Clin d'oeil~*^000000";
			Emotion e_kis;
			specialeffect2 EF_VALLENTINE2;
			next;
			mes "^3355FFSon clin d'oeil fait battre votre coeur de façon intense.^000000";
			close;
		}
	}
}

prontera,183,333,1	script	Aelle#qsk_dan02	79,{
	if (BaseJob == Job_Dancer) {
		if (DANCER_SK == 3) {
			mes "[Aelle]";
			mes "Pssst, hé! Même si c'est illégal, j'ai quelques points de téléportation vers le Donjon des Orcs, Glast Heim, et la Mine Abandonnée. Ça vous intéresse?";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "En fait, euh... Je cherche quelqu'un. Vous ne connaîtriez pas la soeur de Cannelle par hasard?";
			next;
			mes "[Aelle]";
			mes "Ah, alors ma coincée de soeur vous envoie, c'est ça? J'aurais dû m'en douter à voir vos frusques... Vous voulez apprendre ce qu'est la classe, pas vrai?";
			next;
			mes "[Aelle]";
			mes "Erf... Je suis vraiment déçue. Vous n'êtes pas venue pour mes téléportations, pas vrai? Vous êtes sûre?";
			set DANCER_SK,4;
			close;
		}
		else if (DANCER_SK == 4) {
			mes "[Aelle]";
			mes "Bon, d'accord... Alors vous êtes là pour que je vous apprenne à aguicher le public, c'est ça? Eh bien, ça ne sera pas si simple...";
			next;
			mes "[Aelle]";
			mes "Vous donner tous les détails... Vous expliquer tout cela... Je sens que je vais avoir la gorge sèche. Rapportez-moi un rafraîchissement avant que nous ne commencions...";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "Oh... D'accord. J'imagine que je peux vous donner une potion ou deux, ou même une bouteille de jus de...";
			next;
			mes "[Aelle]";
			mes "Euh... Vous vous moquez de moi ou quoi? Je veux une boisson d'homme, moi! Enfin...";
			next;
			mes "[Aelle]";
			mes "Je veux de l'alcool! Oh, et apportez-moi aussi de quoi grignoter - que j'aie des forces pour vous enseigner tout ça au moins. Oui, apportez-moi des fruits et des biscuits!";
			next;
			mes "[Aelle]";
			mes "Bref, je voudrais...";
			mes "^FF00001 Pomme^000000, ^FF00003 Bananes^000000 et";
			mes "^FF00005 Cookies Cuits à Point^000000. Ah, et présentez-moi tout ça dans de la ^FF0000Porcelaine^000000, d'accord? Et n'oubliez pas l'^FF0000Alcool^000000, c'est compris?";
			next;
			mes "[Aelle]";
			mes "Une dernière chose... N'en parlez pas aux bigots dans l'église, d'accord? Je ne veux pas avoir de problèmes.";
			set DANCER_SK,5;
			close;
		}
		else if (DANCER_SK == 5) {
			if (countitem(970) > 0 && countitem(512) > 0 && countitem(513) > 2 && countitem(538) > 4 && countitem(736) > 0) {
				mes "[Aelle]";
				mes "Bien, vous avez rapporté de quoi grignoter! J'ai si faim que je vais tout finir!";
				next;
				mes "^3355FFAelle dévore la nourriture comme une bête affamée.^000000";
				next;
				mes "[Aelle]";
				mes "C'était si bon! Je n'avais pas mangé autant depuis bien longtemps! Ce n'est pas dans cette satanée église qu'on se régale comme ça!";
				mes "Ah, oui, la leçon!";
				next;
				mes "[Aelle]";
				mes "Bon, le geste le plus subtil et le plus attirant a de tous temps été le ^FF0000clin d'oeil^000000. Ça m'a sauvé par le passé, et sa maîtrise ne pourra que rajouter à vos charmes.";
				next;
				mes "[Aelle]";
				mes "Pour un oeil inexercé, le clin d'oeil ne ressemble qu'à un simple mouvement, pas vrai? Vous baissez votre paupière, puis vous la relevez. Mais si vous le faites bien... Si vous...";
				next;
				mes "[Aelle]";
				mes "Oh, ça fait si longtemps que je n'avais pas autant bu... Erf... Je me sens un peu... Euh... Enfin, fermez juste votre... ^333333*burp*^000000 Bref, faire un clin d'oeil est si simple...~";
				next;
				mes "[Aelle, ivre]";
				mes "Allez-y! Faites comme moi! Faites un clin d'oeil, comme ça, d'accord? Vous voyez?";
				mes "^333333*Clin d'oeil*^000000";
				Emotion e_no1;
				next;
				mes "["+ strcharinfo(0) +"]";
				mes "Ouah...";
				mes "C'était si... C'est si beau! Et tout ça avec un simple clin d'oeil!";
				next;
				mes "[Aelle, complètement ivre]";
				mes "Oui... Le... Le secret c'est de... *Burp* Tout est dans le...";
				mes "Bwahahahaahah!";
				mes "Ça tourne!";
				Emotion e_heh;
				next;
				mes "^3355FFComplètement ivre, Aelle vous tapote la joue, et se met à rire de façon hystérique.^000000";
				percentheal -10,0;
				specialeffect2 EF_HIT2;
				Emotion e_dots,1;
				delitem 970,1; //Alchol
				delitem 512,1; //Apple
				delitem 513,3; //Banana
				delitem 538,5; //Well_Baked_Cookie
				delitem 736,1; //White_Platter
				set DANCER_SK,6;
				close;
			}
			else {
				mes "[Aelle]";
				mes "Comment est-ce que voudriez que je vous apprenne quoi que ce soit si je tourne de l'oeil pendant la leçon? Il me faut de quoi me caler le ventre~";
				next;
				mes "[Aelle]";
				mes "Alors, je voudrais manger...";
				mes "^FF00001 Pomme^000000, ^FF00003 Bananes^000000 et ^FF00005 Cookies Cuits à Point^000000. Ah, et n'oubliez pas de le ramener de la ^FF0000Porcelaine^000000 pour le service. N'oubliez pas non plus l'^FF0000Alcool^000000, d'accord?";
				close;
			}
		}
		else if (DANCER_SK == 6) {
			mes "["+ strcharinfo(0) +"]";
			mes "Vous ne m'avez toujours pas donné le secret pour rajouter tant de charme et de provocation à un simple clin d'oeil. Il faut vraiment que...";
			next;
			mes "[Aelle, complètement ivre]";
			mes "ZzzZzz... Hein?";
			mes "Oui, je sais, je suis un vrai génie...";
			mes "...ZzzzZzzzZzZzzzZZZzz...";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "Non mais réveillez-vous! Je me suis donnée tant de mal pour apprendre cette technique!";
			next;
			mes "[Aelle, énervée]";
			mes "LA FERME! J'AI ENVIE DE DORMIR, D'ACCORD? Allez-vous en! Allez voir ma soeur... Cannelle...";
			mes "...ZzzzzZzZZZzzzZZ....";
			set DANCER_SK,7;
			close2;
			warp "prontera",156,272;
			end;
		}
		else if (DANCER_SK > 6 && DANCER_SK <= 8) { // Bugfix: <= 8 pas <8 (en plus sans ça, ce check serait complètement ridicule. [NoH]
			mes "["+ strcharinfo(0) +"]";
			mes "Aelle, il faut vraiment que vous m'aidiez et que vous m'appreniez à faire des clins d'oeil...";
			next;
			mes "[Aelle]";
			mes "^333333*Hic*^000000 Zzz... Hein? Faut surtout que j'vous apprendre à me laisser tranquille! Allez parler à ma soeur, je suis trop crevée, là. Laissez-moi... Zzzzzzzzzzz.";
			close2;
			warp "prontera",156,272;
			end;
		}
		else if (DANCER_SK > 8) {
			mes "[Aelle, sobre]";
			mes "Oooooh...";
			mes "Ce mal de tête...";
			Emotion e_an;
			next;
			mes "[Aelle]";
			mes "Oh, ouah, regardez-vous! Vous avez beaucoup plus de charme que la dernière fois! Je ne me souviens pas vous avoir déjà vue aussi élégante et raffinée!";
			next;
			mes "[Aelle]";
			mes "Gardez toujours confiance en votre beauté, utilisez vos charmes comme d'une arme... Vous pourrez avoir tout ce que vous voudrez en ce monde! Bwahahahahaha!";
			close;
		}
		else {
			mes "[Aelle]";
			mes "Pssst, hé! Même si c'est illégal, j'ai quelques points de téléportation vers le Donjon des Orcs, Glast Heim, et la Mine Abandonnée. Ça vous intéresse?";
			next;
			mes "[??????]";
			mes "Aelle, ma soeur...";
			mes "Vous êtes encore en train d'essayer de monnayer ces portails de téléportation illégaux? Ma soeur?";
			next;
			mes "[Aelle]";
			mes "Oh mince! C'est le pasteur! Si quelqu'un vous demande, je ne faisais rien de mal!";
			Emotion e_omg;
			next;
			mes "[Aelle]";
			mes "Merci, "+ ( sex ? "mon chéri" : "ma chérie" ) +"... Je sais que vous garderez pour vous cette petite histoire de contrebande. ^333333*Clin d'oeil*^000000";
			Emotion e_no1;
			close;
		}
	}
	else {
		mes "[Aelle]";
		mes "Pssst, hé! Même si c'est illégal, j'ai quelques points de téléportation vers le Donjon des Orcs, Glast Heim, et la Mine Abandonnée. Ça vous intéresse?";
		next;
		mes "[??????]";
		mes "Aelle, ma soeur...";
		mes "Vous êtes encore en train d'essayer de monnayer ces portails de téléportation illégaux? Ma soeur?";
		next;
		mes "[Aelle]";
		mes "Oh mince! C'est le pasteur! Si quelqu'un vous demande, je ne faisais rien de mal!";
		Emotion e_omg;
		next;
		mes "[Aelle]";
		mes "Merci, "+ ( sex ? "mon chéri" : "ma chérie" ) +"... Je sais que vous garderez pour vous cette petite histoire de contrebande. ^333333*Clin d'oeil*^000000";
		Emotion e_no1;
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 for fully working skills only [Lupus]
//= 1.1 Added more new skill quests for more classes [Lupus]
//= Somehow eA engine doesn't let you keep learn't skill V_V'
//= 1.2 Added to correct locations, correct NPC's, fixed
//= some of the items required and made them into real
//= quests. [Reddozen] 
//= 1.3 Fixed bugs and minor typos. Optimized [Lupus]
//= 1.3a fixed an item ID typo, thx 2Spiritual Kid
//= 1.3b Splitted into different files [DracoRPG]
//============================================================
