//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Sage Skills Quests
//===== By: ================================================== 
//= Lupus, Reddozen
//===== Current Version: ===================================== 
//= 1.3c
//===== Compatible With: ===================================== 
//= eAthena Revision 3800+
//===== Description: ========================================= 
//= Temp quests for new skills for 2nd classes
//===== Additional Comments: ================================= 
//= 1.0 for fully working skills only [Lupus]
//= 1.1 Added more new skill quests for more classes [Lupus]
//= Somehow eA engine doesn't let you keep learn't skill V_V'
//= 1.2 Added to correct locations, correct NPC's, fixed
//= some of the items required and made them into real
//= quests. [Reddozen] 
//= 1.3 Fixed bugs and minor typos. Optimized [Lupus]
//= 1.3a fixed an item ID typo, thx 2Spiritual Kid
//= 1.3b Splitted into different files [DracoRPG]
//= 1.3c Fixed some typos [IVBela]
//============================================================


//============================================================
// SAGE SKILL - CREATE CONVERTER + ELEMENTAL CHANGE
//============================================================
yuno_in03,176,24,3	script	Mischna	755,{
	mes "[Mischna]";
	if(BaseJob!=Job_Sage) goto L_sageno;
	if(getskilllv("SA_CREATECON")) goto L_elemental;

	mes "Je peux vous apprendre quelque chose de très utile si vous me rapportez quelques petites choses...";
	next;
	
	mes "[Mischna]";
	mes "J'aurais besoin de:";	
	mes "10 Queues de Scorpion";
	mes "7 Cornes";
	mes "12 Coquilles Arc-en-Ciel";
	mes "10 Coquilles d'Escargot";
	mes "4 Parchemins Vierges";
	next;
	
	mes "[Mischna]";
	mes "Voyons-voir ce que vous avez rapporté...";
	next;
	
	if(countitem(904)<10 || countitem(947)<7 || countitem(1013)<12 || countitem(946)<10 || countitem(7433)<4) goto L_noitems;//Items: Scorpion_Tail, Horn, Rainbow_Shell, Snail's_Shell, Blank_Scroll,
	delitem 904, 10;//Items: Scorpion_Tail,
	delitem 947, 7;//Items: Horn,
	delitem 1013, 12;//Items: Rainbow_Shell,
	delitem 946, 10;//Items: Snail's_Shell,
	delitem 7433, 4;//Items: Blank_Scroll,
	
	mes "[Mischna]";
	mes "Parfait, vous avez tout apporté! Je vais vous enseigner mes talents...";
	skill 1007,1,0;
	next;
	
	mes "[Mischna]";
	mes "Vous sentez-vous plus proche de la nature?";
	close;

L_noitems:
	mes "[Mischna]";
	mes "Je suis désolé, mais il vous manque certains objets...";
	close;
	
L_sageno:
	mes "Bonjour, mon nom est Mischna. Comment allez-vous?";
	mes "J'espère que vous apprécierez votre séjour dans la Guilde des Sages!";
	close;

L_sagefail:
	mes "[Mischna]";
	mes "Je suis désolé, mais il vous manque certains objets...";
	close;
	
L_alreadyhave:
	mes "Vous êtes un étudiant exemplaire! C'est dommage que je n'aie plus rien à vous apprendre.";
	close;
			
L_elemental:
	if(getskilllv(1008) || getskilllv(1017) || getskilllv(1018) || getskilllv(1019)) goto L_alreadyhave;
	
	mes "Je peux vous apprendre encore une petite chose, mais une seule!...";
	next;
	
	mes "[Mischna]";
	mes "Selon ce que vous me rapporterez, je vous apprendrai à dompter un certain élément:";
	mes "20 minerais Sang Rouges (Feu)";
	mes "20 Cristaux Bleus (Eau)";
	mes "20 minerais Vent Condensé (Vent)";
	mes "20 minerais Brut de Terre (Terre)";
	next;
	
	mes "[Mischna]";
	mes "N'oubliez pas, je ne pourrais vous enseigner qu'une seule chose! Choisissez bien!";
	next;
	
	menu "Transformation Élémentale (Eau)",sage_1, "Transformation Élémentale (Terre)",sage_2, "Transformation Élémentale (Feu)",sage_3, "Transformation Élémentale (Vent)",sage_4;

		sage_1:
		if(countitem(991)<20)goto L_sagefail;//Items: Crystal_Blue,
		delitem 991, 20;		//Items: Crystal_Blue,
		skill "SA_ELEMENTWATER",1,0;
		goto L_alreadyhave;

		sage_2:
		if(countitem(993)<20)goto L_sagefail;//Items: Green_Live,
		delitem 993, 20;//Items: Green_Live,
		skill "SA_ELEMENTGROUND",1,0;
		goto L_alreadyhave;

		sage_3:
		if(countitem(990)<20)goto L_sagefail;//Items: Red_Blood,
		delitem 990, 20;//Items: Red_Blood,
		skill "SA_ELEMENTFIRE",1,0;
		goto L_alreadyhave;

		sage_4:
		if(countitem(992)<20)goto L_sagefail;//Items: Wind_of_Verdure,
		delitem 992, 20;//Items: Wind_of_Verdure,
		skill "SA_ELEMENTWIND",1,0;
		goto L_alreadyhave;
}
