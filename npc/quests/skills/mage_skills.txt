//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script =======================================
//= Mage Skill Quest
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.6
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Quest for skills: Energy Coat
//===== Additional Comments: ================================= 
//= 1.5 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//= 1.6 Fixed a typo in job check. (Bugreport:1531) [L0ne_W0lf]
//============================================================

geffen_in,151,119,4	script	Blizzardriss#qsk_mg	64,{
	mes "[Blizzardriss]";
	mes "Hé! Mon ami! Si vous êtes un Mage intéressé par découvrir les compétences cachées, vous êtes au bon endroit!";
	next;
	switch(select("A Propos des Compétences Cachées:Apprendre ^3355FFEnergy Coat^000000:Mettre fin à la conversation")) {
	case 1:
		mes "[Blizzardriss]";
		mes "Depuis des années j'ai étudié la magie ancienne de Geffen, et j'ai récemment découvert un sortilège inusité mais très pratique!";
		next;
		mes "[Blizzardriss]";
		mes "Ce sortilège utilise votre énergie pyschocinétique afin de couvrir votre corps d'un bouclier magique.";
		next;
		mes "[Blizzardriss]";
		mes "Seuls les utilisateurs confirmés en magie arriveront à maîtriser ce sortilège correctement!";
		next;
		mes "[Blizzardriss]";
		mes "Ok Ok, ne vous embalez pas trop vite. Ecoutez attentivement ce que je vais vous dire.";
		mes "Ceux qui souhaitent utiliser ce sortilège doivent puiser l'énergie au fond d'eux-mêmes!";
		next;
		mes "[Blizzardriss]";
		mes "Si vous souhaitez apprendre ce sort, il vous faudra d'abord rapporter certains matériaux nécessaires au processus.";
		next;
		mes "[Blizzardriss]";
		mes "Il vous faudra: trois ^3355FFGlass Bead^000000,";
		mes "un ^3355FF1 carat Diamond^000000,";
		mes "cinq ^3355FFShell^000000, et";
		mes "un ^3355FFSolid Shell^000000.";
		next;
		mes "[Blizzardriss]";
		mes "Vous aurez également besoin d'avoir suffisamment d'expérience. Pour les Mages, le Niveau de Job 35 est requis au minimum.";
		next;
		mes "[Blizzardriss]";
		mes "Pour les Sorciers ou les Sages, qui sont expérimentés en utilisation de la magie, il n'y a pas de Niveau minimum requis.";
		next;
		mes "[Blizzardriss]";
		mes "Ok...";
		mes "Alors, êtes-vous intéressé?";
		close;
	case 2:
		if (getskilllv("MG_ENERGYCOAT") == 1) {
			mes "[Blizzardriss]";
			mes "On dirait que vous maîtrisez déjà la compétence Energy Coat. Je suis désolée, mais je n'ai plus rien à vous apprendre...";
			close;
		}
		else {
			if (countitem(746) > 2 && countitem(730) > 0 && countitem(935) > 4 && countitem(943) > 0) {
				if ((JobLevel > 34 && BaseJob == Job_Mage) || BaseJob == Job_Wizard || BaseJob == Job_Sage) {
					mes "[Blizzardriss]";
					mes "Très bien, nous pouvons commencer votre apprentissage.";
					mes "Je vais maintenant vous aider à révéler vos énergies cachées...";
					mes ". . . . . .";
					mes ". . . . . . . . . . . . .";
					mes ". . . . . . . . . . . . . . . . . . . . . . . . . .";
					next;
					mes "[Blizzardriss]";
					mes "Anciens pouvoirs de Geffen, je m'en remets à vous! Que votre présence nous apporte illumination et honneur!";
					mes " ...";
					next;
					mes "[Blizzardriss]";
					mes "Prêtez-moi votre pouvoir!";
					mes "Délivrez nos esprits!";
					mes "Bouclier de Mana! Armure Métallique!";
					mes ". . . . . . . . . . . .";
					mes "ENERGY COAT! ! !";
					next;
					delitem 746,3; //Glass_Bead
					delitem 730,1; //Crystal_Jewel
					delitem 935,5; //Shell
					delitem 943,1; //Solid_Shell
					skill 157,1,0;
					mes "[Blizzardriss]";
					mes ". . . . .";
					mes "Voilà, c'est fait... Vous avez appris le sortilège ^3355FFEnergy Coat^000000 .";
					mes "Faîtes en bon usage.";
					next;
					mes "[Blizzardriss]";
					mes "N'apportez pas la honte sur la classe des magiciens en utilisant vos sortilèges de mauvaise façon. De grands pouvoirs impliquent de grandes responsabilités.";
					close;
				}
			}
			mes "[Blizzardriss]";
			mes "Et bien!!";
			mes "Vous n'avez pas écouté mes explications?!";
			mes "Vous n'êtes pas prêt à ce que je vous assiste dans votre apprentissage.";
			next;
			mes "[Blizzardriss]";
			mes "Si vous avez besoin que je vous explique à nouveau, n'hésitez pas à me demander. Je serai ravie de vous donner un cours de rattrapage... à condition que vous écoutiez!";
			close;
		}
	case 3:
		mes "[Blizzardriss]";
		mes "Le sage connaît la patience.";
		mes "Entraînez-vous et revenez me voir lorsque vous serez prêt.";
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= v1.0a Now using functions found in "Global_Functions.txt"
//= for class checks.[kobra_k88]
//= 1.2 Added Baby Class Support [Lupus]
//= 1.3 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 1.4 Fixed exploit [Lupus]
//= 1.4a Fixed some typos [IVBela]
//= 1.4b changed perm. variables to temp ones [Lupus]
//============================================================
