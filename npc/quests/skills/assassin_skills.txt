//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena, corrigé par Icaros
//===== eAthena Script ======================================= 
//= Assassin Skills Quests
//===== By: ================================================== 
//= Lupus, Reddozen
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Quests for skills: Venom Knife, Sonic Acceleration
//===== Additional Comments: ================================= 
//= 1.4 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//= 1.5 Replaced effect numerics with constants. [L0ne_W0lf]
//============================================================

in_moc_16,14,27,5	script	Assassin#realman	884,{
	if (BaseJob == Job_Assassin && ASSN_SK2 == 1) {
		if (getskilllv("AS_VENOMKNIFE") == 0) {
			mes "[Killtin]";
			mes "Votre visage m'est familier... Ah oui, vous êtes celui à qui j'ai enseigné la compétence ^990099Dague Empoisonnée^000000. Alors, qu'est-ce qui vous amène cette fois-ci?";
			next;
			mes "[Killtin]";
			mes "Comment...?!";
			mes "Vous avez tout oublié? Il vous est arrivé quoi, un coup sur la tête? Bon d'accord, d'accord, je vais tout vous réapprendre.";
			next;
			mes "[Killtin]";
			mes "Tout d'abord, vous devez équiper une arme de type Couteau, puis utiliser la compétence Empoisonnement sur ce couteau. Ensuite vous devez envoyer votre couteau empoisonné sur la cible. Comment faire?";
			next;
			mes "[Killtin]";
			mes "Tout est dans le geste du poignet. Regardez-moi attentivement. Là, vous voyez? Il vous faudra de l'entraînement pour arriver à rendre votre mouvement le plus fluide possible. Il faut toujours revoir les bases avant de se perfectionner. Alors, vous voulez essayer?";
			specialeffect EF_INVENOM;
			next;
			mes "[Killtin]";
			mes "Bien... Très bien...";
			mes "Votre mouvement du poignet est parfait. Oui...";
			mes "Hum. Vous êtes certain que vous aviez oublié comment faire? Je suppose que vous aviez juste besoin qu'on vous rafraîchisse la mémoire.";
			specialeffect2 EF_INVENOM;
			next;
			mes "[Killtin]";
			mes "Très bien, je pense que maintenant vous maîtrisez parfaitement les Dagues Empoisonnées. Partez à présent, et n'oubliez pas que vous vous battez pour l'honneur de la Guilde des Assassins!";
			skill "AS_VENOMKNIFE",1,0;
			close;
		}
		else {
			mes "[Killtin]";
			mes "Alors, est-ce que vous utilisez souvent les ^990099Dagues Empoisonnées^000000?";
			mes "Soyez prudent, assurez-vous toujours que vos victimes méritent leur sort!";
			close;
		}
	}
	else if (ASSN_SK2 == 1 && ASSN_SK == 7) {
		mes "[Killtin]";
		mes "Alors comme ça vous connaissez tout sur tout des techniques des Assassins? Ne prenez pas trop la grosse tête. Avoir trop confiance en soi n'est pas bon au cours d'un combat. Efforcez-vous de devenir plus fort, en l'honneur de la Guilde des Assassins.";
		close;
	}
	else if (ASSN_SK2 == 1) {
		mes "[Killtin]";
		mes "Alors, est-ce que vous utilisez souvent les ^990099Dagues Empoisonnées^000000?";
		mes "Soyez prudent, assurez-vous toujours que vos victimes méritent leur sort!";
		close;
	}
	else {
		if (BaseJob == Job_Assassin) {
			mes "[Killtin]";
			mes "Hum? Oh, vous êtes un membre de la Guilde des Assassins. Parfait, vous arrivez juste à temps.";
			next;
			mes "[Killtin]";
			mes "Notre Chef de Guilde a récemment développé de nouvelles compétences pour nous autres Assassins. Je suis chargé d'enseigner ces nouvelles compétences à tous les membres de la guilde.";
			next;
			select("Des nouvelles compétences?");
			mes "[Killtin]";
			mes "Tout à fait, "+ strcharinfo(0) +".";
			mes "La première compétence permet d'améliorer l'efficacité des Coups Supersoniques, et la seconde est une attaque à distance appelée ^990099Dague Empoisonnée^000000.";
			next;
			mes "[Killtin]";
			mes "Si vous avez des questions, n'hésitez pas à m'en faire part. C'est mon boulot de vous apprendre tout ce que je sais!";
			next;
			while(1) {
				switch(select("^0000FFAmélioration de Coups Supersoniques^000000:^990099Dague Empoisonnée^000000:Discuter")) {
				case 1:
					mes "[Killtin]";
					mes "Si vous êtes Assassin depuis longtemps, vous devez être familier avec les Coups Supersoniques, qui inflige 8 coups puissants à un ennemi en une seule attaque sanglante et rapide.";
					next;
					mes "[Killtin]";
					mes "Cependant, de par leur vélocité extrême, les Coups Supersoniques ne sont pas aussi précis qu'ils le devraient. Après des années de recherches et de tests, notre chef de guilde a mis au point une technique pour palier à ce problème.";
					next;
					mes "[Killtin]";
					mes "Il a créé une nouvelle compétence appelée 'Accélération Supersonique', que les Assassins peuvent utiliser sur eux-mêmes pour augmenter concentration, et améliorer leur discernement des points faibles de l'ennemi.";
					next;
					mes "[Killtin]";
					mes "En effet, l'Accélération Supersonique permet de doubler les dégâts infligés avec un Coup Supersonique normal! Si vous utilisez souvent les Coups Supersoniques, cela vous sera sans nul doute utile.";
					next;
					mes "[Killtin]";
					mes "Je vous suggère également d'apprendre à manier des Dagues Empoisonnées. Ensuite vous pourrez parler à Esmille, la jolie Assassin Cross juste là, à ma droite, pour apprendre l'Accélération Supersonique.";
					next;
					break;
				case 2:
					mes "[Killtin]";
					mes "Comme vous le savez bien, nos méthodes d'attaques ne sont pas à proprement parlé à distance. Bien sûr, nous pouvons utiliser des Arcs, et nous possédons quelques compétences ayant une portée plus éloignée que le contact direct, mais leurs usages sont tout de même assez limités.";
					next;
					mes "[Killtin]";
					mes "Les Dagues Empoisonnées ont été développées dans le but de contrer notre faiblesse à longue distance. D'habitude, nous utilisons la compétence d'Empoisonnement, et nous la lançons sur un ennemi pour l'empoisonner.";
					next;
					break;
				case 3:
					mes "[Killtin]";
					mes "Si vous le souhaiter, je peux vous apprendre à manier des ^009900Dagues Empoisonnées^000000. Alors qu'en dites-vous?";
					next;
					while(1) {
						if (select("Apprendre à manier des Dagues Empoisonnées.:Sans moi!") == 1) {
							mes "[Killtin]";
							mes "Tout d'abord, vous devez équiper une arme de type Couteau, puis utiliser Empoisonnement sur ce couteau. Ensuite il vous faudra lancer la lame sur l'ennemi. Tout est dans un petit mouvement du poignet. Regardez-moi attentivement, et observez.";
							next;
							mes "[Killtin]";
							mes "Vous voyez? Le mouvement doit être fluide et précis. Maîtrisez ce mouvement de base avant de vous perfectionner. Vous voulez essayer? Allez, c'est à votre tour.";
							specialeffect EF_INVENOM;
							next;
							mes "[Killtin]";
							mes "Hé, c'était plutôt pas mal. Vous apprenez vite. Hé hé, mais je suppose que c'est aussi grâce à mes excellentes techniques d'enseignement.";
							next;
							specialeffect2 EF_INVENOM;
							next;
							mes "[Killtin]";
							mes "Très bien, vous devrez encore vous exercer un peu, mais vous maîtriser déjà l'essentiel. Vous pouvez vous servir de Dagues Empoisonnées en combat, rien ne vaut l'expérience d'une vraie bataille pour juger de la qualité d'une compétence.";
							set ASSN_SK2,1;
							set ASSN_SK,1;
							skill "AS_VENOMKNIFE",1,0;
							next;
							mes "[Killtin]";
							mes "Bien, c'est tout ce que je peux vous apprendre. Utilisez les Dagues Empoisonnées comme un expert, abattez vos ennemis pour l'honneur de la Guilde de Assassins!";
							close;
						}
						if (.@teach ==1) {
							mes "[Killtin]";
							mes "Vous ne voulez pas apprendre à manier des Dagues Empoisonnées? Mais... c'est mon travail de vous l'apprendre! Vous ne partirez pas d'ici sans mon autorisation!";
							next;
						}
						else {
							mes "[Killtin]";
							mes "Vous ne pouvez pas désobéir à un ordre provenant de notre chef de guilde... Que vous le vouliez ou non, cette compétence fera de vous un meilleur Assassin. Vous pouvez me croire sur parole, alors, vous ne voulez toujours pas apprendre cette compétence?";
							next;
						}
					}
				}
			}
		}
		else if (BaseJob == Job_Thief) {
			mes "[Killtin]";
			mes "Un voleur...? Hum.";
			mes "C'est un boulot respectable. Mais si tu veux devenir plus fort, il faudra continuer à t'entraîner. Reviens me voir plus tard, j'aurais peut-être des choses à t'enseigner.";
			close;
		}
		else {
			mes "[Assassin]";
			mes "...............................";
			mes "Vas voir ailleurs si j'y suis.";
			close;
		}
	}
}

in_moc_16,23,27,5	script	Assassin#realgirl	885,{
	if (BaseJob == Job_Assassin && ASSN_SK == 7) {
		if (getskilllv("AS_SONICACCEL") == 0) {
			mes "[Esmille]";
			mes "Hum? Ah, vous êtes devenu un Assassin Cross! Vous avez effectué une renaissance et vous disposez à présent d'une nouvelle force.";
			next;
			mes "[Esmille]";
			mes "Vous aurez certainement besoin d'apprendre l'Accélération Supersonique à nouveau, si vous avez perdu la mémoire... Comme je suis sympa, je vais vous l'apprendre à nouveau.";
			next;
			if (getskilllv("AS_SONICBLOW") == 0) {
				mes "[Esmille]";
				mes "Tout d'abord, apprenez à faire des Coups Supersoniques. La compétence que j'enseigne vous sera complètement inutile si vous ne savez pas comment effectuer des Coups Supersoniques. Je vous attendrai ici.";
				close;
			}
			mes "[Esmille]";
			mes "Tout d'abord, avant d'effectuer des Coups Supersoniques, vous devez vérifier que vos pieds sont bien positionnés, comme ceci. Ensuite, aussi rapidement et précisément que possible, déplacez votre poids de ce côté.";
			specialeffect EF_SONICBLOW;
			next;
			mes "[Esmille]";
			mes "Faites attention, c'est le point le plus important. Regardez où sont positionnées mes mains par rapport à l'angle de mes bras. C'est la forme que vous devez impérativement mémoriser.";
			specialeffect EF_SONICBLOW;
			next;
			mes "[Esmille]";
			mes "Très bien, c'est tout pour le moment. C'est à votre tour d'essayer. N'ayez pas peur, je vous donnerai des conseils.";
			next;
			specialeffect2 EF_SONICBLOW;
			next;
			mes "[Esmille]";
			mes "Hum, votre placement n'est pas tout à fait stable. Cela pourrait vous aider si vous positionnez votre centre de gravité comme ceci...";
			next;
			specialeffect2 EF_SONICBLOW;
			next;
			mes "[Esmille]";
			mes "Ah, vous n'êtes pas loin de la perfection. Concentrez-vous pour que la transition entre votre position de préparation et votre coup soit plus fluide.";
			next;
			mes "[Esmille]";
			mes "Oui, c'est tout à fait ça...!";
			mes "Vous avez très bien exécuté ce coup.";
			mes "Bon travail, "+ strcharinfo(0) +".";
			skill "AS_SONICACCEL",1,0;
			set ASSN_SK,7;
			next;
			mes "[Esmille]";
			mes "Vous comprenez à présent?";
			mes "Vous ne devriez avoir aucun problème pour vous remémorer cette compétence à présent. Je ne peux rien vous apprendre de plus. Je vous souhaite donc bonne chance pour vos voyages.";
			close;
		}
		else {
			mes "[Esmille]";
			mes "Je pense qu'utiliser l'Accélération Supersonique au cours d'un combat peut vous apporter un avantage décisif. Abattez vos ennemis, pour l'honneur de la Guilde des Assassins!";
			close;
		}
	}
	else if (ASSN_SK == 7) {
		mes "[Esmille]";
		mes "Je pense qu'utiliser l'Accélération Supersonique au cours d'un combat peut vous apporter un avantage décisif. Abattez vos ennemis, pour l'honneur de la Guilde des Assassins!";
		close;
	}
	else if (ASSN_SK == 6) {
		mes "[Esmille]";
		mes "Concentrez-vous sur l'entraînement... Si nous continuons à être sans cesse interrompus, vous n'arriverez jamais à apprendre quoi que ce soit. Maintenant, écoutez attentivement.";
		next;
		mes "[Esmille]";
		mes "Tout d'abord, avant d'effectuer des Coups Supersoniques, vous devez vérifier que vos pieds sont bien positionnés, comme ceci. Ensuite, aussi rapidement et précisément que possible, déplacez votre poids de ce côté.";
		specialeffect EF_SONICBLOW;
		next;
		mes "[Esmille]";
		mes "Faites attention, c'est le point le plus important. Regardez où sont positionnées mes mains par rapport à l'angle de mes bras. C'est la forme que vous devez impérativement mémoriser.";
		specialeffect EF_SONICBLOW;
		next;
		mes "[Esmille]";
		mes "Très bien, c'est tout pour le moment. C'est à votre tour d'essayer. N'ayez pas peur, je vous donnerai des conseils.";
		next;
		specialeffect2 EF_SONICBLOW;
		next;
		mes "[Esmille]";
		mes "Hum, votre placement n'est pas tout à fait stable. Cela pourrait vous aider si vous positionnez votre centre de gravité comme ceci...";
		next;
		specialeffect2 EF_SONICBLOW;
		next;
		mes "[Esmille]";
		mes "Ah, vous n'êtes pas loin de la perfection. Concentrez-vous pour que la transition entre votre position de préparation et votre coup soit plus fluide.";
		next;
		specialeffect2 EF_SONICBLOW;
		next;
		mes "[Esmille]";
		mes "Ah, vous n'êtes pas loin de la perfection. Concentrez-vous pour que la transition entre votre position de préparation et votre coup soit plus fluide.";
		specialeffect2 EF_SONICBLOW;
		next;
		mes "[Esmille]";
		mes "Oui, c'est tout à fait ça...!";
		mes "Vous avez très bien exécuté ce coup.";
		mes "Bon travail, "+ strcharinfo(0) +".";
		skill "AS_SONICACCEL",1,0;
		set ASSN_SK,7;
		next;
		mes "[Esmille]";
		mes "Vous comprenez à présent?";
		mes "Vous ne devriez avoir aucun problème pour vous remémorer cette compétence à présent. Je ne peux rien vous apprendre de plus. Je vous souhaite donc bonne chance pour vos voyages.";
		close;
	}
	else if (ASSN_SK == 5) {
		mes "[Esmille]";
		mes "Alors comment se passe votre mission? Je suppose que vous avez accompli la tâche que je vous avais assignée?";
		next;
		if ((ASSN_SK == 5 && countitem(726) > 0) || (ASSN_SK == 5 && countitem(723) > 0) || (ASSN_SK == 5 && countitem(720) > 0)) {
			select("Ce trésor convient-il?");
			mes "[Esmille]";
			mes "Oh, cette pierre précieuse...!";
			mes "Elle est si captivante~";
			mes "Je n'ai jamais rien vu d'aussi beau! Vous avez fait du bon boulot...";
			next;
			mes "[Esmille]";
			mes "Cette pierre précieuse est à vous, gardez-la. En vérité, je n'ai pas besoin de trésors, je voulais juste une preuve de vos aptitudes. On dirait que vous êtes prêt à ce que je vous enseigne l'Accélération Supersonique.";
			set ASSN_SK,6;
			next;
			mes "[Esmille]";
			mes "Tout d'abord, avant d'effectuer des Coups Supersoniques, vous devez vérifier que vos pieds sont bien positionnés, comme ceci. Ensuite, aussi rapidement et précisément que possible, déplacez votre poids de ce côté.";
			specialeffect EF_SONICBLOW;
			next;
			mes "[Esmille]";
			mes "Faites attention, c'est le point le plus important. Regardez où sont positionnées mes mains par rapport à l'angle de mes bras. C'est la forme que vous devez impérativement mémoriser.";
			specialeffect EF_SONICBLOW;
			next;
			mes "[Esmille]";
			mes "Très bien, c'est tout pour le moment. C'est à votre tour d'essayer. N'ayez pas peur, je vous donnerai des conseils.";
			next;
			specialeffect2 EF_SONICBLOW;
			next;
			mes "[Esmille]";
			mes "Hum, votre placement n'est pas tout à fait stable. Cela pourrait vous aider si vous positionnez votre centre de gravité comme ceci...";
			next;
			specialeffect2 EF_SONICBLOW;
			next;
			mes "[Esmille]";
			mes "Ah, vous n'êtes pas loin de la perfection. Concentrez-vous pour que la transition entre votre position de préparation et votre coup soit plus fluide.";
			next;
			specialeffect2 EF_SONICBLOW;
			next;
			mes "[Esmille]";
			mes "Oui, c'est tout à fait ça...!";
			mes "Vous avez très bien exécuté ce coup.";
			mes "Bon travail, "+ strcharinfo(0) +".";
			skill 1003,1,0;
			set ASSN_SK,7;
			next;
			mes "[Esmille]";
			mes "Vous comprenez à présent?";
			mes "Vous ne devriez avoir aucun problème pour vous remémorer cette compétence à présent. Je ne peux rien vous apprendre de plus. Je vous souhaite donc bonne chance pour vos voyages.";
			close;
		}
		else {
			mes "[Esmille]";
			mes "Hum...";
			close;
		}
	}
	else if (ASSN_SK == 2 || ASSN_SK == 3 || ASSN_SK == 4) {
		mes "[Esmille]";
		mes "Alors comment se passe votre mission?";
		mes "Si vous avez oublié la localisation du trésor que je vous ai demandé de retrouver, je peux vous résumer rapidement les informations.";
		next;
		mes "[Esmille]";
		mes "Rendez-moi ce service: trouvez un trésor non loin";
		if (ASSN_SK == 2) {
			mes "de ^FF0000Vieux Cercueils^000000 à Sphinx.";
		}
		else if (ASSN_SK == 3) {
			mes "de ^FF0000Statues de Pierres^000000 à Sphinx.";
		}
		else if (ASSN_SK == 4) {
			mes "d'une crypte immergée au ^FF0000sous-sol^000000 des Pyramides.";
		}
		mes "Seuls les personnes expérimentées peuvent explorer cet endroit. Accomplissez cette mission, et je reconnaîtrai votre valeur.";
		close;
	}
	else if (ASSN_SK == 1) {
		mes "[Esmille]";
		mes "Ah. " + ( isday() ? "Bonjour" : "Bonsoir" ) + ", collègue. Avez-vous entendu les dernières nouvelles de la Guilde des Assassins?";
		mes "Ah, vous avez parlé à Killtin? Bien, bien...";
		next;
		mes "[Esmille]";
		mes "Je suis en charge de l'enseignement de l'Accélération Supersonique à tous les Assassins qui sont intéressés. Je peux vous en dire plus, si Killtin ne vous a pas tout expliqué.";
		next;
		while(1) {
			switch(select("Dites m'en plus...:Apprendre l'Accélération Supersonique!")) {
			case 1:
				mes "[Esmille]";
				mes "L'Accélération Supersonique est une ^FF0000compétence de support, utilisée conjointement avec des Coups Supersoniques^000000.";
				mes "Les Assassins ne peuvent l'utiliser que sur eux-mêmes, pour améliorer leurs propres capacités.";
				next;
				mes "[Esmille]";
				mes "Si vous êtes familier avec les Coups Supersoniques, vous devez savoir combien il est difficile d'infliger des dégâts fatals: cette attaque est bien trop rapide et peu précise...";
				next;
				mes "[Esmille]";
				mes "Cependant, si vous apprenez l'Accélération Supersonique, vous pourrez palier à ce problème de précision, et améliorer le potentiel de puissance des Coups Supersoniques.";
				next;
				mes "[Esmille]";
				mes "Cette compétence est réellement géniale. C'est notre chef de guilde qui l'a inventée... C'est un homme parmi les hommes, je dois dire!";
				Emotion e_lv;
				next;
				break;
			case 2:
				mes "[Esmille]";
				mes "Ah, je suis ravie de voir que vous êtes enthousiaste à l'idée d'apprendre cette compétence. Mais avant toutes choses, nous devons vérifier certaines de vos aptitudes... ^FFFFFF ^000000";
				next;
				if (getskilllv("AS_SONICBLOW") == 0) {
					mes "[Esmille]";
					mes "Tout d'abord, apprenez à faire des Coups Supersoniques. La compétence que j'enseigne vous sera complètement inutile si vous ne savez pas comment effectuer des Coups Supersoniques. Je vous attendrai ici.";
					close;
				}
				mes "[Esmille]";
				mes "Afin de me prouver vos capacités, je vais vous confier une tâche à accomplir. Votre mission est de me rapporter";
				switch(rand(1,3)) {
				case 1:
					mes "un trésor provenant de Sphinx.";
					mes "Cherchez des ^FF0000Vieux Cercueils^000000, ils contiennent souvent des objets de valeur. Considérez ceci comme un test pour évaluer votre force.";
					set ASSN_SK,2;
					next;
					mes "[Esmille]";
					mes "Si vous n'accomplissez pas cette tâche, je considèrerai que vous ne méritez pas que je vous apprenne l'Accélération Supersonique. Bonne chose, et dépêchez-vous un peu, je n'ai pas que ça à faire.";
					close;
				case 2:
					mes "un trésor provenant de Sphinx.";
					mes "Cherchez des ^FF0000Statues de Pierres^000000, ils contiennent souvent des objets de valeur. Considérez ceci comme un test pour évaluer votre force.";
					set ASSN_SK,3;
					next;
					mes "[Esmille]";
					mes "Si vous n'accomplissez pas cette tâche, je considèrerai que vous ne méritez pas que je vous apprenne l'Accélération Supersonique. Bonne chose, et dépêchez-vous un peu, je n'ai pas que ça à faire.";
					close;
				case 3:
					mes "un trésor provenant des Pyramides.";
					mes "Cherchez une ^FF0000crypte immergée au sous-sol^000000, elle contient sûrement des objets de valeur. Considérez ceci comme un test pour évaluer votre force.";
					set ASSN_SK,4;
					next;
					mes "[Esmille]";
					mes "Si vous n'accomplissez pas cette tâche, je considèrerai que vous ne méritez pas que je vous apprenne l'Accélération Supersonique. Bonne chose, et dépêchez-vous un peu, je n'ai pas que ça à faire.";
					close;
				}
			}
		}
	}
	else {
		if (BaseJob == Job_Assassin) {
			mes "[Assassin]";
			mes "Ah. " + ( isday() ? "Bonjour" : "Bonsoir" ) + ", collègue. Avez-vous entendu les dernières nouvelles de la Guilde des Assassins?";
			Emotion e_heh;
			next;
			select("Nouvelles de la Guilde des Assassins?");
			mes "[Assassin]";
			mes "Hum. Vous n'êtes pas au courant? Si vous voulez en savoir plus, vous devriez parler à Killtin. Il vous expliquera tout ça mieux que moi.";
			close;
		}
		mes "[Assassin]";
		mes "Hum. Si vous connaissez des Assassins, dites-leur de venir ici, si ce n'est pas déjà fait.";
		close;
	}
}

in_sphinx2,259,213,0	script	Vieux Cercueil#qsk_as	-1,7,8,{
OnTouch:
	if (ASSN_SK == 2) {
		mes "^3355FFC'est un très vieux cercueil";
		mes "dont le couvercle est cassé";
		mes "et légèrement entrebâillé.";
		mes "D'un coup d'oeil, vous repérez";
		mes "quelque chose de brillant à l'intérieur.^000000";
		specialeffect EF_CONE;
		next;
		while(1) {
			switch(select("Mettre vos mains à l'intérieur.:Inspecter l'ouverture.:Soulever le couvercle.:Retourner le cercueil.:Ne pas y toucher.")) {
			case 1:
				mes "^3355FFVous glissez prudemment vos mains à l'intérieur du cercueil et essayez de retrouver à tâtons l'objet brillant que vous avez vu.";
				mes "...............................^000000";
				next;
				mes "^3355FFQuelque chose à l'intérieur du cercueil vous mord la main!^000000";
				sc_start SC_Poison,30000,0;
				sc_start SC_Bleeding,10000,0;
				Emotion e_omg,1;
				close;
			case 2:
				mes "^3355FFVous essayez de jeter un oeil par l'entrebâillement du cercueil, entre le bord et le couvercle.";
				mes "...............................^000000";
				next;
				mes "^3355FFVous percevez quelque chose qui semble remuer à l'intérieur, mais il fait trop sombre pour que vous puissiez distinguer quoi que ce soit.^000000";
				sc_start SC_Blind,30000,0;
				next;
				break;
			case 3:
				mes "^3355FFVous vous faites un tour de reins en essayant de soulever le couvercle du cercueil...^000000";
				sc_start SC_Curse,30000,0;
				next;
				break;
			case 4:
				mes "^3355FFVous n'avez même pas la force de soulever le cercueil.^000000";
				next;
				break;
			case 5:
				close;
			}
		}
	}
}

in_sphinx2,212,267,0	script	Vieux Cercueil#qsk_as2	-1,5,6,{
OnTouch:
	if (ASSN_SK == 2) {
		mes "^3355FFC'est un très vieux cercueil";
		mes "dont le couvercle est cassé";
		mes "et légèrement entrebâillé.";
		mes "D'un coup d'oeil, vous repérez";
		mes "quelque chose de brillant à l'intérieur.^000000";
		specialeffect EF_CONE;
		next;
		while(1) {
			switch(select("Mettre vos mains à l'intérieur.:Inspecter l'ouverture.:Soulever le couvercle.:Retourner le cercueil.:Ne pas y toucher.")) {
			case 1:
				mes "^3355FFVous glissez prudemment vos mains à l'intérieur du cercueil et essayez de retrouver à tâtons l'objet brillant que vous avez vu.";
				mes "...............................^000000";
				next;
				if (rand(1,3) != 3) {
					mes "^3355FFVous glissez prudemment vos mains à l'intérieur du cercueil et essayez de retrouver à tâtons l'objet brillant que vous avez vu.";
					mes "...............................^000000";
					next;
					mes "^3355FFQuelque chose à l'intérieur du cercueil vous mord la main!^000000";
					sc_start SC_Poison,30000,0;
					sc_start SC_Bleeding,10000,0;
					Emotion e_omg,1;
					close;
				}
				else {
					mes "^3355FFVos doigts entrent en contact avec un objet solide que vous extirpez, non sans mal, du cercueil. Vous avez trouvé un Saphir! C'est certainement ce qu'Esmille recherchait.^000000";
					set ASSN_SK,5;
					getitem 726,1; //Blue_Jewel
					close;
				}
			case 2:
				mes "^3355FFVous essayez de jeter un oeil par l'entrebâillement du cercueil, entre le bord et le couvercle.";
				mes "...............................^000000";
				next;
				mes "^3355FFVous percevez quelque chose qui semble remuer à l'intérieur, mais il fait trop sombre pour que vous puissiez distinguer quoi que ce soit.^000000";
				sc_start SC_Blind,30000,0;
				next;
				break;
			case 3:
				mes "^3355FFVous vous faites un tour de reins en essayant de soulever le couvercle du cercueil...^000000";
				sc_start SC_Curse,30000,0;
				next;
				break;
			case 4:
				mes "^3355FFVous n'avez même pas la force de soulever le cercueil.^000000";
				next;
				break;
			case 5:
				close;
			}
		}
	}
}

in_sphinx2,13,163,0	script	Statue de Pierre#qsk	-1,7,7,{
OnTouch:
	if (ASSN_SK == 3) {
		specialeffect EF_CONE;
		mes "^3355FFC'est une Statue de Pierre très ancienne. Elle est couverte de fissures, on dirait qu'elle va tomber en miettes d'ici peu. Une faible lueur se dégage du pied de la statue. Le sol semble suffisamment meuble pour pouvoir y creuser...^000000";
		specialeffect EF_CONE;
		next;
		if (select("Creuser pour trouver l'objet qui luit.:Ne rien faire.") == 1) {
			mes "^3355FFAlors que vos doigts creusent la terre meuble, un nuage de gaz jaune se dégage lentement.^000000";
			if (rand(1,3) != 3){
				mes "^3355FFVous tombez inconscient.^000000";
				sc_start SC_Sleep,30000,0;
				close;
			}
			mes "^3355FFVous prenez votre respiration, et extirpez un objet du sol avant de vous dégager rapidement du nuage de gaz. Vous avez trouvé un Rubis! C'est certainement ce qu'Esmille recherchait.^000000";
			set ASSN_SK,5;
			getitem 723,1; //Cardinal_Jewel
		}
		close;
	}
}

in_sphinx2,13,146,0	script	Statue de Pierre#qsk2	-1,7,7,{
OnTouch:
	if (ASSN_SK == 3) {
		specialeffect EF_CONE;
		mes "^3355FFC'est une Statue de Pierre très ancienne. Elle est couverte de fissures, on dirait qu'elle va tomber en miettes d'ici peu. Une faible lueur se dégage du pied de la statue. Le sol semble suffisamment meuble pour pouvoir y creuser...^000000";
		specialeffect EF_CONE;
		next;
		if (select("Creuser pour trouver l'objet qui luit.:Ne rien faire.") == 1) {
			mes "^3355FFAlors que vos doigts creusent la terre meuble, un nuage de gaz jaune se dégage lentement.^000000";
			if (rand(1,3) != 3) {
				mes "^3355FFVous tombez inconscient.^000000";
				sc_start SC_Sleep,30000,0;
				close;
			}
			mes "^3355FFVous prenez votre respiration, et extirpez un objet du sol avant de vous dégager rapidement du nuage de gaz. Hélas, ce n'est qu'un morceau de verre brisé.^000000";
			Emotion e_omg,1;
		}
		close;
	}
}

moc_pryd04,85,96,0	script	#crypt	-1,3,3,{
OnTouch:
	if (ASSN_SK == 4) {
		specialeffect EF_CONE;
		mes "^3355FFQuelque chose luit faiblement sous la surface de l'eau...^000000";
		next;
		if (select("Le ramasser.:L'ignorer.") == 1) {
			mes "^3355FFDès que vous plongez vos mains dans l'eau glacée, votre bras se tétanise. Dépêchez-vous si vous ne voulez pas finir congelé!^000000";
			next;
			if (rand(1,3) != 3) {
				mes "^3355FFC'est trop tard!";
				mes "Votre corps tout entier s'engourdit...^000000";
				sc_start SC_Freeze,10000,0;
				close;
			}
			mes "^3355FFVous ramassez le plus vite possible l'objet luisant avant que l'eau ne vous engourdisse le reste du corps. Vous avez trouvé une Aigue-Marine! C'est certainement ce qu'Esmille recherchait.^000000";
			set ASSN_SK,5;
			getitem 720,1; //Skyblue_Jewel
		}
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 for fully working skills only [Lupus]
//= 1.1 Added more new skill quests for more classes [Lupus]
//= Somehow eA engine doesn't let you keep learn't skill V_V'
//= 1.2 Added to correct locations, correct NPC's, fixed
//= some of the items required and made them into real
//= quests. [Reddozen] 
//= 1.3 Fixed bugs and minor typos. Optimized [Lupus]
//= 1.3a fixed an item ID typo, thx 2Spiritual Kid
//= 1.3b Splitted into different files [DracoRPG]
//============================================================
