//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par MageGaHell, corrigé par Myllena et Icaros
//===== eAthena Script ======================================= 
//= Wizard Skills Quests
//===== By: ================================================== 
//= Lupus, Reddozen
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Quests for skills: Sight Blaster
//===== Additional Comments: ================================= 
//= 1.4 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//= 1.5 Replaced effect numerics with constants. [L0ne_W0lf]
//============================================================

gef_tower,115,36,4	script	Miaou::Meow#q_wiz	876,{
	if (BaseJob == Job_Wizard) {
		if (WIZ_SK == 100) {
			if (getskilllv("WZ_SIGHTBLASTER") == 0) {
				mes "[Miaou]";
				mes "Quoi...?! Vous avez oublié l'incroyable compétence que je vous ai apprise?";
				mes "La renaissance n'est pas une excuse!";
				mes "Très bien, vous voulez que je vous la réapprenne, hein?";
				mes "Alors, c'est parti pour votre leçon!";
				next;
				mes "^3355FF*BAM! BOOM! CRASH!*^000000";
				specialeffect2 EF_SUI_EXPLOSION;
				next;
				mes "[Miaou]";
				mes "Ne soyez pas si prétentieux avec moi juste parce que vous avez effectué une renaissance.";
				mes "Vous feriez mieux de vous rappeler comment utiliser cette compétence, avant que je vous fouette un autre coup!";
				next;
				mes "^3355FFVous vous rappelez miraculeusement comment utiliser le Feu Gardien avant que Miaou ne puisse vous administrer une autre vicieuse et douloureuse correction.";
				specialeffect2 EF_ABSORBSPIRITS;
				skill "WZ_SIGHTBLASTER",1,0;
				next;
				mes "[Miaou]";
				mes "Vous vous en souvenez, ça y est?";
				mes "Bien. Je vais maintenant retourner faire une petite sieste.";
				mes "Si vous me dérangez encore, je vous le ferai regretter toute votre vie. Vous avez ma parole!";
				close;
			}
			else {
				mes "[Miaou]";
				mes "Zzz... Zzz...";
				mes "Catherine... Je vous ferai regretter de m'avoir transformé en chaton... Je vais vous... Vous faire... Zzz...";
				close;
			}
		}
		else if (WIZ_SK == 0) {
			mes "[Miaou]";
			mes "*Ouuaaaah~*";
			mes "Si fatiiigué...";
			mes "Hum? Vous voulez me parler? Ne me dérangez pas si vous n'avez rien à me dire!";
			next;
			if (select("Un chat?:Maintenant j'ai sommeil aussi...") == 1) {
				mes "[Miaou]";
				mes "Ouais. Ouais, Je suis un chat.";
				mes "Pour l'instant, tout de moins. Maudite soit cette Catherine! Elle m'a donné cette forme après que j'ai... emprunté... certaines gemmes sans sa permission.";
				mes "Mais j'allais lui demander!";
				next;
				mes "[Miaou]";
				mes "Dès que je serai redevenu humain et que j'aurai récupéré mes pouvoirs, ma vengeance sera terrible! Catherine, tout le monde, vous allez y passer! Je vous changerai tous en chat!";
				close;
			}
			mes "[Miaou]";
			mes "Ha! Ouais...";
			mes "Faire une p'tite sieste me semble être la meilleure idée que j'ai entendue depuis... Oh!";
			mes "Attendez. Vous êtes un Sorcier, n'est-ce pas?";
			mes "J'étais si fatigué que je n'avais même pas remarqué. Ah, ça c'est bien...";
			next;
			mes "[Miaou]";
			mes "Maintenant, je suis censé enseigner aux Sorciers de nouvelles compétences au nom de la Guilde.";
			mes "Bien que je n'en ai pas vraiment envie, je crois que je n'ai pas bien le choix. Donc, qu'est-ce que vous en dites?";
			next;
			if (select("Naan, j'suis trop crevé...:Oui, j'aimerais apprendre une compétence.") == 1) {
				mes "[Miaou]";
				mes "^333333*Bâillement*^000000";
				mes "Très bien. Si vous voulez l'apprendre plus tard, revenez me voir et rappelez-moi quelle compétence vous souhaitez apprendre. Bon, alors, je vais retourner à ma sieste... Zzz... Zzz...";
				close;
			}
			mes "[Miaou]";
			mes "Vraiment? Bien, Bien... Vous savez, j'enseigne cette compétence rien que pour Catherine me redonne ma forme humaine. C'est juste trop humiliant d'être un chat!";
			next;
			mes "[Miaou]";
			mes "Attendez, maintenant que j'y pense, ce chien, là, Maria, traîne dans les pattes de Catherine depuis bien plus longtemps et elle n'a toujours pas retrouvé sa forme humaine!";
			mes "N-non! je ne peux pas perdre espoir maintenant!";
			next;
			mes "[Miaou]";
			mes "Oh, laissons ça de côté pour le moment.";
			mes "Maintenant, vous devez bien savoir que les Sorciers ont les plus puissantes compétences d'attaque à distance mais qu'ils sont faibles dans les combats rapprochés, non?";
			next;
			mes "[Miaou]";
			mes "La nouvelle compétence que je vais vous apprendre a été spécialement étudiée pour compenser cette faiblesse en combat rapproché.";
			mes "D'abord, vous commencez l'incantation...";
			next;
			mes "[Miaou]";
			mes "Ensuite, pendant toute la durée du sort, vous serez protégé par un bouclier qui repoussera et blessera les ennemis qui s'approcheraient un peu trop près.";
			mes "N'est-ce pas l'idéal durant une bataille un peu risquée?";
			next;
			select("Je pourrais repousser l'ennemi?");
			mes "[Miaou]";
			mes "Bien sûr que vous pourrez, enfin, une fois que vous aurez appris cette compétence. Hummm... C'est vrai qu'il vous faudra avoir une bonne maîtrise de la façon dont on repousse les ennemis.";
			mes "Humm... Il vous faudra peut-être de meilleures explications...";
			next;
			mes "[Miaou]";
			mes "Vous savez quoi? Les Chevaliers sont des experts pour ce qui est des compétences qui repoussent les ennemis au loin. Alors vous devriez aller en trouver un à Prontera, afin qu'il vous explique la théorie.";
			next;
			mes "[Miaou]";
			mes "Dirigez-vous à 11h sur votre Mini-Carte, à Prontera, et vous trouverez la Chevalerie. Quelqu'un là-bas pourra vous expliquer comment marchent les compétences qui repoussent les ennemis...";
			set WIZ_SK,1;
			close;
		}
		else if (WIZ_SK == 1) {
			mes "[Miaou]";
			mes "Qu'est-ce que vous fichez encore ici? Je vous ai dit d'aller à la Chevalerie de Prontera et de demander à quelqu'un de vous enseigner la théorie sur les compétences qui repoussent les ennemis.";
			close;
		}
		else if (WIZ_SK == 2) {
			mes "[Miaou]";
			mes "Hum? Oh bien, vous avez appris les bases sur les compétences qui repoussent les ennemis. Maintenant, vous êtes presque prêt à apprendre la technique que je suis censé vous enseigner.";
			next;
			mes "[Miaou]";
			mes "Je ne suis pas un philanthrope vous savez, alors...";
			mes "J'attends quelques... dons... en échange de mes instructions!";
			mes "Mmmm, qu'est-ce qui pourrait être correct comme paiement? Ah, je suis un peu juste au niveau des pierres enchantées en ce moment...";
			next;
			mes "[Miaou]";
			mes "Allez me chercher ceci:";
			mes "^FF000010 Cristaux Bleus^000000,";
			mes "^FF000010 minerais de Brut de Terre^000000,";
			mes "^FF000010 minerais de Sang Rouge^000000 et";
			mes "^FF000010 minerais de Vent Condensé^000000.";
			next;
			mes "[Miaou]";
			mes "Ce n'est pas tant que ça, alors arrêtez de vous plaindre et revenez aussi vite que possible.";
			mes "Pendant ce temps, je vais faire une autre petite sieste...";
			set WIZ_SK,3;
			close;
		}
		else if (WIZ_SK == 3) {
			if (countitem(991) < 10 || countitem(993) < 10 || countitem(990) < 10 || countitem(992) < 10) {
				mes "[Miaou]";
				mes "^333333*Ouaaaahh~*^000000 Humm?";
				mes "C-Comment osez-vous revenir ici sans tout ce que je vous ai demandé?! Vous avez oublié ce que je voulais, c'est ça? Grrrr...";
				next;
				mes "[Miaou]";
				mes "J'ai dit qu'il me fallait:";
				mes "^ff000010 Cristaux Bleus^000000,";
				mes "^ff000010 minerais de Brut de Terre^000000,";
				mes "^ff000010 minerais de Sang Rouge^000000 et";
				mes "^ff000010 minerais de Vent Condensé^000000!";
				next;
				mes "[Miaou]";
				mes "N'oubliez pas, cette fois!";
				mes "Et ne songez même pas à me réveiller si vous n'avez pas tout ce que j'ai demandé!";
				close;
			}
			mes "[Miaou]";
			mes "*Oouuaaahhhh~* Q-Quoi?";
			mes "Oh, c'est vous. M'avez-vous rapporté toutes les pierres enchantées dont j'ai besoin?";
			mes "Hum. Bien, voyons si vous les avez toutes...";
			mes "Une, deux, trois...";
			next;
			mes "[Miaou]";
			mes ".................";
			mes "Laissons tomber. Je ne le sens pas de compter comme ça, ça risque de m'endormir à nouveau.";
			mes "Laissez-moi juste prendre les pierres et vous apprendre ce sort pour que je puisse retourner dormir.";
			next;
			mes "[Miaou]";
			mes "Cette compétence est appelée le ^FF0000Feu Gardien^000000. Plutôt cool, hein?";
			mes "Sinon, c'est un sort de Feu qui nécessite que vous utilisiez la Révélation avant de pouvoir l'activer. Maintenant, essayons.";
			next;
			specialeffect2 EF_SUI_EXPLOSION;
			mes "^3355FF*BAM! BOOM!*^000000";
			next;
			mes "[Miaou]";
			mes "......";
			mes "Humm... Non, pas exactement comme ça, mais c'est pas si mal pour un premier essai. Rappelez-vous que vous devez vraiment vouloir repousser l'ennemi. Essayez encore une fois, d'accord?";
			next;
			mes "[Miaou]";
			mes "Vous y êtes presque! Poussez! Poussez avec votre putain d'esprit! Bien, poussez avec votre esprit les flammes que vous avez invoquez avec la Révélation. Non, pas si fort!";
			next;
			mes "[Miaou]";
			mes "Enfin...!";
			mes "Vous avez pigez le truc!";
			mes "Cela vous a demandé pas mal d'efforts, mais finalement on y est arrivé.";
			specialeffect2 EF_ABSORBSPIRITS;
			delitem 991,10; //Crystal_Blue
			delitem 993,10; //Yellow_Live
			delitem 990,10; //Boody_Red
			delitem 992,10; //Wind_Of_Verdure
			set WIZ_SK,100;
			skill "WZ_SIGHTBLASTER",1,0;
			next;
			mes "[Miaou]";
			mes "Cette leçon m'a vraiment fatigué. Je pense que je vais faire une sieste bien méritée, maintenant. Allez-vous en et si vous connaissez d'autres Sorciers intéressés par cette compétence, envoyez-les moi.";
			close;
		}
		else {
			mes "[Miaou]";
			mes "Zzz... Zzz...";
				mes "Catherine... Je vous ferai regretter de m'avoir transformé en chaton... Je vais vous... Vous faire... Zzz...";
				close;
		}
	}
	else {
		mes "[Miaou]";
		mes "*Ouuaaaah~*";
		mes "Si fatiiigué...";
		mes "Hum? Vous voulez me parler? Ne me dérangez pas si vous n'avez rien à me dire!";
		next;
		if (select("Un chat?:Maintenant j'ai sommeil aussi...") == 1) {
			mes "[Miaou]";
			mes "Ouais. Ouais, Je suis un chat.";
			mes "Pour l'instant, cependant. Maudite soit cette Catherine! Elle m'a donné cette forme après que j'ai... emprunté... certaines gemmes sans sa permission.";
			mes "Mais j'allais lui demander!";
			next;
			mes "[Miaou]";
			mes "Dès que je serai redevenu humain et que j'aurai récupéré mes pouvoirs, ma vengeance sera terrible! Vous, Catherine, tout le monde y passera! Je vous changerai tous en chat!";
			close;
		}
		mes "[Miaou]";
		mes "Ha! Ouais...";
		mes "Faire une petite sieste, c'est la meilleure idée que j'ai entendue aujourd'hui. Hummm. ^333333*Ouaaaahh~*^000000";
		mes "C'est parti, c'est l'heure de dormir...";
		mes "ZzzzzzzZZZzzz... ZzZzzzz...";
		close;
	}
}

prt_in,82,98,3	script	Simon Mayace#q_wiz	46,{
	if (WIZ_SK == 1) {
		mes "[Simon]";
		mes "Ah, vous devez être un talentueux adepte des arts magiques. Hummm, vous devez être un de ces Sorciers ou Haut-Sorciers? Je n'ai jamais su faire la différence!";
		mes "Donc, que puis-je pour votre service?";
		next;
		if (select("Tout va bien, merci.:J'aimerais apprendre à repousser l'ennemi.") == 1) {
			mes "[Simon]";
			mes "Je suis surpris que vous soyez devenu magicien: avec votre physique, j'aurais plutôt pensé que vous préféreriez porter une épée. Ha Ha, c'est juste l'opinion d'un humble Chevalier!";
			close;
		}
		mes "[Simon]";
		mes "Attendez, vous me demandez de vous enseigner une technique d'attaque qui repousse l'ennemi? Mais vous pouvez vous défendre plus efficacement avec des sorts comme les Pluies de Feu ou les Murs de Flammes, non?";
		next;
		select("Oui, mais au contact...");
		mes "[Simon]";
		mes "Oh, je vois. Oui, Sorcier, ce n'est pas très efficace dans les combats au corps à corps. D'accord, je comprends mieux pourquoi vous voulez apprendre quelques techniques pour repousser l'ennemi.";
		mes "Alors, je peux au moins vous apprendre quelques bases...";
		next;
		mes "[Simon]";
		mes "De nos jours, tous les aventuriers connaissent les bases de l'attaque.";
		mes "On frappe et on prépare l'action suivante, qui est soit un autre coup, soit une esquive, soit une parade. C'est simple, non?";
		next;
		mes "[Simon]";
		mes "Pour repousser un ennemi, vous devez attaquer tout en utilisant un coup d'une force continue. Tout est dans la continuité. Maintenant, si vous visez la tête, la distance à laquelle vous repoussez l'ennemi sera moindre.";
		next;
		mes "[Simon]";
		mes "Si vous faites une poussée sur les jambes, votre ennemi tombera devant vous.";
		mes "Donc la meilleure cible pour repousser l'ennemi, c'est le torse, car cela permet de mieux contrôler la poussée.";
		next;
		mes "[Simon]";
		mes "Eh bien, je vous ai expliqué tout ce que je savais sur la manière dont on repousse un ennemi.";
		mes "Concentrez-vous et essayez de mettre la théorie en pratique par vous-même. Je suis sûr que vous y arriverez. ";
		mes "Bonne chance dans vos voyages, aventurier...";
		set WIZ_SK,2;
		close;
	}
	else {
		mes "[Simon]";
		mes "Ne vous méprenez pas: la maîtrise de l'épée est un art. Les techniques peuvent sembler primaires à première vue, mais il y a toute une stratégie derrière, et les Chevaliers la maîtrise parfaitement.";
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 for fully working skills only [Lupus]
//= 1.1 Added more new skill quests for more classes [Lupus]
//= Somehow eA engine doesn't let you keep learn't skill V_V'
//= 1.2 Added to correct locations, correct NPC's, fixed
//= some of the items required and made them into real
//= quests. [Reddozen] 
//= 1.3 Fixed bugs and minor typos. Optimized [Lupus]
//= 1.3a fixed an item ID typo, thx 2Spiritual Kid
//= 1.3b Splitted into different files [DracoRPG]
//= 1.3c Removed duplicates [Toms]
//============================================================
