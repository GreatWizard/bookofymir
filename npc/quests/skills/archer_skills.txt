//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena, corrigé par Icaros
//===== eAthena Script =======================================
//= Archer Skill Quest
//===== By: ==================================================
//= eAthena dev team
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Quest for skills: Arrow Crafting, Arrow Repel
//===== Additional Comments: ================================= 
//= 1.5 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//============================================================

moc_ruins,118,99,5	script	Roberto#qsk_ac	88,{
	if (BaseClass == Job_Archer) {
		if (getskilllv("AC_MAKINGARROW") == 1) {
			mes "[Roberto]";
			mes "Oh, vous êtes originaire de Payon, je peux le reconnaître!";
			mes "Je suis ravi de vous rencontrer!";
			mes "Comment allez-vous?";
			mes "Ah! Ces flèches!";
			mes "Vous les avez fabriquées vous-même, n'est-ce pas?";
			next;
			mes "[Roberto]";
			mes "Haha...!";
			mes "C'est vraiment pratique, vous ne trouvez pas?";
			mes "Haha... Enfin, je suis content de vous avoir appris quelque chose d'utile. Revenez me voir de temps en temps pour donner des nouvelles du pays.";
			mes "Et bien à plus tard~";
			close;
		}
		else if ((Basejob == Job_Archer && JobLevel >= 30) || BaseJob == Job_Hunter || BaseJob == Job_Dancer || BaseJob == Job_Bard) {
			mes "[Roberto]";
			mes "Hé!";
			mes " ";
			mes "Vous êtes...";
			next;
			if (countitem(907) > 19 && countitem(921) > 6 && countitem(7012) > 40 && countitem(1019) > 12 && countitem(501) > 0) {
				mes "[Roberto]";
				mes "Vous avez tout rapporté!";
				mes "Merci beaucoup.";
				mes "Et bien, comme promis, je vais vous apprendre à créer des flèches par vous-même.";
				next;
				delitem 907,20; //Resin
				delitem 921,7; //Mushroom_Spore
				delitem 7012,41; //Tough_Scalelike_Stem
				delitem 1019,13; //Wooden_Block
				delitem 501,1; //Red_Potion
				skill 147,1,0;
				mes "[Roberto]";
				mes "Vous n'avez plus à vous inquiéter de votre stock de flèches à présent.";
				mes "Oh, pendant que j'y pense, avez-vous réussi à rencontrer un dénommé Jason à Payon? Faites attention à lui, c'est un gars un peu farouche.";
				next;
				mes "[Roberto]";
				mes "Souvenez-vous de vous méfier de Jason à Payon.";
				mes "N'oubliez pas!";
				mes "Bon, et bien, à plus tard~ Merci pour les objets~";
				close;
			}
			else {
				mes "[Roberto]";
				mes "Un Archer, ici à Morroc!?";
				mes "Quel plaisir de vous voir! Rencontrer un pote Archer dans un endroit comme ça! *snif* *snif*!";
				mes "Je suis venu seul à Morroc...";
				mes "Je suis un petit nouveau dans la ville, et il y a tant de pression ici... waaah~";
				mes "Je me sens assez seul à vrai dire~";
				next;
				if (select("Ça doit être dur. Courage!:C'est vous qui l'avez choisi.") == 1) {
					mes "[Roberto]";
					mes "Oui. Merci...";
					mes "Ça ne doit pas être simple pour vous non plus. Arrivez-vous à trouver des flèches en boutique par ici? Pas vraiment, hein? Je comprends...";
					mes "C'est pourquoi je fabrique moi-même mes propres flèches.";
					next;
					select("Hein, vraiment?!");
					mes "[Roberto]";
					mes "Ouais! Je réunis différents matériaux et je fabrique des flèches avec ça.";
					mes "C'est vraiment utile si on veut survivre seul dans ce dur monde.";
					mes "Si vous voulez, je pourrais vous apprendre.";
					next;
					select("Ce serait vraiment merveilleux.");
					mes "[Roberto]";
					mes "Mais... Je ne peux pas vous apprendre cela gratuitement.";
					mes "Rien n'est gratuit dans ce monde, vous comprenez~";
					mes "Hum... Et si on faisait comme ça: vous m'apportez quelques objets dont j'ai besoin, et en échange je vous apprends cette compétence.";
					next;
					mes "[Roberto]";
					mes "Je me sens très seul depuis que j'ai quitté ma ville natale. J'aimerais arriver à soigner mon mal du pays en retrouvant des objets typiques de là-bas.";
					mes "Rapportez-moi 20 gouttes de Résine des arbres de la forêt de Payon, et 1 Potion Rouge vendue en boutique.";
					next;
					mes "[Roberto]";
					mes "Ah, et aussi 13 Troncs des Saules qui vivent près de la forêt de Payon,";
					mes "41 Tiges Semblant Faites d'Acier, et";
					mes "7 Spores de Champignons.";
					next;
					mes "[Roberto]";
					mes "Si vous m'apportez tout cela... Je vous apprendrai la compétence qui permet de créer des flèches soi-même.";
					mes "Je vous attendrai ici.";
					close;
				}
				mes "[Roberto]";
				if (Sex)
					mes "...Hé, mademoiselle!";
				else
					mes "...Hé, monsieur!";
				mes "...Faites bien attention une fois la nuit tombée...";
				close;
			}
		}
		else {
			mes "[Roberto]";
			mes "Hum... Qu'est-ce qu'il y a?";
			mes "Qu'est-ce que vous regardez?";
			close;
		}
	}
	else {
		mes "[?]";
		mes "Hé... Qu'est-ce qu'il y a? C'est la première fois que vous voyez un Archer ou quoi?";
		mes "Allez donc voir ailleurs si j'y suis. Je ne parle qu'aux Archers de haut niveau.";
		mes "Allez ouste, du balais!";
		close;
	}
}

payon,103,63,3	script	Jason#qsk_ac	88,3,3,{
	if (BaseClass == Job_Archer) {
		if (getskilllv("AC_CHARGEARROW") == 1) {
			mes "[Jason]";
			mes "Ah, nous nous rencontrons à nouveau.";
			mes "C'est étrange... À chaque fois que je revois quelqu'un, je me mets à éternu... atchoum!";
			mes "Ehhh... enfin ça fait plaisir de vous revoir.";
			mes "Ehhhh... mais euh... ne venez plus, s'il vous plaît.";
			close;
		}
		else if ((Basejob == Job_Archer && JobLevel >= 35) || BaseJob == Job_Hunter || BaseJob == Job_Dancer || BaseJob == Job_Bard) {
			mes "[Jason]";
			mes "Non... ma blessure ne guérit pas...";
			mes "Ah... J'aurais dû faire plus attention...";
			next;
			mes "Ce satané Roberto...";
			mes "Hummm... Ah!";
			mes "Vous! Depuis quand êtes-vous là?";
			mes "Hum... vous avez l'air très expérimenté.";
			mes "Quelqu'un comme vous doit sûrement être déjà capable d'utiliser des Flèches Repoussantes.";
			next;
			switch(select("Qu'est-ce que c'est que ça?:Apprenez-moi s'il vous plaît!")) {
			case 1:
				mes "[Jason]";
				mes "...Vous plaisantez, n'est-ce pas?";
				mes "Ce n'est pas possible, vous ne savez même pas ce qu'est une Flèche Repoussante, à votre niveau? Vous êtes quelqu'un de bizarre.";
				next;
				mes "[Jason]";
				mes "Bon, très bien, je vais vous expliquer.";
				next;
				mes "[Jason]";
				mes "Les Flèches Repoussantes sont une compétence qui vous permet de repousser votre ennemi s'il s'approche trop près de vous lors d'un combat.";
				mes "Vous devez bien viser votre cible, et hop! Contrairement à un sortilège magique, on ne peut pas l'annuler.";
				next;
				mes "[Jason]";
				mes "C'est très utile pour les Archers, qui ne sont pas très doués dans des combats rapprochés.";
				mes "Si vous souhaitez que je vous apprenne cette compétence, vous n'avez qu'à effectuer une petite commission pour moi, et nous serons quittes.";
				next;
				mes "[Jason]";
				mes "J'aurais besoin d'une Arbalète Lourde, et une neuve, s'il vous plaît!";
				mes "Mais également 10 Tentacules, 10 Becs d'Oiseau, 3 Queues de Yoyos... (c'est pour leur élasticité), et enfin 2 Émeraudes. Et pour finir, surtout ne pas oublier... 36 bouteilles de Jus de Bananes, j'en raffole!";
				next;
				mes "[Jason]";
				mes "......Ahem!";
				mes "Si vous me rapportez tout ça, je vous enseignerai comment utiliser une Flèche Repoussante.";
				mes "Bon et bien, à plus tard.";
				mes "(je vais devenir fou si je ne trouve pas rapidement du jus de bananes...)";
				close;
			case 2:
				if (countitem(721) > 1 && countitem(942) > 2 && countitem(962) > 9 && countitem(925) > 9 && countitem(532) > 35) {
					mes "[Jason]";
					mes "Ok! C'est parfait!";
					mes "Je vais vous apprendre la célèbre compétence 'Flèche Repoussante'!";
					next;
					delitem 721,2; //Azure_Jewel
					delitem 942,3; //Yoyo_Tail
					delitem 962,10; //Tentacle
					delitem 925,10; //Bill_Of_Birds
					delitem 532,36; //Banana_Juice
					skill 148,1,0;
					mes "[Jason]";
					mes "Oh, vous apprenez plus vite que je ne l'aurais imaginé!";
					mes "Finalement je n'aurais pas besoin d'Arbalète Lourde, vous pouvez la garder~";
					mes "Faites bon usage de votre nouvelle compétence!";
					mes "Hé~ Hé~!";
					close;
				}
				else {
					mes "[Jason]";
					mes "Hum... Quel dommage.";
					mes "Il vous manque des choses. Je vous répète la liste: j'aurais besoin de 2 Émeraudes, 3 Queues de Yoyos, 10 Tentacules, 10 Becs d'Oiseau, et 36 bouteilles de Jus de Bananes!";
					next;
					mes "[Jason]";
					mes "Ah, et n'oubliez pas l'Arbalète Lourde! Toute neuve!";
					close;
				}
			}
		}
		else {
			mes "[Jason]";
			mes "Oh... Vous êtes un jeune Archer. Essayez de vous entraîner encore un peu!";
			close;
		}
	}
	else {
		mes "[?]";
		mes "Quelle valeur à la vie pour un misérable comme moi?";
		close;
	}

OnTouch:
	mes "[???]";
	mes "Errrrrrr...";
	close;
}

//============================================================ 
// Old changelog
//============================================================ 
//= v1.0 Roberto message text is based off RO npc.  Jason message
//= text is custom from old version.
//= All items are from official quests though.[kobra_k88]
//= v1.0a Now using functions found in "Global_Functions.txt"
//= for class checks.[kobra_k88]
//= 1.2 Added Baby Class Support [Lupus]
//= 1.3 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 1.4 Fixed exploits [Lupus]
//= 1.4a Fixed some typos [IVBela]
//= 1.4b changed perm. variables to temp ones [Lupus]
//============================================================
