//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par NoH
//===== eAthena Script ======================================= 
//= Blacksmith Skills Quests
//===== By: ================================================== 
//= Lupus, Reddozen, Samuray22
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Quests for skills: Dubious Salesmanship, Greed
//===== Additional Comments: ================================= 
//= 1.6 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//============================================================

geffen,178,72,3	script	Akkie#qsk_bs	726,{
	if (BaseJob == Job_Blacksmith) {
		if (BLACK_SK == 7 && getskilllv("BS_UNFAIRLYTRICK") == 0) {
			mes "[Akkie]";
			mes "Je vois que vous avez effectué votre renaissance depuis la dernière fois que nous nous sommes rencontrés. Oh... Vous devez avoir oublié tout ce que je vous ai enseigné. Eh bien, nous allons recommencer. ~";
			next;
			mes "[Akkie]";
			mes "Laissez-moi vous rafraîchir la mémoire avec ce faux zény comme nous avons fait la dernière fois. Vous vous rappelez comment forger de la Fausse Monnaie maintenant? Vous devriez désormais en être capable...";
			set BLACK_SK,8;
			skill "BS_UNFAIRLYTRICK",1,0;
			close;
		}
		else if (BLACK_SK == 8) {
			mes "[Akkie]";
			mes "Bonjour~";
			mes "Comment va la forge? J'apprécie que vous vous arrêtiez pour me saluer. ~";
			next;
			mes "[Akkie]";
			mes "Je sais que je suis un peu ennuyante parfois quand je donne des cours sur la Fausse Monnaie, mais quelqu'un dans la guilde doit bien s'en charger...";
			mes "Quoi qu'il en soit... A bientôt, et bonne chance dans vos aventures, d'accord?";
			close;
		}
		if (JobLevel > 29) {
			if (BLACK_SK == 0) {
				mes "[Akkie]";
				mes "Bonjour~";
				mes "N'est-ce pas une belle journée? Parfait pour la chasse!";
				next;
				mes "[Akkie]";
				mes "Hum, laissez-moi me présenter. Je m'appelle Akkie, je suis la représentante de la Guilde des Forgerons qui a été envoyée pour apprendre à forger de la Fausse Monnaie à nos membres.";
				next;
				mes "[Akkie]";
				mes "Cette technique vous permet de réduire de 10% le nombre de zénys dépensés quand vous utilisez Mammonite. C'est une compétence passive qui pourrait vous être utile, "+ strcharinfo(0) +".";
				next;
				mes "[Akkie]";
				mes "Même si je viens de commencer, est-ce que vous voudriez que je vous apprenne à forger de la Fausse Monnnaie?";
				next;
				if (select("Bien sûr.:Non merci.") == 1) {
					mes "["+ strcharinfo(0) +"]";
					mes "Bien sûr. J'essaierai d'apprendre cette nouvelle compétence. Qui sait? Ça pourrait être utile.";
					next;
					mes "[Akkie]";
					mes "Oh, fantastique! Vous ne le regretterez pas! Maintenant, laissez-moi vous expliquer le principe de cette compétence. Uh, où est-ce que j'ai fourré le... Ah, laissez-moi relire les  directives.";
					next;
					mes "[Akkie]";
					mes "En utilisant de la Fausse Monnaie, vous pourrez réduire le coût en zénys de Mammonite de 10%. Puisque c'est pour utiliser contre des ennemis, nous pouvons nous le permettre...";
					next;
					mes "[Akkie]";
					mes "Maintenant, il va falloir que vous me rapportiez quelques matières premières afin que je puisse vous montrer comment forger de faux zénys. S'il vous plaît, rapportez-moi 1 morceau d'Acier, 5 morceaux de Charbon, 1 Marteau de Fer et... et...";
					next;
					mes "[Akkie]";
					mes "Hum... Est-ce que vous pourriez me laisser une minute, le temps que je regarde? Je suis désolée...";
					mes "^333333(Je pensais pourtant avoir souligné ça dans le manuel... Où est-ce que c'est?)^000000";
					next;
					mes "[Akkie]";
					mes "Oh! Oh, j'ai trouvé! Laissez-moi reprendre depuis le début. Euh...";
					next;
					mes "["+ strcharinfo(0) +"]";
					mes "............";
					next;
					mes "[Akkie]";
					mes "^0000FF1 morceau d'Acier^000000,";
					mes "^0000FF5 morceaux de Charbon^000000,";
					mes "^0000FF1 Marteau de Fer^000000,";
					mes "^0000FF500 zénys^000000 et";
					mes "^0000FF1 fiole de Détrimindexta^000000.";
					mes "Est-ce que vous pourriez me rapporter tout cela?";
					next;
					mes "["+ strcharinfo(0) +"]";
					mes "Bien sûr, bien sûr. Mais est-ce que vous pourrez vraiment m'enseigner cette compétence? Je veux dire...";
					next;
					mes "[Akkie]";
					mes "Oh non, je savais que vous alliez me demander ça! Je veux dire, hum... Ça devrait aller! Ça fait trois que je travaille et... Enfin... Si vous me rapportez tout ça, ça ira. Vous verrez vous pourrez apprendre cette compétence très facilement!";
					next;
					mes "[Akkie]";
					mes "Vous devez me faire confiance! Vous ne serez pas déçu"+ ( sex ? "" : "e" ) +", je vous le promets! Cette compétence est tellllllement utile! Rapportez-moi tout cela, et vous verrez!";
					set BLACK_SK,1;
					close;
				}
				mes "[Akkie]";
				mes "Qu... Quoi?! Mais pourquoi pas? Je... Je ne suis pas assez bonne? S'il vous plait... Je vous promets de donner le meilleur de moi-même! Et puis... Je dois remplir mon cota pour la guilde...";
				close;
			}
			else if (BLACK_SK == 1) {
				if (countitem(999) > 0 && countitem(1003) > 4 && countitem(971) > 0 && countitem(613) > 0) {
					if (Zeny >= 500) {
						mes "[Akkie]";
						mes "Oh, vous revoilà! ~";
						mes "Voyons si vous m'avez rapporté tout ce qu'il faut pour forger de faux zénys.";
						next;
						mes "[Akkie]";
						mes "Bien joué! On dirait que vous avez tout ce qu'il faut. Patientez un peu en attendant que je prépare tout ça, d'accord?";
						delitem 1003,5; //Coal
						delitem 999,1; //Steel
						delitem 971,1; //Detrimindexta
						delitem 613,1; //Iron_Hammer
						set zeny,zeny-500;
						set BLACK_SK,2;
						close;
					}
					else {
						mes "[Akkie]";
						mes "Hum... Vous avez tous les matériaux, mais il me faut toujours 500 zénys. J'en ai besoin pour servir de modèle pour forger de fausses pièces.";
						close;
					}
				}
				else {
					mes "[Akkie]";
					mes "Oh, vous revoilà! ~";
					mes "Voyons si vous m'avez rapporté tout ce qu'il faut pour forger de faux zénys.";
					next;
					mes "[Akkie]";
					mes "Hum... Il vous manque encore quelques matériaux. Laissez-moi vous redonner la liste de ce qu'il faut me rapporter. Faites vite, pour que nous puissions commencer l'entraînement sans attendre.";
					next;
					mes "[Akkie]";
					mes "^0000FF1 morceau d'Acier^000000,";
					mes "^0000FF5 morceaux de Charbon^000000,";
					mes "^0000FF1 Marteau de Fer^000000,";
					mes "^0000FF500 zénys^000000 et";
					mes "^0000FF1 fiole de Détrimindexta^000000.";
					mes "N'oubliez pas, d'accord?";
					close;
				}
			}
			else if (BLACK_SK == 2) {
				mes "[Akkie]";
				mes "Ah! Vous êtes revenu"+ ( sex ? "" : "e" ) +" plus rapidement que je ne le pensais. Hum... Par où est-ce que je devrais commencer?";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes "...";
				mes "......";
				mes ".........";
				next;
				mes "[Akkie]";
				mes "Je suis désolée! J'ai fait quelque chose de travers pendant que je forgeais les faux zénys et... tout est perdu. Je suis tellement désolée! S'il vous plaît, n'en parlez pas à la Guilde! Donnez-moi une autre chance!";
				next;
				mes "[Akkie]";
				mes "Je vous promets que ça n'arrivera pas la prochaine fois! J'aurais juste besoin de 3 autres morceaux de charbon et d'un morceau d'Acier! Ne vous fâchez pas, s'il vous plaît! C'est juste un accident...";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes "D'accord, d'accord... Vous êtes sûre que c'est tout ce dont vous avez besoin?";
				next;
				mes "[Akkie]";
				mes "Oui, oui! Oh, merci pour votre générosité! Rapportez-moi simplement ^FF00001 morceau d'Acier^000000 et ^FF00003 morceaux de Charbon^000000 et nous reprendrons tout depuis le début!";
				set BLACK_SK,3;
				close;
			}
			else if (BLACK_SK == 3) {
				if (countitem(999) > 0 && countitem(1003) > 2) {
					mes "[Akkie]";
					mes "Oh, merci de m'avoir rapporté tout cela. Bon, je vais faire de mon mieux pour y arriver cette fois. Laissez-moi une minute, le temps de me préparer.";
					delitem 999,1; //Steel
					delitem 1003,3; //Coal
					set BLACK_SK,4;
					close;
				}
				else {
					mes "[Akkie]";
					mes "J'aurais juste besoin que vous me rapportiez 1 morceau d'Acier et 3 morceaux de Charbon. Ensuite, je devrais pouvoir vous apprendre à forger de la Fausse Monnaie.~";
					close;
				}
			}
			else if (BLACK_SK == 4) {
				mes "[Akkie]";
				mes "......";
				mes ".........";
				mes "............";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes "...?";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes "Oh non...";
				mes "Est-ce que... Est-ce que...";
				mes "Quelque chose est encore arrivé?";
				next;
				mes "[Akkie]";
				mes "*Snif* J'ai... J'ai cassé le Marteau de Fer que vous m'aviez donné! Je ne peux pas finir le travail sans marteau! Waaah~";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes "^333333(Bon, j'imagine que je vais devoir lui apporter un autre  ^FF0000Marteau de Fer^333333, ou je ne pourrai jamais apprendre cette fichue compétence!)^000000";
				set BLACK_SK,5;
				close;
			}
			else if (BLACK_SK == 5) {
				if (countitem(613) > 0) {
					mes "[Akkie]";
					mes "^333333*Snif*^000000 Vous m'avez vraiment rapporté un autre Marteau de Fer? Mer... Merci d'être si patient"+ ( sex ? "" : "e" ) +" avec moi!";
					next;
					mes "[Akkie]";
					mes "Bon, est-ce que vous pourriez me laisser un moment? Je vais réessayer.";
					delitem 613,1; //Iron_Hammer
					set BLACK_SK,6;
					close;
				}
				else {
					mes "[Akkie]";
					mes "Waah~ Je n'arrive pas à croire que j'ai cassé le Marteau de Fer. Est-ce que vous pourriez m'en rapporter un autre pour que je puisse terminer le travail et vous apprendre à faire de la fausse monnaie?";
					close;
				}
			}
			else if (BLACK_SK == 6) {
				mes "[Akkie]";
				mes "Oh, vous revoilà! Ouiiii, j'y suis finalement arrivé! J'ai réussi à forger de la fausse monnaie, et je l'ai même testée avec Mammonite!";
				next;
				mes "[Akkie]";
				mes "Oh, avant que je ne vous apprenne cette compétence, je suis censée vous parler de mon collègue, M. Bonnard. Il pourra vous apprendre à maîtriser votre Cupidité et à la tourner à votre avantage. Vous devriez aller lui parler ensuite";
				next;
				mes "[Akkie]";
				mes "Bref, revenons à nos moutons. Tout d'abord, je vais vous apprendre à forger de faux zénys. Regardez ça... N'est-ce pas facile? Mais bien sûr, vous ne pourrez les utiliser que pendant le combat avec Mammonite, pas en ville pour vous payer la dernière hache à la mode!";
				set BLACK_SK,7;
				skill "BS_UNFAIRLYTRICK",1,0;
				next;
				mes "[Akkie]";
				mes "Et puis les neuf dixièmes des zénys utilisés avec Mammonite devront tout de même être vrais, ou cette compétence perdra de son efficacité. Bon, c'est tout pour la leçon. Vous pouvez maintenant utiliser de la Fausse Monnaie!";
				next;
				mes "[Akkie]";
				mes "Encore merci de m'avoir pardonné mes erreurs. Je sais que j'ai vraiment dû être un boulet... J'espère que cette technique vous sera utile.";
				mes "Revenez me voir à l'occasion, d'accord?";
				close;
			}
			else {
				if (rand(1,2) == 1) {
					mes "[Akkie]";
					mes "Oh, est-ce que vous avez déjà parlé à M. Bonnard à Geffen? C'est le représentant de la Guilde des Forgerons qui est en charge de vous apprendre à tourner votre Cupidité à votre avantage.";
					next;
					mes "[Akkie]";
					mes "Si vous recherchez vraiment des matériaux pour la forge, la maîtrise de votre Cupidité pourra vous être très utile. Je sais que cette compétence a mauvaise réputation mais... Il ne faut pas se fier à son nom~";
					close;
				}
				else {
					mes "[Akkie]";
					mes "Bonjour~";
					mes "Comment va la forge? J'apprécie que vous vous arrêtiez pour me saluer. ~";
					next;
					mes "[Akkie]";
					mes "Je sais que je suis un peu ennuyante parfois quand je donne des cours sur la Fausse Monnaie, mais quelqu'un dans la guilde doit bien s'en charger...";
					mes "Quoi qu'il en soit... A bientôt, et bonne chance dans vos aventures, d'accord?";
					close;
				}
			}
		}
		else {
			mes "[Akkie]";
			if (Upper == 1) {
				mes "Bonjour, "+ strcharinfo(0) +". J'ai été envoyée par la Guilde des Forgerons pour apprendre une nouvelle technique à nos membres, mais votre niveau n'est pas encore assez élevé pour que je puisse déjà vous l'enseigner...";
				next;
				mes "[Akkie]";
				mes "Continuez à pratiquer la forge, et une fois que vous serez prêt"+ ( sex ? "" : "e" ) +", je pourrai vous enseigner cette technique. Au revoir, et bonne chance!~";
			}
			else {
				mes "Bonjour. Je m'appelle Akkie - je suis de la Guilde des Forgerons. Je suis ici pour enseigner une nouvelle technique aux Forgerons, mais on dirait que votre Niveau de Métier est encore trop bas...";
				next;
				mes "[Akkie]";
				mes "Je suis désolée, "+ strcharinfo(0) +", mais je ne pourrai vous l'apprendre que lorsque vous maîtriserez un peu mieux votre métier. Continuez à vous entraîner en attendant, d'accord?";
			}
			close;
		}
	}
	else {
		mes "[Akkie]";
		mes "Ah, quelle belle journée pour la forge! J'aurais envie d'aller derrière une magnifique cascade, d'emporter du matériel de haute qualité, et de me mettre à faire virevolter mon marteau!";
		close;
	}
}

geffen,172,52,1	script	M. Bonnard::Goodman#qsk_bs	826,{
	if (BaseJob == Job_Blacksmith) {
		if (BLACK_SK2 == 2 && Upper == 1 && getskilllv("BS_GREED") == 0) {
			mes "[M. Bonnard]";
			mes "Ah, Je vois que avez effectué votre renaissance. Ça me réjouit de voir des Forgerons devenir Maîtres afin d'améliorer leur art.";
			next;
			mes "[M. Bonnard]";
			mes "Hum? Vous avez oublié comment maîtriser votre Cupidité? Regardez-moi, ça devrait vous rafraîchir la mémoire. Vous devriez en être capable maintenant~";
			skill "BS_GREED",1,0;
			set BLACK_SK2,3;
			close;
		}
		else if (BLACK_SK2 == 3) {
			mes "[M. Bonnard]";
			mes "Comment va la forge? Vous savez, lorsque vous vous sentez frustré"+ ( sex ? "" : "e" ) +" par l'artisanat, il n'y a rien de tel que de reprendre la route et de voir du pays pour vous changer les idées. Et puis vous pourrez rapporter de vos voyages des matériaux rares pour forger de nouvelles choses...";
			close;
		}
		else {
			if (BLACK_SK2 == 0) {
				mes "[M. Bonnard]";
				mes "Hé! Je suis un expert en techniques de Forgerons envoyé par la Guilde. Tout le monde m'appelle Bonnard. Vous êtes "+( Upper == 1 ? "Maître" : "" )+". Forgeron, n'est-ce pas?";
				mes "Pffft, c'est juste une question rhétorique.";
				next;
				mes "[M. Bonnard]";
				mes "J'ai de bonnes nouvelles. J'ai pour tâche d'enseigner une nouvelle technique développée par les Guildes des Marchands et des Forgerons. Il s'agit d'apprendre à maîtriser votre ^FF0000Cupidité^000000 pour l'employer à votre avantage. C'est à vous de décider si vous voulez l'apprendre ou non, mais je pense que ça serait une bonne idée de le faire.";
				next;
				mes "[M. Bonnard]";
				mes "Si vous le désirez, je peux vous enseigner à maîtriser votre Cupidité sans attendre. Qu'en dites-vous, "+ ( sex ? "jeune homme" : "jeune "+ callfunc("ster") ) +"?";
				next;
				if (select("Je suis intéressé"+ ( sex ? "" : "e" ) +"!:Plus tard, peut-être~") == 1) {
					mes "[M. Bonnard]";
					mes "Hé! Vous avez fait le bon choix! Maîtriser votre Cupidité vous permettra de ramasser tous les objets dans un rayon de 2 cases autour de vous. C'est très utile pour récupérer d'un coup tous les objets trouvés sur les monstres.";
					next;
					mes "[M. Bonnard]";
					mes "Mais commençons par le commencement. Je dois d'abord vous tester afin de voir si vous êtes prêt"+ ( sex ? "" : "e" ) +" à apprendre cette compétence. Allez remplir votre inventaire jusqu'à ce qu'il soit si plein que vous ne puissiez même plus y caser un objet pesant 500.";
					next;
					mes "[M. Bonnard]";
					mes "Revenez me voir lorsque ce sera fait. Bonne chance~";
					set BLACK_SK2,1;
					close;
				}
				mes "[M. Bonnard]";
				mes "Je vois. Être occupé"+ ( sex ? "" : "e" ) +" est toujours bon signe, mais n'oubliez pas que le temps est un bien précieux qui ne devrait être consacré qu'aux choses les plus importantes, c'est compris?";
				close;
			}
			else if (BLACK_SK2 == 1) {
				set .@now_weight,MaxWeight-Weight;
				if (.@now_weight < 5000) {
					mes "[M. Bonnard]";
					mes "Déjà de retour, hein? Bon, laissez-moi voir ce que vous trimballez là. Voyons, voyons...";
					next;
					mes "[M. Bonnard]";
					mes "Bien joué! Vous avez réussi. Vous rendez-vous compte que le temps passé à amasser tous ces objets vaut plus que leur valeur en zénys?";
					next;
					mes "[M. Bonnard]";
					mes "Je crois que les "+ ( Upper == 1 ? "Maîtres" : "" ) +" Forgerons devraient être d'abord et avant tout des artisans. Devenir riche et célèbre n'est rien de plus qu'une conséquence indirecte. Les "+ ( Upper == 1 ? "Maîtres" : "" ) +" Forgerons les plus honorables savent bien cela et placent leur orgueil dans leur art.";
					next;
					if (Upper == 1) {
						mes "[M. Bonnard]";
						mes "Maintenant, si vous n'accordez pas trop d'importance au prix des objets, vous pourrez les abandonner beaucoup plus facilement. Regardez-moi. Vous voyez? Vous pouvez aussi le faire maintenant.";
						next;
					}
					mes "[M. Bonnard]";
					mes "Vous venez d'apprendre à maîtriser votre Cupidité. N'oubliez pas que j'attends de vous que vous que vous créiez de véritables chefs d'oeuvre.";
					skill "BS_GREED",1,0;
					set BLACK_SK2,2;
					close;
				}
				else {
					mes "[M. Bonnard]";
					mes "Hum. Vous êtes revenu"+ ( sex ? "" : "e" ) +" trop tôt. Vous n'avez pas atteint l'objectif que j'avais fixé pour vous. Je vais vous le répéter, faites bien attention.";
					next;
					mes "[M. Bonnard]";
					mes "Allez remplir votre inventaire jusqu'à ce qu'il soit si plein que vous ne puissiez même plus y caser un objet pesant 500. Revenez me voir lorsque ce sera fait.";
					close;
				}
			}
			else {
				mes "[M. Bonnard]";
				mes "Comment est-ce que cela va depuis la dernière fois? Est-ce que la maîtrise de votre Cupidité vous a rendu service? J'espère que ça vous aura aidé à trouver de quoi forger des objets de haute qualité pour vos compagnons.";
				close;
			}
		}
	}
	mes "[M. Bonnard]";
	mes "J'aurais envie d'explorer les montagnes à la recherche de matériaux rares et intéressants, que je pourrais utiliser pour la forge. C'est mon plus grand plaisir en tant que Forgeron.";
	close;
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 for fully working skills only [Lupus]
//= 1.1 Added more new skill quests for more classes [Lupus]
//= Somehow eA engine doesn't let you keep learn't skill V_V'
//= 1.2 Added to correct locations, correct NPC's, fixed
//= some of the items required and made them into real
//= quests. [Reddozen] 
//= 1.3 Fixed bugs and minor typos. Optimized [Lupus]
//= 1.3a fixed an item ID typo, thx 2Spiritual Kid
//= 1.3b Splitted into different files [DracoRPG]
//= 1.3c Inverted the weight check for Greed [DracoRPG]
//= 1.4 Updated to the official One [Samuray22]
//= 1.5 replaced item "names" with item id. Fixed a bug with
//=		item deletion [Lupus]
//============================================================
