//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing, corrigé par Icaros
//===== eAthena Script ======================================= 
//= Hunter Skills Quests
//===== By: ================================================== 
//= Lupus, Reddozen, Kisuka
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= eAthena Revision 3800+
//===== Description: ========================================= 
//= Temp quests for new skills for 2nd classes
//===== Additional Comments: ================================= 
//= 1.0 for fully working skills only [Lupus]
//= 1.1 Added more new skill quests for more classes [Lupus]
//= Somehow eA engine doesn't let you keep learn't skill V_V'
//= 1.2 Added to correct locations, correct NPC's, fixed
//= some of the items required and made them into real
//= quests. [Reddozen] 
//= 1.3 Fixed bugs and minor typos. Optimized [Lupus]
//= 1.3a fixed an item ID typo, thx 2Spiritual Kid
//= 1.3b Splitted into different files [DracoRPG]
//= 1.3c Fixed some typos [IVBela]
//= 1.3d Changed NPC Name to the official [Lupus]
//= 1.4	Moved Arpesto from tu_archer and added his script. [Kisuka]
//============================================================


//============================================================
// HUNTER SKILL - PHANTASMIC ARROW
//============================================================
pay_arche,109,169,3	script	Arpesto	712,{
	if (Class == Job_Hunter || Class == Job_Sniper) {
		if (qskill_hunter == 100) {
			if (getskilllv("HT_PHANTASMIC")) {
				mes "[Arpesto]";
				mes "Hum, je suis désolé d'avoir réagi comme je l'ai fait quand tu m'as blessé par accident avec cette attaque. C'est juste que... j'étais en train de m'entraîner, alors avec la fatigue... le manque de sucre dans le sang... Je l'ai mal pris.";
				next;
				mes "[Raiden Karus]";
				mes "Ha! Admets-le, vieil homme, tu avais peur, tout simplement!";
				mes "Quant à moi...";
				donpcevent "Raiden Karus#tu::OnKik";
				next;
				mes "[Arpesto]";
				mes "Qu'est-ce que tu es en train de raconter, jeune freluquet? Moi, avoir peur?!";
				close;
			}else{
				mes "[Arpesto]";
				mes "Je vous ai déjà vu quelque part, j'en suis certain. Raiden, ce type te dit quelque chose, non?";
				next;
				mes "[Raiden Karus]";
				mes "Attends, on voit passer des centaines d'Archers et de Chasseurs, sans parler des Tireurs d'Élite! Comment veux-tu que je me souvienne de tout le monde?! En tous cas, il a l'air en forme, tu peux me faire confiance là-dessus!";
				next;
				mes "[Arpesto]";
				mes "D'accord, d'accord. Si Raiden pense que vous en valez la peine, alors c'est probablement le cas. J'ai envie de vous enseigner ma technique secrète: la 'Flèche Fantasmagorique.'";
				next;
				mes "[Raiden Karus]";
				mes "Attends, attends. Ça y est, je me souviens, ce type a déjà appris cette compétence. Il suffit de lui montrer une nouvelle fois, ça va lui revenir.";
				next;
				mes "[Arpesto]";
				mes "Mince, il suffit que je lui remontre une fois? Dans ce cas ça sera gratuit...";
				mes "Bon regardez bien. Vous devriez être capable de tirer une 'Flèche Fantasmagorique' maintenant.";
				mes "Faites bien attention à vous, d'accord?";
				skill 1009,1,0;
				close;
			}
		}
		else if (qskill_hunter == 1) {
			if (countitem(724) > 4 && countitem(7115) > 4 && countitem(537) > 29) {
				mes "[Arpesto]";
				mes "Hum? Vous revoilà! Est-ce que vous avez tout rapporté? Ah, bien~";
				mes "Je vais maintenant vous enseigner ma technique secrète...";
				mes "La Première Forme d'Arpesto!";
				next;
				mes "[Raiden Karus]";
				mes "Attends! Attends... Tu ne l'appelais pas la Troisième Forme d'Arpesto ou un truc dans le genre avant?";
				donpcevent "Raiden Karus#tu::OnSwt";
				next;
				mes "[Arpesto]";
				mes "Euh, oui, la Troisième Forme d'Arpesto, c'est vrai. C'est juste que... Il y en a tellement maintenant... Bon, je vais vous enseigner la compétence 'Flèche de la Dernière Chance'.";
				next;
				mes "[Raiden Karus]";
				mes "Nan mais attends! Ça c'est l'ancien nom que tu lui avais donné, non?";
				emotion 23;
				next;
				emotion 23;
				mes "[Arpesto]";
				mes "Qu'est-ce que tu es en train de raconter?";
				next;
				mes "[Arpesto]";
				mes "Oh, oui, c'est vrai. On s'était dit que ce nom était très moyen. Comment est-ce qu'on avait dit qu'on l'appellerait maintenant? Euh... Ah, oui! La 'Flèche Fantasmagorique?'";
				emotion 9;
				next;
				mes "[Arpesto]";
				mes "...";
				mes "......";
				emotion 28;
				next;
				mes "[Raiden Karus]";
				mes "Hum....";
				mes "Hummm........";
				next;
				mes "[Raiden Karus]";
				mes "Oui, c'est ça! La '^3131FFFlèche Fantasmagorique^000000' sonne quand même vachement mieux que l'autre nom que tu nous avais pondu!";
				donpcevent "Raiden Karus#tu::OnIc";
				next;
				mes "[Arpesto]";
				mes "Eh bien tu vois que des fois tu sers à quelque chose! Bref, revenons-en au sujet. Je vais maintenant vous enseigner ma super compétence: la Flèche Fantasmagorique. Bien, préparez-vous mentalement et physiquement!";
				next;
				mes "[Arpesto]";
				mes "Phan-!";
				mes "Ta-!";
				mes "Ssssss-!";
				mes "Mic-!";
				next;
				mes "[Arpesto]";
				mes "ARRRRRRRROOOOOOW~!";
				specialeffect EF_FLASHER;
				specialeffect2 EF_FLASHER;
				next;
				mes "[Arpesto]";
				mes "^333333*Souffle Souffle*^000000";
				mes "Voilà, je viens d'utiliser une autre de mes techniques secrètes pour vous enseigner la compétence, directement dans votre esprit. Vous devriez pouvoir l'utiliser maintenant. Allez-y, essayez!";
				next;
				mes "^3355FF*Swwwwwww!*";
				mes "*Bang!*^000000";
				specialeffect EF_HIT2;
				emotion 23;
				emotion 23,1;
				next;
				mes "[Arpesto]";
				mes "^333333*Souffle Souffle*";
				mes "*Tousse Tousse*";
				mes "*Tousse Tousse*^000000";
				next;
				mes "[Arpesto]";
				mes "POURQUOI MOIIIII?!";
				donpcevent "Raiden Karus#tu::OnOmg";
				next;
				mes "^3355FF*Baffe*^000000";
				next;
				mes "[Raiden Karus]";
				mes "Ar-Arpesto, non!";
				mes "...........................";
				mes "";
				mes "Oh. Oh, Allez! C'est juste une égratignure! Ça n'est rien, arrête de faire ton bébé!";
				next;
				mes "[Raiden Karus]";
				mes "Eh, ne vous en faites pas pour Arpesto. C'est juste un vieux fou sénile. Je m'en occupe. Amusez-vous bien avec cette nouvelle compétence. À la prochaine~";
				delitem 724,5;
				delitem 7115,5;
				delitem 537,30;
				set qskill_hunter,100;
				skill 1009,1,0;
				close;
			}else{
				mes "[Arpesto]";
				mes "Mais dépêchez-vous, enfin! Ramenez-moi";
				mes "^3131FF5 Rubis Maudits^000000,";
				mes "^3131FF30 portions de Nourriture pour Animal de Compagnie^000000 et";
				mes "^3131FF5 Plumes de Harpies^000000.";
				mes "Je pourrai ensuite vous enseigner comment utiliser la 'Flèche de la Dernière Chance'.";
				close;
			}
		}
		else {
			if (getskilllv("HT_PHANTASMIC")) {
				mes "[Arpesto]";
				mes "Ah, c'est vous. Vous m'avez démontré plus tôt que ma technique d'enseignement était parfaite. Mais s'il vous plaît, la prochaine fois, utilisez cette compétence sur un ennemi qui vous menace, hein, pas sur moi! On est d'accord? Très bien.";
				set qskill_hunter,100;
				close;
			}
			if (JobLevel < 40) {
				mes "[Arpesto]";
				mes "Est-ce que c'est le maître qui vous envoie? Hum, vous avez l'air bien jeune à première vue. Je suis désolé mais je ne peux rien enseigner à un jeunot comme vous.";
				next;
				mes "[Arpesto]";
				mes "Vous devriez aller vous entraîner encore un peu pour vous habituer à votre profession. Quand vous serez prêt je pourrai alors vous enseigner mes techniques spéciales.";
				close;
			}else{
				mes "[Arpesto]";
				mes "Le maître vous envoie? Ah, vous avez les yeux perçants d'un vrai Chasseur! Vous devriez être à même d'apprendre ma technique spéciale: la Troisième Forme d'Arpesto...";
				next;
				mes "[Arpesto]";
				mes "C'est une technique très utile que j'ai créée un jour où je combattais des Méduses et que je me suis retrouvé à court de flèches. Je l'appelle tout simplement '^3131FFla Flèche de la Dernière Chance^000000'. Est-ce que vous voudriez l'apprendre?";
				next;
				if(select("Oui, s'il vous plaît!:Hummmm...") == 1) {
					mes "[Arpesto]";
					mes "Bien, bien! Je savais que vous ne pourriez pas résister, et que vous apprécieriez les compétences à leur juste valeur. Cependant, j'aimerais vous demander un petit quelque chose en échange de mes enseignements.";
					next;
					mes "[Arpesto]";
					mes "Même si je n'aime pas demander à un Chasseur comme moi qu'il me paie, cette compétence m'a demandé tant de sacrifices et m'a même blessé dans ma chair...";
					next;
					mes "[Raiden Karus]";
					mes "Eh! Espèce de menteur! J'étais là quand tu as inventé cette technique! Tu t'ennuyais et tu étais en train de faire l'idiot avec ton arc!";
					donpcevent "Raiden Karus#tu::OnKik";
					next;
					emotion 23;
					mes "[Arpesto]";
					mes "^333333*Ahem*^000000 Raiden, s'il te plaît, si c'est pour raconter des idioties comme ça tu ferais mieux de te taire. Ce n'est qu'après d'innombrables batailles contre Eddga que j'ai réussi à créer et perfectionner la 'Flèche de la Dernière Chance'.";
					next;
					mes "[Raiden Karus]";
					mes "^333333*Tss tss*^000000 Huh. Tu n'avais pas dit que tu l'avais créée quand tu te battais contre des Méduses?";
					next;
					mes "[Arpesto]";
					mes "Quoi qu'il en soit, ramenez-moi les objets suivants en paiement pour mes enseignements.";
					next;
					mes "[Arpesto]";
					mes "Rapportez-moi";
					mes "^3131FF5 Rubis Maudits^000000,";
					mes "^3131FF30 portions de Nourriture pour Animal de Compagnie^000000 et";
					mes "^3131FF5 Plumes de Harpies^000000.";
					mes "Je vous enseignerai ensuite cette compétence.";
					next;
					mes "[Raiden Karus]";
					mes "30 portions de nourriture pour animaux domestiques? Qu'est-ce que tu vas faire de ça?";
					next;
					mes "[Arpesto]";
					mes "Je...";
					mes "Je les donnerai à manger à mon faucon domestique.";
					next;
					mes "[Raiden Karus]";
					mes "...";
					mes "......";
					mes ".........";
					mes "......Ouais, c'est ça...";
					donpcevent "Raiden Karus#tu::OnThink";
					set qskill_hunter,1;
					close;
				}
				mes "[Arpesto]";
				mes "Eh bien, c'est vous qui choisissez. Mais je suis sûr qu'un jour ou l'autre ma technique vous aurait rendu un fier service. Vous pourrez toujours revenir me voir si vous changez d'avis.";
				close;
			}
		}
	}else{
		mes "[Arpesto]";
		mes "Le ciel est toujours dégagé mais des vents portant avec eux malheur et désespoir ont commencé à souffler sur nos terres...";
		next;
		donpcevent "Raiden Karus#tu::OnHng";
		mes "[Raiden Karus]";
		mes "Alors quoi? Le danger arrive? Relax, papi, je m'en charge!";
		next;
		emotion 19;
		mes "[Arpesto]";
		mes "Oh oh... Ce gamin est énergique, mais il n'a aucun savoir-vivre.";
		mes "Les jeunes, de nos jours...";
		close;
	}
}
