//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena
//===== eAthena Script =======================================
//= Juice Maker Quest
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 2.1
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= [Aegis COnversion]
//= Learn where to turn apples, bananas, carrots, and grapes
//= into juice.
//= Condition if done: (MISC_QUEST & 1)
//===== Additional Comments: =================================
//= 1.9 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=	Added tracking variables in, which are unset when complete.
//= 2.0 Added missing checkweight. [L0ne_W0lf]
//= 2.1 Fixed dialog for if your missing the needed items (He always said the dialog for grape juice). [Kisuka]
//============================================================

prt_in,49,172,3	script	Marianne#juice	53,{
	if (MISC_QUEST&1 || morison_meat == 15) {
		mes "[Marianne]";
		mes "Merci de ne pas avoir donné de viande à mon fils. J'essaye de lui donner une alimentation équilibrée, avec des ^0000ffFruits^000000 et des légumes, mais il ne veut rien manger si ce n'est de la viande...";
		next;
		mes "[Marianne]";
		mes "Si seulement je pouvais presser les ^0000fffruits^000000 pour en faire du ^0000ffJus^000000...";
		mes "Vous pensez que mon enfant accepterait d'en boire?";
		next;
		set mother_marienu,0;
		set morison_meat,0;
		set MISC_QUEST,MISC_QUEST | 1;
		mes "[Marianne]";
		mes "Ah! Je me souviens maintenant... Il y a une ville où on peut se procurer n'importe quel ^0000fffruit^000000 et en faire du ^0000ffJus du Fruits^000000.";
		next;
		mes "[Marianne]";
		mes "... Etait-ce... ^ff0000Payon^000000... ou ^ff0000Morroc^000000...?";
		close;
	}
	if (mother_marienu == 1) {
		mes "[Marianne]";
		mes "Morrison!! Mange-donc tes fruits, d'accord? Des fruits~! Tu ne veux pas devenir un petit garçon obèse, hein?";
		mes "*Pff*~!! Tel père, tel fils...";
		next;
		if (select("Lui parler:S'en aller") == 1) {
			mes "[Marianne]";
			mes "Hum? ...Vous!";
			mes "Oh MON DIEU!!... Oh seigneur...";
			mes "VOUS ETES CELUI QUI A DONNE DE LA ^ff0000VIANDE^000000 A MONDE FILS! Comment osez-vous?! Est-ce que ça vous arrive de réfléchir deux minutes?";
			next;
			mes "[Marianne]";
			mes "Arrrgh~~!! Débarrassez le plancher immédiatement!!!!!!";
			close;
		}
		mes "[Marianne]";
		mes "Morrison!! Pff... ce gosse me rendra folle.";
		close;
	}
	if (morison_meat > 0) {
		set mother_marienu,1;
		mes "[Marianne]";
		mes "Oh MON DIEU!!... Oh seigneur...";
		next;
		mes "[Marianne]";
		mes "QUE FAITES VOUS!! COMMENT OSEZ VOUS nourrir mon fils!! Et avec de la ^ff0000VIANDE^000000 qui plus est!!!";
		next;
		mes "[Marianne]";
		mes "Arrrgh~~!! Débarrassez le plancher immédiatement!!!!!!";
		close;
	}
	mes "[Marianne]";
	mes "Morrison!! Mange-donc tes fruits, d'accord? Des fruits~!";
	next;
	mes "[Marianne]";
	mes "*soupire*~ Tel père, tel fils...";
	next;
	switch(select("Lui parler:S'en aller")) {
		mes "[Marianne]";
		mes "(~Soupire~)... C'est Morrison... Mon unique petit garçon. Je suis bouleversée car il ne veut manger aucun ^0000fffruit^000000 et encore moins des légume.";
		next;
		mes "[Marianne]";
		mes "Regardez-le..., il devient furieux dès qu'il entend parler de ^0000fffruits^000000...";
		next;
		mes "[Marianne]";
		mes "Il ne veut manger qu'une seule chose, de la ^ff0000viande^000000... (soupire)...";
		mes "Mais il lui faut des ^0000fffruits^000000 et des légumes pour une alimentation équilibrée.";
		close;
	}
	mes "[Marianne]";
	mes "Morrison!! Pff... ce gosse me rendra folle.";
	close;
}

prt_in,47,173,3	script	Morrison#juice	97,{
	if (MISC_QUEST&1 || morison_meat == 15) {
		mes "[Morrison]";
		mes "Pffff... Allez-vous en! Vous vous FICHEZ de moi! Je vais MOURIR à cause de ces stupides fruits et légumes, alors occupez-vous de vos OIGNONS!";
		close;
	}
	if (morison_meat > 9) {
		mes "[Morrison]";
		mes "Ah... J'ai bien mangé~";
		mes "Si vous me donnez encore de la viande, je crois que je vais EXPLOSER.";
		if (morison_meat == 10) {
			next;
			set morison_meat,morison_meat+1;
			mes "[Morrison]";
			mes "Et... euh... Tenez, c'est pour vous.";
			next;
			mes "[Morrison]";
			mes "C'est ma réserve secrète de bonbons. Elle est bien cachée, ma mère ne l'a jamais vue!";
			mes "Puisque vous m'avez donné de la viande, je vous en donne quelques uns!" ;
			close;
			getitem 529,3; //Candy
			getitem 530,1; //Candy_Striper
		}
		close;
	}
	mes "[Morrison]";
	mes "Wahhhhh!!! Wahh... Pitié! Je n'en veux PLUS!";
	mes "Je... je ne veux plus manger ces stupides ^0000ffFruits^000000 ni même de Légumes!!";
	next;
	if (countitem(517) > 0) {
		switch(select("Lui parler:Lui montrer la viande:S'en aller")) {
		case 1:
			mes "[Morrison]";
			mes "Arhhggg!! J'en ai MARRE!!! Je ne veux plus jamais manger de ^0000ffFruits^000000!";
			mes "Leur goût est ACIDE... Je dois tout éplucher avant de les manger...";
			mes "C'est juteux, collant et spongieux... C'est vraiment DEGOUTANT!!!!";
			next;
			mes "[Morrison]";
			mes "Je DETESTE les FRUITS! En plus ça ne remplit pas le ventre. (~soupire~)";
			mes "Donnez-moi quelque chose de comestible... Je veux... ^ff0000DE LA VIANDE^000000!!!";
			close;
		case 2:
			mes "[Morrison]";
			mes "...(sniff)....(sniff)(sniff).... Oh! Est-ce que c'est ce que je crois? De la ^ff0000viande^000000???";
			next;
			mes "[Morrison]";
			mes "Vous avez de la ^ff0000viande^000000?!!! ~(bave avec envie)~";
			mes "Miam... cette douce odeur me donne SI FAAIIIMMMM!!!";
			next;
			mes "[Morrison]";
			mes "... Hum... excusez-moi... pourriez-vous me donner un peu de votre viande, S'IL VOUS PLAAAAAÎÎTT...?";
			next;
			if (select("Lui donner de la viande:L'ignorer") == 1) {
				delitem 517,1; //Meat
				set morison_meat,morison_meat+1;
				mes "[Morrison]";
				mes "Whaouh~!!! C'est de la viande! De la vraie viande!";
				mes "Miaaam! Quelle délicieuse sensation sur le palais...";
				mes "Oh, merci infiniment. Miam miam...";
				close;
			}
			set morison_meat,15;
			mes "[Morrison]";
			mes "Snif...";
			mes "Snif... Très bien... D'accord, j'ai compris. Espèce d'égoïste! Les adultes sont tous les mêmes...~";
			close;
		case 3:
			mes "[Morrison]";
			mes "Wahhhh!!! Peu importe combien de fois j'essayerai, je ne peux vraiment pas manger de légumes, et encore moins de ^0000ffFruits^000000! C'est vraiment écoeurant!!";
			close;
		}
	}
	if (select("Lui parler:S'en aller") == 1) {
		mes "[Morrison]";
		mes "Arhhggg!! J'en ai MARRE!!! Je ne veux plus jamais manger de ^0000ffFruits^000000!";
		mes "Leur goût est ACIDE... Je dois tout éplucher avant de les manger...";
		mes "C'est juteux, collant et spongieux... C'est vraiment DEGOUTANT!!!!";
		next;
		mes "[Morrison]";
		mes "Je DETESTE les FRUITS! En plus ça ne remplit pas le ventre. (~soupire~)";
		mes "Donnez-moi quelque chose de comestible... Je veux... ^ff0000DE LA VIANDE^000000!!!";
		close;
	}
	mes "[Morrison]";
	mes "Wahhhh!!! Peu importe combien de fois j'essayerai, je ne peux vraiment pas manger de légumes, et encore moins de ^0000ffFruits^000000! C'est vraiment écoeurant!!";
	close;
}

payon_in03,188,146,5	script	Marx Hansen#juice	86,{
	if (checkweight(1201,1) == 0) {
		mes "- Attendez! -";
		mes "- Vous transportez trop d'objet sur vous. Déposez-en quelques uns dans votre Stock Kafra et revenez me voir. -";
		close;
	}
	if (MISC_QUEST&1) {
		mes "[Marx Hansen]";
		mes "Bienvenue.";
		mes "Qu'est-ce qui vous amène?";
		next;
		switch(select("Fabriquer du Jus de Fruits.:Comment fabriquer du Jus de Fruits?:Rien...")) {
		case 1:
			mes "[Marx Hansen]";
			mes "Quel sorte de Jus de Fruits voulez-vous?";
			next;
			switch(select("Jus de Pommes:Jus de Bananes:Jus de Carottes:Jus de Raisins:Annuler")) {
			case 1:
				set .@fruit,512;
				set .@juice,531;
				break;
			case 2:
				set .@fruit,513;
				set .@juice,532;
				break;
			case 3:
				set .@fruit,515;
				set .@juice,534;
				break;
			case 4:
				set .@fruit,514;
				set .@juice,533;
				break;
			case 5:
				mes "[Marx Hansen]"											;
				mes "Très bien...";
				mes "A plus tard.";
				close;
			}
			if (countitem(.@fruit) == 0 || countitem(713) == 0 || Zeny < 3) {
				mes "[Marx Hansen]";
				mes "Oh...";
				mes "Je suis désolé, mais il vous manque des ingrédients. Pour fabriquer du "+getitemname(.@juice)+", j'ai besoin de 1 "+getitemname(.@fruit)+" et de 1 Bouteille Vide, ainsi que 3 Zenys par bouteille pour la fabrication.";
				next;
				mes "[Marx Hansen]";
				mes "Revenez quand vous aurez tout ce qu'il faut, et je vous ferai le plus rafraîchissant des Jus de Fruits!";
				close;
			}

			mes "[Marx Hansen]";
			mes "Combien en voulez-vous?";
			next;
			switch(select("Autant que possible.:Je vais vous indiquer la quantité.:Annuler.")) {
			case 1:
				set .@make,countitem(.@fruit);
				break;
			case 2:
				mes "[Marx Hansen]";
				mes "Choisissez un nombre compris entre 1 et 100. Si vous avez changé d'avis, indiquez '0'. Avec tout ce que vous avez sur vous, je peux vous faire "+countitem(.@fruit)+" jus de fruits au maximum.";
				next;
				while(1) {
					input .@input;
					if (.@input == 0) {
						mes "[Marx Hansen]"																									;
						mes "D'accord...";
						mes "A bientôt.";
						close;
					}
					else if (.@input > 100) {
						mes "[Marx Hansen]"																									;
						mes "Vous ne seriez pas un peu dur de la feuille? Je ne peux pas fabriquer plus de 100 Jus de Fruits en même temps!";
						next;
					}
					else {
						break;
					}
				}
				set .@make,.@input;
				break;
			case 3:
				mes "[Marx Hansen]";
				mes "D'accord...";
				mes "A bientôt.";
				close;
			}

			set .@total_zeny,3 * .@make;

			if (countitem(.@fruit) < .@make || countitem(713) < .@make || Zeny < .@total_zeny) {
				mes "[Marx Hansen]";
				mes "Oh...";
				mes "Je suis désolé, mais il vous manque des ingrédients.";
				close;
			}
			delitem .@fruit,.@make;
			delitem 713,.@make;
			set zeny,zeny-.@total_zeny;
			getitem .@juice,.@make;

			mes "[Marx Hansen]";
			mes "Et voilà! De l'excellent Jus de Fruits, frais et rafraîchissant. De la première qualité!";
			next;
			mes "[Marx Hansen]";
			mes "Et bien...";
			mes "Au plaisir de vous revoir.";
			close;
		case 2:
			mes "[Marx Hansen]";
			mes "Aux premiers âges de l'humanité, les fruits étaient les seuls aliments disponibles à la consommation.";
			mes "Les fruits étaient vitaux pour la survie des hommes, et sans eux, la mort était presque assurée.";
			mes "En ce temps, Mère Nature était remerciée pour ses dons généreux.";
			next;
			mes "[Marx Hansen]";
			mes "De nos jours, la nourriture est disponible à profusion, sous de nouvelles variétés, goûts et formes.";
			mes "Avec un tel choix dans l'alimentation, les fruits ne sont plus consommés autant qu'auparavant.";
			mes "C'est particulièrement le cas parmi les enfants, qui préfèrent la pizza et les bonbons.";
			next;
			mes "[Marx Hansen]";
			mes "A cause de cela, j'ai voulu trouver un moyen pour faire prendre conscience aux jeunes que les fruits sont importants.";
			mes "J'ai découvert qu'une fois pressés en jus, les fruits deviennent plus faciles à digérer, et le goût est apprécié même des jeunes enfants.";
			next;
			mes "# Jus de Fruits #";
			mes "^CC4E5C- Jus de Pommes -^000000";
			mes "Apple x 1, Empty Bottle x 1, 3 Zenys.";
			mes "";
			mes "^E3CF57- Jus de Bananes -^000000";
			mes "Banana x 1, Empty Bottle x 1, 3 Zenys.";
			mes "";
			mes "^ED9121- Jus de Carottes -^000000";
			mes "Carrot x 1, Empty Bottle x 1, 3 Zenys."										;
			mes "";
			mes "^CC00FF- Jus de Raisins -^000000";
			mes "Grape x 1, Empty Bottle x 1, 3 Zenys.";
			close;
		case 3:
			mes "[Marx Hansen]";
			mes "Hé!";
			mes "Si vous entrez chez les gens, la moindre des choses c'est de leur parler!";
			close;
		}
	}
	else {
		mes "[Marx Hansen]";
		mes "Bienvenue.";
		mes "En quoi puis-je vous aider?";
		next;
		if (select("Discuter:S'en aller") == 1) {
			mes "[Marx Hansen]";
			mes "Aux premiers âges de l'humanité, les fruits étaient les seuls aliments disponibles à la consommation.";
			mes "Les fruits étaient vitaux pour la survie des hommes, et sans eux, la mort était presque assurée.";
			mes "En ce temps, Mère Nature était remerciée pour ses dons généreux.";
			next;
			mes "[Marx Hansen]";
			mes "De nos jours, la nourriture est disponible à profusion, sous de nouvelles variétés, goûts et formes.";
			mes "Avec un tel choix dans l'alimentation, les fruits ne sont plus consommés autant qu'auparavant.";
			mes "C'est particulièrement le cas parmi les enfants, qui préfèrent la pizza et les bonbons.";
			next;
			mes "[Marx Hansen]";
			mes "A cause de cela, j'ai voulu trouver un moyen pour faire prendre conscience aux jeunes que les fruits sont importants.";
			mes "J'ai découvert qu'une fois pressés en jus, les fruits deviennent plus faciles à digérer, et le goût est apprécié même des jeunes enfants.";
			close;
		}
		mes "[Marx Hansen]";
		mes "Hé!";
		mes "Si vous entrez chez les gens, la moindre des choses c'est de leur parler!";
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.1 fixed input number check [Lupus]
//= 1.2 fixed misplaced Grape<->Carrot juices [Lupus]
//= 1.3 added a loopless menu 'as many as possible' [Lupus]
//= 1.3a Turned the juice making part of the Juicer npc into a
//= subfunc.  Changed fruit amount needed to 1.[kobra_k88]
//= Fixed exploits [Lupus]
//= 1.6 fixed typo (where did @ipnut came from?) [Lance]
//= 1.8 got rid of vars: 'MEAT',"MARIANNE","MORRISON"
//= 	From now, all common quests start using bits of the
//=	same variable MISC_QUEST [Lupus]
//============================================================ 
