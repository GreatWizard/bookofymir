//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena, corrigé par Icaros
//===== eAthena Script ======================================= 
//= Quest NPCs related to Niflheim
//===== By: ================================================== 
//= The eAthena Dev Team
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Piano Key Ouest
//= - Escape from Niflheim
//= - Variables in use: nif_esc, MISC_QUEST (Bit 32)
//= Message Delivery Quest
//= - Deliver a message to Erious' departed wife.
//= - Variables in use: nif_revive, MISC_QUEST (Bit 16384)
//===== Additional Comments: ================================= 
//= 1.0 Initial release, moved Book of Devil and Piano quest
//=	from npc/cities/niflheim.txt [Evera]
//= 1.1 Optimized Book of Devil quest. used bit-wise var,
//=	Optimized Piano Quest - now uses 1 variable [Lupus]
//= 1.2 Fixed bad NPC header data to comply with rev. 11603. [L0ne_W0lf]
//= 1.3 Moved the Book of Devil quest to custom folder. [L0ne_W0lf]
//=	Updated to Aegis 10.3 Standed, added Message Delivery Quest.
//=	Rescripted Piano Quest. Prior quest progress will be lost.
//= 1.4 Fixed NPCs using ID 111 when it shoudl have been 139. [L0ne_W0lf]
//= 1.5 Swapped NPC IDs back from last change (139 back to 111). [L0ne_W0lf]
//=     Clicking will do nothing, you have to still "touch" the npc. 
//============================================================ 

//=============================================================
//Piano Quest
//=============================================================
niflheim,224,243,3	script	Alreg#nif	795,{
	if (nif_piano) set nif_piano,0;
	mes "[Alreg]";
	mes "Muahaha, j'adore la viande...";
	mes "Tu as l'air... délicieux!";
	next;
	mes "[Alreg]";
	mes "J'ai envie de te goûter...";
	next;
	mes "^FF3355*Chronk* *Miam* *Slurp*^000000";
	if (nif_esc == 0 && (MISC_QUEST & 32) == 0) {
		set piano_root,rand(1,2);
		percentheal -60,0;
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "NooOOoOOoOoOnNnN~!";
		next;
		mes "[Alreg]";
		mes "Ahhh, ça faisait longtemps que je n'avais pas mangé de viande si... délicieuse.";
		mes "Laisse-moi t'offrir ceci en guise de remerciement.";
		mes "*Burrrp*";
		set nif_esc,rand(1,2);
		getitem 7184,1; //Piano_Keyboard
		close;
	}
	percentheal -30,0;
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Aïe~! Qu'est-ce que vous faites, vous voulez me tuer ou quoi?!";
	next;
	mes "[Alreg]";
	mes "Hum? Oh, non non non.";
	mes "Je voulais simplement te goûter.";
	close;
}

nif_in,105,81,3	script	Crayu#nif	794,{
	mes "[Crayu]";
	mes "Les vivants...";
	mes "Écoutent souvent ce poème...";
	mes "Il est souvent récité, mais son auteur et sa date d'écriture sont inconnus...";
	next;
	mes "[Crayu]";
	mes "^FF0000Quand le soleil se couche sur les collines occidentales,";
	mes "^FF0000Quand le crépuscule déploie son obscurité de velours,";
	mes "^FF0000La mélodie de la mort enveloppe ton âme,";
	mes "^FF0000Et le Seigneur de la Mort réveille son courroux.^000000";
//Myllena: j'ai conservé les anciennes paroles pour les deux dernières lignes, le changement original des textes étant très moyen
	next;
	if (nif_esc == 1 || nif_esc == 4) {
		mes "[Crayu]";
		mes "Oh attendez! Voici un petit quiz pour vous!";
		switch(rand(1,4)) {
		case 1:
			set .@songline$,"Quand le soleil se couche sur les collines occidentales,";
			mes "Récitez-moi la première ligne du poème.";
			break;
		case 2:
			set .@songline$,"Quand le crépuscule déploie son obscurité de velours,";
			mes "Récitez-moi la deuxième ligne du poème.";
			break;
		case 3:
			set .@songline$,"La mélodie de la mort enveloppe ton âme,";
			mes "Récitez-moi la troisième ligne du poème.";
			break;
		case 4:
			set .@songline$,"Et le Seigneur de la Mort réveille son courroux.";
			mes "Récitez-moi la quatrième ligne du poème.";
			break;
		}
		next;
		input .@input$;
		if (.@input$ == .@songline$) {
			mes "[Crayu]";
			mes "Excellent...!";
			mes "Je vois que vous m'avez écouté attentivement~!";
			mes "Mon nom est Crayu, je suis un poète errant. Vous avez été mon meilleur public jusqu'à présent.";
			mes "Tenez, voici votre récompense.";
			next;
			if (nif_esc == 1) {
				set nif_esc,3;
			}
			else {
				set nif_esc,6;
			}
			getitem 7184,1; //Piano_Keyboard
			mes "[Crayu]";
			mes "J'espère que vous écouterez les autres bardes avec autant d'intérêt que celui que vous m'avez porté. Adieu!";
			close;
		}
		else {
			mes "[Crayu]";
			mes "*Soupir*... Si vous aviez fait un peu plus attention, vous auriez su la bonne réponse!";
			mes "Revenez me voir si vous voulez tenter votre chance à nouveau, et essayez de vous souvenir du poème cette fois!";
			close;
		}
	}
	next;
	mes "[Crayu]";
	mes "Je me demande ce que signifient ces paroles...";
	mes "Si on lit attentivement, on dirait qu'il y a un sens profond... comme si un secret bien gardé était caché à l'intérieur de ces mots...";
	close;
}

nif_in,31,20,3	script	Kuzkahina#nif	794,{
	if (nif_esc == 2 || nif_esc == 3) {
		mes "[Kuzkahina]";
		mes "Le commerce est vraiment mauvais ces temps-ci...";
		mes "Même du temps où j'étais vivant, mes affaires n'allaient déjà pas bien!";
		next;
		mes "[Kuzkahina]";
		mes "Hé! Toi là-bas! Rends-toi utile, et débarrasse-moi de ceci, veux-tu?";
		set nif_esc,nif_esc+2;
		getitem 7184,1; //Piano_Keyboard
		next;
		mes "[Kuzkahina]";
		mes "Pourquoi le commerce est-il si mauvais ces derniers temps? ...";
		mes "Comment j'ai pu embaucher des employés aussi nazes...?";
		mes "*marmonne*";
		close;
	}
	mes "[Kuzkahina]";
	mes "Le commerce est vraiment mauvais ces temps-ci...";
	mes "Même du temps où j'étais vivant, mes affaires n'allaient déjà pas bien!";
	close;
}

niflheim,208,103,1	script	#Graveyard1	111,1,1,{
	end;

OnTouch:
	if (nif_esc == 5 || nif_esc == 7 || nif_esc == 8 || nif_esc == 10) {
		mes "^3355FFTout près d'une tombe";
		mes "vous apercevez quelque chose";
		mes "à moitié enfoui dans le sol.^000000";
		next;
		mes "^3355FFVoulez-vous le ramasser?^000000";
		if (select("Oui:Non") == 1) {
			if (nif_esc < 10) {
				set nif_esc,nif_esc+2;
			}
			else if (nif_esc == 10) {
				set nif_esc,11;
			}
			getitem 7184,1; //Piano_Keyboard
		}
		close;
	}
	end;
}

niflheim,169,71,1	script	#Graveyard2	111,1,1,{
	end;

OnTouch:
	if (nif_esc == 6 || nif_esc == 9) {
		mes "^3355FFTout près d'une tombe";
		mes "vous apercevez quelque chose";
		mes "à moitié enfoui dans le sol.^000000";
		next;
		mes "^3355FFVoulez-vous le ramasser?^000000";
		if (select("Oui:Non") == 1) {
			set nif_esc,nif_esc+2;
			getitem 7184,1; //Piano_Keyboard
		}
		close;
	}
	end;
}

nif_in,115,181,0	script	#Piano	111,1,1,{
	end;

OnTouch:
	if (nif_esc == 0 && (MISC_QUEST & 32) == 0) {
		mes "^3355FFCe piano est immense et semble très lourd.";
		mes "Vous avez la certitude que si quelqu'un en jouait, sa musique serait absolument magnifique.^000000";
		next;
		mes "^3355FFAlors que vous examinez le piano, vous vous rendez compte qu'il manque 7 touches au milieu du clavier. Si seulement vous pouviez les retrouver, vous pourriez jouer sur ce superbe instrument...^000000";
		close;
	}
	if (nif_esc == 11) {
		set nif_esc,12;
		delitem 7184,countitem(7184); //Piano_Keyboard // Bugfix: si le joueur n'a pas 6 touches, le serveur va gémir... [NoH]
		mes "^3355FFVous glissez les touches de piano que vous avez retrouvées, une à une dans les emplacements vides.^000000";
		next;
		mes "^3355FFMais vous réalisez qu'à l'extrémité gauche du clavier, il manque encore une touche...^000000";
		close;
	}
	end;
}

nif_in,114,181,0	script	#Piano3	111,1,1,{
	end;

OnTouch:
	if (nif_esc == 12 || nif_esc == 13 || MISC_QUEST & 32) {
		mes "^3355FFAlors que vous vous penchez au-dessus de l'emplacement vide de sa touche, pour l'examiner de plus près...^000000";
		next;
		soundeffect "complete.wav",0;
		next;
		if (nif_esc == 12) {
			set MISC_QUEST,MISC_QUEST | 32;
			set nif_piano, 0;
		}
		mes "^3355FFLa longue ombre de votre corps tombe inerte sur le piano. Vous vous sentez soudain plus léger, et votre vision commence à se brouiller...^000000";
		close2;
		warp "nif_in",179,163;
		end;
	}
	end;
}

nif_in,188,168,3	script	Sorcière#nif	792,{
	mes "[Kirkena]";
	mes "Hum?... Mais vous êtes un humain... vivant?!";
	mes "Ça n'a pas dû être facile pour vous d'arriver jusqu'ici...";
	mes "Quelle que soit la raison de votre présence ici, ce n'est pas un lieu pour les vivants...";
	next;
	mes "[Kirkena]";
	mes "Je vais utiliser mes pouvoirs pour vous reconduire en lieu sûr, mais jamais vous ne devriez revenir ici.";
	close2;
	warp "umbala",138,208;
	end;
}

//=============================================================
// Message Delivery Quest
//=============================================================
prt_in,161,20,3	script	Erious#nif	92,{
	if (MISC_QUEST & 16384) {
		mes "[Erious]";
		mes "Faites attention à vous au cours de vos voyages, aventurier.";
		close;
	}
	if (nif_revive == 0) {
		mes "[Erious]";
		mes "*Snif*Snif*";
		mes "*Pleure* Wahhh...!";
		mes "Elly, comment as-tu pu me laisser ici tout seul...";
		emotion e_sob;
		next;
		if (select("Le réconforter.:L'ignorer.") == 1) {
			mes "[Erious]";
			mes "Ah, merci de votre gentillesse.";
			mes "*Snif* Ma femme est morte alors que j'étais en voyage. J'ai tellement de regrets de ne pas avoir pu être à ses côtés quand elle est décédée...";
			next;
			mes "[Erious]";
			mes "J'aimerais tant parler à ma très chère Elly, même si je sais que c'est trop tard maintenant.";
			mes "*Pleure* Mais... Il y a encore un espoir. Mon dernier espoir, ma dernière chance...";
			next;
			mes "[Erious]";
			mes "J'ai entendu dire qu'il existe quelque part en ce monde, une ville où réside les morts. Peut-être que je pourrais y rencontrer ma femme... Mais je ne peux pas laisser mes enfants seuls ici sans moi. *Pleure*";
			next;
			if (select("Je pourrais lui porter un message?:Je suis désolé de ce qui vous arrive.") == 1) {
				mes "[Erious]";
				mes "Oh, c'est vrai, vous pourriez faire ça pour moi?";
				mes "Merci du fond du coeur pour votre générosité. Je vous serai toujours redevable.";
				next;
				mes "[Erious]";
				mes "Vous êtes un aventurier, n'est-ce pas?";
				mes "Si vos voyages vous mènent à la ville des morts, et si par chance vous rencontriez ma femme...";
				next;
				mes "[Erious]";
				mes "Transmettez à Elly ce message de ma part. Dites-lui que ce qui est arrivé le mois dernier était entièrement ma faute.";
				mes "Merci pour votre aide, et faites attention à vous.";
				set nif_revive,1;
				close;
			}
			mes "[Erious]";
			mes "*Pleure* Et si jamais je ne la revoyais plus jamais? Si seulement je pouvais lui parler, ne serait-ce qu'une dernière fois...";
			close;
		}
		mes "[Erious]";
		mes "*Pleure* Eeeeelllllyyyy~~~~!";
		mes "Je n'aurais jamais pu croire que c'était la dernière fois que je la voyais...";
		mes "*Snif*...";
		close;
	}
	else if (nif_revive == 1) {
		mes "[Erious]";
		mes "J'ai entendu dire qu'il existe quelque part en ce monde, une ville où réside les morts.";
		mes "D'après mon souvenir, elle serait située quelque part à côté d'un endroit paisible.";
		next;
		mes "[Erious]";
		mes "Tous les jours et toutes les nuits, des cris d'agonie et de désespoir se font entendre interminablement. J'espère que cela vous aidera dans votre recherche.";
		close;
	}
	else if (nif_revive == 5 || nif_revive == 6) {
		mes "[Erious]";
		mes "Vous... Vous avez trouvé la ville des morts? J'apprécie sincèrement tout le mal que vous vous êtes donné pour moi et... pour ma femme. Avez-vous pu la rencontrer?";
		next;
		if (select("Oui.:Je suis désolé, pas encore.") == 1) {
			if (nif_revive == 6 && countitem(934) > 0) {
				mes "[Erious]";
				mes "C'est... C'est vrai? Alors, qu'a-t-elle dit?";
				mes "Est-ce qu'elle... est-ce qu'elle me pardonne?";
				next;
				select("Oui. Et elle m'a donné... ceci.");
				delitem 934,1; //Mementos
				mes "[Erious]";
				mes "Ah... Je reconnais cette boîte, c'est un cadeau que je lui avais offert lorsque nous avons commencé à nous fréquenter.";
				mes "Elle était atteinte d'une maladie chronique très grave, qu'elle avait contractée bien avant que nous nous rencontrions. Je lui avais promis que je réussirai à la guérir...";
				next;
				mes "[Erious]";
				mes "C'est pour ça que je suis parti en voyage... Pour trouver un remède, ou du moins un médicament qui pourrait la soulager... Mais mes efforts ont été vains.";
				mes "...................";
				mes ".........";
				next;
				mes "[Erious]";
				mes "Quoi qu'il en soit, j'apprécie votre générosité. Je voudrais vous remercier, pour m'excuser de tous les problèmes que je vous ai causés. Ce que je possède de plus précieux...";
				mes "Ce sont ces médicaments, que j'avais trouvé au cours de mon voyage, pour guérir la maladie de ma femme.";
				next;
				mes "[Erious]";
				mes "Prenez ces médicaments, en gage de ma gratitude.";
				next;
				if (checkweight(610,26)) {
					set MISC_QUEST,MISC_QUEST | 16384;
					set nif_revive,0;
					getitem 607,2; //Yggdrasilberry
					getitem 608,2; //Seed_Of_Yggdrasil
					getitem 610,10; //Leaf_Of_Yggdrasil
					mes "[Erious]";
					mes "Merci pour tout.";
					mes "Je vous souhaite chance et bonne santé au cours de vos aventures.";
					close;
				}
				mes "[Erious]";
				mes "J'aimerais vous donner quelque chose, en récompense de tout ce que vous avez fait pour nous. Mais vous semblez courber l'échine... Vous devriez vous décharger un peu, avant que je vous offre ma récompense.";
				close;
			}
			else if (nif_revive == 6 && countitem(934) < 1) {
				mes "[Erious]";
				mes "Humm... c'est étrange. Je suis certain qu'elle vous aurait donné quelque chose pour prouver que vous vous êtes rencontrés. N'auriez-vous rien oublié de m'apporter?";
				close;
			}
			mes "[Erious]";
			mes "Je crois que nous nous sommes mal compris, à moins que vous ne soyez en train d'essayer d'arnaquer un homme dans la douleur du deuil.";
			close;
		}
		if (nif_revive == 5) {
			mes "[Erious]";
			mes "Je vois... Vous n'avez pas réussi à trouver ma femme... Cependant je comprends que ce que je vous ai demandé était difficile à réaliser...";
			next;
			mes "[Erious]";
			mes "Vous vous êtes donné beaucoup de peine pour m'aider, alors j'aimerais vous récompenser. Prenez ceci en gage de ma gratitude.";
			next;
			if (checkweight(610,10)) {
				set MISC_QUEST,MISC_QUEST | 16384;
				set nif_revive,0;
				getitem 610,10; //Leaf_Of_Yggdrasil
				mes "[Erious]";
				mes "Merci pour tout.";
				mes "Je vous souhaite chance et bonne santé au cours de vos aventures.";
				close;
			}
			mes "[Erious]";
			mes "J'aimerais vous donner quelque chose, en récompense de tout ce que vous avez fait pour nous. Mais vous semblez courber l'échine... Vous devriez vous décharger un peu, avant que je vous offre ma récompense.";
			close;
		}
		mes "[Erious]";
		mes "Je vois...";
		mes "...Attendez! Est-ce que ceci appartient à ma femme, Elly? Pourquoi essayez-vous de tromper un homme en souffrance...?";
		close;
	}
	end;
}

umbala,215,181,3	script	Billik	733,{
	if (nif_revive > 1 || MISC_QUEST & 16384) {
		mes "[Billik]";
		mes "Et bien, ça fait longtemps qu'on ne vous avait pas vu dans le coin. Nous nous rencontrerons peut-être un jour à nouveau, dans la ville des morts...";
		mes "Hahahaha~";
		emotion e_no1;
		close;
	}
	if (nif_revive == 1) {
		mes "[Billik]";
		mes "La ville des morts...";
		mes "C'est un lieu extrêmement dangereux.";
		mes "Pourquoi voudriez-vous visiter pareil endroit?";
		next;
		if (select("Je dois retrouver quelqu'un là-bas.:Cela fait partie de mon aventure.") == 1) {
			mes "[Billik]";
			mes "Quelqu'un à retrouver là-bas...";
			mes "Humm... Je suppose que vous souhaitez entrer en contact avec une personne défunte...";
			mes "Je vois que vous êtes même très déterminé.";
			next;
			mes "[Billik]";
			mes "*Soupir* D'accord, si vous voulez vraiment aller là-bas, il vous faudra trouver un homme du nom de 'Feylin'.";
			mes "Si vous le voyez, dites-lui bien que vous venez de ma part.";
			next;
			mes "[Billik]";
			mes "Ce n'est pas ce qu'on pourrait appeler quelqu'un de confiance, mais il adore les fleurs, en particulier les roses. Donc si vous lui apportez une rose, il saura que c'est moi qui vous envoie.";
			set nif_revive,2;
			next;
			mes "[Billik]";
			mes "La ville des morts est située au Nord d'ici. Souvenez-vous que vous rendre là-bas sera extrêmement dangereux, même si vous pensez être un aventurier averti...";
			mes "Donc... soyez prudent.";
			close;
		}
		mes "[Billik]";
		mes "Hahaha~ Je suppose que nous partageons tous deux un goût prononcé pour l'aventure! Même si c'est un lieu très dangereux, je vous garantis que vos efforts seront récompensés une fois là-bas. Bon courage.";
		close;
	}
	else if (nif_revive == 0) {
		mes "[Billik]";
		mes "Qu'est-ce qui vous amène ici? Je suis occupé, si vous n'avez rien à me demander, veuillez partir.";
		close;
	}
}

niflheim,153,215,3	script	Feylin	794,{
	if (nif_revive == 2) {
		mes "[Feylin]";
		if (Sex == 1) {
			mes "Pauvre garçon...";
		}
		else {
			mes "Pauvre fille...";
		}
		mes "Mourir à un si jeune âge...";
		mes "Je suis toujours attristé de voir les jeunes rendre l'âme.";
		next;
		mes "[Feylin]";
		mes "............??";
		mes "Oh, mais vous n'êtes pas encore mort on dirait? C'est incroyable, il est si rare de voir un vivant dans cette ville!";
		next;
		mes "[Feylin]";
		mes "Êtes-vous venu pour secourir quelqu'un?";
		mes "Si vous pensez pouvoir parler à une personne défunte, désolé mais vous vous trompez d'interlocuteur.";
		next;
		mes "[Feylin]";
		mes "Personne à Niflheim n'a le pouvoir d'exaucer pareil miracle...";
		next;
		mes "[Feylin]";
		mes "Je comprends que vous ayez eu du mal à venir ici, mais malheureusement vous êtes venu pour rien. J'espère que vous pourrez repartir d'où vous venez, avant qu'il ne soit trop tard.";
		set nif_revive,3;
		close;
	}
	else if (nif_revive == 3) {
		mes "[Feylin]";
		mes "Je vous ai demandé de partir de cette ville immédiatement. Je suis désolé mais je ne peux rien pour vous.";
		emotion e_an;
		next;
		if (select("Mais...:Je suis désolé.") == 1) {
			mes "[Feylin]";
			mes "Vous avez un petit pois dans la tête? Ou bien vous êtes complètement borné. Je ne sais pas qui vous a fait croire qu'il était possible de contacter les morts...";
			next;
			mes "[Feylin]";
			mes "Mais ce n'est certainement pas moi qui pourrais vous aider. De plus, parler de cette éventualité est tabou.";
			set nif_revive,4;
			close;
		}
		mes "[Feylin]";
		mes "Vous devriez repartir d'où vous venez le plus rapidement possible. Personne ne devrait séjourner à Niflheim très longtemps.";
		close;
	}
	else if (nif_revive == 4) {
		if (countitem(748) > 0) {
			mes "[Feylin]";
			mes "Hum, c'est Billik qui vous envoie?";
			mes "Je lui suis redevable, donc je suppose que je suis sensé vous aider.";
			mes "Mais laissez-moi vous prévenir.";
			mes "Ce ne sera pas gratuit, et le prix que je demande sera peut-être plus haut que ce que vous imaginez...";
			next;
			if (select("Je veux réellement aider Billik.:Je ne suis plus très sûr de moi.") == 1) {
				if (BaseLevel > 79 && BaseLevel < 100) {
					mes "[Feylin]";
					mes "D'accord, mais vous devez avant tout me promettre que vous ne parlerez à personne de ce que vous verrez ici.";
					next;
					mes "[Feylin]";
					mes "La sorcellerie nécessite de grands sacrifices. Sans parler du fait que le sortilège a une chance d'échouer.";
					mes "Si vous souhaitez malgré tout essayer ce sortilège, il vous faudra apporter 10 Feuilles d'Yggdrasil, nécessaires pour l'incantation. Souhaitez-vous continuer?";
					next;
					if (select("Oui:Non") == 1) {
						if (countitem(610) > 9) {
							mes "[Feylin]";
							mes "Je vois. Alors, commençons.";
							mes "Woombahsokasabahah!";
							mes "Woombah woombah hoombabah!";
							mes "Yeeeeep!";
							next;
							set .@nif_revive1,rand(1,100);
							if (.@nif_revive1 > 0) && (.@nif_revive1 < 88) {
								mes "[Feylin]";
								mes "...Je suis désolé.";
								mes "Je n'ai pas réussi à rappeler l'esprit de la personne que vous souhaitiez voir. Je suis sincèrement désolé.";
								emotion e_omg;
								delitem 748,1; //Witherless_Rose
								delitem 610,10; //Leaf_Of_Yggdrasil
								set nif_revive,5;
								percentheal -99,0;
								next;
								mes "[Feylin]";
								mes "Malheureusement, il est peut-être impossible d'invoquer l'esprit de cette personne...";
								close;
							}
							else if (.@nif_revive1 > 87) && (.@nif_revive1 < 101) {
								delitem 748,1; //Witherless_Rose
								delitem 610,10; //Leaf_Of_Yggdrasil
								percentheal -50,0;
								mes "[Feylin]";
								mes "..........";
								mes "..........";
								next;
								mes "[Elly]";
								mes "..........";
								mes ".....Umm...";
								mes "...Où... suis-je? Je croyais être morte...";
								mes "Est-ce que je vous connais?";
								next;
								select("J'apporte un message de la part de votre mari.");
								mes "[Elly]";
								mes "Oh... Je vois... Je suis désolée de ne pas avoir pu lui dire au revoir avant de mourir...";
								mes "Que... Que dit le message de mon mari?";
								next;
								select("Il souhaite que vous lui pardonniez.");
								mes "[Elly]";
								mes "*Soupir* C'était un mari si aimant...";
								mes "Il était toujours très attentionné, même aux petites choses de la vie. C'est moi qui devrais lui présenter des excuses...";
								next;
								mes "[Elly]";
								mes "Pourriez-vous lui donner cette boîte, et lui dire d'essayer de m'oublier? Il doit continuer à vivre, refaire sa vie avec quelqu'un et être heureux à nouveau. Et pour ce qui s'est produit le mois dernier... je lui pardonne.";
								set nif_revive,6;
								getitem 934,1; //Mementos
								next;
								mes "[Elly]";
								mes "Je suis heureuse d'avoir pu avoir des nouvelles de mon mari une dernière fois. Mais je dois y aller à présent. Merci pour tout ce que vous avez fait.";
								mes "Adieu...";
								next;
								mes "[Elly]";
								mes ".....................";
								mes "....................";
								next;
								mes "[Feylin]";
								mes "Je suppose que j'ai accompli ce que vous désiriez. Eh bien, faites attention à vous quand vous repartirez...";
								close;
							}
						}
						else {
							mes "[Feylin]";
							mes "J'ai besoin de 10 Feuilles d'Yggdrasil pour procéder à l'incantation. Apportez-les au plus vite.";
							close;
						}
					}
					mes "[Feylin]";
					mes "Je ne peux pas vous promettre que le sortilège fonctionne... Mais si vous souhaitez essayer, revenez me voir avec 10 Feuilles d'Yggdrasil.";
					close;
				}
				mes "[Feylin]";
				mes "Hum... Vous ne semblez pas avoir la force requise pour endurer l'incantation d'un sortilège. Vu votre état d'hésitation mentale, le sortilège pourrait bien vous tuer sur le coup.";
				mes "Nous ne pouvons pas prendre un tel risque.";
				close;
			}
			mes "[Feylin]";
			mes "Vous avez pris la bonne décision. Ce n'est pas une bonne idée de pratiquer ce type de sorcellerie, surtout vu les risques encourus...";
			close;
		}
		mes "[Feylin]";
		mes "Peu importe combien de fois vous vous adresserez à moi, je ne peux pas vous aider. Partez, maintenant.";
		close;
	}
	mes "[Feylin]";
	mes "Qu'est-ce qui vous amène dans une ville aussi dangereuse? Repartez d'où vous venez le plus vite possible.";
	close;
}
