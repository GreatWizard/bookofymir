//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing
//===== eAthena Script ======================================= 
//= Thief Class Tutorial and Job Specific Quest
//===== By: ================================================== 
//= Fix up by Jukka
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena SVN (Testet in Trunk 88xx)
//===== Description: ========================================= 
//=
//===== Additional Comments: ================================= 
//= 1.0 Fully working
//= 1.1 optimized [Lupus]
//=	TODO: Test, Add correct misceffects numbers
//= 1.2 Fixed experience gains to match upcoming rate adjustments. [SinSloth]
//= 1.3 Fixed a few minor mistakes [Playtester]
//============================================================ 

//=================================================Thief Trainer====================================================
moc_ruins,66,164,4	script	Entraîneur pour Voleurs::Thief Trainer#T	84,{
	mes "[Yierhan]";
	if(Class == Job_Novice){
		mes "Hein...? Un Novice? Tu es encore en train de te demander quelle profession tu vas choisir, c'est ça?";
		next;
		mes "[Yierhan]";
		mes "Écoute, si jamais tu décides de devenir voleur - ce qui serait une bonne idée je dois dire - reviens discuter un peu. Je te montrerai les ficelles du métier!";
		close;

	} else if(Class == Job_Thief || Class == Job_Thief_High || Class == Job_Assassin || Class ==Job_Assassin_Cross || Class == Job_Rogue || Class == Job_Stalker){
		if(Class == Job_Thief_High || Class == Job_Assassin || Class == Job_Assassin_Cross || Class == Job_Rogue || Class == Job_Stalker){
			if(tu_thief01 < 8){
				mes "Mais qu'est-ce que vous faites là? Je ne peux plus rien vous apprendre: vous en savez déjà bien plus que moi!";
				next;
				mes "[Yierhan]";
				mes "En fait je pense que c'est vous qui pourriez me montrer quelques trucs! Allez! Je veux voir de nouvelles techniques!";
				close;
			}
		}
		if(tu_thief01 == 0){
			mes "Salut, mon pote! Je m'appelle Yierhan. C'est moi qui ai pour tâche d'entraîner les apprenti-voleurs.";
			next;
			switch(select("M'entraîner?:M'entraîner? Là, maintenant?")){
			case 1:
				mes "[Yierhan]";
				mes "Ouais, un petit entraînement. Il y a certaines personnes qui naissent avec un don pour ce genre de choses, mais pour d'autres c'est un peu plus compliqué, alors c'est juste 'au cas où'.";
				break;
			case 2:
				mes "[Yierhan]";
				mes "Là, maintenant? Bah oui, là et maintenant! Mais si jamais tu n'es pas prêt"+ ( sex ? "" : "e" ) +", j'imagine que je peux attendre.";
				close;
				break;
			}
			mes "[Yierhan]";
			mes "Bon, déjà, les voleurs se battent au corps à corps. Enfin... la plupart. Certains préfèrent utiliser un arc. Mais quoi qu'il en soit, on est vachement doué niveau rapidité!";
			next;
			mes "[Yierhan]";
			mes "Donc, pour les attaques au corps à corps, quelle stat augmente tes dommages? Allez, ça tu devrais le savoir si tu as passé un peu de temps dans le Camp d'Entraînement des Novices...";
			next;
			switch(select("^6B8E23Intelligence^000000:^2F4F2FForce^000000:^23238EDextérité^000000")){
			case 1:
				mes "[Yierhan]";
				mes "Quoi?! L'^6B8E23Intelligence^000000 affecte les dégâts magiques, la défense magique, et quelques compétences. Tu as déjà vu un Voleur utiliser la magie?!";
				next;
				mes "[Yierhan]";
				mes "On dirait que tu n'as même pas mis les pieds dans le Camp d'Entraînement! C'est pas un reproche, hein, mais pour info, c'est la ^2F4F2FForce^000000 qui augmente tes dégâts physiques, compris?";
				next;
				mes "[Yierhan]";
				mes "Donc si jamais tu sens que tu ne tapes pas assez fort, il faudra travailler un peu ta ^2F4F2FForce^000000. A toi de voir jusqu'à quel point.";
				set tu_thief01, 1;
				getexp 200,100;
				//misceffect EF_HIT5;
				close;
				break;
			case 2:
				mes "[Yierhan]";
				mes "Ouais, c'est ça! Si jamais tu veux taper plus fort, il faut travailler un peu ta ^2F4F2FForce^000000. Ça te permettra d'ailleurs d'augmenter aussi ta Limite de Poids pour pouvoir porter plus de choses.";
// Je supprime ce passage: il répète presque la même chose - on a compris la première fois je pense [NoH]
//				next;
//				mes "[Yierhan]";
//				mes "So if you're feeling like the damage you're making is pretty weak, you might want more stat points in ^2F4F2FSTR^000000. How high your raise your own STR is really up to you.";
				next;
				mes "[Yierhan]";
				mes "Après, la ^23238EDextérité^000000 et la ^23238EChance^000000 peuvent aussi jouer, mais franchement, ce n'est rien comparé à la Force. J'insiste, mais ce n'est ^660000rien du tout^000000.";
				set tu_thief01, 1;
				getexp 400,200;
				//misceffect EF_HIT5;
				close;
				break;
			case 3:
				mes "[Yierhan]";
				mes "Quoi? La ^23238EDextérité^000000 joue sur la précision de tes attaques, pas sur leur puissance! Enfin... à moins que tu n'utilises un arc, mais sinon, ça n'augmente que très peu tes dégâts.";
				next;
				mes "[Yierhan]";
				mes "On dirait que tu n'as même pas mis les pieds dans le Camp d'Entraînement! C'est pas un reproche, hein, mais pour info, c'est la ^2F4F2FForce^000000 qui augmente tes dégâts physiques, compris?";
				next;
				mes "[Yierhan]";
				mes "Donc si jamais tu sens que tu ne tapes pas assez fort, il faudra travailler un peu ta ^2F4F2FForce^000000. A toi de voir jusqu'à quel point.";
				set tu_thief01, 1;
				getexp 200,100;
				//misceffect EF_HIT5;
				close;
				break;
			}
		} else if(tu_thief01 == 1){
			mes "Bon, on a assez parlé des stats. Tu sais quoi? Je crois qu'on va parler un peu des compétences qu'on utilise, nous, les Voleurs.";
			next;
			mes "[Yierhan]";
			mes "Je te propose de travailler un peu tes compétences, et de revenir me voir quand tu en auras amélioré quelques unes.";
			set tu_thief01, 2;
			close;
		} else if(tu_thief01 == 2){
			mes "Bon, alors voyons comment tu te débrouilles avec les techniques de Voleur. Tu te bas différemment en fonction des compétences que tu maîtrises, pas vrai? Elles sont aussi importantes que les stats!";
			next;
			set @chk_th_skill1, getskilllv("TF_DOUBLE");
			set @chk_th_skill2, getskilllv("TF_MISS");
			set @chk_th_skill3, getskilllv("TF_STEAL");
			set @chk_th_skill4, getskilllv("TF_HIDING");
			set @chk_th_skill5, getskilllv("TF_POISON");
			set @chk_th_skill6, getskilllv("TF_DETOXIFY");

			if(@chk_th_skill1 == 0 && @chk_th_skill2 == 0 && @chk_th_skill3 == 0 && @chk_th_skill4 == 0 && @chk_th_skill5 == 0 && @chk_th_skill6 == 0){
				mes "[Yierhan]";
				mes "Oh mais on dirait que tu ne maîtrises encore aucune compétence de voleurs! Allez, on se bouge un peu! C'est tes compétences qui feront la différence entre toi et un blanc-bec de Novice!";
				close;
			} else {
				if(@chk_th_skill1 > 0){
					mes "[Yierhan]";
					mes "Ah, comme ça, tu maîtrises les Doubles Attaques au niveau "+@chk_th_skill1+". Pas mal! Cette technique te permet d'asséner deux coups au lieu d'un seul - génial, non?";
					next;
					mes "[Yierhan]";
					mes "C'est une Compétence Passive, alors tu pourras toujours l'utiliser, même si tu es à court de mana. Plus tu la maîtrises à un haut niveau, et plus tu feras de Doubles Attaques.";
					next;
				}
				if(@chk_th_skill2 > 0){
					mes "[Yierhan]";
					mes "Voyons... Tu maîtrises l'Esquive Améliorée au niveau "+@chk_th_skill2+"? Ça te permet d'avoir plus de chances d'esquiver les coups de tes ennemis.";
					next;
					mes "[Yierhan]";
					mes "C'est comme les Doubles Attaques: il s'agit d'une compétence passive. Ça ne te coûtera aucun Point de Mana. Si tu n'aimes pas t'en prendre plein la tête, c'est une compétence pour toi!";
					next;
				}
				if(@chk_th_skill3 > 0){
					mes "[Yierhan]";
					mes "Alors comme ça tu maîtrises les techniques de Vol au niveau "+@chk_th_skill3+"!~ C'est cette compétence qui a donné son nom à notre profession! Le seul truc, c'est que tu ne peux pas l'utiliser sur d'autres gens...";
					next;
					mes "[Yierhan]";
					mes "Cela dit, tu peux quand même voler des monstres. Tu pourras récupérer quelques bons trucs comme ça si jamais tu as de la chance. Oh, et au passage, ça ne change rien aux chances que tu as de trouver un objet sur un montre sans ça.";
					next;
				}
				if(@chk_th_skill4 > 0){
					mes "[Yierhan]";
					mes "Tu maîtrises aussi la Disparition au niveau "+@chk_th_skill4+"? Oui, c'est quelque choses que tu peux apprendre après avoir maîtrisé le Vol.";
					next;
					mes "[Yierhan]";
					mes "C'est une technique que tu utilises pour te cacher sous terre, genre quand un paquet de monstres vient de te tomber dessus. Mais faut quand même faire gaffe...";
					next;
					mes "[Yierhan]";
					mes "Certains monstres pourront quand même te voir, même si tu te caches comme un"+ ( sex ? "" : "e" ) +" pro. Et il y en a même qui peuvent te faire sortir de ta cachette!";
					next;
				}
				if(@chk_th_skill5 > 0){
					mes "[Yierhan]";
					mes "Ah, je vois que tu maîtrises l'Empoisonnement au niveau "+@chk_th_skill5+". T'aimes bien les trucs dangereux, pas vrai?";
					next;
					mes "[Yierhan]";
					mes "Grâce à cette technique, tu peux empoisonner ton ennemi pour un certain temps. Il perdra alors ses Points de Vie petit à petit, et sa défense sera réduite.";
					next;
					mes "[Yierhan]";
					mes "Cela dit, garde quand même en tête que si le monstre est trop balèze, tu n'arriveras peut-être pas à l'empoisonner, alors ne fais pas n'importe quoi.";
					next;
				}
				if(@chk_th_skill6 > 0){
					mes "[Yierhan]";
					mes "Détoxication au niveau "+@chk_th_skill6+"? Si jamais tu as pris le temps d'apprendre ça, c'est que tu dois être quelqu'un de super prudent"+ ( sex ? "" : "e" ) +".";
					next;
					mes "[Yierhan]";
					mes "C'est une technique que tu ne peux apprendre qu'après avoir maîtrisé l'Empoisonnement. Elle te permet de contrer les effets du poison sur une cible.";
					next;
				}
			}
			mes "[Yierhan]";
			mes "Bon, j'imagine que si tu veux en savoir plus sur les autres techniques, je peux t'en parler rapidement.";
			set tu_thief01, 3;
			getexp BaseLevel*30,BaseLevel*15;
			//misceffect EF_HIT5;//CHECK
			close;
		} else if(tu_thief01 == 3){
			mes "Alors... Tu veux que je te parle d'autre chose ou c'est tout ce que tu voulais savoir?";
			next;
			while(1){
				switch(select("Double Attaque:Esquive Améliorée:Vol:Disparition:Empoisonnement:Détoxification:Annuler")){
				case 1: 
					mes "[Yierhan]";
					mes "Comme son nom l'indique, la Double Attaque te permet d'asséner deux coups au lieu d'un seul.";
					next;
					mes "[Yierhan]";
					mes "C'est une Compétence Passive, alors elle est toujours active, et elle ne consomme pas de mana. Plus son niveau est élevé, et plus tu as de chance de frapper deux fois de suite.";
					set @read_d, 1;
					next;
					break;
				case 2:
					mes "[Yierhan]";
					mes "L'Esquive Améliorée augmente de façon significative ton Esquive, beaucoup plus que ne le peuvent les autres classes. Pourquoi te prendre des coups quand tu peux les éviter?";
					next;
					mes "[Yierhan]";
					mes "C'est comme les Doubles Attaques: il s'agit d'une compétence passive. Ça ne te coûtera aucun Point de Mana. Si tu n'aimes pas t'en prendre plein la tête, c'est une compétence pour toi!";
					set @read_f, 1;
					next;
					break;
				case 3:
					mes "[Yierhan]";
					mes "Le Vol est une technique qui te permet de te procurer quelques objets gratos! Malheureusement, tu ne peux pas l'utiliser sur d'autres personnes...";
					next;
					//misceffect EF_STEAL;
					mes "[Yierhan]";
					mes "Cela dit, tu peux quand même voler des monstres. Tu pourras récupérer quelques bons trucs comme ça si jamais tu as de la chance. Oh, et au passage, ça ne change rien aux chances que tu as de trouver un objet sur un montre sans ça.";
					set @read_s, 1;
					next;
					break;
				case 4:
					mes "[Yierhan]";
					mes "La Dissimulation est une Compétence Active qui te permet de te cacher sous terre. Tu ne peux y rester que pour un temps, et ça draine ta mana, mais parfois ça vaut mieux que de se faire prendre!";
					next;
					mes "[Yierhan]";
					mes "C'est une technique que tu utilises pour te cacher, genre quand un paquet de monstres vient de te tomber dessus. Mais faut quand même faire gaffe...";
					next;
					mes "[Yierhan]";
					mes "Certains monstres pourront quand me te voir, même si tu te caches comme un"+ ( sex ? "" : "e" ) +" pro. Et il y en a même qui peuvent te faire sortir de ta cachette!";
					set @read_p, 1;
					next;
					break;
				case 5: 
					mes "[Yierhan]";
					mes "Grâce à cette technique, tu peux empoisonner ton ennemi pour un certain temps. Il perdra alors ses Points de Vie petit à petit, et sa défense sera réduite.";
					next;
					mes "[Yierhan]";
					mes "Cela dit, garde quand même en tête que si le monstre est trop balèze, tu n'arriveras peut-être pas à l'empoisonner, alors ne fais pas n'importe quoi.";
					set @read_h, 1;
					next;
					break;
				case 6: 
					mes "[Yierhan]";
					mes "Comme les Voleurs utilisent parfois du poison, il nous faut quelque chose pour contrer ses effets.";
					next;
					mes "[Yierhan]";
					mes "C'est une technique que tu ne peux apprendre qu'après avoir maîtrisé l'Empoisonnement. Elle te permet de contrer les effets du poison sur une cible.";
					set @read_r, 1;
					next;
					//misceffect EF_DETOXICATION;
					next;
					break;
				case 7:
					mes "[Yierhan]";
					mes "Ouais... J'en ai marre de parler de ça de toutes façons. Passons à l'étape suivante de ton entraînement...";
					next;
					mes "[Yierhan]";
					mes "Maintenant que tu sais tout sur les compétences de Voleur, je vais te demander de les améliorer un peu avant de revenir me voir, c'est compris?";
					set tu_thief01, 4;
					//misceffect EF_HIT5;
					if(@read_d){
						getexp 300,100;
					}
					if(@read_f){
						getexp 300,100;
					}
					if(@read_s){
						getexp 300,100;
					}
					if(@read_p){
						getexp 300,100;
					}
					if(@read_h){
						getexp 300,100;
					}
					if(@read_r){
						getexp 300,100;
					}
					close;
					break;
				}
			}
		} else if(tu_thief01 == 4){
			mes "Bon, on a vu ensemble les compétences, et tu t'es entraîné"+ ( sex ? "" : "e" ) +" un peu, pas vrai? En tous cas tu as intérêt...";
			next;
			mes "[Yierhan]";
			mes "Maintenant je voudrais que tu t'entraînes un peu au combat. Ça va te permettre d'apprendre à utiliser tes techniques au mieux, en fonction des circonstances.";
			next;
			mes "[Yierhan]";
			mes "Vas me chercher 10 ^ff0000Plumes d'Oiseau^000000. Tu peux en récupérer sur les Pickys qu'il y a dans le coin. Ça ne devrait pas être trop difficile. Oh, et tu pourras utiliser ces Ailes pour revenir.";
			set tu_thief01, 5;
			savepoint "moc_ruins",80,164;
			getitem 602, 1;
			getexp 100,50;
			//misceffect EF_HIT5;
			close2;
			warp "moc_fild12",158,373;
			end;
		} else if(tu_thief01 == 5 || tu_thief01 == 6){
			if(countitem(916) < 10){	//Feather_Of_Birds
				mes "Hé, tu n'as pas les 10 ^ff0000Plumes d'Oiseau^000000 que je t'avais demandé! Garde en tête que l'expérience est plus importante que les connaissances.";
				close2;
				warp "moc_fild07",203,38;
				end;
			} else {
				mes "Ah, te revoilà!~ Bien joué, "+ ( sex ? "mon pote" : "ma grande" ) +". Ces plumes sont le symbole de tes progrès.";
				next;
				mes "[Yierhan]";
				mes "J'espère que tu continueras à faire de ton mieux. Tu as des questions?";
				next;
				if(tu_thief01 == 6){
					switch(select("En ce qui concerne les marques.", "Non.")){
					case 1:
						mes "[Yierhan]";
						mes "Ah... Tu as vu les empreintes, hein? Hum... Je ne voulais pas t'en parler, comme tu viens tout juste de devenir "+ ( sex ? "Voleur" : "Voleuse" ) +", mais...";
						next;
						mes "[Yierhan]";
						mes "Il y a eu du grabuge au sud de la ville. Ça s'est passé vers minuit, alors il n'y a pas beaucoup de mode au courant. Je suis resté assez tard là-bas pour une réunion de la guilde, et j'ai pu tout voir.";
						next;
						mes "[Yierhan]";
						mes "Je suis retourné sur le lieu du combat plus tard, mais tout ce que j'ai trouvé, c'est des empreintes en direction du sud. On dirait que ça a un rapport avec les Assassins... Il y a des traces de poison un peu partout. Mais c'est juste une supposition.";
						next;
						mes "[Yierhan]";
						mes "Si ça t'intéresse, tu peux suivre les empreintes, mais ne t'en approche pas trop: il y a encore des traces de poison.";
						set tu_thief01, 8;
						getitem 1207, 1;
						getexp 1000,500;
						//misceffect EF_HIT5;
						close;
						break;
					case 2:
						mes "[Yierhan]";
						mes "Bon, d'accord. J'imagine que tu seras devenu"+ ( sex ? "" : "e" ) +" super balèze la prochaine fois que je te verrai! Ah, et tiens, prends ça aussi...";
						mes "Hahaha...";
						set tu_thief01, 7;
						getitem 1207, 1;
						getexp 500,200;
						//misceffect EF_HIT5;
						close;
						break;
					}
				} else {
					switch(select("C'était sympa de discuter...", "Non.")){
					case 1:
						mes "[Yierhan]";
						mes "Hahaha... Ouais, c'était sympa de discuter. J'espère que tu ne rigoleras pas avec ton entraînement. Si tu bosses dur, tu pourrais devenir "+ ( sex ? "un grand Voleur" : "une grande Voleuse" ) +"!";
						next;
						mes "[Yierhan]";
						mes "Ah, et tiens, prends ça aussi. Hahaha...";
						set tu_thief01, 7;
						getitem 1207, 1;
						getexp 500,200;
						//misceffect EF_HIT5;
						close;
						break;
					case 2:
						mes "[Yierhan]";
						mes "Bon, d'accord. J'imagine que tu seras devenu"+ ( sex ? "" : "e" ) +" super balèze la prochaine fois que je te verrai! Ah, et tiens, prends ça aussi...";
						mes "Hahaha...";
						set tu_thief01, 7;
						getitem 1207, 1;
						getexp 500,200;
						//misceffect EF_HIT5;
						close;
						break;
					}
				}
			}
		} else if(tu_thief01 == 7){
			mes "Il y a eu du grabuge au sud de la ville. Ça s'est passé vers minuit, alors il n'y a pas beaucoup de mode au courant. Je suis resté assez tard là-bas pour une réunion de la guilde, et j'ai pu tout voir.";
			next;
			mes "[Yierhan]";
			mes "Je suis retourné sur le lieu du combat plus tard, mais tout ce que j'ai trouvé, c'est des empreintes en direction du sud. On dirait que ça a un rapport avec les Assassins... Il y a des traces de poison un peu partout. Mais c'est juste une supposition.";
			next;
			mes "[Yierhan]";
			mes "Si ça t'intéresse, tu peux suivre les empreintes, mais ne t'en approche pas trop: il y a encore des traces de poison.";
			next;
			mes "[Yierhan]";
			mes "Pour ta gouverne, les Herbes Vertes et les Potions Vertes annulent les effets du poison. Ah, et si jamais quelqu'un sort une Gemme Rouge, fais bien gaffe, il pourrait être en train d'essayer de t'empoisonner.";
			set tu_thief01, 8;
			getexp 200,100;
			//misceffect EF_HIT5;
			close;
		} else if(tu_thief01 == 8){
			mes "Alors, qu'est-ce que tu deviens? Combattre un ennemi inconnu est toujours risqué. On dirait que des étrangers s'en mêlent, alors fais bien gaffe.";
			next;
			mes "[Yierhan]";
			mes "Tu devrais te méfier de quand tu pars à l'aventure. Pas seulement des monstres, mais aussi des gens qui t'accompagnent, et parfois même de tes camarades.";
			close;
		} else if(tu_thief01 < 26){
			mes "J'ai entendu dire qu'un assassin qui était en mission n'est jamais revenu. Sa guilde a annoncé qu'il avait mené sa mission à bien, mais il n'est pas rentré. C'était quelqu'un de très compétent, je crois me souvenir.";
			next;
			mes "[Yierhan]";
			mes "Quoi qu'il en soit, fais bien gaffe à toi.";
			close;
		} else if(tu_thief01 == 26){
			mes "Alors, tu as accompli ta mission? Je sais que ça n'est pas simple...";
			next;
			switch(select("J'enquête toujours.:Pas encore.:Oui.")){
			case 1:
				mes "[Yierhan]";
				mes "Ah... Je vois. Je sais que ce n'est pas simple. Continue le bon boulot.";
				close;
				break;
			case 2:
				mes "[Yierhan]";
				mes "Ah... Je vois. Je sais que ce n'est pas simple. Continue le bon boulot.";
				close;
				break;
			case 3:
				mes "[Yierhan]";
				mes "Oh, c'est vrai? Hé bien je t'écoute! J'attendais vraiment ce moment.";
				next;
				mes "- Vous lui parlez des conclusions de votre enquête et du morceau de vêtement que vous avez retrouvé. -";
				next;
				mes "[Yierhan]";
				mes "Hum... Je vois. Fais-moi voir ce bout de tissu.";
				next;
				mes "- Vous lui donnez le morceau de vêtement. -";
				next;
				mes "[Yierhan]";
				mes "Hum... Est-ce que ce ne serait pas...? Je vois, je vois... T'as vraiment fait du bon boulot. Tu viens de me rapporter des informations très intéressantes.";
				next;
				mes "[Yierhan]";
				mes "Tu vois le motif sur le tissu? C'est un code propre aux Assassins. Celui-là ressemble à ceux sur les vêtements des Assassins de haut rang.";
				next;
				mes "[Yierhan]";
				mes "Avec ça, je devrais pouvoir trouver ce qui s'est passé cette nui-là. Merci.";
				next;
				mes "[Yierhan]";
				mes "Laissons des membres de la guilde plus expérimentés continuer l'enquête. Tu as parfaitement fait ton boulot - il est temps de se reconcentrer sur ton entraînement.";
				next;
				mes "[Yierhan]";
				mes "Tiens, prends ça en guide de récompense.";
				set tu_thief01, 27;
				getitem 2307, 1;
				set Zeny, Zeny + 5000;
				getexp 8000,3000;
				next;
				mes "[Yierhan]";
				mes "A la prochaine!";
				close;
				break;
			}
		} else {
			mes "Tu devrais toujours chérir tes rêves. La vie ne saurait s'approcher de la perfection sans rêves.";
			close2;
		}
	} else if(Class == Job_Mage || Class == Job_Mage_High || Class == Job_Wizard || Class == Job_High_Wizard || Class == Job_Sage || Class == Job_Professor){
		if(tu_magician01 < 7){
			mes "J'entraîne les Voleurs. Tu tu n'en es pas un, je le vois bien! Il fallait devenir "+ ( sex ? "Voleur" : "Voleuse" ) +" si jamais tu voulais que je t'enseigne des trucs! Hahaha.";
			close;
		} else if(tu_magician01 == 7){
			mes "Hum...? C'est bizarre, d'habitude il n'y a que les Voleurs qui traînent dans le coin. Qu'est-ce que quelqu'un comme toi fait ici? A moins que...";
			next;
			switch(select("Je viens de la part de 'Mana.'")){
			case 1:
				mes "[Yierhan]";
				mes "Ah, je t'attendais, l'ami"+ ( sex ? "" : "e" ) +". Bien, bien. J'avais demandé un coup de à Mana.";
				next;
				mes "[Yierhan]";
				mes "Tu arrives au bon moment. Tu vois, on a découvert des choses étranges dans le Sud de Morroc.";
				next;
				mes "[Yierhan]";
				mes "En gros, on a trouvé des traces de poison, qui aurait été utilisé en combat. On mène l'enquête à ce sujet, mais on doit aussi s'occuper d'autres choses plus urgentes.";
				next;
				mes "[Yierhan]";
				mes "C'est pour ça que j'ai demandé l'aide de la Guilde des Mages. Je suis assez proche de Mana, alors...";
				next;
				mes "[Yierhan]";
				mes "Quoi qu'il en soit, il faudrait que tu ailles au Sud de Morroc - ça devrait être le meilleur endroit pour commencer l'enquête. Tu verras ce qu'on a trouvé juste après la porte Sud de la ville.";
				set tu_magician01, 8;
				break;
			}
		} else if(tu_magician01 < 26){
			mes "On raconte qu'un Assassin assez doué n'est jamais rentré de mission...";
			next;
			mes "[Yierhan]";
			mes "Le truc bizarre, c'est que la Guilde des Assassins a annoncé que la mission a bel et bien été menée à bien.";
			next;
			mes "[Yierhan]";
			mes "Fais bien gaffe, l'ami"+ ( sex ? "" : "e" ) +". On dirait que ta mission pourrait avoir un lien avec ce mystérieux Assassin...";
			close;
		} else if(tu_magician01 == 26){
			mes "Hum... Qu'est-ce que tu deviens? J'ai eu trop de boulot pour me concentrer sur l'enquête.";
			next;
			switch(select("J'enquête toujours.:Il me faut plus de temps.:J'ai fini d'enquêter.")){
			case 1:
				mes "[Yierhan]";
				mes "Ah... Je vois. Je sais que ça n'est pas simple. Continue le bon boulot!";
				close2;
				break;
			case 2:
				mes "[Yierhan]";
				mes "Ah... Je vois. Je sais que ça n'est pas simple. Continue le bon boulot!";
				close2;
				break;
			case 3:
				mes "[Yierhan]";
				mes "Oh, c'est vrai? Hé bien je t'écoute! J'attendais vraiment ce moment.";
				next;
				mes "- Vous lui parlez des conclusions de votre enquête et du morceau de vêtement que vous avez retrouvé. -";
				next;
				mes "[Yierhan]";
				mes "Hum... Je vois. Fais-moi voir ce bout de tissu.";
				next;
				mes "- Vous lui donnez le morceau de vêtement. -";
				next;
				mes "[Yierhan]";
				mes "Hum... Est-ce que ce ne serait pas...? Je vois, je vois... T'as vraiment fait du bon boulot. Tu viens de me rapporter des informations très intéressantes.";
				next;
				mes "[Yierhan]";
				mes "Tu vois le motif sur le tissu? C'est un code propre aux Assassins. Celui-là ressemble à ceux sur les vêtements des Assassins de haut rang.";
				next;
				mes "[Yierhan]";
				mes "Avec ça, je devrais pouvoir trouver ce qui s'est passé cette nui-là. Merci.";
				next;
				mes "[Yierhan]";
				mes "Ah, prends ça en guise de remerciement. Je vais envoyer un message à  'Mana' à ton sujet. A la prochaine!";
				set tu_magician01, 27;
				set Zeny, Zeny + 5000;
				getexp 5000,2000;
				close2;
				break;
			}
		} else {
			mes "Comme je le pensais bien, tout cela n'a pas été fait par un seul groupe. Il y en avait au moins deux ou trois d'impliqués. Hum... J'ai peur que quelque chose de terrible ne se produise bientôt.";
			close;
		}
	}
	mes "Les tempêtes de sable sont la chose la plus romantique dans le désert! Hahaha.";
	close;
}
