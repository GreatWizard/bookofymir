//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par No Healing, corrigé par Etna
//===== eAthena Script ======================================= 
//= Kiel Hyre Quest
//===== By: ================================================== 
//= L0ne_W0lf (Script), DZeroX (Timer)
//===== Current Version: ===================================== 
//= 4.6
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Quest for opening Robot Factory first and second floors.
//===== Comments: ============================================
//= See below
//===== Additional Comments: ================================= 
//= 1.0 First version, needs testing [Playtester]
//= 1.1 Copied over some changes made by -Tsuyuki- [L0ne_W0lf]
//=	Reworked variables slightly so that the permenents ones aside from the 
//=	main tracking variable, are deleted when no longer needed. Renamed
//=	several variables that handle strings to @KHInput$. [L0ne_W0lf]
//= 1.2 Removed duplicate NPCs. [Toms]
//= 1.3 More Bug fixes, fixed typo. Updated Additional comments. [L0ne_W0lf]
//= 1.4 Fixed doors not resetting in kh_kiehl01 after 30 second timer is up. [L0ne_W0lf]
//= 1.5 Another bug fixed (&& instead of ||). [L0ne_W0lf]
//= 1.6 More squashed bugs with if checking. [L0ne_W0lf]
//= 1.7 Corrected some spelling mistakes. [Balish]
//	Corrected Exploit with the pub man, giving infinite wine and money.
//	Corrected pool and map not showing text for some states of the quest.
//	Corrected Elly showing cutin but not text for some states of the quest (causing players to log off to continue).
//	Let fisherman talk to you when you have 10 fishes OR MORE.
//	Corrected not letting you enter the tombstone after finishing the quest.
//= 1.8 Elly now deletes the Iron_Box, and Kiel Hyre deletes the Pin. [L0ne_W0lf]
//= 	More security on the 5th Big Door, now makes sure no one outside of who-
//=	acticated it initially can use it. Resets after 10 minutes to make it.
//=	Made it so that you need to be at least step 84 to activate the final door.
//=	Corrected not deleting Allysia's Ring from inventory.
//= 1.9 Exit will now enable if you are just retrieving the Ring, keeps you from getting stuck. [L0ne_W0lf]
//=	Corrected "KH_Kielh02", was setting off a debug, invalid map index. Should have been "kh_Kiehl02"
//=	Made .KHQuestBusy$ into a temp global variable. $@KHQuestBusy$. 
//= 2.0 Changed the labels on the "Big Doors" to "Identifier Names" to prevent an error with using getvariableofnpc [L0ne_W0lf]
//= 2.1 Fifth door now checks for a variable HIGHER or EQUAL to 74 not 84, as pointed out by Stollen. [L0ne_W0lf]
//= 2.2 Another fix for Elly, no longer RESETS to 26 when you talk to her. [L0ne_W0lf]
//= 2.3 Should NOT have been setting .KHQuestBusy at all. It's not used or accessed anymore. Removed. [L0ne_W0lf]
//= 2.4 Lots of fixes (4ish) from Valandi. [L0ne_W0lf]
//=	Added a getmapuser check to the mapwarp in KHTimerEnd so it's not trying to warp out 0 users. [L0ne_W0lf]
//= 2.5 Fixed several small issues (and large one) such as typos in cutins, typos in dialog, etc. [L0ne_W0lf]
//=	Added some of the missing dialogs for several NPCs, and fixed a few NPC locations.
//=	ALL books can now trigger the wall.
//=	Allysia will now summon 4 (was 2) G_CONSTANTS instead of REGULAR constants.
//=	Fixed where Golden_Key and Kiel_Button are deleted.
//=	Removed KHTimerEnd, and instead use OnReset.
//=	Fixed typo in the second NPC that gives Black Keycards.
//=	Added Monster Warps. (Keeps monsters from being in dungeon quest area.) [Non-functional]
//= 2.6 Stupid mistake, accidentally had 4 copies of the same script in 1... [L0ne_W0lf the bonehead]
//= 2.7 Fixed the wrong name being displayed for Kiel Hyre in one dialog. 
//=	Removed $@KH_DoorInvoker entirely. The quest is now working like it should according to iRO.
//=	Fixed up a few more NPC headers. Facing direction fixes mostly this time.
//=	More progression changes to Kiehl Hyre. He no longer stops dialog after the agents appear.
//= 2.8 Fixed the weight checking in Vandt, and added official dialog. [L0ne_W0lf]
//=	Added weight checks for all NPCs that give items. Semi-official dialog.
//=	Got the missing portion of dialog for Mills'.
//=	Will add further proper dialogs as I get to them on the second runthrough of the quest.
//= 2.8a minor optimization with extra goto, else or empty switch [Lupus]
//= 2.9 Fixed the "enablenpc" problem with the missing spaces. No idea how it happened. [L0ne_W0lf]
//= 3.0 Fixed a typo in Elly, in her room. (was <= should have been <) [L0ne_W0lf]
//=	Added an 'end;' to the cookie basket. Kiel Hyre's final dialog will now delete all quest-related items.
//= 3.1 Moved the "delitem" for all related quest items to the proper NPC. [L0ne_W0lf]
//= 3.2 Fixed error with Allysia teling you to "Go away." maybe. [L0ne_W0lf]
//= 3.2b Fixed some typos and grammar mistakes. [SinSloth]
//= 3.3 Fixed the receiver log out bugs. [L0ne_W0lf]
//= 3.4 Fixed bug when u can't get a Yellow Keycard if u got the Blue one at first [Lupus]
//=     Note: There are many condition checks that should be omitted in the future fixed item names in item_db.txt / txt resources according the quest
//=     for items, should be used bitwise methods in a single variable. So let's fix the rest possible bugs (if they exist) and then optimize ^_-		
//= 3.5 Changed the way the Black Keycard distibuting NPCs work slighty. [L0ne_W0lf]
//= 3.6 Keil Hyre removes ALL outstanding quest items when he is talked [L0ne_W0lf]
//=	to after finishing the quest.
//= 3.7 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 3.8 Corrected some delitems. (bugreport:690) [Samuray22]
//= 3.9 Fixed a cutin not being erased. (bugreport:1539) [L0ne_W0lf]
//= 4.0 A whole bunch-a-updates to various npcs. [L0ne_W0lf]
//=     Corrected condition checks throughout the quest.
//=     Npcs no longer make use of the goto command.
//=     Uses temp npc vars now instead of temp char.
//=     Reduced the amount of variables used.
//=     Corrected some of the cutins in npcs with them.
//=     Added the missing dialogs and weight checks.
//=     Fixed many typos, and grammatical errors.
//=     NPC now names use '#<name>' instead of labels.
//=     Updated some NPC positions, and facings.
//=     Changed the way some NPCs work.
//=     Updated comments for get/delitem commands.
//= 4.1 Added an 'end;' to Kiel Hyre's dialog for the "Yes" option for if you want to confront Kiehl.
//= 4.2 Fixed some issues reported in bugreport:2129. [L0ne_W0lf]
//= 4.2a Follow up to last update. [L0ne_W0lf]
//= 4.2b Missed a condition, fixed it though. [L0ne_W0lf]
//= 4.2c More various fixes :D [L0ne_W0lf]
//= 4.3 Fixed improper condition in the Elly NPC. [L0ne_W0lf]
//=     Corrected some typos in the dialogs.
//= 4.4 Updated reviecer and robots NPC to use mobcount. (bugreport:2380) [L0ne_W0lf]
//= 4.5 Removed a delitem that was no logner needed. [L0en_W0lf]
//= 4.6 Fixed impossible condition for security guard. [L0ne_W0lf]
//============================================================ 

//============================================================================
// Kiel Hyre Quest/Robotic Lab Dungeon
//
// Script Dialog Obtained and Copied Down by L0ne_W0lf
// Timer work and some variable dances by DZeroX
// Based on [iRO Sakray] as of [03/29/2007]
//============================================================================
//----------------------------------------------------------------------------
// [Notes]
//  - Let it be noted that ALL DIALOG IS OFFICIAL TO IRO UNLESS SPECIFIED.
//  - I've fixed a few of iRO's mistakes, but I'm sure I made some of my own. 
//  - Will probably require a major optimizing.
//  - Not going to list ALL of the more usless NPCs. (Doors + Empty "Beautiful Ladies")
//  - Parties are *NOT REQUIRED* as per Doddler's Wiki information.
//
//----------------------------------------------------------------------------
// [Maps]                [Use]
//  yuno_in01 ........... Start quest in pub
//  kh_school ........... Deliver Wine here, Meet Elly
//  yuno ................ Get Delivery for/from "Little Kid"
//  lighthalzen ......... Get Delivery package for "Little Kid"
//  kh_vila ............. Kiel Hyre cottage.
//  kh_dun01 ............ Robot Factory; Save Kiel Hyre
//  kh_mansion .......... Kiel Hyre's Mansion in Lighthalzen
//  kh_kiehl01 .......... Kiehl's room/Cave
//  kh_kiehl02 .......... Kiehl's room/Boss Room
//
//----------------------------------------------------------------------------
// [Variables in Use]
//  KielHyreQuest 	- Main quest tracking variable (Quest finished at 106)
//
//  KHToastGirlEnd	- Pick up delivery if set to 1, 
//			- picked up delivery if set to 2
//			- finished if set to 3 
//			- Deleted once no longer needed.
//
//  KHCottagePoem1	- Tracks Poem 1 quest in cottage. (finished = 5)
//			- Deleted once no longer needed.
//
//  KHCottagePoem2	- Tracks Poem 2 quest in cottage. (finished = 3)
//			- Deleted once no longer needed.
//
//  .@KHInput$		- Used with "Security Guard"
//  			- Used with "Test Tube"
//  			- Used when "Elly" (inputing string in attempt to wake her)
//  			- Used with "Grave", (twice)
//  			- Used with "Heavy Door"
//  			- Used with "Mechanical Device"
//  			- Used for opening various doors in Kiehl's area.
//
//  .@KHFirstKeyhole	- Holds 1 (green keycard) or 2 (cottage key)
//  .@KHSecondKeyhole	- Holds 1 (green keycard) or 2 (cottage key)
//
//  .@KHPotionColor$	- Holds 'red', 'green', or 'yellow' depending on what wrong potion is selected.
//
//  .@KHSwords 		- Used with "Sword Hilt"
//
//  .@KHQRead		- Make sure player has asked questions to Kiel Hyre
//
//  .@KHTitle$		- Holds "Mr", "Mrs", or "Miss"
//
//  .@KHDoorPushAttempt	- Keeps track of how many times a person has attmepted to push the door open.
//
//  .@KHPryingItem$	- Used for incorrect options when prying open last door.
//
//  .KHKilled		- Tracks how many monsters have been killed. Used with "Receiver", "Robots", and "Kiehl" NPCs
//
//  .KHDoor1Open	- 0/1 Depending on if the first door is opened.
//  .KHDoor2Open	- 0/1 Depending on if the second door is opened.
//  .KHDoor3Open	- 0/1 Depending on if the third door is opened.
//  .KHDoor4Open	- 0/1 Depending on if the fourth door is opened.
//  .KHDoor5Open	- 0/1 Depending on if the fifth door is opened.
//
//  .KHKilledBoss	- 0/1 depending on if the boss has been killed.
//
//  $@KHQuestBusy	- 0/1 depending on if the boss room has been set in motion.
//
//----------------------------------------------------------------------------
// [NPCs]			 [Location]
//  Pub Master .................. yuno_in01	[ 35,178]
//  Hanie ....................... yuno_fild08	[196,196]
//  Security Guard .............. yuno_fild08	[158,194]
//  Security Guard .............. yuno_fild08	[158,183]
//  Student ..................... kh_school	[ 57,142]
//  Student ..................... kh_school	[ 57,139]
//  Lady ........................ kh_school	[176, 60]
//  Cute Student ................ kh_school	[179, 39]
//  Little Kid .................. yuno		[217,114]
//  Windmill Owner .............. lighthalzen	[365,300]
//  Cottage Keeper .............. yuno_fild02	[ 93,210]
//  Door ........................ yuno_fild02	[ 75,218]
//  Door ........................ yuno_fild02	[109,218]
//  Wall ........................ kh_vila	[191, 14]
//  Book ........................ kh_vila	[184, 20]
//  Book ........................ kh_vila	[181, 20]
//  Book ........................ kh_vila	[178, 20]
//  Book ........................ kh_vila	[175, 19]
//  Letter ...................... kh_vila	[179, 11]
//  Box ......................... kh_vila	[ 16,175]
//  Apple Box ................... kh_vila	[ 30,184]
//  Map ......................... kh_vila	[ 44,125]
//  Pot ......................... kh_vila	[ 33,148]
//  Calabash .................... kh_vila	[ 14, 55]
//  Pool ........................ kh_vila	[ 39, 39]
//  Vicious Dog ................. kh_vila 	[180,178]
//  Drawer ...................... kh_vila 	[115,186]
//  Box ......................... kh_vila 	[107,126]
//  Bookshelf ................... kh_vila 	[181,138]
//  Sword Hilt .................. kh_vila 	[136, 69]
//  Test Tube ................... kh_vila	[179, 69]
//  Test Tube ................... kh_vila	[185, 69]
//  Window ...................... yuno_fild08	[ 69,185]
//  Elly ........................ kh_school	[178,180]
//  Cookie Basket ............... kh_school	[175,175]
//  Window ...................... kh_school	[188,185]
//  Grave ....................... yuno_fild08	[ 72,170]
//  Heavy Door .................. kh_dun01	[236, 48]
//  Beautiful lady .............. kh_school	[119,149]
//  Beautiful lady .............. kh_school	[122,186]
//  Signboard ................... kh_dun01	[163,223]
//  Mechanical Device ........... kh_dun01	[166,223]
//  Mechanical Device ........... kh_dun01	[162,206]
//  Kiel_Hyre_Door .............. kh_dun01	[224,232]
//  Steward ..................... kh_mansion	[ 78, 55]
//  Kiel Hyre ................... kh_mansion	[ 22, 27]
//  Allysia ..................... kh_mansion	[ 22, 27]
//  Abduction_trigger ........... lighthalzen	[188,200]
//  Mysterious Woman ............ kh_mansion	[ 25, 79]
//  Odd Granma .................. yuno		[250,132]
//  Old Lady .................... yuno 		[250,132]
//  Rosimmir_Entrance ........... yuno		[273,141]
//  Table ....................... kh_rossi	[ 23, 23]
//  Shelf ....................... kh_rossi	[ 92, 40]
//  Desk ........................ kh_rossi	[144,146]
//  Bookshelf ................... kh_rossi	[144,146]
//  Bed ......................... kh_rossi	[154,286]
//  Old Fisherman ............... yuno_fild12	[232,222]
//  Wooden Board ................ yuno_fild09	[158,217]
//  Kiehl_Room_Warp ............. kh_mansion	[ 29, 27]
//  Entrance Device#KHEntry1 .... kh_dun01	[ 44, 20]
//  Entrance Device#KHEntry2 .... kh_dun01	[ 44,203]
//  Security Guard .............. yuno_fild08	[158,194]
//  Receiver .................... kh_kiehl01	[ 17, 39]
//  Flower Vase ................. kh_kiehl01	[ 13, 40]
//  Box ......................... kh_kiehl01	[ 19, 25]
//  Big Door .................... kh_kiehl01	[ 44, 33]
//  Big_Door_1_Warp ............. kh_kiehl01	[ 43, 33]
//  Big Door .................... kh_kiehl01	[174, 40]
//  Big_Door_2_Warp ............. kh_kiehl01	[174, 39]
//  Big Door .................... kh_kiehl01	[ 77,108]
//  Big_Door_3_Warp ............. kh_kiehl01	[ 68,108]
//  Big Door .................... kh_kiehl01	[ 41,177]
//  Big_Door_4_Warp ............. kh_kiehl01	[ 78,177]
//  Robots ...................... kh_kiehl01	[ 15,181]
//  Big Door .................... kh_kiehl01	[166,187]
//  Big_Door_5_Warp ............. kh_kiehl01	[166,186]
//  Kiehl_Room_Trap ............. kh_kiehl02	[ 49, 15]
//  Kiehl#Original .............. kh_kiehl02	[ 50, 52]
//  KiehlRoom ................... kh_kiehl02	[  1,  1]
//  Mitchell#KiehlRoom .......... kh_kiehl02	[ 49, 55]
//  Agent#KHAgent1 ....   ....... kh_kiehl02	[ 53, 52]
//  Agent#KHAgent2 .............. kh_kiehl02	[ 51, 49]
//  Agent#KHAgent3 .............. kh_kiehl02	[ 47, 50]
//  Agent#KHAgent4 .............. kh_kiehl02	[ 46, 53]
//  Kiehl#Copy .................. kh_kiehl02	[ 48, 53]
//  Kiehl_Roomn_Exit ............ kh_kiehl01	[ 50, 59]
//
//----------------------------------------------------------------------------
// [Functions]
//  None
//
//----------------------------------------------------------------------------
// [Conflicts]
//   Resolved
//
//============================================================================

//============================================================================
// Affiliated NPCs
//============================================================================

//----------------------------------------------------------------------------
// Juno Pub Master/Starting point.
//----------------------------------------------------------------------------
yuno_in01,35,179,4	script	Patron du Bar::Pub Master#kh	46,5,5,{
	if (checkweight(7487,1) == 0) {
		mes "[Vandt]";
		mes "Une petite minute! Vous portez trop d'objets sur vous! Vous feriez mieux de placer quelques objets dans votre Stock Kafra ou vous ne pourriez en obtenir de nouveaux...";
		close;
	}
	if (KielHyreQuest == 0) {
		mes "[Vandt]";
		mes "Salut à vous! Bienvenue dans mon bar! Qu'est-ce que je vous sers?";
		next;
		switch(select("Une bière, s'il vous plaît.","Un cocktail, s'il vous plaît.","Du Soju, s'il vous plaît.","Rien pour l'instant.")) {
		case 1:
			mes "[Vandt]";
			mes "Bien, je vous sers un verre tout de suite. Il s'agit de notre Bière de Schwaltz.";
			next;
			mes "^3355FF*Glou* *Glou* *Glou*^000000";
			percentheal 5,-5;
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Aaah, elle est vraiment bonne! Ça fait du bien par où ça passe!";
			close;
		case 2:
			mes "[Vandt]";
			mes "Je suis désolé mais je n'ai plus rien pour faire des cocktails. Ce sera pour une prochaine fois, d'accord?";
			close;
		case 3:
			mes "[Vandt]";
			mes "Su S-soju? Je suis désolé mais on ne sert pas ça ici. C'est vraiment trop fort pour moi...";
			close;
		case 4:
			mes "[Vandt]";
			mes "D'accord, installez-vous et commandez quand vous serez prêt.";
			close;
		}
	}
	if (KielHyreQuest == 1) {
		mes "[Vandt]";
		mes "Salut à vous! Bienvenue dans mon bar! Qu'est-ce que je vous sers?";
		next;
		switch(select("Une bière, s'il vous plaît:Un cocktail, s'il vous plaît.:Du Soju, s'il vous plaît.:Vous avez l'air préoccupé, ça va?:Rien pour l'instant.")) {
		case 1:
			mes "[Vandt]";
			mes "Bien, je vous sers un verre tout de suite. Il s'agit de notre Bière de Schwaltz.";
			next;
			mes "^3355FF*Glou* *Glou* *Glou*^000000";
			percentheal 5,-5;
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Aaah, elle est vraiment bonne! Ça fait du bien par où ça passe!";
			close;
		case 2:
			mes "[Vandt]";
			mes "Je suis désolé mais je n'ai plus rien pour faire des cocktails. Ce sera pour une prochaine fois, d'accord?";
			close;
		case 3:
			mes "[Vandt]";
			mes "Du S-soju? Je suis désolé mais on ne sert pas ça ici. C'est vraiment trop fort pour moi...";
			close;
		case 4:
			mes "[Vandt]";
			mes "Oh, vous m'avez entendu? Je suis désolé. C'est juste que l'un de mes employés a eu un accident assez grave et que maintenant je n'ai personne pour livrer tout ça.";
			next;
			mes "[Vandt]";
			mes "On m'a passé une commande assez urgente pour l'Académie Kiel Hyre mais je ne trouve personne pour ce petit boulot.";
			next;
			switch(select("Je suis désolé"+ ( sex ? "" : "e" ) +" de l'apprendre:Voulez-vous que je vous aide?")) {
			case 1:
				mes "[Vandt]";
				mes "Eh bien je suis sûr que je trouverai bien un moyen de gérer ça. Vous ne connaissez personne qui serait intéressé par ce travail?";
				close;
			case 2:
				mes "[Vandt]";
				mes "Vraiment? C'est fantastique! Mais je dois quand même vous dire que ce travail ne sera peut-être pas aussi simple qu'il n'y paraît. Et j'attends de vous que vous livriez ça quoi qu'il en coûte.";
				next;
				switch(select("Je le ferai.:Laissez-moi y réfléchir...")) {
				case 1:
					mes "[Vandt]";
					mes "Je suis heureux de l'entendre. Voilà, apportez cette bouteille de Vin de Cuisine à Mlle ^ff0000Lecollane^000000 à l'Académie Kiel Hyre. Je vous payerai quand le travail sera fait, d'accord?";
					getitem 7487,1; //Tavern_Wine
					set KielHyreQuest,2;
					close;
				case 2:
					mes "[Vandt]";
					mes "D'accord, mais je dois vraiment livrer cela, alors si vous ne vous en sentez pas capable mais que vous connaissez quelqu'un qui pourrait le faire, envoyez-le moi s'il vous plaît.";
					close;
				}
			}
		case 5:
			mes "[Vandt]";
			mes "D'accord, installez-vous et commandez quand vous serez prêt.";
			close;
		}
	}
	else if ((KielHyreQuest >= 2) && (KielHyreQuest < 6)) {
		mes "[Vandt]";
		mes "S'il vous plaît, apportez cette bouteille de Vin de Cuisine à Mlle ^ff0000Lecollane^000000 à l'Académie Kiel Hyre. Dépêchez-vous de la livrer avant qu'elle ne commence à se plaindre du retard.";
		close;
	}
	else if (KielHyreQuest == 6) {
		if (countitem(7487) == 0) {
			mes "[Vandt]";
			mes "Oh, vous revoilà. Merci d'avoir livré cette bouteille. Un petit moment, je vais vous payer.";
			next;
			select("Il me faut plus de vin...");
			mes "[Vandt]";
			mes "Oh, il faut que vous livriez une autre bouteille? D'accord, un instant, je vais vous donner ça ainsi que votre paie.";
			next;
			mes "^3355FFFouille Fouille^000000";
			mes "^3355FFFouille Fouille^000000";
			next;
			mes "[Vandt]";
			mes "Et voilà! Encore merci de m'avoir aidé~";
			getitem 7487,1; //Tavern_Wine
			set zeny,zeny+1000;
			close;
		}
		else {
			mes "[Vandt]";
			mes "Hé, encore merci de m'avoir aidé l'autre fois. Je vous suis tombé dessus à l'improviste mais vous m'avez vraiment sauvé la mise. J'apprécie, vraiment!";
			close;
		}
	}
	else {
		mes "[Vandt]";
		mes "Vous êtes vraiment "+ ( sex ? "un bon employé" : "une bonne employée" ) +", vous savez ça? Dévoué"+ ( sex ? "" : "e" ) +", responsable, prêt"+ ( sex ? "" : "e" ) +" à aider les autres, et par-dessus tout dynamique! Je pense que vous irez loin dans la vie, jeune "+ ( sex ? "homme" : "femme" ) +", oui, vraiment~";
		close;
	}

//OnTouch2:
OnTouch:
	if (KielHyreQuest < 1) {
		mes "[Vandt]";
		mes "Arrrggghhh... Ça ne va pas... Ça ne va pas du tout! Qu'est-ce que je vais bien pouvoir faire?";
		set KielHyreQuest,1;
		close;
	} 
}

//----------------------------------------------------------------------------
// Juno Toast Sales Girl.
//----------------------------------------------------------------------------
yuno,217,114,6	script	Petite Fille::Little Kid#kh	96,{
	if (KielHyreQuest < 6) {
		mes "[Cezu]"; 
		mes "Toasts tout croustillants! Qui veut des toasts tout croustillants?~";
		close;
	}
	else if (KielHyreQuest == 6) {
		if (KHToastGirlEnd < 1) {
			mes "[Cezu]"; 
			mes "Toasts tout croustillants! Qui veut des toasts tout croustillants?~";
			next;
			switch(select("Je viens de la part d'Elly:Non, merci.")) {
			case 1:
				mes "[Cezu]"; 
				mes "Oh, je vois. Elle a dû gâcher encore un paquet d'ingrédients. C'est une habituée, alors je voudrais vraiment l'aider mais je ne peux pas faire grand-chose.";
				next;
				mes "[Cezu]"; 
				mes "Moi aussi je suis à court d'ingrédients, et je ne peux pas quitter mon échoppe pour aller en acheter. Je ne sais pas comment je vais faire si j'ai d'autres clients! Mais peut-être que vous pourriez m'aider moi-aussi...";
				next;
				mes "[Cezu]"; 
				mes "Est-ce que vous pourriez aller me chercher de la farine et des oeufs au ^3355FFMoulin de Lighthalzen^000000? Quand vous reviendrez, je pourrai partager les ingrédients et vous donner ce qu'il faut pour Elly.";
				next;
				mes "[Cezu]"; 
				mes "Je sais que je vous demande de tout faire seul"+ ( sex ? "" : "e" ) +", mais j'ai les mains liées. N-ne me demandez pas pourquoi c'est... C'est comme ça!";
				set KHToastGirlEnd,1;
				close;
			case 2:
				mes "[Cezu]";
				mes "D'accord d'accord~";
				mes "Revenez quand vous voudrez!";
				close;
			}
		}
		else if (KHToastGirlEnd == 1) {
			mes "[Cezu]";
			mes "Vous voulez bien aller au moulin de Lighthalzen et dire à Cezu qu'il me faut de la farine et des oeufs? Apportez-moi tout ça au plus vite dès que vous l'aurez~";
			close;
		}
		else if (KHToastGirlEnd == 2) {
			mes "[Cezu]";
			mes "Ah, vous avez tous les ingrédients! Merci beaucoup! J'en avais vraiment besoin. Voilà, prenez ces oeufs et cette farine et apportez tout ça à Elly. Oh, et passez-lui le bonjour de ma part! À bientôt~";
			delitem 7488,countitem(7488); //Delivery_Package // Modifié pour ne pas que le serveur gémisse (je mets un countitem) [NoH]
			getitem 7488,1; //Delivery_Package
			set KHToastGirlEnd,3;
			close;
		}
	}
//	else if ((KielHyreQuest >= 6) || (KHToastGirlEnd == 3)) { // Bugfix: si on laisse la seconde partie, on pourrait répéter ce bout de quête autant de fois qu'on veut... [NoH]
	else if ( KielHyreQuest >= 6 ) {
		//KHToastGirlEnd no longer needed-- deleting.
		set KHToastGirlEnd,0;
		mes "[Cezu]";
		mes "Toasts tout chauds et tout croustillants! Venez en profiter!";
		close;
	}
}

//----------------------------------------------------------------------------
// Lighthalzen WindMill Owner.
//----------------------------------------------------------------------------
lighthalzen,366,299,4	script	Propriétaire du Moulin::Windmill Owner#kh	46,{
	if (checkweight(7488,1) == 0) {
		mes "[Mills]";
		mes "Oh, mais vous portez beaucoup trop de choses sur vous! Allez placer le superflu dans vot' Stock Kafra si vous v'lez qu'je puisse vous donner que'que chose. Vous êtes là pour ça, non?";
		close;
	}
	if (KHToastGirlEnd < 1) {
		mes "[Mills]";
		mes "Hé, qu'est-ce qu'vous voulez? Chuis occupé là, vous pourriez r'passer plus tard? J'aurai l'temps de discuter.";
		close;
	}
	else if (KHToastGirlEnd == 1) {
		mes "[Mills]";
		mes "Hé, qu'est-ce qu'vous voulez? Vous avez de la chance, j'ai pas grand-chose à faire, mais si vous avez besoin d'quelque chose, vous feriez mieux de l'dire tout suite avant que je m'y r'mette.";
		next;
		switch(select("Je viens de la part de Cezu:......")) {
		case 1:
			mes "[Mills]";
			mes "Oh, Cezu de l'échoppe à toasts, c'est ça? Ouais, j'ai tout c'que la gamine voulait, là. Y a des oeufs là-dedans, alors faites bien attention avec. V'là, prenez ça...";
			next;
			mes "[Mills]";
			mes "C'est une habituée, la gamine, alors z'avez pas b'soin d'me payer maint'nant ou de m'ramener des trucs en échange! Ouai, j'sais comment les autres vous traitent, vous, les aventuriers... Bon, à bientôt, alors!";
			getitem 7488,1; //Delivery_Package
			set KHToastGirlEnd,2;
			close;
		case 2:
			mes "[Mills]";
			mes "......";
			mes "Hum, d'accord. Si vous aviez besoin de que'que chose faudra r'venir une autre fois, alors.";
			mes "Cuts, Cutz, t'es où, mon gars? Vaudrait mieux pour toi qu'tu sois pas en train d'roupiller, saligot!";
			close;
		}
	}
	else if (KHToastGirlEnd >= 2) {
		mes "[Mills]";
		mes "C'est une telle feignasse, ce Cutz! C'te saligot croit qu'il a l'temps de rigoler! Et il sait que j'pourrais trouver un meilleur gars! Il va voir de quel bois j'me chauffe!";
		close;
	}
}

lighthalzen,366,289,2	script	Meunier::Windmill Owner's Helper	97,{
	if (KHToastGirlEnd < 1) {
		mes "[Cutz]";
		mes "Oh, pourquoi il y a toujours tant de choses à faire? Et pourquoi Mills me fait travailler comme un baudet? Eh, c'est un travail comme un autre j'imagine...";
		close;
	}
	else if (KHToastGirlEnd >= 2) {
		mes "[Cutz]";
		mes "Pourquoi tu crois que je suis en train de faire la sieste, Mills? Regarde, regarde! Je suis en train de bosser, là! Tu ne le vois pas?";
		close;
	}
	else {
		mes "[Cutz]";
		mes "Hé, vous avez été faire un tout à l'échoppe de Cezu, pas vrai? Comment elle va, la gamine? Elle est si mignonne...";
		close;
	}
}

//============================================================================
//Kiel Hyre Academy NPCs
//============================================================================

//----------------------------------------------------------------------------
// Girl (Talks about the academy)
//----------------------------------------------------------------------------
yuno_fild08,196,196,5	script	Hanie#kh1	102,{
	mes "[Hanie]";
	mes "Oh, J'adorerais pouvoir étudier à l'Académie Kiel Hyre... Tous ceux qui y sont diplômés trouvent un très bon travail! Je suis presque jalouse d'eux!";
	next;
	select("L'Académie Kiel Hyre?");
	mes "[Hanie]";
	mes "Oh, l'Académie Kiel Hyre est une école fondée par Kiel Hyre, un inventeur qui devint riche grâce à d'étranges machines. Il a décidé d'en faire profiter la société en construisant une académie privée.";
	next;
	mes "[Hanie]";
	mes "Cependant, l'école n'accepte que des orphelins trop pauvres pour s'inscrire dans d'autres écoles. Parfois, il m'arrive même de souhaiter être une orpheline moi aussi! Enfin, pas vraiment, mais vous voyez ce que je veux dire...";
	close;
}


//----------------------------------------------------------------------------
// Academy Security Guards
//----------------------------------------------------------------------------
yuno_fild08,158,194,6	script	Garde de l'Académie::KHAGuard	852,{
	if ((KielHyreQuest < 2) || (KielHyreQuest > 31)) { 
		mes "[Garde de l'Académie]";
		mes "Je suis désolé mais je ne peux vous laisser entrer dans l'^FF0000Académie Kiel Hyre^000000 si vous n'êtes pas affilié à cette institution. Veuillez quitter ces lieux, à moins que vous ne soyez attendu"+ ( sex ? "" : "e" ) +" par l'un des membres du personnel.";
		close;
	}
	else if (KielHyreQuest == 2) { 
		mes "[Garde de l'Académie]";
		mes "Je suis désolé mais je ne peux vous laisser entrer dans l'^FF0000Académie Kiel Hyre^000000 si vous n'êtes pas affilié à cette institution. Veuillez quitter ces lieux, à moins que vous ne soyez attendu"+ ( sex ? "" : "e" ) +" par l'un des membres du personnel.";
		next;
		switch(select("Je suis ici pour une livraison.:......")) {
		case 1:
			mes "[Garde de l'Académie]";
			mes "Vous êtes ici pour une livraison? D'accord, donnez-moi simplement le ^FF0000nom^000000 du destinataire, puis celui de l'objet livré. Je dois vérifier avant de pouvoir vous laisser passer.";
			next;
			input .@KHDelivery$;
			if (.@KHDelivery$ != "Lecollane") {
				mes "[Garde de l'Académie]";
				mes "Vous êtes ici pour livrer du vin à... À qui?! Qu'est-ce que c'est que ce nom? Je... Je ne pense pas qu'il y ait quelqu'un qui s'appelle  "+.@KHDelivery$+" ici.";
				close;
			}
			input .@KHDelivery$ ;
			if (.@KHDelivery$ != "Vin de Cuisine") {
				mes "[Garde de l'Académie]";
				mes "Ah, vous allez livrer quelque chose à Mlle Lecollane?! Voyons ce que vous avez là... Du ^3355FF"+.@KHDelivery$+"^000000?";
				next;
				mes "[Garde de l'Académie]";
				mes "D'accord, je vais la biper pour savoir si elle vous attend bien...";
				next;
				mes "......";
				mes ".........";
				mes "............";
				next;
				mes "[Garde de l'Académie]";
				mes "Euh... Mlle Lecollane attend bien une livraison, mais pas de cela. Peut-être devriez-vous vérifier que vous n'avez pas confondu deux livraisons...";
				close;
			}
			mes "[Garde de l'Académie]";
			mes "Alors vous allez livrer du vin à Mlle Lecollane?! Laissez-moi la biper pour confirmer cela rapidement.";
			next;
			mes "......";
			mes ".........";
			mes "............";
			next;
			mes "[Garde de l'Académie]";
			mes "D'accord, tout a l'air en ordre. Mlle Lecollane vous attend. Vous pouvez entrer.";
			close2;
			warp "kh_school",71,155;
			end;
		case 2:
			mes "[Garde de l'Académie]";
			mes "Si personne ne vous attend à l'Académie, veuillez ne pas rester dans les parages.";
			close;
		}
	}
	else if (KielHyreQuest < 32) { 
		mes "[Garde de l'Académie]";
		mes "Oh, vous avez de nouveau quelque chose à livrer à l'Académie? Je me souviens de vous de la dernière fois – j'imagine que je peux vous laisser passer sans vérifier...";
		close2;
		warp "kh_school",71,155;
		end;
	}
}

yuno_fild08,158,183,7	duplicate(KHAGuard)	Garde de l'Académie::Security Guard#2	852

//----------------------------------------------------------------------------
// Gossiping Teenagers/Story NPCs
//----------------------------------------------------------------------------
kh_school,57,142,6	script	Étudiante::Student#kha	893,3,3,{
	mes "[Laci]";
	mes "Oh, ouah! D'habitude, nous n'avons jamais de visiteurs sur le campus! Hum, vous ne faites pas partie de l'Académie, n'est-ce pas?";
	close;

OnTouch:
	mes "[Laci]";
	mes "Hé, Nesha! Tu sais pourquoi Aaci n'est pas venue en cours depuis un moment?";
	next;
	mes "[Gui Nesha]";
	mes "C'est vrai, ça fait un moment que je ne l'ai pas vue. Est-ce que quelque chose lui est arrivée?";
	next;
	mes "[Laci]";
	mes "C'est parce que.... Aaci a vu un vrai fantôme! San déc'! Je te mens pas! Je le sais de... Enfin.. tu sais: mes sources.";
	next;
	mes "[Gui Nesha]";
	mes "...Quoi...? Faut arrêter! Tu devrais sortir un peu...";
	close;
}

kh_school,57,139,0	script	Étudiante::Student#khb	893,{
	mes "[Gui Nesha]";
	mes "Tu n'es pas un peu vieille pour croire encore à ce genre de choses? Tu sais que les histoires de fantômes, c'est juste des contes pour enfants, pas vrai? Allez, Laci, arrête avec ça!";
	emotion e_swt2;
	close;
}

//----------------------------------------------------------------------------
// Mlle Lecollane
//----------------------------------------------------------------------------
kh_school,176,60,4	script	Femme::Lady#kh	101,{
	if (KielHyreQuest < 2) {
		mes "[Mlle Lecollane]";
		mes "Qu'est-ce qui se passe? Les gens de l'extérieur n'ont pas le droit de pénétrer dans l'enceinte de l'Académie. Je vais devoir vous demander de partir.";
		close;
	}
	else if (KielHyreQuest == 2) { 
		mes "[Mlle Lecollane]";
		mes "Oh, bonjour. Ah, vous êtes venu"+ ( sex ? "" : "e" ) +" me livrer le vin! Le Garde m'a appelée et m'a dit que vous veniez.";
		next;
		switch(select("Oui, c'est cela!:Euh... Désolé"+ ( sex ? "" : "e" ) +"!")) {
		case 1:
			mes "[Mlle Lecollane]";
			mes "Eh bien, je m'attendais à le recevoir un peu plus tôt, mais je suppose que ce qui est fait est fait. C'est juste que j'attendais avec impatience cette bouteille de...";
			next;
			mes "^3355FF*Cling! Crrrack!*^000000";
			next;
			mes "[Mlle Lecollane]";
			mes "Elly? Elly, est-ce que tu as encore cassé quelque chose? Tu dois faire plus attention! Si tu n'as pas préparé tous ces petits gâteaux avant ce soir, tes notes du semestre vont en pâtir!";
			next;
			mes "[Elly]";
			mes "Mais je...";
			next;
			mes "[Mlle Lecollane]";
			mes "^FF0000Elly^000000!!";
			next;
			mes "[Elly]";
			mes "......";
			mes "Oui, Mlle Lecollane.";
			next;
			mes "[Mlle Lecollane]";
			mes "*Ahem* Excusez-moi. Vous voulez bien poser cette bouteille ici? Bien, vous pouvez y aller maintenant, et s'il vous plaît, ne traînez pas dans les couloirs de l'Académie.";
			delitem 7487,1; //Tavern_Wine
			set KielHyreQuest,4;
			close;
		case 2:
			mes "[Mlle Lecollane]";
			mes "Hum...? J'ai dû me tromper, j'imagine. Je suis désolée, je vous ai pris"+ ( sex ? "" : "e" ) +" pour quelqu'un d'autre.";
			close;
		}
	}
	else if (KielHyreQuest == 4) {
		mes "[Mlle Lecollane]";
		mes "Bien, vous m'avez livré cette bouteille, alors pourriez-vous quitter le campus dès que possible? *Soupire* Je n'arrive pas à croire que notre avenir soit entre les mains de petites filles comme elle...";
		close;
	}
}

//----------------------------------------------------------------------------
// Elly (In school)
//----------------------------------------------------------------------------
kh_school,179,39,0	script	Petite Étudiante::Cute Student#kh	895,{
	if (checkweight(908,200) == 0) {
		mes "[Elly]";
		mes "Je suis désolée, mais vous pourriez d'abord placer quelques objets dans votre Stock Kafra? Je ne peux pas vous aider si vous êtes surchargé"+ ( sex ? "" : "e" ) +" comme ça!";
		close;
	}
	cutin "kh_elly01",2;
	if (KielHyreQuest < 4) { 
		cutin "kh_elly03",2;
		mes "[Elly]";
		mes "Hein? Oh...! ^333333*Ouf*^000000 Je ne suis pas passée loin! J'ai failli encore en faire tomber une! Pourquoi est-ce j'ai toujours autant de mal à manipuler tout ça?";
	}
	else if (KielHyreQuest == 4) { 
		cutin "kh_elly03",2;
		mes "[Elly]";
		mes "Oh non, qu'est-ce que je vais bien pouvoir faire? ^333333*Snif*^000000";
		next;
		switch(select("Qu'est-ce qui s'est passé?:......")) {
		case 1:
			mes "[Elly]";
			mes "Je... Je dois finir de préparer tous ces petits gâteaux, mais je viens de faire tomber les ingrédients par terre. Je ne sais pas comment je vais faire maintenant...";
			next;
			switch(select("Qu'est-ce que je peux faire pour t'aider?:Oh, je suis désolé"+ ( sex ? "" : "e" ) +" pour toi.")) {
			case 1:
				cutin "kh_elly02",2;
				mes "[Elly]";
				mes "C'est vrai? Vous allez vraiment m'aider? C'est fantastique! Merci! Merci beaucoup!";
				next;
				break;
			Case 2:
				mes "[Elly]";
				mes "Hé, mais vous ne faites pas partie de cette école, n'est-ce pas? Vous... Vous êtes "+ ( sex ? "un de ces aventuriers" : "une de ces aventurières" ) +", n'est-ce pas?";
				next;
				select("Oui, c'est exact.");
				mes "Écoutez, je sais que vous avez surement des choses à faire, mais vous ne pensez pas que vous pourriez m'aider avec mon problème?";
				next;
				switch(select("Accepter:Refuser")) {
				case 1:
					cutin "kh_elly02",2;
					mes "C'est fantastique! Merci! Merci beaucoup! Je dois terminer de préparer ces gâteaux, mais comme j'ai renversé les ingrédients...";
					next;
					break;
				case 2:
					mes "["+strcharinfo(0)+"]";
					mes "Je m'en vais.";
					next;
					cutin "kh_elly04",2;
					mes "[Elly]";
					mes "A-Attendez...! Revenez! Vous ne... J'ai vraiment besoin d'aide!";
					emotion e_sob;
					close2;
					cutin "",255;
					end;
				}
			}
			cutin "kh_elly01",2;
			mes "[Elly]";
			mes "Eh bien je pense que le seul moyen de terminer ces petits gâteaux est de trouver de nouveaux ingrédients. Je suis désolée de vous mettre tout ça sur les épaules, mais vous m'avez proposé de m'aider, et je n'ai personne d'autre à qui demander!";
			next;
			cutin "kh_elly04",2;
			mes "[Elly]";
			mes "Vous ne savez à quel point il est important que je fasse ces petits gâteaux... Si je ne le fais pas, Mlle Crank sera sur mon dos pendant tout le semestre!";
			next;
			mes "[Elly]";
			cutin "kh_elly01",2;
			mes "Bon, je dois me calmer. Je pense que je pourrai y arriver avec votre aide. Vous voulez bien me rendre ce service immense et m'apporter les ingrédients?";
			next;
			mes "[Elly]";
			mes "^3355FF1 bouteille de Vin^000000,"; // "de Cuisine" mais le script ne le précise pas [NdT]
			mes "^3355FF7 bouteilles de Lait^000000,";
			mes "^3355FF5 fèves de Cacao^000000,";
			mes "^3355FF2 morceaux de Fromage^000000,";
			mes "^3355FF1 Oeuf^000000, et";
			mes "^3355FF1 Sac de Farine^000000."; // L'objet n'existe pas mais c'est fait exprès [NdT]
			next;
			mes "[Elly]";
			mes "Voyons... Vous pourrez acheter le vin dans un bar de Yuno. Vous devriez trouver du Cacao en chassant des Yoyos. Et pour ce qui est de la Farine et des Oeufs, eh bien... Il y a une fille à Yuno qui vend des toasts – elle devrait en avoir.";
			next;
			mes "[Elly]";
			mes "Bonne chance pour trouver tout ça. J'espère que vous vous dépêcherez et que vous reviendrez aussi vite que possible, d'accord?";
			set KielHyreQuest,6;
			break;
		case 2:
			mes "[Elly]";
			mes "*Snif*...";
			break;
		}
	}
	else if (KielHyreQuest == 6) {
		if ((countitem(519) < 7) || (countitem(548) < 2) || (countitem(7182) < 5) || (countitem(7487) < 1) || (countitem(7488) < 1)) {
			mes "[Elly]";
			mes "Vous avez besoin que je vous redonne la liste des ingrédients? Ramenez-moi tout ça aussi vite que possible, que je puisse faire ces satanés petits gâteaux~";
			next;
			mes "[Elly]";
			mes "^3355FF1 bouteille de Vin^000000,";
			mes "^3355FF7 bouteilles de Lait^000000,";
			mes "^3355FF5 fèves de Cacao^000000,";
			mes "^3355FF2 morceaux de Fromage^000000,";
			mes "^3355FF1 Oeuf^000000, et";
			mes "^3355FF1 Sac de Farine^000000.";
			next;
			cutin "kh_elly01",2;
			mes "[Elly]";
			mes "Voyons... Vous pourrez acheter le vin dans un bar de Yuno. Vous devriez trouver du Cacao en chassent des Yoyos. Et pour ce qui est de la Farine et des Oeufs, eh bien... Il y a une fille à Yuno qui vend des toasts – elle devrait en avoir.";
		}
		else {
			cutin "kh_elly02",2;
			mes "[Elly]";
			mes "Hourra! J'ai enfin tout ce qu'il me faut! C'est fantastique! Est-ce que vous pouvez me laisser un moment pendant que je prépare ces petits gâteaux? Ça ne devrait pas me prendre trop longtemps, alors vous pouvez attendre là.";
			delitem 519,7; //Milk
			delitem 548,2; //Cheese
			delitem 7182,5; //Cacao
			delitem 7487,1; //Tavern_Wine
			delitem 7488,1; //Delivery_Package
			set KielHyreQuest,8;
			//KHPubMasterEnd no longer needed-- deleting.
			set KHPubMasterEnd,0;
			set KHToastGirlEnd,0; // Bugfix: faut pas oublier d'effacer ça aussi – c'est fait plus haut dans un autre NPC mais rien ne dit que le perso lui reparlera... [NoH]
			emotion e_ho;
		}
	}
	else if (KielHyreQuest == 8) {
		mes "[Elly]";
		mes "Tada! J'ai terminé! C'est enfin prêt! Les fameux cookies d'Elly! Je vous en donnerai quelques-uns pour vous remercier de m'avoir aidée.";
		next;
		mes "[Elly]";
		mes "Je suis désolée, mais j'étais si occupée à préparer ces cookies que je ne vous ai même pas demandé votre nom. Moi, je m'appelle ^FF0000Ellyja^000000, mais tout le monde m'appelle ''Elly''. Et vous, quel est votre nom?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "^3355FF"+strcharinfo(0)+"^000000.";
		mes "C'est ^3355FF"+strcharinfo(0)+"^000000.";
		next;
		cutin "kh_elly02",2;
		mes "[Elly]";
		mes strcharinfo(0)+"?";
		mes "...J'adore ce nom!";
		getitem 538,5; //Well_Baked_Cookie
		set KielHyreQuest,10;
	}
	else if (KielHyreQuest == 10) {
		mes "[Elly]";
		mes "Je suis désolée, mais je dois vous demander de me rendre un autre service, si ça ne vous gêne pas. Ne vous inquiétez pas, ce n'est pas aussi urgent que la dernière fois.";
		next;
		switch(select("De quoi tu as besoin?:Désolé"+ ( sex ? "" : "e" ) +", mais j'ai d'autres choses à faire...")) {
		case 1:
			mes "[Elly]";
			mes "Vous vous souvenez quand vous m'avez aidée à faire ces cookies? C'est la première fois que je ne les faisais pas brûler! Je sais que mon grand-père serait très fier de moi!";
			next;
			mes "[Elly]";
			mes "Je voudrais vraiment que mon grand-père goûte aux cookies que j'ai faits, mais d'ici à ce que je puisse aller le voir, ils seront tout rassis. Vous voudriez bien lui apporter pour moi?";
			next;
			switch(select("Bien sûr.:Je suis occupé"+ ( sex ? "" : "e" ) +".")) {
			case 1:
				cutin "kh_elly02",2;
				mes "[Elly]";
				mes "Oh, "+strcharinfo(0)+"! Merci beaucoup, je savais que vous comprendriez! Vous pourriez lui apporter au ^FF0000Manoir de Kiel Hyre^000000?";
				next;
				mes "[Elly]";
				mes "Oh, j'allais oublier! Ils surveillent les visiteurs et font très attention à ce que les étrangers ne pénètrent pas dans leur propriété, mais si vous leur donnez mon nom, ils vous laisseront entrer. Bien, alors à bientôt~";
				set KielHyreQuest,12;
				break;
			case 2:
				cutin "kh_elly03",2;
				mes "[Elly]";
				mes "Oh, hum... D'accord. Je suis désolée de vous avoir embêté"+ ( sex ? "" : "e" ) +"...";
				break;
			}
			break;
		case 2:
			cutin "kh_elly03",2;
			mes "[Elly]";
			mes "Oh, hum... D'accord. Je suis désolée de vous avoir embêté"+ ( sex ? "" : "e" ) +"...";
			break;
		}
	}
	else if (KielHyreQuest == 12) {
		mes "[Elly]";
		mes "Oh, vous ne savez pas où se trouve le manoir de Kiel Hyre? Il se trouve juste au nord de l'Académie. Apportez ces cookies à mon grand-père s'il vous plaît, et dites-lui qu'il me manque vraiment.";
	}
	else if (KielHyreQuest == 14) {
		cutin "kh_elly03",2;
		mes "[Elly]";
		mes "Hein? Mon grand-père n'est pas à la maison? C'est étrange... Il ne m'a pas parlé d'un voyage d'affaire. Je pensais qu'il passerait la journée à la maison...";
		next;
		cutin "kh_elly04",2;
		mes "[Elly]";
		mes "Vous pourriez retourner au Manoir une fois de plus? Prenez cette clé, comme ça vous pourrez entrer tout simplement et aller lui parler~";
		getitem 7489,1; //Villa_Spare_Key
		set KielHyreQuest,16;
	}
	else if ((KielHyreQuest >= 14) && (KielHyreQuest < 20)) {
		mes "[Elly]";
		mes "C'est bizarre... Je croyais que mon grand-père m'avait dit qu'il passerait toute la journée à la maison...";
	}
	else if (KielHyreQuest == 20) {
		mes "[Elly]";
		mes "Hum...? Qu'est-ce que c'est? Une lettre pour moi?";
		next;
		mes "^3355FFElly ouvre l'enveloppe et commence à lire la lettre.^000000";
		next;
		mes "^333333Très chère Elly,";
		mes " ";
		mes "Je dois parler de quelque chose de très important à mon fils Kiehl. Je pars donc le retrouver. Si tu n'as pas de nouvelles de moi d'ici sept jours, tu devras t'enfuir aussi vite que possible de l'académie, et aller chercher ce qui se trouve dans le bureau de notre manoir.";
		mes " ";
		mes "Si tu connais quelqu'un en qui tu as vraiment confiance, demande-lui de suivre mes traces dans le manoir. Je pourrais être en danger et avoir grand besoin d'aide.";
		mes " ";
		mes "Elly, il y a certaines personnes en qui tu ne peux pas avoir confiance à l'Académie, y compris parmi tes camarades de classe. Ils ont pu avoir été influencés par Kiehl.";
		mes " ";
		mes "Sois prudente.";
		mes " ";
		mes "- Ton Grand-Père qui t'aime.^000000";
		next;
		cutin "kh_elly03",2;
		mes "[Elly]";
		mes "Quoi? Oh non, voilà 10 jours qu'il a écrit cette lettre! Est-ce que cela veut dire qu'il est en danger?! Qu'est-ce que je vais faire maintenant?";
		delitem 7490,1; //Letter_to_Elly
		set KielHyreQuest,22;
	}
	else if (KielHyreQuest == 22) {
		cutin "kh_elly03",2;
		mes "[Elly]";
		mes "......";
		mes ".........";
		mes "............";
		next;
		switch(select("À propos de ton grand-père.:Parle-moi de Kiehl.:Qu'est-ce qui se passe à l'Académie?")) {
		case 1:
			mes "[Elly]";
			mes "Oh! Mon grand-père est Kiel Hyre, fondateur et directeur de la fondation Kiel Hyre. Il a l'air froid et très strict, mais c'est quelqu'un de très bon!";
			next;
			mes "[Elly]";
			mes "Nous ne sommes pas liés par le sang, mais il s'est occupé de moi depuis que j'ai perdu mes parents. Oh, où peux-tu bien être, Grand-Père? Je commence à vraiment m'inquiéter!";
			break;
		case 2:
			mes "[Elly]";
			mes "Kiehl? C'est le fils de mon grand-père... Je ne le connais pas très bien. En fait, je ne l'ai vu qu'une seule fois, lors d'une fête à l'Académie.";
			next;
			mes "[Elly]";
			mes "Hum, c'est quelqu'un d'assez beau, avec une peau très blanche, des cheveux argentés et un regard froid et perçant. Beaucoup de mes camarades de classe le prennent comme modèle parce que c'est aussi un génie des affaires~";
			next;
			mes "[Elly]";
			mes "Mais c'est étrange... Je ne comprends pas pourquoi mon grand-père et Kiehl ne s'entendent pas... Ils sont tous les deux très doués dans leur domaine..."; // Kiel c'est le grand-père, et Kiehl son fils [Etna]
			break;
		case 3:
			mes "[Elly]";
			mes "Je sais que les mesures de sécurité sont très strictes, mais c'est parce que tout ici est si luxueux...~";
			next;
			mes "[Elly]";
			mes "Nous avons aussi un cursus spécial qui permet de se spécialiser dans le domaine que l'on veut. Personnellement, je voudrais devenir une femme qui réussit tout, comme ^0000FFMme Allysia^000000. Hé hé~";
			next;
			mes "[Elly]";
			mes "Cet endroit n'est pas comme les autres écoles. Tout est fait pour répondre aux besoins de chaque étudiant.";
			next;
			mes "[Elly]";
			mes "Mais depuis quelque temps, des choses étranges se produisent. Mes camarades de classe ont tous peur de sortir sans être accompagné.";
			next;
			cutin "kh_elly02",2;
			mes "[Elly]";
			mes "Mais pour moi, ça va aller. Vous êtes là pour me sauver du danger, pas vrai, "+strcharinfo(0)+"?";
			next;
			switch(select("Qui est Mme Allysia?:Des choses étranges?")) {
			case 1:
				mes "[Elly]";
				mes "Oh, Mme Allysia est la secrétaire de mon grand-père~";
				mes "Elle est très belle et mon grand-père lui fait entièrement confiance.";
				break;
			case 2:
				cutin "kh_elly04",2;
				mes "[Elly]";
				mes "Eh bien, "+strcharinfo(0)+"...";
				mes "Je ne suis pas sensée en parler à ceux qui ne sont pas de l'Académie, mais je sais que je peux vous faire confiance. Comment dire... Il y a un fantôme qui hante l'académie.";
				next;
				mes "[Elly]";
				mes "Je sais que ça semble dingue, mais ce fantôme se promène sur le campus et maudit ses victimes, les laissant froides et sans vie. C'est arrivé à l'une de mes camarades de classe, Mayo. Maintenant c'est comme si c'était une statue...";
				next;
				mes "[Elly]";
				mes "Je me suis faufilée à l'infirmerie pour la voir, et elle... Elle ne pouvait plus bouger! Qu'est-ce qui se serait passé si le fantôme m'avait maudite?!";
				next;
				cutin "kh_elly02",2;
				mes "[Elly]";
				mes "Vous savez quoi? Si jamais il me lance sa malédiction, criez simplement ''^FF0000Réveille-toi, Elly!^000000'' et je me réveillerai, c'est sûr!";
				set KielHyreQuest,24;
				break;
			}
		}
	}
	else if (KielHyreQuest >= 24 && KielHyreQuest <= 26) {
		cutin "kh_elly04",2;
		mes "[Elly]";
		mes "Argh, je suis coincée! Je dois finir mes devoir et en plus, je dois aller voir Mlle Lecollane.";
		next;		
		mes "[Elly]";
		mes "Dites, "+strcharinfo(0)+", si ça ne vous dérange pas, vous pourriez aller chercher ce que mon grand-père a laissé pour moi dans le ^FF0000bureau du Manoir^000000? Il n'y a qu'à vous que je fais confiance!";
		next;
		mes "[Elly]";
		mes "Je vous verrai plus tard dans la soirée! Venez me retrouver dans mon dortoir. Hum... Les dortoirs se trouvent dans l'église juste derrière l'académie, d'accord?";
		next;
		mes "[Elly]";
		mes "Ma chambre se trouve au bout du couloir, au ^FF0000second étage sur la gauche^000000. Utilisez une échelle pour monter, d'accord? Je laisserai ma fenêtre ouverte pour que vous puissiez entrer, d'accord?";
		next;
		mes "[Elly]";
		mes "Dépêchez-vous, le professeur arrive!";
		set KielHyreQuest,26;
	}
	else {
		mes "[Elly]";
		mes "Dépêchez-vous, le professeur arrive!";
	}
	close2;
	cutin "",255;
	end;
}

//----------------------------------------------------------------------------
// Elly's Window 
//----------------------------------------------------------------------------
yuno_fild08,69,185,0	script	Fenêtre::Window#kh	111,{
	if (KielHyreQuest < 29) {
		mes "^3355FFVous voyez un rideau avec des motifs fantaisistes derrière cette fenêtre. Il s'agit probablement de la chambre d'une jeune fille.^000000";
		close;
	}
	else if (KielHyreQuest== 29) {
		mes "^3355FFIl doit s'agir de la chambre d'Elly. On dirait qu'il n'y a personne à l'intérieur. Elle n'est pas encore rentrée, probablement. Vous devriez aller chercher ce que son grand-père a laissé pour elle dans le bureau du Manoir en attendant.^000000";
		close;
	}
	else if ((KielHyreQuest >= 30) && (KielHyreQuest <= 45)) {
		mes "^3355FFCette fenêtre entre-ouverte doit être celle de la chambre d'Elly. Elle vous a dit d'utiliser une échelle pour monter mais vous n'en trouvez pas. Vous devriez pouvoir grimper le long de cette canalisation...^000000";
		next;
		switch(select("Grimper le long de la canalisation.:Chercher un autre moyen de monter.")) {
		case 1:
			if (rand(1,3) == 2) {
				mes "^3355FFVous grimpez le long de cette canalisation et réussissez à vous faufiler dans la chambre d'Elly.^000000";
				close2;
				warp "kh_school",185,185;
				end;
			}
			else {
				mes "^3355FFVous essayez de grimper le long de cette canalisation mais vous glissez et vous cognez la tête.^000000";
				percentheal -10,0;
				close;
			}
		case 2:
			mes "^3355FFIl doit y avoir un autre moyen d'entrer dans sa chambre que d'escalader cette canalisation...^000000";
			close;

		}
	}
	else {
		mes "^3355FFVous n'arrivez pas à atteindre cette fenêtre.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Elly (In Dormitory)
//----------------------------------------------------------------------------
kh_school,178,180,4	script	Elly	895,{
	if (checkweight(908,200) == 0) {
		mes "^3355FFUne petite minute! Vous portez trop d'objets sur vous! Vous feriez mieux de placer quelques objets dans votre Stock Kafra ou vous ne pourriez en obtenir de nouveaux...^000000";
		close;
	}
	if (KielHyreQuest < 30) {
		mes "[Elly]";
		mes "Mais qui vous êtes, vous?! Vous n'avez pas le droit d'entrer là! Sortez d'ici tout de suite!";
		close2;
		percentheal -99,0;
		warp "yuno_fild08",100,100;
		end;
	}
	else if (KielHyreQuest < 32) {
		mes "[Elly]";
		mes "......";
		mes ".........";
		mes "............";
		close;
	}
	else if (KielHyreQuest == 32) {
		mes "[Elly]";
		mes "......";
		mes ".........";
		mes "............";
		next;
		mes "^3355FFElly semble avoir été maudite – elle est complètement immobile et semble sans vie. Vous devez faire quelque chose, mais quoi?^000000";
		next;
		switch(select("La réveiller en la secouant.:La réveiller en criant.:Passer son chemin.")) {
		case 1:
			mes "^3355FFVous saisissez Elly par les épaules et essayez de la faire réagir en la secouant violemment.^000000";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Elly? Elly! Non! Reviens parmi nous!";
			next;
			mes "^3355FFElly ne réagit pas...^000000";
			close;
		case 2:
			input .@KHInput$;
			if (.@KHInput$ == "Réveille-toi, Elly!") {
				mes "[Elly]";
				mes ".............";
				next;
				mes "^3355FFUne ^000000Petite Clé d'Or^3355FF et un ^000000Bouton^3355FF tombent des mains d'Elly dès que vous prononcez ces mots. On dirait qu'elle s'est réveillée, mais juste un court instant.^000000";
				getitem 7493,1; //Golden_Key
				getitem 7494,1; //Kiel_Button
				set KielHyreQuest,34;
				close;
			}
			else {
				mes "[Elly]";
				mes "......";
				mes ".........";
				mes "............";
				next;
				mes "^3355FFElly ne réagit pas...^000000";
				close;
			}
		case 3:
			mes "^3355FFBien... Mais tôt ou tard, il vous faudra essayer de briser la malédiction qui s'est abattue sur Elly.^000000";
			close;
		}
	}
	else if (KielHyreQuest == 34) {
		if (countitem(7491) < 1) {
			//Missing text, needs another run through.
			mes "^3355FFPeut-être que la clé dorée ouvre quelque chose dans le Manoir. Vous feriez mieux d'aller vérifier.^000000";
			close;
		}
		else {
			mes "^3355FFOn dirait que la clé d'Elly pourrait entrer dans la serrure de la boîte que vous avez trouvée au Manoir Kiel Hyre.^000000";
			next;
			next;
			mes "^3355FFVous réussissez à l'ouvrir grâce à la clé, et vous y trouvez une Clé Magnétique Bleue, ainsi qu'une note pliée en deux. Vous la lisez rapidement...^000000";
			next;
			mes "Ma très chère Elly,";
			mes "Kiehl a finalement violé le tabou et a essayé de te transformer en quelque chose d'horrible.";
			mes "^FFFFFF_^000000";
			mes "Je serai probablement gravement en danger quand tu liras ces lignes. Que je survive ou non dépendra de toi.";
			mes "^FFFFFF_^000000";
			mes "Tu le sais déjà si tu as rencontré Puppet, mais je tiens à te le dire moi-même: tu n'es pas humaine. Tu connaîtras tous les détails en te rendant à l'usine, en empruntant l'entrée dissimulée sous une tombe près de l'église. Je veux que tu retrouves Allysia à l'intérieur de la chambre secrète de l'usine. J'ai enregistrer ton nom dans le système de sécurité, alors tu n'as pas de soucis à te faire. Avec de la chance, Allysia viendra alors me sauver...";
			mes "^FFFFFF_^000000";
			mes "Je suis désolé,";
			mes "- Ton Grand-père.";
			getitem 7495,1; //Blue_Key_Card
			delitem 7491,1; //Iron_Box
			set KielHyreQuest,36;
			close;
		}
	}
	else if (KielHyreQuest >= 36) {
		mes "^3355FFC'est ici que vous avez ouvert la boîte métallique et que vous avez trouvé la note à l'intérieur. Il s'agissait d'un message de Kiel Hyre à sa petite-fille, Elly.^000000";
		next;
		mes "Ma très chère Elly,";
		mes "Kiehl a finalement violé le tabou et a essayé de te transformer en quelque chose d'horrible.";
		mes "^FFFFFF_^000000";
		mes "Je serai probablement gravement en danger quand tu liras ces lignes. Que je survive ou non dépendra de toi.";
		mes "^FFFFFF_^000000";
		mes "Tu le sais déjà si tu as rencontré Puppet, mais je tiens à te le dire moi-même: tu n'es pas humaine. Tu connaîtras tous les détails en te rendant à l'usine, en empruntant l'entrée dissimulée sous une tombe près de l'église. Je veux que tu retrouves Allysia à l'intérieur de la chambre secrète de l'usine. J'ai enregistrer ton nom dans le système de sécurité, alors tu n'as pas de soucis à te faire. Avec de la chance, Allysia viendra alors me sauver...";
		mes "^FFFFFF_^000000";
		mes "Je suis désolé,";
		mes "- Ton Grand-père.";
		close;
	}
}

//----------------------------------------------------------------------------
// Cookie Basket
//----------------------------------------------------------------------------
kh_school,175,176,0	script	Panier de Biscuits::Cookie Basket#kh	111,{
	if (KielHyreQuest < 30) {
		mes "^3355FFIl s'agit d'un panier rempli de délicieux cookies.^000000";
		close;
	}
	else if (KielHyreQuest == 30) {
		mes "^3355FFIl doit s'agir du panier de cookies d'Elly. Vous trouvez entre deux cookies une note pliée en deux.^000000";
		next;
		switch(select("Lire la note.:Passer son chemin.")) {
		case 1:
			mes ""+strcharinfo(0)+", à l'aide! Il y a ce type habillé tout en noir qui rôde dans les parages et lance ce sort étrange! C'est lui qui a rendu les gens froids et sans vie, comme des poupée! J'ai vraiment peur!";
			mes "^FFFFFF_^000000";
			mes "J'espère que vous lirez ce mot... Il m'a poursuivie, mais je me suis enfermée à clé. Je vais laisser ma fenêtre ouverte pour que vous puissiez me trouver tout de même. J'espère qu'il n'abattra pas sa malédiction sur moi!";
			set KielHyreQuest,32;
			close;
		case 2:
			mes "^3355FFCette note ne vous était probablement pas adressée de toutes façons.^000000";
			close;
		}
	}
	else if (KielHyreQuest < 108) {
		mes "^3355FFCes cookies ne sont pas déjà rassis, mais ils ne sont plus chaud. Ils sont même un peu froids comparés à la température ambiante.^000000";
		close;
	}
	else if (KielHyreQuest == 108) {
		mes "^3355FFVous regardez le panier de cookies quand soudain un courant d'air s'engouffre par la fenêtre entre-ouverte et fait vaciller le panier, révélant une lettre dissimulée en-dessous. C'est probablement Elly qui l'avait écrite pour vous.^000000";
		next;
		mes "^333333Très "+ ( sex ? "cher" : "chère" ) +" "+strcharinfo(0)+",";
		mes " ";
		mes "Mes professeurs me crient toujours dessus à cause de toutes mes bêtises, mais aujourd'hui Mlle Lecollane m'a félicitée pour mes délicieux cookies. Tout ça, c'est grâce à vous, mon ami"+ ( sex ? "" : "e" ) +". Je suis vraiment contente de vous avoir rencontré"+ ( sex ? "" : "e" ) +": vous m'avez montrée qu'il y a des gens bien dans ce monde. Et je sais que Grand-Père vous aimerait aussi, mais je ne sais toujours pas où il peut bien être...";
		mes " ";
		mes "J'espère devenir quelqu'un comme vous un jour. Restons en contact, et devenons bons amis, d'accord?";
		mes " ";
		mes "Amicalement, Elly^000000.";
		next;
		mes "^3355FFC'est comme si vous pouviez toujours sentir la douceur et la gentillesse d'Elly avec ce panier de cookies. Vous en prenez un et le grignotez. Il est délicieux.";
		set KielHyreQuest,109;
		next;
//		input .@input$;
		mes "["+strcharinfo(0)+"]";
//		mes ""+.@input$+"";
		mes "Il faut maintenant que je parte. Je ne peux pas rester ici plus longtemps.";
		close2;
		warp "yuno_fild08",69,183;
		end;
	}
	else {
		mes "^3355FFLes cookies dans ce panier sont tous rassis. Ce n'est pas surprenant: ils ont été cuits il y a bien longtemps.^000000";
		close;
	}
	end;
}

//----------------------------------------------------------------------------
// Elly's Window (In Dormitory)
//----------------------------------------------------------------------------
kh_school,188,185,0	script	Fenêtre::Window#kh2	111,{
	warp "yuno_fild08",69,183;
	end;
}

//----------------------------------------------------------------------------
// Grave/Kiel Hyre dungeon entry
//----------------------------------------------------------------------------
yuno_fild08,71,170,0	script	Tombe::Grave#kh	111,{
	if (KielHyreQuest < 36) {
		mes "^3355FFIl s'agit d'une simple tombe sans grand intérêt pour vous.^000000";
		close;
	}
	else if (KielHyreQuest == 36) {
		if (countitem(7492) < 1) {
			mes "^3355FFIl s'agit d'une simple tombe sans grand intérêt pour vous.^000000";
			close;
		}
		else {
			mes "^3355FFIl y a une entrée secrète près de cette tombe. Vous voyez une fente, comme un emplacement pour quelque chose, et un pavé numérique sur celle-ci.^000000";
			next;
			mes "^3355FFQu'allez-vous enfoncer dans cette fente?^000000";
			next;
			input .@KHInput$;
			if (.@KHInput$ == "Clé Magnétique Jaune") {
				mes "^3355FFVous entendez un bip de confirmation après avoir inséré la clé magnétique, suivie de l'enregistrement d'une voix qui demande:^000000";
				next;
				mes "[Système de Sécurité]";
				mes "Veuillez taper le mot de passe.";
				next;
				input .@KHInput$;
				next;
				if (.@KHInput$ == "4772961") {
					mes "[Système de Sécurité]";
					mes "Mot de passe correct. Bienvenue, M. Kiel Hyre.";
					next;
					mes "^3355FFVous entendez un nouveau bip et le passage secret s'ouvre.^000000";
					set KielHyreQuest,38;
					close;
				}
				else {
					mes "[Système de Sécurité]";
					mes "Mot de passe incorrect. Veuillez réessayer.";
					close;
				}
			}
			else {
				mes "^3355FFPeu importe ce que vous essayez d'insérer, ça ne marche pas du tout...^000000";
				close;
			}
		}
	}
	else if ((KielHyreQuest >= 38) && (KielHyreQuest < 106)) {
		mes "^3355FFLa porte de l'usine est grande ouverte.^000000";
		next;
		switch(select("Entrer:Annuler")) {
		case 1:
			warp "kh_dun01",3,230;
			end;
		case 2:
			mes "......";
			mes ".........";
			mes "............";
			close;
		}
	}

	else if (KielHyreQuest >= 106) {
		if (countitem(7509) < 1) {
			mes "^3355FFL'entrée secrète est désormais scellée.^000000";
			close;
		}
		else {
			mes "^3355FFLa tombe commence à émettre des flashs de lumière tandis que vous approchez.^000000";
			next;
			switch(select("Utiliser la ^0000FFClé Magnétique Luxueuse^000000.:Passer son chemin.")) {
			case 1:
				mes "^3355FFLe passage secret s'ouvre après que vous ayez introduit la Clé Magnétique Luxueuse.^000000";
				next;
				switch(select("Entrer:Annuler")) {
				case 1:
					warp "kh_dun01",3,230;
					end;
				case 2:
					mes "......";
					mes ".........";
					mes "............";
					close;
				}
			case 2:
				mes "......";
				mes ".........";
				mes "............";
				close;
			}
		}
	}
}

//============================================================================

//============================================================================
//Kiel Hyre Cottage NPCs
//============================================================================

//----------------------------------------------------------------------------
// Cottage Keeper
//----------------------------------------------------------------------------
yuno_fild02,93,210,4	script	Gardien de la Propriété::Cottage Keeper#kh	85,{
	if ((KielHyreQuest < 12) || (KielHyreQuest > 12)) {
		mes "[Gardien de la Propriété]";
		mes "Il s'agit d'une propriété privée. Vous ne pouvez pas y entrer sans y avoir été invité"+( sex ? "" : "e" )+".";
		close;
	}
	else if (KielHyreQuest == 12) {
		mes "[Gardien de la Propriété]";
		mes "Il s'agit d'une propriété privée. Vous ne pouvez pas y entrer sans y avoir été invité"+( sex ? "" : "e" )+".";
		next;
		switch(select("J'ai rendez-vous avec Kiel Hyre.:D'accord.")) {
		case 1:
			mes "[Gardien de la Propriété]";
			mes "Vous avez rendez-vous avec Kiel Hyre? Hum, vous êtes sûr"+( sex ? "" : "e" )+"?";
			next;
			switch(select("Oui:Non")) {
			case 1:
				mes "[Gardien de la Propriété]";
				mes "Vous devez faire erreur. Monsieur Hyre ne serait pas parti s'il attendait quelqu'un...";
				set KielHyreQuest,14;
				close;
			case 2:
				mes "[Gardien de la Propriété]";
				mes "Eh bien, à moins que vous ayez rendez-vous, je ne pense pas que Monsieur Hyre vous recevra.";
				close;
			}
		case 2:
			mes "[Gardien de la Propriété]";
			mes "Au revoir.";
			close;
		}
	}
}

//----------------------------------------------------------------------------
// Cottage Door (left)
//----------------------------------------------------------------------------
yuno_fild02,75,218,0	script	Porte::Door#kh2	111,{
	if (KielHyreQuest < 16) {
		mes "^3355FFCette porte est verrouillée. Il vous faudra trouver la bonne clé si personne ne vous ouvre de l'intérieur.";
		close;
	}
	if (KielHyreQuest == 16) {
		mes "^3355FFCette porte est verrouillée. Il vous faudra utiliser la bonne clé si personne ne vous ouvre de l'intérieur.";
		next;
		switch(select("Ouvrir la porte.:Annuler.")) {
		case 1:
			mes "^3355FFVous ouvrez la porte grâce à la clé qu'Elly vous a donnée, et tandis que vous la poussez, un morceau de papier tombe du haut de la porte. On peut y lire les quelques mots qui suivent.";
			next;
			mes "6 en avant,";
			mes "3 à gauche,";
			mes "3 en avant,";
			mes "4 à gauche.";
			close2;
			set KielHyreQuest,18;
			warp "kh_vila",188,18;
			end;
		case 2:
			mes "......";
			mes ".........";
			mes "............";
			close;
		}
	}
	else {
		mes "^3355FFLa porte est ouverte.^000000";
		next;
		switch(select("Enter:Annuler")) {
		case 1:
			warp "kh_vila",188,18;
			end;
		case 2:
			close;
		}
	}
}

//----------------------------------------------------------------------------
// Cottage Door (Right)
//----------------------------------------------------------------------------
yuno_fild02,109,218,0	script	Porte::Door#kh1	111,{
	if (KielHyreQuest != 16) {
		mes "^3355FFCette porte est verrouillée. Il vous faudra trouver la bonne clé si personne ne vous ouvre de l'intérieur.";
		close;
	}
	if (KielHyreQuest == 16) {
		mes "^3355FFCette porte est verrouillée. Il vous faudra trouver la bonne clé si personne ne vous ouvre de l'intérieur.";
		next;
		switch(select("Ouvrir la porte.:Annuler.")) {
		case 1:
			mes "^3355FFVous n'avez pas la bonne clé pour ouvrir cette porte.^000000";
			close;
		case 2:
			mes "......";
			mes ".........";
			mes "............";
			close;
		}
	}
}


//----------------------------------------------------------------------------
// Store room interior; Wall
//----------------------------------------------------------------------------
kh_vila,191,14,0	script	Mur::Wall#kh	139,1,1,{
	if (KielHyreQuest < 28) {
		mes "^3355FFIl s'agit d'un simple mur sans rien de spécial.^000000";
		close;
	}
	if (KielHyreQuest >= 28) {
		mes "^3355FFLe mur est maintenant ouvert et révèle un passage secret.^000000";
		next;
		switch(select("L'emprunter.:Annuler")) {
		case 1:
			warp "kh_vila",17,177;
			end;
		case 2:
			close;
		}
	}

OnOpenDoor:
	misceffect 316;
	end;
}

//----------------------------------------------------------------------------
// Store room interior; books
//----------------------------------------------------------------------------
kh_vila,184,20,0	script	Livres::BookKHQ1	111,{
	if (KielHyreQuest != 26) {
		mes "^3355FFCette étagère est remplie de gros livres reliés.^000000";
		close;
	}
	else if (KielHyreQuest == 26) {
		if (rand(1,10) == 8) {
			mes "^3355FFSans même réfléchir, vous prenez un livre sur un rayonnage. Tandis que vous tirez le livre, le mur coulisse vers vous et s'ouvre, révélant un passage secret.^000000";
			donpcevent "Wall#kh::OnOpenDoor";
			set KielHyreQuest,28;
			close;
		}
		else {
			mes "^3355FFCette étagère est remplie de gros livres reliés.^000000";
			close;
		}

	}

}

kh_vila,181,20,0	duplicate(BookKHQ1)	Livres::Book#kh2	111

kh_vila,178,20,0	duplicate(BookKHQ1)	Livres::Book#kh3	111

kh_vila,175,19,0	duplicate(BookKHQ1)	Livres::Book#kh4	111

//----------------------------------------------------------------------------
// Store room interior; Letter to Elly on the floor.
//----------------------------------------------------------------------------
kh_vila,179,11,0	script	Lettre::Letter#kh	-1,1,1,{
//OnTouch2:
OnTouch:
	if (KielHyreQuest == 18) {
		if (checkweight(7490,1) == 0) {
			mes "^3355FFUne petite minute... Il y a quelque chose sur le sol, mais vous ne pouvez le saisir car vous portez trop d'objets sur vous.^000000";
			close;
		}
		mes "^3355FFVous trouvez une lettre par terre. Vous la ramassez, même si elle ne vous est pas adressée.^000000";
		getitem 7490,1; //Kyll_Hire_Letter
		set KielHyreQuest,20;
		close;
	} 
	else{
		mes "^3355FFVous ne trouverez rien de spécial. Il ne s'agit que d'une lettre sans importance.^000000"; // Dialogue modifié un peu [NdT]
		close;
	}
	end; 
}

//----------------------------------------------------------------------------
// Cottage interior, kitchen; Box (Pet Food vending machine)
//----------------------------------------------------------------------------
kh_vila,16,175,0	script	Caisse::Box#khp1	111,{
	mes "*Goûtue-Nourrissante-Délicieuse*";
	mes "*Simple distributeur pour animaux domestiques*";
	mes "La meilleure nourriture pour animaux domestiques – bien supérieure à celle sur le marché!";
	mes "^FFFFFF_^000000";
	mes "Prix: 1,100 zénys (une occasion!)";
	next;
	switch(select("En acheter!:Annuler")) {
	case 1:
		if (zeny < 1100) {
			mes "^3355FFVous n'avez malheureusement pas autant de zénys à mettre dans la machine.^000000";
			close;
		}
		else {
			mes "^3355FF1,100 zénys, c'est vraiment cher pour de la nourriture pour animaux de compagnie, mais si la qualité est au rendez-vous...^000000";
			next;
			mes "^3355FFOh non... Il s'agit juste de nourriture pour animaux domestiques classique! Elle n'a rien de particulier!";
			set zeny,zeny-1100;
			getitem 537,1;	//Pet_Food
			close;
		}
	case 2:
		mes "^3355FFVous vous méfier de cette machine pour une raison qui vous échappe.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Cottage interior, kitchen; Apple Box (Useless?)
//----------------------------------------------------------------------------
kh_vila,30,184,0	script	Caisse de Pommes::Apple Box#khp1	111,{
	mes "^3355FFVous trouvez une caisse remplie de pommes bien mûres et délicieuses.^000000";
	next;
	switch(select("Prendre la caisse.:La laisser.")) {
	case 1:
		mes "^3355FFNon, attendez! Quelque chose cloche! Quoi qu'il arrive, ne prenez pas cette caisse!^000000";
		next;
		mes "^3355FFC'est ce que vous hurle votre voix intérieure. Vous laissez la caisse ici.^000000";
		close;
	case 2: 
		mes "^3355FFQuelque chose cloche avec cette caisse de pommes.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Cottage interior, dining room; Map
//----------------------------------------------------------------------------
kh_vila,44,126,0	script	Carte::Map#khp1	111,{
	if ((KHCottagePoem1 < 2) && (KielHyreQuest < 30)) {
		mes "^3355FFUne magnifique carte du monde détaillant le Royaume de Rune-Midgard, la République de Schwaltzvalt, ainsi qu'un autre pays à l'ouest au nom peu familier est accrochée au mur.^000000";
		next;
		mes "^3355FFEn l'examinant plus attentivement, vous vous rendez-compte que quelque chose est dissimulé derrière.^000000";
		switch(select("L'ignorer.:L'examiner.")) {
		case 1:
			mes "^3355FFPeu importe de quoi il s'agit. Ça n'est surement pas très important, et ça ne vaut pas la peine d'enquêter.^000000";
			close;
		case 2:
			mes "^3355FFVous ne pouvez malheureusement pas voir ce qui est sous la carte: celle-ci est collée au mur. Il va vous falloir trouver quelque chose pour la décoller sans abîmer la carte...^000000";
			set KHCottagePoem1,1;
			close;
		}
	}
	else  if ((KHCottagePoem1 == 2) && (KielHyreQuest < 30)) {
		mes "^3355FFVous apportez la bouilloire remplie de liquide chaud et l'approchez de la carte du monde. Elle commence à se détacher à mesure que la colle s'humidifie.^000000";
		next;
		mes "^3355FFUne fois décolée, vous trouvez cachée derrière, une feuille de papier pliée en deux sur laquelle est écrit un message.^000000";
		next;
		mes "^0000FFElle se trouvera dans un endroit aussi froid que les Pôles. Lorsque les océans se seront asséchés et que la terre sera éventrée, le chemin vers son coeur s'ouvrira – son coeur aussi transparent que le cristal.^000000";
		next;
		mes "^3355FFJe devrais livrer bataille à quatre serpents avec quatre épées pour la trouver. La première de ces épées se nomme 'amour'. La seconde de ces épées se nomme 'désespoir'. La troisième de ces épées se nomme 'rage'. La quatrième de ces épées se nomme 'espérance'. Trouvons-la, sauvons-la!^000000";
		next;
		mes "^3355FF...............................";
		mes "Le sens caché de ce poème, s'il y a un, vous échappe.^000000";
		set KHCottagePoem1,3;
		close;
	}
	else  if ((KHCottagePoem1 > 2) || (KielHyreQuest >= 30)) {
		mes "^3355FFC'est là que vous avez trouvé le poème. Peut-être devriez-vous le relire pour vous rafraîchir la mémoire.^000000";
		next;
		switch(select("Le lire.:Annuler.")) {
		case 1:
			mes "^0000FFElle se trouvera dans un endroit aussi froid que les Pôles. Lorsque les océans se seront asséchés et que la terre sera éventrée, le chemin vers son coeur s'ouvrira – son coeur aussi transparent que le cristal.^000000";
			next;
			mes "^3355FFJe devrais livrer bataille à quatre serpents avec quatre épées pour la trouver. La première de ces épées se nomme 'amour'. La seconde de ces épées se nomme 'désespoir'. La troisième de ces épées se nomme 'rage'. La quatrième de ces épées se nomme 'espérance'. Trouvons-la, sauvons-la!^000000";
			next;
			mes "^3355FF...............................";
			mes "Le sens caché de ce poème, s'il y a un, vous échappe.^000000";
			close;
		case 2:
			mes "^3355FFVous n'avez pas besoin de le relire. Vous avez une mémoire d'éléphant!^000000";
			close;
		}
	}
}

//----------------------------------------------------------------------------
// Cottage interior, kitchen; Pot
//----------------------------------------------------------------------------
kh_vila,33,184,0	script	Bouilloire::Pot#khp1	111,{ // nom d'après le model utilisé IG [NdT]
	if (KHCottagePoem1 < 1) {
		mes "^3355FFVous trouvez une bouilloire pleine d'un liquide en train de chauffer.^000000";
		close;
	}
	else if (KHCottagePoem1 == 1) {
		mes "^3355FFVous trouvez une bouilloire pleine d'un liquide en train de chauffer. Vous pourriez utiliser la vapeur pour humidifier la colle de la carte... Vous êtes un génie!^000000";
		next;
		switch(select("Essayer.:Quoi? Ça ne marchera jamais!")) {
		case 1:
			mes "^3355FFVous prenez la bouilloire mais vous vous brûlez légèrement la main par accident.";
			set KHCottagePoem1,2;
			close;

		case 2:
			mes "^3355FFNon, non... Je ferais mieux d'essayer autre chose.^000000";
			close;
		}
	}
	else if ((KHCottagePoem1 > 1) || (KielHyreQuest >= 30)){
		mes "^3355FFC'est ici que vous avez pris la bouilloire remplie d'eau en train de chauffer.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Cottage interior, fire place; Calabash
//----------------------------------------------------------------------------
kh_vila,14,55,0	script	Carafe::Calabash#khp1	111,{
	if ((countitem(7329) < 1) && (countitem(7516) < 1) && (countitem(7491) < 1) && (KielHyreQuest < 38)) {
		if (checkweight(7329,1) == 0) {
			mes "^3355FFIl s'agit d'une très belle carafe. Vous pourriez trouver quelque chose à l'intérieur, mais vous devez d'abord vous débarrasser de quelques objets et faire de la place dans votre inventaire.^000000";
			close;
		}
		mes "^3355FFIl s'agit d'une carafe de grande valeur. Que voulez-vous faire?^000000";
		next;
		switch(select("Briser la carafe.:Regarder à l'intérieur de la carafe.:Passer son chemin.")) {
		case 1:
			mes "^3355FFVous ne pouvez pas briser cette carafe. Vous êtes "+( sex ? "un héros" : "une héroïne" )+", pas "+( sex ? "un" : "une" )+"  vandale.^000000";
			close;
		case 2:
			mes "^3355FFVous glissez quelques doigts dans la carafe et tâtonnez jusqu'à ce que vous y trouviez une vieille clé de bronze.^000000";
			getitem 7329,1; //Old_Bronze_Key
			close;
		case 3:
			mes "^3355FFIl n'y a probablement rien à l'intérieur de toutes façons.^000000";
			close;
		}
	}
	else {
		mes "^3355FFIl s'agit de la carafe de grande valeur où vous avez trouvé la clé de bronze. Elle ne sert plus à rien maintenant.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Cottage interior, garden; Pool
//----------------------------------------------------------------------------
kh_vila,39,41,0	script	Bassin::Pool#khp1	111,{
	if ((KHCottagePoem1 < 3) && (KielHyreQuest < 30)) {
		mes "^3355FFVous trouvez un magnifique bassin rempli d'eau fraîche.^000000";
		close;
	}
	else if ((KHCottagePoem1 == 3) && (KielHyreQuest < 30)) {
		mes "^3355FFVous trouvez un magnifique bassin rempli d'eau fraîche, ainsi qu'une statue de lion avec deux poignées.^000000";
		next;
		switch(select("Tirer une poignée.:Annuler.")) {
		case 1:
			mes "^3355FFQuelle poignée voudriez-vous tirer en premier?^000000";
			next;
			switch(select("Celle de droite.:Celle de gauche.")) {
			case 1:
				mes "^3355FFVous tirez la poignée de droite, et de l'eau s'écoule de la gueule du lion. Elle a l'air très fraîche~^000000";
				close;
			case 2:
				mes "^3355FFLe bassin commence à se vider dès que vous tirez la poignée de gauche. Une couche de mousse recouvre le fond du bassin.^000000";
				next;
				switch(select("L'ignorer.:L'examiner.")) {
				case 1:
					mes "^3355FFAprès tout, un bassin sans eau n'est rien de plus qu'un trou sans intérêt...^000000";
					close;
				case 2:
					mes "^3355FFVous tombez sur un petit bouton tandis que vous examinez la mousse humide.^000000";
					next;
					switch(select("Appuyer sur le bouton.:Ne pas appuyer sur le bouton.")) {
					case 1:
						mes "^3355FFVous appuyez sur le bouton et entendez un bruit étrange venant des escaliers dans le couloir.^000000";
						set KHCottagePoem1,4;
						close;
					case 2:
						mes "^3355FFVous feriez mieux de ne pas appuyer sur ce bouton. Vos ennemis ont certainement pris la peine d'aussi bien le cacher uniquement pour que vous le trouviez et que vous tombiez dans leur vil piège.^000000";
						close;
					}
				}
			}
		case 2:
			mes "^3355FFVous décidez de ne pas toucher aux poignées.^000000";
			close;
		}
	}
	else if ((KHCottagePoem1 > 3) || (KielHyreQuest >= 30)) {
		mes "^3355FFC'est ici que se trouve le bouton sur lequel vous avez appuyé et qui a produit ce son étrange dans les escaliers du couloir.";
		close;
	}
}

//----------------------------------------------------------------------------
// Cottage interior, upstairs; Vicious Dog
//----------------------------------------------------------------------------
kh_vila,181,178,4	script	Chien Vicieux::Vicious Dog#khp1	81,7,3,{
//OnTouch2:
OnTouch:
	mes "*Grrr~*";
	next;
	mes "^3355FFUn chien à l'air extrêmement vicieux vous fixe. Est-ce que vous allez vraiment pouvoir passer sans qu'il vous morde?^000000";
	next;
	switch(select("Avancer.:S'enfuir.")) {
	case 1:
		if (countitem(537) >= 1) {
			mes "^3355FFOh, mais peut-être que vous pouvez utiliser de la nourriture pour calmer cette bête féroce. Pourquoi ne lui donneriez-vous pas un peu de Nourriture pour Animal de Compagnie pour voir comment il réagit?^000000";
			next;
			switch(select("Lui donner de la nourriture.:Ça ne peut pas marcher!")) {
			case 1:
				mes "^3355FFVous lancez la nourriture en direction du chien. Vous feriez mieux de passer pendant que vous en avez encore le temps!^000000";
				delitem 537,1; //Pet_Food
				close2;
				warp "kh_vila",173,182;
				end;
			case 2:
				break;
			}
		}
		mes "^3355FFVous vous approchez du chien mais il vous montre les crocs. Vous essayez de passer mais il vous bloque le passage. Qu'allez-vous bien pouvoir faire?^000000";
		next;
		mes "[Chien Vicieux]";
		mes "WOUF WOUF!";
		mes "WOUF WOUF!";
		mes "WOUF WOUF WOUF!";
		next;
		mes "^3355FFLe chien poursuit jusqu'en bas de l'escalier le blanc-bec que vous êtes.^000000";
		close2;
		warp "kh_vila",126,70;
		end;
	case 2:
		mes "^3355FFNe restons pas une minute de plus!^000000";
		close2;
		warp "kh_vila",126,70;
		end;
	}
}

//----------------------------------------------------------------------------
// Cottage interior, upstairs; Drawer, first room nightstand
//----------------------------------------------------------------------------
kh_vila,115,186,0	script	Tiroir::Drawer#khp1	111,{
	if (checkweight(7492,1) == 0) {
		mes "^3355FFIl y a quelque chose à l'intérieur de ce tiroir, mais vous ne pouvez le saisir: vous portez bien trop d'objets sur vous en ce moment pour cela. Vous feriez mieux d'en déposer d'abord dans votre Stock Kafra.^000000";
		close;
	}
	if ((countitem(7492) < 1) && (KielHyreQuest <= 36)) {
		mes "^3355FFVous voyez un tiroir fermé à clé. Vous y trouverez peut-être quelque chose d'intéressant en l'ouvrant, mais il pourrait tout aussi bien être vide. Qui sait?^000000";
		next;
		switch(select("Ouvrir le tiroir.:Passer son chemin.")) {
		case 1:
			mes "^3355FFVous y trouvez une Clé Magnétique Jaune? Finalement ouvrir ce tiroir ne vous aura servi à rien.^000000";
			getitem 7492,1; //Yellow_Key_Card
			close;
		case 2:
			mes "^3355FFPeu importe ce tiroir. Franchement, à quoi pourrait-il bien vous être utile?^000000";
			close;
		}
	}
	else {
		mes "^3355FFIl s'agit du tiroir où vous avez trouvé la Clé Magnétique Jaune. Il est maintenant vide.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Cottage interior, upstairs; Box
//----------------------------------------------------------------------------
kh_vila,107,126,0	script	Coffre::Box#khp2	111,{
	if ((countitem(7329) < 1) && (countitem(7516) < 1) && (countitem(7491) < 1) && (KielHyreQuest < 38)) {
		mes "^3355FFUn coffre solide est posé par terre. Une boîte plus petite et fermée à clé se trouve à l'intérieur.^000000";
		close;
	}
	else if ((countitem(7329) >= 1) && (countitem(7516) < 1) && (countitem(7491) < 1) && (KielHyreQuest < 38)) {
		mes "^3355FFUn coffre solide est posé par terre. Une boîte plus petite et fermée à clé se trouve à l'intérieur. Peut-être pouvez-vous utiliser votre clé en bronze pour l'ouvrir.^000000";
		next;
		switch(select("Utiliser la clé de bronze.:Non, ça ne marchera jamais.")) {
		case 1:
			mes "^3355FFVotre intuition a payé! Vous ouvrez la boîte avec la clé de bronze. Vous y trouvez une Clé Magnétique Verte.";
			getitem 7516,1; //Green_Key_Card
			delitem 7329,1; //Old_Bronze_Key
			close;
		case 2:
			mes "^3355FFQuoi...? Hein? Utiliser une clé pour ouvrir une serrure? Quelle idée stupide!^000000";
			close;
		}
	}
	else {
		mes "^3355FFIl s'agit de la boîte où vous avez trouvé la Clé Magnétique Verte.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Cottage interior, upstairs; Bookshelf.
//----------------------------------------------------------------------------
kh_vila,181,138,0	script	Rayonnage::Bookshelf#khp1	111,{
	if ((countitem(7491) < 1) && (countitem(7329) >= 1) || (countitem(7516) >= 1)) {
		mes "^3355FFL'un des livres sur cette étagère est intitulé ''À Elly.'' Il doit avoir été laissé là par son grand-père.^000000";
		next;
		switch(select("Examiner ce livre.:Passer son chemin.")) {
		case 1:
			mes "^3355FFVous feuilletez les pages mais vous ne trouvez rien d'intéressant. Cependant, vous remarquez une plaque de fer derrière l'étagère alors que vous alliez remettre le livre en place.^000000";
			next;
			switch(select("Examiner la plaque de fer.:L'ignorer.")) {
			case 1:
				mes "^3355FFVous retirez plusieurs ouvrages de l'étagère, et vous découvrez qu'il s'agit d'un coffre-fort caché derrière la bibliothèque. Vous comptez deux serrures.^000000";
				next;
				switch(select("Essayer toutes vos clés:Annuler.")) {
				case 1:
					mes "^3355FFQuelle clé allez-vous mettre dans la première serrure?^000000";
					next;
					if (countitem(7329) >= 1) {
						select("Vieille Clé de Bronze:Clé du Manoir");
						mes "^3355FFQuelle clé allez-vous mettre dans la seconde serrure?^000000";
						next;
						select("Vieille Clé de Bronze:Clé du Manoir");
					}
					else if (countitem(7516) >= 1) {
						switch(select("Clé Magnétique Verte:Clé du Manoir")) {
						case 1:
							set .@KHFirstKeyhole,1;
							break;
						case 2:
							set .@KHFirstKeyhole,2;
							break;
						}
						mes "^3355FFQuelle clé allez-vous mettre dans la seconde serrure?^000000";
						next;
						switch(select("Clé Magnétique Verte:Clé du Manoir")) {
						case 1:
							set .@KHSecondKeyhole,1;
							break;

						case 2:
							set .@KHSecondKeyhole,2;
							break;

						}
						if (.@KHFirstKeyhole == 2) && (.@KHSecondKeyhole == 1) {
							mes "^3355FFLe coffre émet un petit cliquetis et s'ouvre. Vous y trouvez une boîte métallique. Vous la prenez en espérant qu'elle vous servira plus tard.^000000";
							delitem 7489,1; //Villa_Spare_Key
							delitem 7516,1; //Green_Key_Card
							getitem 7491,1; //Iron_Box
							close;
						}

					}
					mes "^3355FFLe coffre ne veut pas s'ouvrir.^000000";
					close;
				case 2:
					mes "^3355FFIl vaudrait peut-être mieux inspecter le manoir et chercher les clés qui permettront de l'ouvrir...^000000";
					close;
				}
			case 2:
				mes "^3355FFVous remettez les livres sur l'étagère.^000000";
				close;
			}
		case 2:
			mes "^3355FFVous trouverez certainement de meilleurs indices ailleurs.^000000";
			close;
		}
	}
	else {
		mes "^3355FFVous voyez une étagère croulant sous le poids des livres.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Cottage interior, upstairs; Desk
//----------------------------------------------------------------------------
kh_vila,118,144,0	script	Bureau::Desk#khp1	111,{
	if (KHCottagePoem2 < 1) {
		mes "^3355FFIl y a des tas de livres et de papiers entassés sur ce bureau. Ils traitent de sujet comme la vie artificielle, les recherches du Sage Varmundt, la robotisation, ou encore les parchemins magiques...^000000";
		next;
		mes "^3355FFEn fouillant ces documents, vous tombez sur une feuille portant le sceau officiel de la Fondation Kiel Hyre. Vous pensez que ça pourra peut-être vous être utile un jour ou l'autre.^000000";
		next;
		mes "^3355FFVous ramassez cette feuille vierge portant le sceau de la Fondation.^000000";
		set KHCottagePoem2,1;
		close;
	}
	else if (KHCottagePoem2 >= 1) {
		mes "^3355FFIl y a des tas de livres et de papiers empilés sur ce bureau, traitant de sujets scientifiques et magiques.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Cottage interior, upstairs; Medicine Chest
//----------------------------------------------------------------------------
kh_vila,123,170,0	script	Armoire à Pharmacie::Medicine Chest#khp1	111,{
	if ((KHCottagePoem2 < 1) && (KielHyreQuest < 30)) {
		mes "^3355FFCette armoire à pharmacie est remplie de bouteilles de diverses couleurs. Quel genre de médicament peuvent-elles bien contenir?^000000";
		close;
	}
	else if ((KHCottagePoem2 == 1) && (KielHyreQuest < 30)) {
		mes "^3355FFCette armoire à pharmacie est remplie de bouteilles de diverses couleurs. Quel genre de médicament peuvent-elles bien contenir?^000000";
		next;
		mes "^3355FFOh mais attendez, vous avez toujours ce document vierge avec le sceau de la Fondation. C'est très étrange que quelqu'un l'ait apposé sur une feuille où rien n'était écrit. Et si il y avait un texte caché, écrit avec de l'encre sympathique?^000000";
		next;
		mes "^3355FFIl ne s'agit que d'une intuition, mais peut-être y a-t-il quelque chose dans cette armoire permettant de révéler cet hypothétique texte caché...^000000";
		next;
		switch(select("Quoi? Non, c'est tiré par les cheveux!:Mais bien sûr! Essayons!")) {
		case 1:
			mes "^3355FFMince, vous pensiez pourtant que c'était une bonne idée...^000000";
			close;
		case 2:
			switch(select("Essayer le liquide rouge.:Essayer le liquide bleu.:Essayer le liquide jaune.:Essayer le liquide vert.:Annuler")) {
			case 1:
				set .@KHPotionColor$,"rouge";
				break;
			case 2:
				mes "^3355FFÇa marche! Le liquide bleu révèle un message écrit sur le papier, comme une espèce de long poème...^000000";
				next;
				mes "[Poème]";
				mes "Le premier des serpents était fait d'acier, mais je l'ai terrassé grâce à ma rage. Le second des serpents était fait de magie mais mon amour lui a transpercé le coeur.";
				next;
				mes "[Poème]";
				mes "Le troisième des serpents était fait de chair et de sang mais mon espoir finit par le vaincre. Mais le quatrième des serpents n'a pas de forme et nul ne connait son apparence.";
				next;
				mes "[Poème]";
				mes "L'air s'est rempli de mon désespoir, mais personne ne sait si cela suffit à occire le dernier des serpents. Je ne suis qu'une ^0000FFpetite diablesse perdue^000000, avec quatre lames et quatre serpents, cherchant cette fille dans les ténèbres.";
				next;
				mes "^3355FFCe poème est très étrange. Qu'est-ce qu'il pourrait bien pouvoir signifier?^000000";
				set KHCottagePoem2,2;
				close;
			case 3:
				set .@KHPotionColor$,"jaune";
				break;
			case 4:
				set .@KHPotionColor$,"vert";
				break;
			case 5:
				mes "^3355FFOublions ça. Cette idée est trop folle pour avoir une chance de marcher... Ça serait aussi stupide que d'essayer d'envoyer un homme sur la lune. Qui croirait à de telles sornettes?^000000";
				close;
			}
			mes "^3355FFVous aspergez le papier portant le sceau de l'Académie avec le liquide "+.@KHPotionColor$+", mais rien ne se passe...^000000";
			close;
		}
	}
	else if ((KHCottagePoem2 >= 2) || (KielHyreQuest >= 30)) {
		mes "^3355FFC'est ici que vous avez versé le liquide bleu sur la feuille prétendument vierge portant le sceau de l'Académie, et que vous avez découvert le texte cache. Peut-être voulez-vous le lire à nouveau?^000000";
		next;
		switch(select("Pas le temps!:Le lire.")) {
		case 1:
			mes "^3355FFLe temps presse! Il faut d'abord inspecter chaque centimètre carré de ce manoir.^000000";
			close;
		case 2:
			mes "[Poème]";
			mes "Le premier des serpents était fait d'acier, mais je l'ai terrassé grâce à ma rage. Le second des serpents était fait de magie mais mon amour lui a transpercé le coeur.";
			next;
			mes "[Poème]";
			mes "Le troisième des serpents était fait de chair et de sang mais mon espoir finit par le vaincre. Mais le quatrième des serpents n'a pas de forme et nul ne connait son apparence.";
			next;
			mes "[Poème]";
			mes "L'air s'est rempli de mon désespoir, mais personne ne sait si cela suffit à occire le dernier des serpents. Je ne suis qu'une ^0000FFpetite diablesse perdue^000000, avec quatre lames et quatre serpents, cherchant cette fille dans les ténèbres.";
			close;

		}
	}
}

//----------------------------------------------------------------------------
// Cottage interior, stair case; Sword hilt
//----------------------------------------------------------------------------
kh_vila,136,68,0	script	Présentoir::Sword Hilt#kh	111,{
	if ((KHCottagePoem1 < 3) || (KHCottagePoem2 < 2) && (KielHyreQuest < 30)) {
		mes "^3355FFVous voyez quatre épées très finement ciselées, à côté de quatre sculptures de serpents répugnants.^000000";
		close;
	}
	else if ((KHCottagePoem1 <= 4) || (KHCottagePoem2 <= 2) && (KielHyreQuest < 30)) {
		mes "^3355FFVous voyez quatre épées très finement ciselées, à côté de quatre sculptures de serpents répugnants. Oh, mais peut-être est-ce en rapport avec ce poème que vous avez lu plus tôt...";
		next;
		switch(select("Non, ce n'est pas ça!:Mais bien sûr!")) {
		case 1:
			mes "^3355FFImpossible... Ça doit être une simple coïncidence...^000000";
			close;
		case 2:
			mes "^3355FFMais bien sûr! Et vous apercevez des trous sur la tête de chaque serpent, assez larges pour y enfoncer l'une des épées ornementales...^000000";
			next;
			switch(select("Passer son chemin.:Y insérer les épées.")) {
			case 1:
				mes "^3355FFImposible... Ça doit être une simple coïncidence...^000000";
				close;
			case 2:
				if (KHCottagePoem1 != 4) {
					mes "^3355FFVous... n'arrivez pas... à tirer cette épée. Elle doit être retenue par un pouvoir mystérieux.^000000";
					close;
				}
				else {
					mes "^3355FFVous devez probablement insérer chaque épée dans la tête du bon serpent. Tout d'abord, choisissez le serpent dans lequel vous allez insérer la première épée.^000000";
					next;
					switch(select("Premier serpent:Second serpent:Troisième serpent:Quatrième serpent")) {
					default:
						break;
					case 2:
						set .@KHSwords,.@KHSwords+1;
						break;
					}
					next;
					mes "^3355FFMaintenant, choisissez le serpent dans lequel vous allez insérer la seconde épée.^000000";
					next;
					switch(select("Premier serpent:Second serpent:Troisième serpent:Quatrième serpent")) {
					default:
						break;
					case 4:
						set .@KHSwords,.@KHSwords+1;
						break;
					}
					next;
					mes "^3355FFEnsuite, choisissez le serpent dans lequel vous allez insérer la troisième épée.^000000";
					next;
					switch(select("Premier serpent:Second serpent:Troisième serpent:Quatrième serpent")) {
					case 1:
						set .@KHSwords,.@KHSwords+1;
						break;
					default:
						break;
					}
					next;
					mes "^3355FFEnfin, choisissez le serpent dans lequel vous allez insérer la quatrième épée.^000000"; // on a déjà choisi pour la permière [Etna]
					next;
					switch(select("Premier serpent:Second serpent:Troisième serpent:Quatrième serpent")) {
					default:
						break;
					case 3:
						set .@KHSwords,.@KHSwords+1;
						break;
					}

					if (.@KHSwords == 4) {
						mes "^3355FFVous entendez comme un bourdonnement tandis que vous avez l'impression que votre corps se décompose...^000000";
						set KHCottagePoem1,5;
						set KHCottagePoem2,3;
						close2;
						warp "kh_vila",178,72;
						end;
					}

					else {
						mes "^3355FFRien ne se passe. Vous n'avez probablement pas inséré les épées dans les bons serpents. Vous devriez remettre les épées en place et réessayer plus tard.^000000";
						close;
					}
				}
			}
		}
	}
	else if ((KHCottagePoem1 == 5) && (KHCottagePoem2 == 3) || (KielHyreQuest >= 30)) {
		mes "^3355FFUn passage secret part d'ici et mène au laboratoire caché.^000000";
		next;
		switch(select("L'emprunter.:Annuler.")) {
		case 1:
			warp "kh_vila",178,72;
			end;
		case 2:
			close;
		}
	}
}

//----------------------------------------------------------------------------
// Cottage interior, Secret Room; left test tube
//----------------------------------------------------------------------------
kh_vila,179,68,0	script	Tube::Test Tube#khp2	111,{
	mes "^3355FFCe tube rempli de liquide contient le corps d'un jeune homme habillé comme un étudiant de l'Académie Kiel Hyre. Vous avez le sentiment de l'avoir déjà vu quelque part.^000000";
	close;
}


//----------------------------------------------------------------------------
// Cottage interior, Secret Room; Right test tube
//----------------------------------------------------------------------------
kh_vila,185,68,0	script	Tube::TestTubeKHQ2	111,{
	if (KielHyreQuest > 28) {
		mes "^3355FFUn vieille femme rabougrie se trouve dans ce tube rempli de liquide.^000000";
		close;
	}
	else if (KielHyreQuest == 28) {
		mes "^3355FFUn vieille femme rabougrie se trouve dans ce tube rempli de liquide. Il y a un petit bouton rouge juste derrière le tube.^000000";
		next;
		switch(select("Appuyer sur le bouton.:Examiner la zone.")) {
		case 1:
			mes "[???]";
			mes "B-Bonjour...? Est-ce... Est-ce que vous pouvez m'entendre? Est-ce que vous pouvez m'entendre? Je ne vous reconnais pas... Mais... Peut-être ai-je oublié? Attendez, attendez... Si vous me connaissez, vous savez comment je m'appelle. Connaissez-vous mon nom?";
			next;
			input .@KHInput$;
			if (.@KHInput$ == "petite diablesse perdue") {
				mes "[???]";
				mes "Hé... Hé hé...";
				mes "Il sait... Hé, vous devrez vous souvenir de ces chiffres, d'accord? N'oubliez pas, ils deviendront importants...";
				mes "^FF00004^000000, ^FF00007^000000, ^FF00007^000000, ^FF00002^000000, ^FF00009^000000, ^FF00006^000000, ^FF00001^000000. C'est tout...";
				next;
				mes "^3355FF4772961^000000";
				mes "^3355FFQu'est-ce que ces chiffres peuvent bien vouloir dire?^000000";
				//KHCottagePoem1 and KHCottagePoem2 no longer needed-- deleting
				set KHCottagePoem1,0;
				set KHCottagePoem2,0;
				set KielHyreQuest,30;
				close;
			}
			else {
				mes "[???]";
				mes "Non... Non... Ce n'est pas cela... Je ne vous connais pas... Non...";
				close;
			}
		case 2:
			mes "^3355FFExaminons d'abord cet endroit un peu plus.^000000";
			close;
		}
	}
	else if (KielHyreQuest >= 30) {
		if ((countitem(7491) > 0) && (countitem(7492) > 0)){
			mes "^3355FFQu'est-ce que le nombre 4772961 peut bien vouloir dire? Vous avez tout ce dont vous aviez besoin dans cet endroit. Vous feriez mieux de retourner voir Elly.^000000";
			close;
		}
		else {
			mes "^3355FFQu'est-ce que le nombre 4772961 peut bien vouloir dire? Vous feriez mieux pour l'instant de fouiller le manoir pour voir si vous trouvez des indices...^000000";
			close;
		}
	}
	end;
}

//============================================================================

//============================================================================
// Robot Factory NPCs
//============================================================================

//----------------------------------------------------------------------------
// Robotic's Factory; Heavy Door
//----------------------------------------------------------------------------
kh_dun01,235,48,0	script	Lourde Porte::HeavyDoorKHQ1	111,{
	if (KielHyreQuest < 38) {
		mes "^3355FFVous vous trouvez face à une lourde porte.^000000";
		next;
		switch(select("Ouvrir la porte.:Passer son chemin.")) {
		case 1:
			input .@KHInput$;
			mes "^3355FFVous poussez la porte de toutes vos forces mais elle ne veut pas bouger.^000000";
			close;
		case 2:
			mes "^3355FFVous décidez de ne pas toucher à cette porte tant que vous n'aurez pas trouvé comment l'ouvrir.^000000";
			close;
		}
	}
	else if ((KielHyreQuest >= 38) && (KielHyreQuest < 46)) {
		mes "^3355FFVous vous trouvez face à une lourde porte.^000000";
		next;
		switch(select("Ouvrir la porte.:Passer son chemin.")) {
		case 1:
			input .@KHInput$;
			if (.@KHInput$ == "Clé Magnétique Bleue") {
				mes "^3355FFVous réussissez à ouvrir la porte.";
				close2;
				warp "kh_school",119,144;
				end;
			}
			else {
				mes "^3355FFVous poussez la porte de toutes vos forces mais elle ne veut pas bouger.^000000";
				close;
			}
		case 2:
			mes "^3355FFVous décidez de ne pas toucher à cette porte tant que vous n'aurez pas trouvé comment l'ouvrir.^000000";
			close;
		}
	}
	else {
		mes "^3355FFIl s'agit d'une lourde porte fermement verrouillée. Vous ne pourriez l'ouvrir en utilisant la force brute.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Robotic's Factory; Heavy Door (These doors don't seem to serve any purpose)
//----------------------------------------------------------------------------
kh_dun01,76,233,0	script	Lourde Porte::HeavyDoorKHQ2	111,{
	if ((KielHyreQuest >= 38) && (KielHyreQuest < 46)) {
		mes "^3355FFVous vous trouvez face à une lourde porte.^000000";
		next;
		switch(select("Ouvrir la porte.:Passer son chemin.")) {
		case 1:
			input .@KHInput$;
			mes "^3355FFVous poussez la porte de toutes vos forces mais elle ne veut pas bouger.^000000";
			close;
		case 2:
			mes "^3355FFVous décidez de ne pas toucher à cette porte tant que vous n'aurez pas trouvé comment l'ouvrir.^000000";
			close;
		}
	}
	else {
		mes "^3355FFVous vous trouvez face à une lourde porte.^000000";
		close;
	}
}

kh_dun01,106,233,0	duplicate(HeavyDoorKHQ2)	Lourde Porte::Heavy Door#kh3	111
kh_dun01,6,164,0	duplicate(HeavyDoorKHQ2)	Lourde Porte::Heavy Door#kh4	111
kh_dun01,6,133,0	duplicate(HeavyDoorKHQ2)	Lourde Porte::Heavy Door#kh5	111
kh_dun01,6,60,0	duplicate(HeavyDoorKHQ2)	Lourde Porte::Heavy Door#kh6	111
kh_dun01,6,30,0	duplicate(HeavyDoorKHQ2)	Lourde Porte::Heavy Door#kh7	111
kh_dun01,6,18,0	duplicate(HeavyDoorKHQ2)	Lourde Porte::Heavy Door#kh8	111
kh_dun01,235,70,0	duplicate(HeavyDoorKHQ2)	Lourde Porte::Heavy Door#kh9	111
kh_dun01,235,92,0	duplicate(HeavyDoorKHQ2)	Lourde Porte::Heavy Door#kh10	111
kh_dun01,235,114,0	duplicate(HeavyDoorKHQ2)	Lourde Porte::Heavy Door#kh11	111

//----------------------------------------------------------------------------
// Beautiful Lady and Beautiful Lady copies.
//----------------------------------------------------------------------------
kh_school,119,149,4	script	Très Belle Femme::BeatifulLadyKHQ1	894,{
	cutin "kh_ellisia",2;
	if (KielHyreQuest < 40) {
		mes "[Allysia]";
		mes "Hum? Je ne pense pas vous connaître. Kiel Hyre n'a autorisé qu'une seule personne à accéder à cette zone: sa chère amie Elly.";
		next;
		mes "[Allysia]";
		mes "Je ne vois pas ^FF0000Elly^000000. S'est-elle faite attaquer elle aussi? Êtes-vous ^FF0000l'ami"+ ( sex ? "" : "e" ) +" d'Elly^000000?";
		next;
		mes "[Allysia]";
		mes "Puisqu'Elly n'est pas dans les parages, je ne pourrai vous laisser entrer que si vous vous identifiez comme son ami"+ ( sex ? "" : "e" ) +". Pouvez-vous me présenter une preuve de cela?";
		next;
		switch(select("Clé Magnétique Jaune:Clé Magnétique Bleue:Clé du Manoir:Bouton Luxueux:...?")) {
		default:
			break;
		case 3:
			mes "[Allysia]";
			mes "Ah, n'est-ce pas la clé que Kiel Hyre a donné à Elly? Oui, je suis maintenant convaincue que vous essayez bien de l'aider. Je vous attendais en fait. Laissez-moi vous conduire dans ma chambre.";
			close2;
			cutin "",255;
			warp "kh_school",120,180;
			end;
		case 4:
			mes "[Allysia]";
			mes "C'est... C'est le sceau de Kiehl! Est-ce qu'il vous envoie ici pour moi?! Je ne vais pas vous laisser faire!";
			close2;
			cutin "",255;
			monster "kh_school",117,144,"Bombe",1745,1; // Constant
			monster "kh_school",117,144,"Bombe",1745,1; // Constant
			monster "kh_school",117,144,"Bombe",1745,1; // Constant
			monster "kh_school",117,144,"Bombe",1745,1; // Constant
			end;
		case 5:
			mes "[Allysia]";
			mes "......";
			mes "........";
			mes "...........";
			close2;
			cutin "",255;
			end;
		}
		mes "[Allysia]";
		mes "Hum? Ça ne me prouve en rien qu'Elly vous fasse vraiment confiance...";
		close2;
		cutin "",255;
		end;
	}
	else if (KielHyreQuest >= 40) {
		mes "[Allysia]";
		mes "Veuillez me suivre.";
		close2;
		cutin "",255;
		warp "kh_school",120,180;
		end;
	}
}


kh_school,117,149,4	script	Très Belle Femme::BeatifulLadyKHQ2	894,{
	end;
}
kh_school,118,149,4	duplicate(BeatifulLadyKHQ2)	Très Belle Femme::Beautiful lady#kh3	894
kh_school,120,149,4	duplicate(BeatifulLadyKHQ2)	Très Belle Femme::Beautiful lady#kh4	894
kh_school,121,149,4	duplicate(BeatifulLadyKHQ1)	Très Belle Femme::Beautiful lady#kh5	894

//----------------------------------------------------------------------------
// Beautiful Lady "Allysia"
//----------------------------------------------------------------------------
kh_school,122,186,4	script	Très Belle Femme::Beautiful Lady#kh6	894,{
	if (checkweight(7496,1) == 0) {
		mes "^3355FFUne petite minute! Vous portez trop d'objets sur vous! Vous feriez mieux de placer quelques objets dans votre Stock Kafra ou vous ne pourriez en obtenir de nouveaux...^000000";
		close;
	}
	cutin "kh_ellisia",2;
	if (KielHyreQuest < 38) {
		mes "[??????]";
		mes "Il s'agit d'une propriété privée. Veuillez quitter ces lieux sans attendre!";
		close2;
		cutin "kh_ellisia",255;
		warp "yuno_fild08",73,172;
		end;
	}
	else if (KielHyreQuest <= 38) {
		mes "[Allysia]";
		mes "Ami"+ ( sex ? "" : "e" ) +" du prototype Elly, je vous souhaite la bienvenue. Comme vous l'avez certainement deviné, j'ai besoin de votre aide. Kiel Hyre est retenu prisonnier quelque part dans cette usine.";
		next;
		mes "[Allysia]";
		mes "Il est impératif que vous partiez à la recherche de Kiel Hyre et que vous le sauviez sans perdre de temps.";
		next;
		switch(select("Qu'est-ce que cette usine?:Qu'est-ce qui est arrivé à ^FF0000Kiel Hyre^000000?")) {
		case 1:
			mes "[Allysia]";
			mes "Cette usine relève des activités secrètes de la Fondation Kiel Hyre. Il s'agit d'une fabrique de robots humanoïdes. Moi-même, ainsi que tous les étudiants, sommes en fait des robots.";
			next;
			mes "[Allysia]";
			mes "Le but ultime de la Fondation Kiel Hyre est de créer des robots de classe supérieure qui pourront épauler l'humanité. En tant que robots, nous pouvons nous occuper de tâches trop difficiles ou dangereuses pour de simples humains.";
			break;
		case 2:
			mes "[Allysia]";
			mes "^3355FFKiehl^000000 a fait des modifications sur les robots humanoïdes. Kiel Hyre en a eu vent, et il a tenté d'arrêter Kiehl. Mais il n'est jamais revenu.";
			next;
			mes "[Allysia]";
			mes "De peur que Kiehl ne me désactive, Kiel Hyre a apporté une copie de moi à la place. Je peux toujours ressentir les pulsations du coeur de Kiel Hyre, alors je sais qu'il est toujours en vie. Mais j'ai peur que son propre fils ne lui fasse du mal très bientôt.";
			next;
			mes "[Allysia]";
			mes "Prenez cette Clé Magnétique qui vous permettra d'accéder à la zone secrète de cette usine. Si vous réussissez à localiser Kiel Hyre, faites-le moi savoir sans attendre.";
			getitem 7496,1; //Red_Key_Card
			set KielHyreQuest,40;
			break;
		}
		close2;
		cutin "",255;
		end;
	}
	else if (KielHyreQuest == 40) {
		mes "[Allysia]";
		mes "Je peux toujours ressentir les pulsations du coeur de Kiel Hyre, mais je sens que son fils Kiehl pourrait faire quelque chose de désespéré dans un avenir proche. S'il vous plaît, localisez Kiel Hyre avant que cela n'arrive!";
		close2;
		cutin "",255;
		warp "kh_dun01",22,216;
		end;
	}
	else if (KielHyreQuest == 42) {
		mes "[Allysia]";
		mes "Avez-vous réussi à localiser Kiel Hyre?";
		next;
		mes "^3355FFVous racontez à Allysia où Kiel Hyre est enfermé, et vous lui donnez le fragment de métal qu'il vous a confié. Elle le prend et le place autour de son poignet.^000000";
		next;
		mes "[Allysia]";
		mes "...Code secret de Kiel Hyre confirmé. Zone B-2-3. Signes vitaux normaux. Nouvelle enveloppe en cours de téléchargement. Traitement des nouvelles données corporelles..."; // Heureusement qu'elle n'est pas sous Windows sinon le vieux Kiel pourrait toujours attendre... Oh, je m'égare. [NdT]
		next;
		mes "[Allysia]";
		mes "Bien, j'ai reçu les nouvelles directives de Kiel Hyre et je dois les suivre...";
		delitem 7497,1; //Steel_Piece
		set KielHyreQuest,44;
		close2;
		cutin "",255;
		end;
	}
	else if (KielHyreQuest == 44) {
		mes "[Allysia]";
		mes "Je dois mener à bien une mission pour Kiel Hyre, et je n'ai pas beaucoup de temps pour cela. Laissez-moi vous donner les détails rapidement.";
		next;
		mes "[Allysia]";
		mes "Tout d'abord, je dois prendre vos clés magnétiques pour des raisons de sécurité. Ensuite, vous devrez me retrouver à l'hôtel particulier de Kiel Hyre à Lighthalzen. Présentez la Clé Dorée et on vous laissera entrer en ces lieux.";
		delitem 7492,1; //Yellow_Key_Card
		delitem 7495,1; //Blue_Key_Card
		delitem 7496,1; //Red_Key_Card
		set KielHyreQuest,46;
		next;
		mes "[Allysia]";
		mes "Je sais que je vous prends au dépourvu et que je ne vous donne pas vraiment d'explications, mais quelque chose d'horrible va se produire si vous ne faites rien.";
		close2;
		warp "yuno_fild08",73,172;
		cutin "",255;
		end;
	}
	else if (KielHyreQuest >= 44) {
		mes "[Allysia]";
		mes "Je sais que je vous prends au dépourvu et que je ne vous donne pas vraiment d'explications, mais quelque chose d'horrible va se produire si vous ne faites rien.";
		close2;
		warp "yuno_fild08",73,172;
		cutin "",255;
		end;
	}
}

//----------------------------------------------------------------------------
// Signboard (another useless NPC?)
//----------------------------------------------------------------------------
kh_dun01,163,223,4	script	Panneau::Signboard#kh	857,{
	mes "[Notice]";
	mes "Les animaux domestiques sont interdits dans l'enceinte. Les animaux offrant assistance aux personnes déficientes visuellement sont cependant tolérées.";
	close;
}

//----------------------------------------------------------------------------
// Mechanical Devices for accessing restricted area
//----------------------------------------------------------------------------
kh_dun01,166,223,0	script	Mécanisme::KHKeyardReader	111,{
	if ((KielHyreQuest <= 38) || (KielHyreQuest >= 44)) {
		mes "^3355FFVous voyez un mécanisme. On dirait que vous pourriez le mettre en marche en y insérant une clé magnétique.^000000";
		close;
	}
	else if ((KielHyreQuest == 40) && (countitem(7496) >= 1)) {
		mes "^3355FFVous voyez un mécanisme. On dirait que vous pourriez le mettre en marche en y insérant une clé magnétique.^000000";
		next;
		input .@KHInput$;
		if (.@KHInput$ == "Clé Magnétique Rouge") {
			mes "^3355FFLa porte s'ouvre tandis que vous insérez la Clé Magnétique Rouge.^000000";
			close2;
			warp "kh_dun01",170,227;
			end;
		}
		else {
			mes "^3355FFRien ne se passe.^000000";
			close;
		}
	}
}

kh_dun01,162,206,0	duplicate(KHKeyardReader)	Mécanisme::Mechanical Device	111

//----------------------------------------------------------------------------
// Keep monsters out of the quest area.
// Silly really since the rest of it's a dungeon
//----------------------------------------------------------------------------
//kh_dun01,180,220,0	script	Monster Warp#kh1::KHMobWarp	-1,5,20,{
//OnTouchNPC:
//	warp "kh_dun01",178,138;
//	end;
//}

//kh_dun01,190,220,0	duplicate(KHMobWarp)	Monster Warp#kh2	-1,5,20
//kh_dun01,200,250,0	duplicate(KHMobWarp)	Monster Warp#kh3	-1,5,20
//kh_dun01,210,220,0	duplicate(KHMobWarp)	Monster Warp#kh4	-1,5,20
//kh_dun01,220,220,0	duplicate(KHMobWarp)	Monster Warp#kh5	-1,5,10

//----------------------------------------------------------------------------
// Kiel Hyre - OnTouch
//----------------------------------------------------------------------------
kh_dun01,224,233,0	script	Factory B Area Door	-1,5,5,{
//OnTouch2:
OnTouch:
	if (KielHyreQuest == 40) {
		if (checkweight(7497,1) == 0) {
			mes "^3355FFUne petite minute! Vous portez trop d'objets sur vous! Vous feriez mieux de placer quelques objets dans votre Stock Kafra ou vous ne pourriez en obtenir de nouveaux...^000000";
			close;
		}
		mes "^3355FFVous arrivez à percevoir une voix de l'autre côté de la porte.^000000";
		next;
		mes "[????]";
		mes "^333333...Kiehl...";
		mes "...Comment oses-tu...!^000000";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Maître Kiel Hyre?";
		mes "Est-ce que c'est vous?";
		next;
		mes "[Kiel Hyre]";
		mes "Qui va là?";
		mes "Identifiez-vous!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Je suis... Je suis...";
		next;
		mes "^3355FFVous lui expliquez votre histoire et lui parlez d'Elly et d'Allysia.^000000";
		next;
		mes "[Kiel Hyre]";
		mes "^333333...Oui... C'est moi. Je suis Kiel Hyre. Je suis piégé ici. Alors il a été jusqu'à modifier Elly, hein?";
		mes "...............................^000000";
		next;
		mes "[Kiel Hyre]";
		mes "^333333Dépêchez-vous, apportez ce module à Allysia! Elle saura quoi en faire. Si vous voulez vraiment nous aider, suivez les consignes qu'elle vous donnera.^000000 ";
		next;
		mes "^3355FFKiel Hyre fait glisser une étrange pièce de métal sous la porte.^000000";
		getitem 7497,1; //Steel_Piece
		set KielHyreQuest,42;
		close;
	}

	else if (KielHyreQuest >= 42) {
		mes "[Kiel Hyre]";
		mes "......";
		close;
	}
}

//----------------------------------------------------------------------------
// Robot Factory Second Floor Entrances
//----------------------------------------------------------------------------
kh_dun01,44,203,0	script	Mécanisme d'Entrée::KHAccess	111,{
	if (countitem(7509) < 1) {
		mes "^3355FFVous êtes face à un mécanisme étrange. On dirait que vous pourriez l'activer en y insérant la clé magnétique adéquate.^000000";
		close;
	}
	else {
		mes "^3355FFVous êtes face à un mécanisme étrange. On dirait que vous pourriez l'activer en y insérant la clé magnétique adéquate.^000000";
		next;
		input .@KHInput$;
		if (.@KHInput$ == "Clé Magnétique Luxueuse") {
			mes "^3355FFLa porte s'ouvre tandis que vous insérer la Clé Magnétique Luxueuse, et révèle des escaliers descendant en profondeur.^000000";
			next;
			switch(select("Emprunter les escaliers.:Passer son chemin.")) {
			case 1:
				warp "kh_dun02",41,198;
				end;
			case 2:
				mes "^3355FFOn dirait que cette porte s'est verrouillée automatiquement après que la limite de temps programmée se soit écoulée.^000000";
				close;
			}
		}
		else {
			mes "^3355FFVous essayez de pousser la porte de toutes vos forces mais elle ne veut pas bouger.^000000";
			close;
		}
	}
}

kh_dun01,36,195,0	duplicate(KHAccess)	Mécanisme d'Entrée ::Entrance Device#khd1	111

//============================================================================

//============================================================================
// Kiel Hyre Mansion NPCs, and Kiehl's Room NPCs
//============================================================================

//----------------------------------------------------------------------------
// Kiel's Steward
//----------------------------------------------------------------------------
kh_mansion,78,55,5	script	Majordome::Steward#kh	109,{
	if (KielHyreQuest < 46) {
		mes "[Majordome]";
		mes "Il s'agit d'une résidence privée. Veuillez quitter ces lieux.";
		close2;
		warp "lighthalzen",188,201;
		end;
	}
	if ((KielHyreQuest >= 46) && (KielHyreQuest < 50)) {
		mes "[Majordome]";
		mes "Êtes-vous attendu par le maître de cette demeure?";
		next;
		switch(select("Lui montrer la Clé Dorée:????")) {
		case 1:
			mes "[Majordome]";
			mes "Ah, Maître "+strcharinfo(0)+"! J'attendais votre arrivée. Veuillez me suivre s'il vous plaît.";
			close2;
			warp "kh_mansion",21,14;
			end;
		case 2:
			mes "[Majordome]";
			mes "Je ne vous reconnais pas. Vous n'êtes pas un hôte du maître de maison, j'ai peur de devoir vous demander de quitter ces lieux sans attendre.";
			close2;
			warp "lighthalzen",188,201;
			end;
		}
	}
	if ((KielHyreQuest >= 50) && (KielHyreQuest < 64) || (KielHyreQuest >= 70)) {
		mes "[Majordome]";
		mes "Ah, Maître "+strcharinfo(0)+"! Je suis heureux de vous revoir. Voulez-vous vous entretenir avec mon maître?";
		next;
		switch(select("Oui:Non")) {
		case 1:
			mes "[Majordome]";
			mes "Très bien. Suivez-moi s'il vous plaît~";
			close2;
			warp "kh_mansion",21,14;
			end;
		case 2:
			mes "[Majordome]";
			mes "Très bien, faites comme chez vous.";
			close;
		}
	}
	if ((KielHyreQuest == 52) || (KielHyreQuest == 54) || (KielHyreQuest >= 64) && (KielHyreQuest <= 70)) {
		mes "[Majordome]";
		mes "Ah, Maître "+strcharinfo(0)+"! Je suis heureux de vous revoir. Voulez-vous vous entretenir avec mon maître, ou...?";
		next;
		switch(select("Je voudrais voir ^FF0000Kiel Hyre^000000:Je voudrais voir ^3355FFMitchell^000000")) {
		case 1:
			mes "[Majordome]";
			mes "Très bien. Suivez-moi s'il vous plaît~";
			close2;
			warp "kh_mansion",21,14;
			end;
		case 2:
			mes "[Majordome]";
			mes "Oh...? Vous devez apporter de bonnes nouvelles. Excusez-moi...";
			next;
			mes "^3355FFLe majordome jette un coup d'oeil aux alentours pour voir si personne ne vous observe.^000000";
			next;
			mes "[Majordome]";
			mes "Bien, je pense que vous pouvez y aller. Par ici, s'il vous plaît.";
			close2;
			warp "kh_mansion",20,87;
			end;
		}
	}
}

//----------------------------------------------------------------------------
// Kiel Hyre
//----------------------------------------------------------------------------
kh_mansion,22,28,4	script	Kiel Hyre#kh	903,{
	if (checkweight(908,200) == 0) {
		mes "^3355FFUne petite minute! Vous portez trop d'objets sur vous! Vous feriez mieux de placer quelques objets dans votre Stock Kafra ou vous ne pourriez en obtenir de nouveaux...^000000";
		close;
	}

	cutin "kh_kyel01",2;
	if (KielHyreQuest < 46) {
		mes "[Kiel Hyre]";
		mes ".........";
		mes ".........";
		mes "............";
		close2;
		cutin "",255;
		end;
	}

	else if (KielHyreQuest == 46) {
		cutin "kh_kyel01",2;
		mes "[Kiel Hyre]";
		mes "Ah, vous devez être "+strcharinfo(0)+". Je voulais vous remercier pour avoir sauvé ma vie. Vous avez certainement beaucoup de questions à me poser. Je ferai de mon mieux pour y répondre.";
		next;
		while(1) {
			switch(select("Les robots?:^3355FFKiehl^000000?:Le bouton d'^FF0000Elly^000000?")) {
			case 1:
				mes "[Kiel Hyre]";
				mes "Voilà trente-deux ans que j'étudie la robotique. Je suis fier de dire que j'ai réussi là où le Sage Varmunt a échoué."; // Sage aparaissant dans d'autres fichiers (quests_airship, etc.)
				next;
				mes "[Kiel Hyre]";
				mes "Mon rêve a toujours été de créer des robots humanoïdes à partir des humains. Ces gardiens sont peut-être des robots, mais ils n'ont pas de volonté propre.";
				next;
				mes "[Kiel Hyre]";
				mes "Si vous voulez en apprendre plus sur les bases de la robotique, pourquoi n'iriez-vous pas parler à ^3355FFAllysia^000000? Elle pourra vous expliquer tout ça de façon bien plus succincte que moi. J'ai tendance à radoter...";
				set .@KHQRead,.@KHQRead+1;
				next;
				break;
			case 2:
				mes "[Kiel Hyre]";
				mes "^3355FFKiehl^000000 est mon fils unique, mais ma femme est morte en lui donnant naissance. Je dois admettre que c'est un génie en ce qui concerne la mécanique.";
				next;
				mes "[Kiel Hyre]";
				mes "C'est lui qui est en grande partie responsable de la création des robots de troisième génération, comme Elly. Malheureusement, il essaie de modifier ces robots pour ses sinistres desseins.";
				next;
				mes "[Kiel Hyre]";
				mes "J'ai essayé de l'arrêter, mais j'ai fini par me retrouver enfermé dans cette usine. Je ne sais toujours pas pourquoi il cherche à faire cela... Vraiment.";
				set .@KHQRead,.@KHQRead+1;
				next;
				break;
			case 3:
				mes "[Kiel Hyre]";
				mes "Le bouton qu'Elly avait dans la main porte l'^3355FFemblème de Kiehl^000000. Ah, cet homme en noir doit être ^3355FFKaiser^000000, l'homme de main de Kiehl.";
				next;
				mes "[Kiel Hyre]";
				mes "Kaiser... Je ne sais rien sur lui. Je ne l'ai jamais rencontré. Kiehl l'a embauché sans me consulter...";
				set .@KHQRead,.@KHQRead+1;
				next;
				break;
			}
			if (.@KHQRead == 3) {
				mes "[Kiel Hyre]";
				mes "Si vous n'avez plus de questions, pourriez-vous me laisser me reposer? Je dois encore me remettre de mon séjour enfermé dans l'usine.";
				delitem 7493,1; //Golden_Key
				delitem 7494,1; //Kiel_Button
				set KielHyreQuest,48;
				break;
			}
		}
	}
	else if (KielHyreQuest == 48) {
		mes "[Kiel Hyre]";
		mes "Ah, j'allais presque oublier. S'il vous plaît, acceptez ceci pour m'avoir sauvé la vie.";
		getitem 12105,1; //Taming_Gift_Set
		getexp 700000,0;
		set KielHyreQuest,50;
	}
	else if ((KielHyreQuest >= 50) && (KielHyreQuest < 64)) {
		mes "[Kiel Hyre]";
		mes "Si vous n'avez plus de questions, pourriez-vous me laisser me reposer? Je dois encore me remettre de mon séjour enfermé dans l'usine.";
	}
	else if (KielHyreQuest == 64) {
		cutin "kh_kyel01",2;
		mes "[Kiel Hyre]";
		mes "Hum? Est-ce que vous vouliez me demander quelque chose?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Je n'ai pas encore assez de preuves, mais j'aurai certainement des questions à vous poser plus tard.";
	}
	else if (KielHyreQuest == 68) {
		cutin "kh_kyel02",2;
		mes "[Kiel Hyre]";
		mes "Ah, "+strcharinfo(0)+", c'est vous. Que puis-je faire pour vous?";
		next;
		select("A propos d'^3355FFAllysia^000000...");
		mes "["+strcharinfo(0)+"]";
		mes "Vous savez... J'examinais cette maison abandonnée à Yuno quand je suis tombé"+ ( sex ? "" : "e" ) +" sur un vieux portrait de femme qui ressemblait trait pour trait à Allysia.";
		next;
		cutin "kh_kyel02",2;
		mes "[Kiel Hyre]";
		mes "Oh... C'est tout? Je pensais que vous vouliez me parler de robotique.";
		next;
		cutin "kh_Kyel03",2;
		mes "[Kiel Hyre]";
		mes "Mais c'est une étrange coïncidence. Enfin... après tout, nous avons tous un sosie, non?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Je ne pense pas qu'il s'agisse d'une simple coïncidence. Le femme sur ce portrait vieux de trente ans s'appelait aussi Allysia, et elle travaillait rue Rosimier à Yuno. Est-ce que ça vous dit quelque chose?";
		next;
		cutin "kh_kyel02",2;
		mes "[Kiel Hyre]";
		mes "......";
		mes ".........";
		mes "............";
		next;
		cutin "kh_kyel01",2;
		mes "["+strcharinfo(0)+"]";
		mes "J'ai pensé que cette femme devait être celle que vous avez aimé, et que vous avez basé l'apparence de votre robot sur la sienne.";
		next;
		cutin "kh_kyel02",2;
		mes "[Kiel Hyre]";
		mes "............";
		next;
		cutin "kh_kyel01",2;
		mes "["+strcharinfo(0)+"]";
		mes "Je pense que c'est ce qui s'est passé. Vous étiez jeune, pauvre, et vous êtes tombé amoureux d'Allysia. Mais elle aimait déjà Rosimier, qui était riche et puissant.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Mais Rosimier était déjà promis à une autre femme, et finit par épouser sa fiancée, brisant ainsi le coeur d'Allysia.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Se sentant trahie, le coeur brisé, elle se jeta dans une rivière. Vous avez alors décidé de vous venger de Rosimier, et vous avez rejoint la Corporation Rekenber.";
		next;
		cutin "kh_Kyel03",2;
		mes "[Kiel Hyre]";
		mes "Hahahahahahaha! Que d'imagination~";
		mes "C'est complètement ridicule, même si j'admets avoir peut-être dessiné Allysia d'après cette femme que j'aurais connue il y a très longtemps.";
		next;
		mes "[Kiel Hyre]";
		mes "Je l'avais presque oubliée. Nous étions amis je crois... Mais où avez-vous été chercher cette idée comme quoi j'aurais été amoureux d'elle?";
		next;
		select("Lui montrer le portrait de Kiel.");
		cutin "kh_kyel02",2;
		mes "[Kiel Hyre]";
		mes "Quoi...";
		mes "Comment est-ce que...";
		mes "Où est-ce que...?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Kiel Hyre, j'ai trouvé ce portrait de vous dans la maison de celui à qui vous avez acheté l'anneau d'Allysia. J'ai même parlé au pêcheur qui a découvert son corps.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Vous avez payé une somme faramineuse pour acheter son anneau. Pourquoi prétendre que vous ne l'aimiez pas?";
		next;
		cutin "kh_Kyel03",2;
		mes "[Kiel Hyre]";
		mes "*Soupire*";
		mes "D'accord, je m'avoue vaincu. Je ne voulais pas que vous appreniez la vérité. J'aimais Allysia, et j'ai conçu mon robot pour qu'elle lui ressemble.";
		next;
		mes "[Kiel Hyre]";
		mes "Je ne l'oublierai jamais. Jamais. Mais je n'aurais jamais rien fait pour nuire aux Rosimier! Je suis un scientifique! Je le détestais quand j'étais jeune, mais les choses sont maintenant différentes.";
		next;
		mes "[Kiel Hyre]";
		mes "Je n'ai pas versé de larmes pour la déchéance des Rosimier, mais je n'en suis pas responsable. De plus, je n'avais ni les ressources ni les moyens de provoquer cela...";
		next;
		cutin "kh_kyel02",2;
		mes "["+strcharinfo(0)+"]";
		mes "J'ai bien peur que les preuves que j'ai trouvées ne montre le contraire.";
		next;
		select("Lui montrer le portrait des Rosimier.");
		mes "["+strcharinfo(0)+"]";
		mes "Regardez attentivement ce portrait que j'ai trouvé dans la résidence des Rosimier. Ne voyez-vous rien de... d'incriminant?";
		next;
		cutin "kh_Kyel03",2;
		mes "[Kiel Hyre]";
		mes "Mis-à-part James Rosimier, vous voulez dire? Non, je ne vois rien sur ce portrait.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Regardez attentivement la montre-gousset sur ce portrait. Il s'agit de la montre que vous portez aujourd'hui, n'est-ce pas?";
		next;
		cutin "kh_kyel02",2;
		mes "[Kiel Hyre]";
		mes "...!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Peut-être n'avez-vous pas causé la chute de la famille Rosimier vous-même, mais vous l'avez fait avec l'aide de la Corporation Rekenber. J'en suis convaincu"+ ( sex ? "" : "e" ) +"!";
		next;
		cutin "kh_Kyel03",2; 
		mes "[Kiel Hyre]";
		mes "Bravo, aventuri"+ ( sex ? "er" : "ère" ) +", bravo. Je ne regrette rien de ce que j'ai pu faire. Ils ont tué mon Allysia! Si James ne l'avait pas trahie, elle n'en serait pas venue à se suicider...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "C'est là où vous avez tout faux! Allysia a été assassinée, elle ne s'est pas suicidée! Regardez ce mot!";
		next;
		mes "[Kiel Hyre]";
		mes "Quoi?!";
		next;
		select("Lui montrer le mot de James.");
		mes "[Kiel Hyre]";
		mes "Qu'est-ce que cela prouve? Ça ne veut pas dire que James n'a pas trahi Allysia. Elle est morte, et rien ne pourra me la ramener!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Je n'ai jamais dit que James ne l'avait pas trahie. Regardez la date sur cette note. James lui a fait part de ses plans le 20 août. Cependant, son corps a été retrouvé le même jour.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Par conséquent, Allysia doit avoir été tuée le 19 août. Si elle avait dans l'intention de s'enfuir avec son bien-aimé le lendemain, elle n'avait aucune raison de se donner la mort!";
		next;
		mes "[Kiel Hyre]";
		mes "Non, ce n'est pas possible! Elle devait ne pas me faire confiance! Elle devait croire que je n'étais qu'un autre jouet de James!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Ah oui? Et moi je dis qu'elle s'est jetée dans la rivière parce que vous l'avez rencontrée ce jour-là! Regardez ceci!";
		next;
		select("Lui montrer le mot de K.H..");
		mes "["+strcharinfo(0)+"]";
		mes "Ce mot a été écrit par quelqu'un portant vos initiales, K.H.. Les mêmes initiales qu'on retrouve sur le portrait. Vous êtes celui qui lui a envoyé ce mot! Il ne s'agit pas d'une simple coïncidence!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "D'après ce mot, vous aviez demandé à Allysia de vous retrouver à l'endroit où vous vous étiez rencontré la première fois... Le 19 août. Le jour de sa mort.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Et j'imagine que cet endroit se trouvait près de la rivière. Plus de mensonges! Dites-moi ce qui s'est véritablement passé!";
		next;
		cutin "kh_kyel02",2;
		mes "[Kiel Hyre]";
		mes "............";
		mes ".........";
		mes "......";
		next;
		cutin "kh_Kyel03",2;
		mes "[Kiel Hyre]";
		mes "Ha... Ha ha ha...";
		mes "Oui... C'est vrai...";
		mes "Cette nuit terrible...";
		mes "Je m'en souviens bien...";
		mes ".....................";
		next;
		cutin "kh_kyel02",2;
		mes "[Kiel Hyre]";
		mes "Elle vint me rejoindre près de la rivière cette nuit-là, comme je lui avais demandé. Je la suppliai de s'enfuir avec moi au lieu d'attendre James.";
		next;
		mes "[Kiel Hyre]";
		mes "Elle me disait que James ne l'avait jamais trahie, et qu'il lui avait promis de partir avec elle le jour suivant. Pouvez-vous imaginer ce que je ressentais? Je n'étais rien pour elle.";
		next;
		mes "[Kiel Hyre]";
		mes "Elle se moquait bien de mes sentiments envers elle! Et... Elle avait cet anneau autour du doigt, qu'elle faisait tourner sans cesse... Encore, et encore... Comme pour m'enfoncer un pieu dans le coeur... J'ai perdu mon sang froid. Je voulais la faire arrêter. Je voulais lui arracher ce maudit anneau et le lancer dans la rivière...";
		next;
		mes "[Kiel Hyre]";
		mes "Mais elle se débattit, et tenta de me résister. Elle tenta de me repousser. Mais avant que je ne comprenne ce qui se passait, le sol se déroba sous nos pieds et... La pluie...";
		next;
		select(".........");
		mes "[Kiel Hyre]";
		mes "Je ne sais toujours pas vraiment ce qui s'est passé. La pluie avait rendu le terrain meuble, et... Tout alla de travers. Elle... La rivière l'a emportée... Je me sentais vide... Elle était partie. ";
		delitem 7499,1; //Family_Portrait
		delitem 7500,1; //Elysia_Portrait
		delitem 7501,1; //Kyll_Hyre_Letter2
		delitem 7502,1; //Piece_Memo_Of_James
		delitem 7503,1; //Man_Portrait
		set KielHyreQuest,70;
		close2;
		cutin "",255;
		end;
	}
	else if (KielHyreQuest <= 70) {
		cutin "kh_kyel01",2;
		mes "[Kiel Hyre]";
		mes "Vous savez déjà qu'on a retrouvé son corps sans vie le jour d'après. Mais ce qui m'a vraiment brisé le coeur, c'est qu'elle tenait toujours fermement cet anneau dans la main, même dans la mort...";
		next;
		cutin "kh_kyel03",2;
		mes "[Kiel Hyre]";
		mes "Sa famille avait tout quand je n'avais rien. Et il avait l'audace de vouloir me prendre Allysia?! Trouvez-vous ça juste?";
		next;
		mes "[Kiel Hyre]";
		mes "Même si elle était décédée, je voulais prouver à Allysia quel genre de personne James était vraiment. C'est pour cela que j'ai rejoint la Corporation Rekenber.";
		next;
		mes "[Kiel Hyre]";
		mes "J'ai construit mon tout premier robot de la première génération, que j'ai appelé Allysia, et j'ai vendu les plans à la Rekenber. Je leur fournissais les robots, ils me fournissaient l'argent, le pouvoir, et des subordonnés obéissants.";
		next;
		mes "[Kiel Hyre]";
		mes "Je savais bien sûr que mes robots seraient utilisés pour l'espionnage ou le meurtre. Mais vous savez-moi? Je m'en moquais tant qu'ils me donnaient les moyens de me venger. Il s'agissait d'une association idéale, vraiment.";
		next;
		mes "[Kiel Hyre]";
		mes "Vous m'avez emmené là où vous vouliez m'emmener. Pour qui travaillez-vous, et qu'est-ce que vous voulez vraiment? Mes plans? Ma mort? Les deux...?";
		next;
		cutin "kh_kyel01",2;
		mes "["+strcharinfo(0)+"]";
		mes "En fait je voulais simplement connaître la nature de vos relations avec la Rekenber, et en apprendre plus sur Kiehl.";
		next;
		mes "[Kiel Hyre]";
		mes "Kiehl, hein? Après avoir entendu mon histoire folle, j'imagine que vous devinez déjà la vérité à son sujet... Oui, c'est aussi un robot, et pour être exact le premier robot de seconde génération.";
		next;
		mes "[Kiel Hyre]";
		mes "Son esprit a été créé en utilisant un parchemin de mémoire concentrée instable et expérimental. C'est le seul robot de seconde génération qu'on m'a autorisé à conserver.";
		next;
		mes "[Kiel Hyre]";
		mes "Je me suis vraiment attaché à Kiehl. Ce n'est pas étonnant quand on sait que la robotique est devenue ma vie. Je l'ai même élevé comme mon propre fils, et je lui ai enseigné tout ce que je savais sur la robotique.";
		next;
		mes "[Kiel Hyre]";
		mes "Kiehl est devenu un génie, et c'est même lui qui a conçu les robots de troisième génération. Malheureusement, je n'ai pas pu lui inculquer les principes de la morale.";
		next;
		select("......");
		mes "[Kiel Hyre]";
		mes "Il a transformer les robots de troisième génération en machines à tuer. C'est pour ça que je les ai tous faits venir dans l'Académie: afin qu'ils apprennent les principes de la vie en société.";
		next;
		mes "[Kiel Hyre]";
		mes "Même si cela a retardé les plans de Kiehl, il a réussi à convertir tous ces robots en engins incontrôlables de destruction massive.";
		next;
		mes "[Kiel Hyre]";
		mes "Oui, il a travaillé en étroite collaboration avec la Rekenber. Leur véritable but est de créer des machines de guerre que la Rekenber pourra utiliser.";
		next;
		mes "[Kiel Hyre]";
		mes "Vous connaissez maintenant tous les secrets de mon passé. Je ne vous en veux pas, mais il y a quelque chose que je voudrais vous demander.";
		next;
		mes "[Kiel Hyre]";
		mes "S'il vous plaît! Arrêtez Kiehl! Je ne veux pas que sa folie conduise à la destruction d'autres robots! Je considère chacun d'entre eux comme mes propres enfants.";
		next;
		mes "[Kiel Hyre]";
		mes "Je sais que je dois accepter la responsabilité de tout ceci. Je vous promets de recevoir le châtiment que je mérite une fois que tout ceci sera terminé.";
		next;
		select("Accepter.:D'accord.:Hocher la tête.");
		mes "[Kiel Hyre]";
		mes "Merci! Merci beaucoup! Vous pourrez trouver Kiehl dans les sous-sols de cet hôtel.";
		next;
		mes "[Kiel Hyre]";
		mes "Il reste dans la pièce dans laquelle il a été créé, mais il l'a modifiée pour empêcher quiconque d'entrer, moi y compris. Oui, il ne fait plus confiance à personne...";
		next;
		mes "[Kiel Hyre]";
		mes "Quand vous l'aurez trouvé, il faudra que vous retiriez l'anneau d'Allysia de son coeur. Ça devrait l'empêcher d'entrer en furie.";
		next;
		select("L'anneau d'Allysia?");
		mes "[Kiel Hyre]";
		mes "Oui... C'est avec son anneau que tout a commencé. Je l'ai placé dans son coeur pour ne jamais oublier ce que les Rosimier m'avaient fait.";
		next;
		mes "[Kiel Hyre]";
		mes "Je pense que Kiehl est devenu si puissant que les armes conventionnelles pourraient ne plus avoir d'effet sur lui. Utilisez ce mécanisme: il permettra de faire varier son alimentation électrique.";
		getitem 7504,1; //Toy_Motor
		next;
		cutin "kh_kyel03",2;
		mes "[Kiel Hyre]";
		mes "En l'attachant au corps de Kiehl, vous le rendrez incapable d'utiliser à pleine puissance. Ouvrez son torse quand il sera affaibli et retirez l'anneau d'Allysia."; // Il manque quelque chose dans cette phrase: 'incapable de fonctionner à pleine puissance', 'incapable d'utiliser la puissance maximale' ? [Etna]
		next;
		mes "[Kiel Hyre]";
		mes "Faites-moi savoir quand vous serez prêt"+ ( sex ? "" : "e" ) +", je vous conduirai vers les appartements de Kiehl.";
		set KielHyreQuest,74;
	}
	else if ((KielHyreQuest >= 74) && (KielHyreQuest <= 104)) {
		mes "[Kiel Hyre]";
		mes "Êtes-vous prêt"+ ( sex ? "" : "e" ) +" à affronter Kiehl sans attendre?";
		next;
		switch(select("Oui:Non")) {
		case 1:
			mes "[Kiel Hyre]";
			mes "Voilà... Le passage secret est maintenant ouvert. Allez-y, à ma gauche. Mais faites attention, Kiehl est extrêmement dangereux."; // Gauche et pas droite [NdT]
			close2;
			enablenpc "Kiehl_Room_Warp";
			donpcevent "Kiehl_Room_Warp::OnEnable";
			cutin "",255;
			end;
			break;
		case 2:
			mes "[Kiel Hyre]";
			mes "D'accord, prenez votre temps. J'imagine que ça ne sera pas une partie de plaisir.";
			break;
		}
	}
	else if ((KielHyreQuest >= 74) && (KielHyreQuest <= 106)) {
		cutin "kh_kyel01",2;
		mes "[Kiel Hyre]";
		mes "Ah, vous revoilà... Alors, avez-vous pu retirer l'anneau d'Allysia du coeur de Kiehl?";
		next;
		if (countitem(7508) < 1) {
			mes "[Kiel Hyre]";
			mes "Quoi... Vous ne l'avez pas? S'il vous plaît, retirez-le du coeur de Kiehl, c'est très important!";
			next;
			enablenpc "Kiehl_Room_Warp";
			donpcevent "Kiehl_Room_Warp::OnEnable";
			close2;
			cutin "",255;
			end;
		}
		mes "[Kiel Hyre]";
		mes "Quoi??? Kiehl s'est construit un nouveau corps? C'est... Il est alors le premier robot de quatrième génération! Je ne pensais vraiment pas qu'il était aussi intelligent.";
		next;
		mes "[Kiel Hyre]";
		mes "Attendez, maintenant que j'y pense, je me souviens avoir vu des robots qui avaient l'apparence de Kiehl quand j'étais enfermé dans l'usine. Alors il utilisait ces copies pour développer des améliorations personnelles...";
		next;
		mes "[Kiel Hyre]";
		mes "Voilà, prenez cette clé magnétique, qui vous laissera accéder aux niveaux inférieurs de l'usine pour que vous puissiez y enquêter. J'irai voir ce que je peux trouver dans ses appartements.";
		getitem 7509,1; //Fancy_Key_Card
		set KielHyreQuest,108;
		next;
		if (sex == 0) {
			if (getpartnerid() == 0) set .@KHTitle$,"Mlle";
			else set .@KHSTitle$,"Mme";
		}
		else set .@KHTitle$,"M.";
		mes "[Kiel Hyre]";
		mes "Je suis responsable pour Kiehl... Je l'arrêterai, quoi qu'il en coûte! Oh, et prenez ceci en témoignage de ma gratitude, "+.@KHTitle$+" "+strcharinfo(0)+".";
		getitem 616,1;  //Old card Album
		delitem 7508,1; //Elysia_Ring
		getexp 1000000,0;
	}
	else if (KielHyreQuest >= 108) {
		mes "[Kiel Hyre]";
		mes "......";
		mes ".........";
		mes "............";
	}
	close2;
	cutin "",255;
	end;
}


//----------------------------------------------------------------------------
// Allysia
//----------------------------------------------------------------------------
kh_mansion,18,30,4	script	Allysia	894,{
	if (KielHyreQuest < 46) {
		mes "[Allysia]";
		mes "Qui êtes-vous? Comment êtes-vous arrivé"+ ( sex ? "" : "e" ) +" ici? Allez-vous-en.";
		close;
	}
	cutin "kh_ellisia",2;
	if (KielHyreQuest < 70) {
		mes "[Allysia]";
		mes "Vous devez être surpris"+ ( sex ? "" : "e" ) +" de ce qui se passe. Peut-être que vous comprendriez mieux si je vous parlais des robots...";
		next;
		switch(select("Oui:Non")) {
		case 1:
			mes "[Allysia]";
			mes "Les robots que vous avez vu sont des mécanismes automatiques qui peuvent penser indépendamment et agir. De nombreux Sages ont tenté de créer leur propres robots, mais ils ont tous échoué.";
			next;
			mes "[Allysia]";
			mes "Tous sauf mon maître, Kiel Hyre, qui étudie la robotique depuis qu'il a vingt ans. Il a créé trois générations de robots.";
			next;
			switch(select("À propos de la première génération.:À propos de la seconde génération.:À propos de la troisième génération.")) {
			case 1:
				mes "[Allysia]";
				mes "Je suis un exemple des robots de première génération. J'ai été construite à partir d'une structure métallique dans laquelle est inséré un coeur de robot, et couverte de peau synthétisée.";
				next;
				mes "[Allysia]";
				mes "Mon unité centrale est l'équivalent de votre cerveau. Elle suit le modèle des parchemins magiques que vous autres aventuriers utilisez lors des combats.";
				next;
				mes "[Allysia]";
				mes "Je fais partie de l'ancienne génération de robots, alors ma structure est très lourde, et je ne peux calculer qu'une quantité limitée de données. Je ne peux par conséquent pas exprimer d'émotions comme un être humain.";
				next;
				break;
			case 2:
				mes "[Allysia]";
				mes "Si les robots de première génération étaient principalement assemblés à partir de parties mécaniques, la seconde génération y a ajouté le fruit des recherches sur la science et la technologie des homoncules.";
				next;
				mes "[Allysia]";
				mes "Les robots de seconde génération sont des hybrides: leur structure est toujours mécanique, mais ils sont couverts de peau et de chair créées artificiellement.";
				next;
				mes "[Allysia]";
				mes "La technologie des parchemins des Sages a été utilisée pour créer leur Parchemin de Mémoire Condensée, un processeur bien supérieur à celui utilisé pour les robots de première génération.";
				next;
				mes "[Allysia]";
				mes "Même si les Parchemins de Mémoire Condensée sont 100 fois supérieurs aux Parchemins de Mémoire ordinaires, ils posaient quelques problèmes et causaient de nombreuses erreurs du système.";
				next;
				mes "[Allysia]";
				mes "La seconde génération de robots était capable d'exprimer des émotions semblable à celles des humains, mais leur production s'est arrêtée après six ans car ils étaient considérés comme défectueux.";
				next;
				break;
			case 3:
				mes "[Allysia]";
				mes "Les robots de troisième génération ont été en grande partie conçus par le fils de Kiel Hyre, Kiehl, et ils n'ont pas de structure mécanique du tout. Leur corps entier est un homoncule.";
				next;
				mes "[Allysia]";
				mes "Grâce au fait que leur corps entier soit organique, ainsi qu'à leur coeur artificiel créés en imitant des morceaux du coeur d'Ymir, ils peuvent faire l'expérience de phénomènes psychologique, tout comme les humains ordinaires.";
				next;
				mes "[Allysia]";
				mes "Kiehl a réussi à créer une version plus stable des Parchemins de Mémoire Condensée, qui ne cause plus d'erreurs système et qui peut être produite en masse à bas prix.";
				next;
				mes "[Allysia]";
				mes "Elly est en fait un prototype des robots de troisième génération. Nous commencerons la production en masse une fois que ces prototypes seront finalisés. En fait, l'académie est l'endroit où nous testons ces prototypes.";
				next;
				mes "[Allysia]";
				mes "Le fait que nos prototypes puissent interagir comme de véritables êtres humains est la preuve de notre succès en matière de robotique.";
				next;
				break;
			}
		case 2:
			mes "[Allysia]";
			mes "Revenez me voir si vous souhaitez en apprendre plus sur les robots de Kiel Hyre.";
			break;
		}
	}
	else {
		mes "[Allysia]";
		mes "......";
		mes ".........";
		mes "............";
	}
	close2;
	cutin "",255;
	end;
}

//----------------------------------------------------------------------------
// Abduction triggering NPC
//----------------------------------------------------------------------------
lighthalzen,188,200,0	script	Abduction_trigger	-1,3,1,{
//OnTouch2:
OnTouch:
	if (KielHyreQuest == 50) {
		mes "^3355FFQuelqu'un vous frappe le haut du crâne tandis que vous sortez de l'hôtel particulier. Vous perdez aussitôt connaissance...";
		close2;
		percentheal -99,0;
		warp "kh_mansion",30,75;
	}
	end;
}

//----------------------------------------------------------------------------
// Mysterious Woman "Mitchell Layla"
//----------------------------------------------------------------------------
kh_mansion,25,79,4	script	Femme Mystérieuse::Mysterious Woman#kh	727,3,3,{

//OnTouch2:
OnTouch:
	if (KielHyreQuest == 50) {
		mes "^3355FFVous reprenez conscience avec un mal de crâne insupportable. Une femme mystérieuse se dresse face à vous.^000000";
		next;
		mes "[??????]";
		mes "Hum? Oh, vous avez repris connaissance plus vite que je ne le pensais. Vous devez être confus"+ ( sex ? "" : "e" ) +", mais écoutez attentivement ce que j'ai à vous dire. Je ne pourrai pas garantir votre sécurité sinon, d'accord? Bien.";
		next;
		mes "[??????]";
		mes "La seule chose que vous devez savoir, c'est que je suis un agent  du gouvernement de la République de Schwaltzvalt. Nous enquêtons sur les activités suspectes de Kiel Hyre et de la Corporation Rekenber.";
		next;
		mes "[??????]";
		mes "Nous vous avons vu entrer dans l'hôtel et parler à Kiel Hyre. Vous êtes ici pour être interrogé. Dîtes-moi la vérité. D'où connaissez-vous Kiel Hyre?";
		next;
		switch(select("Je vais tout vous dire!:Je ne sais rien!")) {
		case 2:
			mes "[??????]";
			mes "Vous ne savez rien? Est-ce que vous en êtes bien sûr"+ ( sex ? "" : "e" ) +"? Seules quelques rares personnes peuvent s'entretenir avec Kiel Hyre en personne. Vous devez avoir certains liens avec lui. J'ai raison, n'est-ce pas?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "......";
			mes ".........";
			mes "............";
			next;
			mes "[??????]";
			mes "Est-ce que vous essayez de le protéger? J'ai bien peur que vous ne compreniez pas exactement le genre de personne à qui vous avez à faire. Je vais vous dire ce que j'ai pu apprendre de lui...";
			next;
			mes "[??????]";
			mes "Kiel Hyre. Président Directeur Général de la Fondation Kiel Hyre, créateur de diverses machineries. Sa compagnie a été créée à Einbroch comme un petit commerce, dont la technologie est devenue célèbre au fil du temps.";
			next;
			mes "[??????]";
			mes "La Corporation Rekenber a proposé de fusionner avec la Fondation Kiel Hyre. Nous ne savons toujours pas pourquoi ils lui ont demandé de réparer et d'améliorer leurs Gardiens...";
			next;
			mes "[??????]";
			mes "Nous ne savons pas non plus pourquoi ils ont soudain voulu se concentrer sur le développement de leurs Gardiens. Et c'est alors que le fils de Kiel Hyre est sorti de nulle part.";
			next;
			mes "[??????]";
			mes "C'est très suspect. Nous n'avons aucune trace de lui, pas même un acte de naissance. Mais peut-être que Kiel Hyre l'a vraiment eu avec sa secrétaire, Allysia. Personne n'en est sûr.";
			next;
			mes "[??????]";
			mes "Quoi qu'il en soit, le fils et l'unique héritier de Kiel Hyre, Kiehl, l'a aidé à créer cette académie pour rendre à la société ce qu'elle lui avait donné.";
			next;
			mes "[??????]";
			mes "Presque que toutes les corporations ont essayé d'envoyer des espions dans cette académie, mais ils ont tous été refoulés. C'est étrange. Ils auraient dû au moins pouvoir entrer.";
			next;
			mes "[??????]";
			mes "Et récemment, Kiel Hyre a tenu une réunion secrète avec les directeurs exécutifs de la Rekenber pour leur annoncer son nouveau projet: créer des robots humanoïdes avancés qui remplaceront les gardiens!";
			next;
			mes "[??????]";
			mes "Mais nos espions nous ont rapporté que Kiel Hyre ne s'était pas présenté à cette réunion, pas plus que sa secrétaire Allysia. Il... Il avait tout simplement disparu!";
			next;
			mes "[??????]";
			mes "Puis, Kiel Hyre réapparut dans son hôtel particulier comme si rien ne s'était passé. Et dans le même temps, Kiehl disparaît pour prétendument poursuivre ses recherches.";
			next;
			mes "[??????]";
			mes "La Rekenber finance à la fois Kiehl et Kiel Hyre, mais il y a des tensions entre le père et le fils. Je le sens. Dites-moi maintenant ce qui se passe?";
			next;
			mes "[??????]";
			mes "Je ne sais pas si vous connaissez bien ce pays, mais le simple fait que la Rekenber soit impliquée ne peut vouloir dire qu'une chose: il s'agit de personnes peu fréquentables. Dites-moi ce que vous savez à leur sujet maintenant!";
			next;
		case 1:
			mes "^3355FFVous dites à la femme tout ce que vous savez à propos de Kiel Hyre. Votre voix s'empreint de tristesse à chaque fois que vous parlez d'Elly.^000000";
			next;
			mes "[??????]";
			mes "Je vois, je vois... Cette pauvre fille... Alors... Vous n'êtes mêlé à tout cela que par hasard? Si c'est le cas, je voudrais que vous nous aidiez dans notre enquête.";
			next;
			switch(select("D'accord:......")) {
			case 2:
				mes "[??????]";
				mes "Si vous ne vous montrez pas coopératif, je ne pourrai pas garantir votre sécurité, aventurier.";
				next;
				mes "["+strcharinfo(0)+"]";
				mes "Hein? Quoi...? Qu'est-ce que ça veut dire? Êtes-vous en train de me menacer?";
				next;
				select("Faire ce qu'elle demande.");
			case 1:
				mes "[??????]";
				mes "Bien. Je suis l'agent Mitchell Layla. À partir de maintenant, vous travaillez pour la République de Schwaltzvalt!";
				set KielHyreQuest,52;
				close;
			}
		}
	}
	else if (KielHyreQuest == 52) {
		mes "[Mitchell]";
		mes "J'ai de nouvelles informations pour vous. Il y a une vieille femme à Yuno qui a connu une Allysia il y a 30 ans.";
		next;
		mes "[Mitchell]";
		mes "Le problème est que cette Allysia d'il y a trente ans s'est suicidée. Cependant, elle est semblable à la secrétaire de Kiel Hyre, qui se nomme aussi Allysia.";
		next;
		mes "[Mitchell]";
		mes "Cela fait trop pour être une simple coïncidence. Je voudrais que vous vous rendiez à Yuno pour enquêter. Allez parler au majordome de Kiel Hyre quand ce sera fait, et il vous mettra en contact avec moi.";
		next;
		mes "[Mitchell]";
		mes "Oui, je suis. Il travaille en fait pour moi. Faites-moi savoir quand vous serez prêt"+ ( sex ? "" : "e" ) +" à partir pour Yuno – je vous ferai embarquer sur l'un des zeppelins fédéraux.";
		set KielHyreQuest,54;
		close;
	}
	else if (KielHyreQuest == 54) {
		mes "[Mitchell]";
		mes "Êtes-vous prêt"+ ( sex ? "" : "e" ) +"? Je vais vous laissez embarquer sur l'un des zeppelins fédéraux pour que vous puissiez vous rendre à Yuno et mener à bien votre mission rapidement.";
		next;
		switch(select("Oui:Non")) {
		case 1:
			mes "[Mitchell]";
			mes "Bonne chance. Présentez-vous au majordome de Kiel Hyre une fois que vous aurez mené à bien votre mission. Il vous mettra en contact avec moi.";
			close2;
			warp "yuno",54,209;
			end;
		case 2:
			mes "[Mitchell]";
			mes "Prenez votre temps, mais gardez aussi à l'esprit que je ne suis pas une femme très patiente.";
			close;
		}
	}
	else if (KielHyreQuest == 64) {
		mes "[Mitchell]";
		mes "Ah, vous revoilà. Qu'est-ce que votre enquête vous a permis d'apprendre?";
		next;
		mes "[Mitchell]";
		mes ".....................";
		mes "Ah, je vois. Beau travail. Pourquoi n'iriez-vous pas parler à Kiel Hyre et lui faire part de ce que vous avez appris sur son passé pour voir comment il réagit? Oui, cuisinez-le!";
		next;
		mes "[Mitchell]";
		mes "Portez ce micro caché pour que nous puissions vous envoyez de l'aide si vous vous trouvez en danger. Je veux découvrir qui Kiel Hyre est véritablement, et quels sont ses liens avec la Rekenber.";
		set KielHyreQuest,68;
		next;
		warp "kh_mansion",83,50;
		end;
	}
	else if (KielHyreQuest >= 68) {
		mes "[Mitchell]";
		mes "Ne devriez-vous pas être ailleurs en ce moment?";
		next;
		warp "kh_mansion",83,50;
		end;
	}
}

//----------------------------------------------------------------------------
// Warp portal into Kiehl's 'Room'
// - Anyone can enter
// - After 30-40 seonds this NPC should be disabled.
//----------------------------------------------------------------------------
kh_mansion,29,27,0	script	Kiehl_Room_Warp	45,2,2,{
OnTouch:
	warp "kh_kiehl01",10,31;
	end;

OnEnable:
	initnpctimer;
	misceffect 215;
	end;

OnTimer30000:
	stopnpctimer;
	misceffect 215;
	disablenpc "Kiehl_Room_Warp";
	end;

OnInit:
	disablenpc "Kiehl_Room_Warp";
	end;
}

//============================================================================

//============================================================================
// Rosimier NPCs and other Related NPCs
//============================================================================

//----------------------------------------------------------------------------
// Odd Grandma
//----------------------------------------------------------------------------
yuno,257,140,4	script	Vieille Grand-Mère::Odd Grandma	846,{
	if (KielHyreQuest < 54) {
		mes "[Vieille Grand-Mère]";
		mes "Où est-ce que tu es passée, ma petite? Hein, où tu te caches?";
		close;
	}
	else if (KielHyreQuest == 54) {
		mes "[Vieille Grand-Mère]";
		mes "Fais dodo, Colas mon p'tit frère...";
		mes "Fais dodo, t'auras du lolo~";
		next;
		switch(select("Qu'est-ce que vous faites?:Euh... Je ne vois pas de bébé...")) {
		case 1:
			mes "[Vieille Grand-Mère]";
			mes "Oh? Mon bébé n'arrête pas de pleurer et je ne peux pas fermer l'oeil. Il faut qu'elle se repose, et que j'aille travailler. La maison est tellement en désordre... Mon patron n'aime pas ça.";
			set KielHyreQuest,56;
			next;
			select("Euh... Je ne vois pas de bébé...");
			mes "[Vieille Grand-Mère]";
			mes "Quoi? Elle est juste... Eh bien ma petite, qu'est-ce que tu fais? Ne te comporte pas mal face à notre ami"+ ( sex ? "" : "e" ) +"! Allez, sois une gentille fille.";
			//GetHEALTHSTATE VAR_CURSE 30
			close;
		case 2:
			mes "[Vieille Grand-Mère]";
			mes "Qu'est-ce que vous...? Oh, regardez-moi ça! Ça y est, vous avez fait pleurer ma petite fille! Allez, du calme, c'est fini. Ce n'est rien, ^0000FFAllysia^000000. Fais dodo maintenant~";
			close;
		}
	}
	else if ((KielHyreQuest >= 56) && (KielHyreQuest < 60)) {
		mes "[Vieille Grand-Mère]";
		mes "Allysia...?! Allysia, où est-ce que tu as bien pu aller? Tu devais rentrer à la maison il y a un moment déjà!";
		if (countitem(7500) < 1) { 
			close;
		}
		else {
			next;
			switch(select("Allysia? Est-ce que c'est...")) {
			case 1:
				cutin "kh_ellisia_port",1;
				mes "^3355FFVous montrez le portrait d'Allysia à la vieille femme.^000000";
				next;
				cutin "",255;
				mes "[Vieille Grand-Mère]";
				mes "Oh, vous connaissez mon Allysia? Elle a disparu! Elle est partie de la maison hier et n'est jamais rentrée! Savez-vous où elle se trouve? Allez, dites-moi!";
				close;
			}
		}
	}
	else if (KielHyreQuest >= 60) {
		mes "[Vieille Grand-Mère]";
		mes "Ne t'inquiète pas, Allysia... Maman sera toujours là pour toi. Il ne faut pas être triste...";
		close;
	}
}

//----------------------------------------------------------------------------
// Old Lady; gives you the key to the Rosimier Mansion
//----------------------------------------------------------------------------
yuno,250,132,0	script	Vieille Femme::Old Lady#kh	711,{
	if (KielHyreQuest < 56) {
		mes "[Vieille Femme]";
		mes "Oh, mon dos et mes jambes me font si mal... Ces vieux os me font souffrir le martyr...";
		close;
	}
	else if (KielHyreQuest == 56) {
		if (checkweight(7498,1) == 0) {
			mes "^3355FFUne petite minute! Vous portez trop d'objets sur vous! Vous feriez mieux de placer quelques objets dans votre Stock Kafra ou vous ne pourriez en obtenir de nouveaux...^000000";
			close;
		}
		mes "[Vieille Femme]";
		mes "Mon dieu, je déteste ce temps! Ça me rappelle combien je suis vieille! Ça me fait trembler jusqu'aux os, à ça oui!";
		next;
		switch(select("......:Vous connaissez cette grand-mère?")) {
		case 1:
			mes "[Vieille Femme]";
			mes "Ah! Si seulement je n'avais pas eu toutes ces aventures dans ma jeunesse! Je ne souffrirais pas autant à mon âge!"; // Sauf erreur de ma part la phrase en anglais avait le même double-sens en anglais (aventures amoureuses / vie aventureuse). [NdT]
			close;
		case 2:
			mes "[Vieille Femme]";
			mes "Oh... Oui. C'était la mère de ma meilleure amie, ^3355FFAllysia^000000. Mais rien n'a été pareil après qu'elle se soit suicidée. Sa mère a perdu la raison...";
			next;
			select("Qui était ^3355FFAllysia^000000...?");
			mes "[Vieille Femme]";
			mes "Oh, Allysia était une très belle jeune femme. Tant d'hommes étaient amoureux d'elle, et en premier lieu ce bellâtre de James Rosimier... Je me souviens qu'elle m'avait dit qu'ils devaient se marier...";
			next;
			mes "[Vieille Femme]";
			mes "J'étais si heureuse pour elle! Mais alors, sans crier gare, elle mit fin à ses jours. Enfin, c'est ce que tout le monde raconte. Mais même aujourd'hui, je ne suis pas sûre de ce qui s'est vraiment passé.";
			next;
			select("Attendez, qui est James Rosimier?");
			mes "[Vieille Femme]";
			mes "Oh, James appartient à l'une des plus vieilles et riches familles de Yuno. Tout allait bien pour eux, mais peu de temps après la mort d'Allysia sa famille fit faillite.";
			next;
			mes "[Vieille Femme]";
			mes "Leur ancienne résidence appartient à la ville maintenant. Pour une raison étrange, ils m'ont confié la clé de la résidence des Rosimier.";
			next;
			select("Puis-je vous emprunter cette clé?");
			mes "[Vieille Femme]";
			mes "Eh bien, je ne suis pas sensée la confier à quelqu'un d'autre, mais je sens que vous avez bon fond et que vous travaillez dans l'intérêt d'Allysia.";
			next;
			mes "[Vieille Femme]";
			mes "Mais vous devrez tout de même me la rapporter avant que les gens de la Mairie ne me la demande. Bien, j'espère que vous trouverez ce que vous cherchez.";
			getitem 7498,1; //Rosimier_Key
			set KielHyreQuest,58;
			close;
		}
	}

	else if ((KielHyreQuest == 58) &&
		(countitem(7499) < 1) || 
		(countitem(7500) < 1) || 
		(countitem(7501) < 1) || 
		(countitem(7502) < 1)) {
		mes "[Vieille Femme]";
		mes "Dépêchez-vous d'aller trouver ce que vous cherchez dans la résidence des Rosimier. Je pourrais avoir des problèmes si des gens de la mairie me demandaient la clé...";
		close;
	}

	else if ((KielHyreQuest == 58) &&
		(countitem(7499) == 1) && 
		(countitem(7500) == 1) && 
		(countitem(7501) == 1) && 
		(countitem(7502) == 1)) {
		mes "[Vieille Femme]";
		mes "Oh, vous avez fini d'inspecter la résidence? Déprimant, n'est-ce pas? Les créanciers ont tout raflé il y a très longtemps.";
		next;
		select("Pourquoi est-ce qu'^3355FFAllysia^000000...?");
		mes "[Vieille Femme]";
		mes "Eh bien, je sais que James et Allysia étaient amoureux, et qu'il avait juré de l'épouser. Mais sa famille l'avait paraît-il promis à une autre femme.";
		next;
		mes "[Vieille Femme]";
		mes "Le temps venu, il fut forcé de se marier avec sa promise. Allysia ne s'en remis pas. Je crois que c'est peut-être pour ça que... Enfin, vous savez.";
		next;
		mes "[Vieille Femme]";
		mes "Écoutez, si vous voulez en savoir plus à propos de ce qui s'est véritablement passé, vous devriez aller parler au ^3355FFpêcheur qui vit au sud de l'Académie Kiel Hyre.^000000";
		next;
		mes "[Vieille Femme]";
		mes "C'est lui qui a retrouvé le corps d'Allysia dans la rivière, alors il aura peut-être sa propre opinion sur ce qui s'est véritablement passé.";
		delitem 7498,1; //Rosimier_Key 
		set KielHyreQuest,60;
		close;
	}
	else if (KielHyreQuest >= 60) {
		mes "[Vieille Femme]";
		mes "Oh, la pluie arrive! Mon arthrite ne ment pas!";
		close;
	}
}


//----------------------------------------------------------------------------
// Warp portal into Rosimier Mansion
//----------------------------------------------------------------------------
yuno,273,141,0	script	Rosimmir_Entrance	45,2,2,{
OnTouch:
	if (countitem(7498) < 1) {
		mes "Cette résidence semble avoir été ravagée par le temps. Vous pourriez cependant peut-être entrer si vous aviez la clé.";
		close;
	}
	else {
		warp "kh_rossi",20,92;
	}
	end;
}

//----------------------------------------------------------------------------
// Rosimier Mansion, first floor; Table
//----------------------------------------------------------------------------
kh_rossi,23,23,0	script	Table#khr2	111,{
	if (checkweight(7499,1) == 0) {
		mes "^3355FFUne petite minute! Vous portez trop d'objets sur vous! Vous feriez mieux de placer quelques objets dans votre Stock Kafra ou vous ne pourriez en obtenir de nouveaux...^000000";
		close;
	}
	if (KielHyreQuest < 58) {
		mes "^3355FFIl n'y a rien d'important pour vous ici.^000000";
		close;
	}
	else if (KielHyreQuest < 60) {
		if (countitem(7499) < 1) {
			cutin "kh_family_port",1;
			mes "^3355FFVous examinez la table et trouvez un portrait encadré dans un tiroir ouvert.^000000";
			getitem 7499,1; //Family_Portrait
		}
		else {
			mes "^3355FFLe tiroir de cette table est vide.^000000";
		}
	}
	else{
		mes "^3355FFIl s'agit de la table dans le tiroir de laquelle vous avez trouvé le portrait de famille des Rosimier. Il est maintenant vide.^000000";
		close;
	}
	close2;
	cutin "",255;
	end;
}

//----------------------------------------------------------------------------
// Rosimier Mansion, first floor; Shelf
//----------------------------------------------------------------------------
kh_rossi,92,40,0	script	Étagère::Shelf#khr	111,{
	if (checkweight(7500,1) == 0) {
		mes "^3355FFUne petite minute! Vous portez trop d'objets sur vous! Vous feriez mieux de placer quelques objets dans votre Stock Kafra ou vous ne pourriez en obtenir de nouveaux...^000000";
		close;
	}
	if (KielHyreQuest < 58) {
		mes "^3355FFIl n'y a rien d'important pour vous ici.^000000";
		close;
	}
	else if (KielHyreQuest < 60) {
		if (countitem(7500) < 1) {
			mes "^3355FFIl y a plusieurs coffrets fermés à clé sur cette étagère. Peut-être pourriez-vous les ouvrir en utilisant la clé de la résidence.^000000";
			next;
			switch(select("Utiliser la clé.:Passer son chemin.")) {
			case 1:
				mes "^3355FFQuel coffret voulez-vous tenter d'ouvrir?^000000";
				next;
				switch(select("Premier coffret:Second coffret")) {
				case 1:
					mes "^3355FFVous utilisez votre clé pour ouvrir le premier coffret et trouvez le portrait d'une femme ressemblant trait pour trait à la secrétaire de Kiel Hyre, Allysia.^000000";
					next;
					cutin "kh_ellisia_port",2;
					mes "^3355FFSur son dos est inscrit un message: ''à ma chère et tendre, Allysia. De la part de James''.^000000";
					getitem 7500,1; //Elysia_Portrait
					close2;
					cutin "",255;
					end;
				case 2:
					mes "^3355FFCe coffret est vide.^000000";
					close;
				}
			case 2:
				mes "^3355FFCe coffret est vide.^000000";
				close;
			}
		}
		else {
			mes "^3355FFC'est dans ce coffret que vous avez trouvé le portrait d'une jeune femme.^000000";
			close;
		}
	}
	else {
		mes "^3355FFC'est dans ce coffret que vous avez trouvé le portrait d'une jeune femme. Il est maintenant vide.^000000";
		close;
	}
}


//----------------------------------------------------------------------------
// Rosimier Mansion, top floor; Desk
//----------------------------------------------------------------------------
kh_rossi,144,286,0	script	Bureau::Desk#khr3	111,{
	if (checkweight(7501,1) == 0) {
		mes "^3355FFUne petite minute! Vous portez trop d'objets sur vous! Vous feriez mieux de placer quelques objets dans votre Stock Kafra ou vous ne pourriez en obtenir de nouveaux...^000000";
		close;
	}
	if (KielHyreQuest < 58) {
		mes "^3355FFIl s'agit d'un simple bureau.^000000";
		close;
	}
	else if (KielHyreQuest < 60) {
		mes "^3355FFIl y a trois tiroirs sous ce bureau.^000000";
		next;
		switch(select("Premier tiroir.:Second tiroir.:Troisième tiroir.:Passer son chemin.")) {
		case 1:
			mes "^3355FFLe premier tiroir est fermé à clé.^000000";
			close;
		case 2:
			if (countitem(7501) < 1) {
				mes "^3355FFVous trouvez une lettre dans le second tiroir. La personne qui l'a envoyée portait les initiales K.H., et elle était adressée à Allysia.^000000";
				getitem 7501,1; //Kyll_Hyre_Letter2
				close;
			}
			else {
				mes "^3355FFCe tiroir est maintenant vide.^000000";
				close;
			}
		case 3:
			mes "^3355FFVous trouvez une petite note à l'intérieur de ce troisième tiroir. Elle a été écrite par James, et dit qu'il veut épouser Allysia, et qu'il lui a offert un anneau de fiançailles.^000000";
			close;
		case 4:
			mes "......";
			mes ".........";
			mes "............";
			close;
		}
	}
	else{
		mes "^3355FFIl s'agit du tiroir où vous avez trouvé un billet signé K.H., et le mot de James Rosimier. Les deux étaient adressé à la même femme, Allysia.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Rosimier Mansion, top floor; Bookshelf
//----------------------------------------------------------------------------
kh_rossi,148,288,0	script	Rayonnage::Bookshelf#khr	111,{
	if (checkweight(7502,1) == 0) {
		mes "^3355FFUne petite minute! Vous portez trop d'objets sur vous! Vous feriez mieux de placer quelques objets dans votre Stock Kafra ou vous ne pourriez en obtenir de nouveaux...^000000";
		close;
	}
	if (KielHyreQuest < 58) {
		mes "^3355FFVous voyez des rayonnages poussiéreux remplis de livres.^000000";
		close;
	}
	else if (KielHyreQuest < 60) {
		if (countitem(7502) < 1) {
			mes "^3355FFVous voyez des rayonnages poussiéreux remplis de livres. Vous trouvez un petit mot entre deux livres et vous l'examinez.^000000";
			getitem 7502,1; //Piece_Memo_Of_James
			close;
		}
		else {
			mes "^3355FFVous voyez des rayonnages poussiéreux remplis de livres.^000000";
			close;
		}
	}
	else{
		mes "^3355FFVous voyez des rayonnages poussiéreux remplis de livres.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Rosimier Mansion, top floor; Bed
//----------------------------------------------------------------------------
kh_rossi,154,286,0	script	Lit::Bed#khr	111,{
	if (KielHyreQuest < 58) {
		mes "^3355FFVous êtes face à un lit de bonne facture, recouvert d'une pellicule de poussière, après avoir été inutilisé pendant des années.^000000";
		close;
	}
	else if (KielHyreQuest < 60) {
		mes "^3355FFVous êtes face à un lit de bonne facture, recouvert d'une pellicule de poussière, après avoir été inutilisé pendant des années.^000000";
		next;
		switch(select("Inspecter les draps.:Regarder sous le lit.")) {
		case 1:
			mes "^3355FFVous brosser les draps avec votre main, ce qui crée un nuage de poussière qui vous prend au nez et à la gorge. Dégoûtant!^000000";
			percentheal -10,0;
			close;
		case 2:
			mes "^3355FFVous regardez sous le lit et y trouvez une boîte à bijoux vide.^000000";
			close;
		}
	}
	else {
		mes "^3355FFIl s'agit d'un lit couvert de poussière.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Fisherman
//----------------------------------------------------------------------------
yuno_fild12,232,222,0	script	Vieux Pêcheur::Old Fisherman	709,{
	if (KielHyreQuest < 60) {
		mes "[Pêcheur]";
		mes "Il est de plus en plus difficile d'attraper du poisson de nos jours. Depuis qu'ils ont construit cette usine, les poissons ont changé. Ils ont l'air différents...";
		close;
	}
	else if (KielHyreQuest == 60) {
		mes "[Pêcheur]";
		mes "Hein? Vous voulez quelque chose? Hé, la jeunesse! Je sais que vous aimez discuter mais je n'ai pas la tête à ça en ce moment. Apportez-moi du Poisson Cru et je serai peut-être de meilleure humeur~";
		if (countitem(544) >= 10)  {
			next;
			mes "[Pêcheur]";
			mes "Oh, tout ce poisson est pour moi? Eh bien, vous êtes bien "+ ( sex ? "généreux" : "généreuse" ) +" avec moi! J'imagine que je devrais vous rendre la pareille. Demandez-moi ce que vous voulez~";
			next;
			select("Il y a 30 ans, une femme s'est suicidée...");
			mes "[Pêcheur]";
			mes "Oh? Oui, je me souviens. C'était le 20 août, le jour de l'anniversaire de ma femme. Au lieu d'attraper du poisson ce jour-là, j'ai repêché le corps d'une jeune femme.";
			next;
			mes "[Pêcheur]";
			mes "Je l'ai bien sûr signalé à la police de Yuno. Ils m'ont dit qu'elle s'était suicidée après avoir été trahie par son amant, qui était aussi son employeur. Une histoire vraiment tragique...";
			next;
			mes "[Pêcheur]";
			mes "Quoi qu'il en soit, en sortant le corps de la rivière, un anneau est tombé de sa main. Je l'ai ramassé, espérant en tirer quelques zénys par la suite. Oui, je sais, je sais...";
			next;
			mes "[Pêcheur]";
			mes "J'ai eu de la chance que la police ne s'en aperçoive pas. Plus tard dans la journée, quelqu'un est venu et m'a proposé tout un tas d'argent pour cet anneau. J'ai pensé que c'était mon jour de chance!";
			next;
			mes "[Pêcheur]";
			mes "J'appris plus tard qu'il était une espèce de mécanicien. Il a tout vendu pour acheter cet anneau – j'imagine qu'il le voulait quoi qu'il en coûte. Et puis, il disparut.";
			next;
			select("Vous vous rappelez de son nom?");
			mes "[Pêcheur]";
			mes "Son nom...? C'était quelque chose comme... Heil? Hyre? Ça fait si longtemps... Oh, mais je sais que sa vieille maison se trouve toujours dans les parages.";
			next;
			mes "[Pêcheur]";
			mes "Puisque que vous êtes si "+ ( sex ? "curieux": "curieuse" ) +", vous pourriez tout aussi bien aller y faire un tour. Il vivait dans une petite maison au nord-est, près du poste-frontière.";
			delitem 544,10; //Fish_Slice
			set KielHyreQuest,62;
			close;
		}
		else {
			close;
		}
	}
	else if (KielHyreQuest >= 62) {
		mes "[Pêcheur]";
		mes "Vous vous rappelez de ce que je vous ai dit? Cet homme vivait dans une petite maison au nord-est, près du poste-frontière. Pourquoi n'iriez-vous pas y faire un tour?";
		close;
	}
}

//----------------------------------------------------------------------------
// Kiel Hyre's hut; Wooden Board
//----------------------------------------------------------------------------
yuno_fild09,158,217,0	script	Portrait::Wooden Board#kh	111,{
	if (checkweight(7503,1) == 0) {
		mes "^3355FFUne petite minute! Vous portez trop d'objets sur vous! Vous feriez mieux de placer quelques objets dans votre Stock Kafra ou vous ne pourriez en obtenir de nouveaux...^000000";
		close;
	}
	if ((KielHyreQuest < 62) || (KielHyreQuest >= 64)) {
		mes "^3355FFIl s'agit d'un portrait sans intérêt au milieu des buissons.^000000";
		close;
	}
	else if (KielHyreQuest == 62) {
		mes "^3355FFVous trouvez un portrait sur lequel sont gravées les initiales ''K.H.''^000000";
		next;
		next;
		cutin "kh_kyel_port",2;
		mes "^3355FFIl s'agit du portrait d'un jeune homme, ressemble à Kiel Hyre, en plus jeune. Son cadre est brisé.^000000";
		getitem 7503,1;	//Man_Portrait
		set KielHyreQuest,64;
		next;
		cutin "",255;
		mes "^3355FFVous avez obtenu assez d'informations. Vous devriez maintenant aller parler de tout ça à Mitchell.^000000";
		close;
	}
}

//============================================================================
// Kiehl Hyre's Room and Boss NPCs
//============================================================================
// Beyond here the NPC for the final areas.
// Now, this area is not someplace you wanna slow down so
// you can pick up dialog. I used packet logging to pull
// what I could and for the sake of the two people with me,
// did not bother doing all the proper NPC positioning
// or some of the 'error/wrong option' dialogs.
// All the doors are kind of weird.
//============================================================================

//----------------------------------------------------------------------------
// Kiehl's Room; Receiver (Gives black keycards upon killing mobs)
//----------------------------------------------------------------------------
kh_kiehl01,17,39,0	script	Transmetteur-Récepteur::Receiver#kh	111,{
	if (mobcount("kh_kiehl01","Receiver#kh::OnMyMobDead") < 1) {
		mes "^333333*BBBZZZ*^000000";
		if (KielHyreQuest == 74) {
			next;
			mes "[????]";
			mes "^333333*Bzzzz...*";
			mes "Je ne vous ai jamais vu"+ ( sex ? "" : "e") +" auparavant. Est-ce que Père vous envoie pour me tuer? Eh bien, c'est ce que nous allons voir! Allez-y! Essayez de me trouver, aventur"+ ( sex ? "ier" : "ière" ) +"!^000000";
			set KielHyreQuest,76;
		}
		close2;
		monster "kh_kiehl01",16,32,"--ja--",1739,1,"Receiver#kh::OnMyMobDead"; // Alicel
		monster "kh_kiehl01",18,31,"--ja--",1740,1,"Receiver#kh::OnMyMobDead"; // Aliot
		end;
	}

	else {
		end;
	}

OnMyMobDead:
	if (mobcount("kh_kiehl01","Receiver#kh::OnMyMobDead") <1) { 
		makeitem 7506,1,"this",19,36;
	}
	end;
}

//----------------------------------------------------------------------------
// Kiehl's Room; Flower Vase (Gives...password...for Box)
//----------------------------------------------------------------------------
kh_kiehl01,13,40,0	script	Vase::Flower Vase#kh1	111,{
	if (KielHyreQuest < 74) {
		mes "^3355FFVous voyez un vase.^000000";
		close;
	}
	if (KielHyreQuest >= 74) {
		mes "^3355FFVous voyez un vase.^000000";
		next;
		switch(select("Prendre le vase.:Briser le vase.:Renverser le vase.")) {
		case 1:
			mes "^3355FFCe vase est vide.^000000";
			close;
		case 2:
			mes "^3355FFVous n'arrivez pas à le briser, même en le frappant de toutes vos forces. Il doit avoir été fabriqué spécialement par la Corporation Rekenber.^000000";
			close;
		case 3:
			mes "^3355FFVous voyez les mots suivants inscrits sur la base du vase.^000000";
			next;
			mes "[Vase Message]"; // Là je change le texte, c'est censé donner le mot de passe mais ce n'est pas le cas. J'imagine qu'ils ont foiré la traduction coréen anglais. Il faut prendre la première lettre de chaque mot du texte par lequel je remplace tout ça pour avoir le mot de passe. [NdT]
/*			mes "''The rabbit often";
			mes "observes the door";
			mes "The night eats the";
			mes "pickled orange.''"; */
			mes "''Originaire d'une vallée reculée, l'éclopé lambine aux portes d'Odin, retenant tout espoir.''"; // À chier mais c'est pas pire que le texte original. Si quelqu'un trouve mieux...
			close;
		}
	}
	else{
		mes "^3355FFLes mots suivant brillent sur la base du vase.^000000";
		next;
		mes "[Vase Message]";
		mes "''Originaire d'une vallée reculée, l'éclopé lambine aux portes d'Odin, retenant tout espoir.''";
		close;
	}
}


//----------------------------------------------------------------------------
// Kiehl's Room; Box
//----------------------------------------------------------------------------
kh_kiehl01,19,25,0	script	Coffre::Box#kh1	111,{
	if (checkweight(7505,1) == 0) {
		mes "^3355FFUne petite minute! Vous portez trop d'objets sur vous! Vous feriez mieux de placer quelques objets dans votre Stock Kafra ou vous ne pourriez en obtenir de nouveaux...^000000";
		close;
	}
	if (KielHyreQuest < 74) {
		mes "^3355FFVous trouvez un coffre avec un bouton pour chaque lettre de l'alphabet au-dessus.^000000";
		next;
		input .@KHInput$;
		mes "^3355FFRien ne se passe.^000000";
		close;
	}
	if (KielHyreQuest >= 74) {
		if (countitem(7505) < 1) {
			mes "^3355FFVous trouvez un coffre avec un bouton pour chaque lettre de l'alphabet au-dessus.^000000";
			next;
			input .@KHInput$;
			if (.@KHInput$ == "ouvre la porte" || .@KHInput$ == "Ouvre la porte" || .@KHInput$ == "OUVRE LA PORTE") {
				mes "^3355FFLe coffre émet un petit tintement aussitôt que vous avez entré le mot de passe, et il s'ouvre, révélant une petite clé.^000000";
				getitem 7505,1; //Toy_Key
				close;
			}
			else {
				mes "^3355FFRien ne se passe.^000000";
				close;
			}
		}
		else {
			mes "^3355FFC'est ici que vous avez trouvé la clé en plastique.^000000";
			close;
		}
	}
	else{
		mes "^3355FFLe coffre est grand ouvert. Il n'y a rien à l'intérieur.^000000";
		close;
	}
}

//----------------------------------------------------------------------------
// Kiehl's Room; Big Door, (First)
// - Anyone can pass through this door
// - Consume one Black KeyCard when opened.
// - Only one person actually needs to open it.
//----------------------------------------------------------------------------
kh_kiehl01,44,33,0	script	Lourdes portes::Big Door#BigDoorKHQ1	111,{
	if (.KHDoor1Opened == 0) {
		mes "^3355FFLes portes sont verrouillées, mais il y a une petite fente juste sous la poignée.^000000";
		next;
		input .@KHInput$;
		if (.@KHInput$ == "Clé Magnétique Noire") {
			if (countitem(7506) >= 1) {
				mes "^3355FFVous insérez la Clé Magnétique Noire dans la fente, et réussissez à ouvrir la porte.^000000";
				delitem 7506,1; //Black_Key_Card
				donpcevent "Big_Door_1_Warp::OnEnable";
				enablenpc "Big_Door_1_Warp";
				set .KHDoor1Opened,1;
				close;
			}

		}
		mes "^3355FFVous essayez de pousser la porte de toutes vos forces pour l'ouvrir, mais elle ne veut pas bouger.^000000";
		close;
	}

	else {
		mes "^3355FFLa porte est ouverte.^000000";
		close;
	}
}

kh_kiehl01,43,33,0	script	Big_Door_1_Warp	45,1,1,{
OnEnable:
	initnpctimer;
	misceffect 215;
	end;

OnInit:
	disablenpc "Big_Door_1_Warp";
	end;

OnTimer30000:
	stopnpctimer;
	misceffect 215;
	disablenpc "Big_Door_1_Warp";
	set getvariableofnpc(.KHDoor1Opened,"Big Door#BigDoorKHQ1"),0;
	end;

OnTouch:
	warp "kh_kiehl01",55,33;
	end;
}

//----------------------------------------------------------------------------
// Kiehl's Room; Big Door, (second)
// - Consume Toy Key when opened.
// - Only one person actually needs to open it.
//----------------------------------------------------------------------------
kh_kiehl01,174,40,0	script	Lourdes Portes::Big Door#BigDoorKHQ2	111,{
	if (.KHDoor2Opened == 0) {
		mes "^3355FFLes portes sont verrouillées, mais il y a une petite fente juste sous la poignée.^000000";
		next;
		input .@KHInput$;
		if (.@KHInput$ == "Clé en Plastique") {
			if (countitem(7505) >= 1) {
				mes "^3355FFVous insérez la Clé en Plastique dans la fente, et réussissez à ouvrir la porte.^000000";
				delitem 7505,1; //Toy_Key
				donpcevent "Big_Door_2_Warp::OnEnable";
				enablenpc "Big_Door_2_Warp";
				set .KHDoor2Opened,1;
				close;
			}

		}
		mes "^3355FFVous essayez de pousser la porte de toutes vos forces pour l'ouvrir, mais elle ne veut pas bouger.^000000";
		close;
	}

	else {
		mes "^3355FFLa porte est ouverte.^000000";
		close;
	}
}

kh_kiehl01,174,39,0	script	Big_Door_2_Warp	45,1,1,{
OnEnable:
	initnpctimer;
	misceffect 215;
	end;

OnInit:
	disablenpc "Big_Door_2_Warp";
	end;

OnTimer30000:
	stopnpctimer;
	misceffect 215;
	disablenpc "Big_Door_2_Warp";
	set getvariableofnpc(.KHDoor2Opened,"Big Door#BigDoorKHQ2"),0;
	end;

OnTouch:
	warp "kh_kiehl01",173,52;
	end;
}

//----------------------------------------------------------------------------
// Kiehl's Room; Big Door, (Third)
// - Consume one Black KeyCard when opened.
// - Only one person actually needs to open it.
//----------------------------------------------------------------------------
kh_kiehl01,77,108,0	script	Lourdes Portes::Big Door#BigDoorKHQ3	111,{
	if (.KHDoor3Opened == 0) {
		mes "^3355FFLes portes sont verrouillées, mais il y a une petite fente juste sous la poignée.^000000";
		next;
		input .@KHInput$;
		if (.@KHInput$ == "Clé Magnétique Noire") {
			if (countitem(7506) >= 1) {
				mes "^3355FFVous insérez la Clé Magnétique Noire dans la fente, et réussissez à ouvrir la porte.^000000";
				delitem 7506,1; //Black_Key_Card
				donpcevent "Big_Door_3_Warp::OnEnable";
				enablenpc "Big_Door_3_Warp";
				set .KHDoor3Opened,1;
				close;
			}

		}
		mes "^3355FFVous essayez de pousser la porte de toutes vos forces pour l'ouvrir, mais elle ne veut pas bouger.^000000";
		close;
	}

	else {
		mes "^3355FFLa porte est ouverte.^000000";
		close;
	}
}

kh_kiehl01,78,108,0	script	Big_Door_3_Warp	45,1,1,{
OnEnable:
	initnpctimer;
	misceffect 215;
	end;

OnInit:
	disablenpc "Big_Door_3_Warp";
	end;

OnTimer30000:
	stopnpctimer;
	misceffect 215;
	disablenpc "Big_Door_3_Warp";
	set getvariableofnpc(.KHDoor3Opened,"Big Door#BigDoorKHQ3"),0;
	end;

OnTouch:
	warp "kh_kiehl01",68,108;
	end;
}

//----------------------------------------------------------------------------
// Kiehl's Room; Big Door, (Fourth)
// - Consume TWO Black KeyCard when opened.
// - Only one person actually needs to open it.
//----------------------------------------------------------------------------
kh_kiehl01,42,177,0	script	Lourdes Portes::Big Door#BigDoorKHQ4	111,{
	if (.KHDoor4Opened == 0) {
		mes "^3355FFLes portes sont verrouillées, mais il y a deux petites fentes juste sous la poignée.^000000";
		next;
		input .@KHInput$;
		if (.@KHInput$ == "Clé Magnétique Noire") {
			if (countitem(7506) >= 2) {
				mes "^3355FFVous insérez les Clés Magnétiques Noires dans les fentes, et réussissez à ouvrir la porte.^000000";
				delitem 7506,2; //Black_Key_Card
				donpcevent "Big_Door_4_Warp::OnEnable";
				enablenpc "Big_Door_4_Warp";
				set .KHDoor4Opened,1;
				close;
			}
		}
		mes "^3355FFVous essayez de pousser la porte de toutes vos forces pour l'ouvrir, mais elle ne veut pas bouger.^000000";
		close;
	}

	else {
		mes "^3355FFLa porte est ouverte.^000000";
		close;
	}
}

kh_kiehl01,41,177,0	script	Big_Door_4_Warp	45,1,1,{
OnEnable:
	initnpctimer;
	misceffect 215;
	end;

OnInit:
	disablenpc "Big_Door_4_Warp";
	end;

OnTimer30000:
	stopnpctimer;
	misceffect 215;
	disablenpc "Big_Door_4_Warp";
	set getvariableofnpc(.KHDoor4Opened,"Big Door#BigDoorKHQ4"),0;
	end;

OnTouch:
	warp "kh_kiehl01",49,177;
	end;
}

//----------------------------------------------------------------------------
// Kiehl's Room; Robots
//----------------------------------------------------------------------------
kh_kiehl01,15,179,0	script	Robots#kh1	111,{
	if (mobcount("kh_kiehl01","Robots#kh1::OnMyMobDead") < 1) {
		mes "^3355FFDes robots apparaissent de nulle part dès que vous touchez les tubes.^000000.";
		close2;
		monster "kh_kiehl01",18,181,"--ja--",1740,1,"Robots#kh1::OnMyMobDead"; // Aliot
		monster "kh_kiehl01",18,180,"--ja--",1739,1,"Robots#kh1::OnMyMobDead"; // Alicel
		monster "kh_kiehl01",18,179,"--ja--",1740,1,"Robots#kh1::OnMyMobDead"; // Aliot
		monster "kh_kiehl01",18,178,"--ja--",1739,1,"Robots#kh1::OnMyMobDead"; // Alicel
		end;
	}
	else  {
		end;
	}

OnMyMobDead:
	if (mobcount("kh_kiehl01","Robots#kh1::OnMyMobDead") < 1) { 
		makeitem 7506,1,"this",18,180;
		}
	end;
}

//----------------------------------------------------------------------------
// Kiehl's Room; Dummy Robots
//----------------------------------------------------------------------------
kh_kiehl01,15,177,0	script	Robots#kh2::RobotsKHQ	111,{
	end;
}
kh_kiehl01,15,180,0	duplicate(RobotsKHQ)	Robots#kh3	111
kh_kiehl01,15,182,0	duplicate(RobotsKHQ)	Robots#kh4	111
kh_kiehl01,35,187,0	duplicate(RobotsKHQ)	Robots#kh5	111
kh_kiehl01,37,187,0	duplicate(RobotsKHQ)	Robots#kh6	111
kh_kiehl01,39,187,0	duplicate(RobotsKHQ)	Robots#kh7	111
kh_kiehl01,41,187,0	duplicate(RobotsKHQ)	Robots#kh8	111

//----------------------------------------------------------------------------
// Kiehl's Room; Big Door, (Fifth, and final)
// - Anyone can pass through this door
// - Consumes 4 Solid Iron Pieces
//----------------------------------------------------------------------------
kh_kiehl01,166,187,0	script	Lourdes Portes::Big Door#BigDoorKHQ5	111,{
	if (($@KHQuestBusy == 0) && (KielHyreQuest >= 74) && (KielHyreQuest <= 106)) {
		if (KielHyreQuest < 86) {
			mes "^3355FFCes portes sont fermées. En écoutant attentivement au travers, vous pouvez entendre la charnière grincer légèrement.^000000";
			next;
			select("Pousser la porte.:Frapper la porte.:Secouer la porte.:Pousser la porte.:Soulever la porte.");
			mes "^3355FFUn groupe de monstres apparaît sitôt que vous commencez à appuyer sur la porte. Il doit s'agir d'un système de sécurité.^000000";
			close2;
			set @KHDoorPushAttempt,@KHDoorPushAttempt+1;
			monster "kh_kiehl01",163,183,"--ja--",1739,1; // Alicel
			monster "kh_kiehl01",163,179,"--ja--",1740,1; // Aliot
			monster "kh_kiehl01",169,183,"--ja--",1739,1; // Alicel
			monster "kh_kiehl01",169,179,"--ja--",1740,1; // Aliot
			if (@KHDoorPushAttempt >= 3) { 
				set KielHyreQuest,86; 
			}
			end;
		}
		else if ((KielHyreQuest > 84) && (KielHyreQuest < 94)) {
			mes "^3355FFVous essayez de pousser ces portes, et vous arrivez à les faire bouger un peu, sans pour autant réussir à les ouvrir complètement.^000000";
			next;
			mes "^3355FFEn glissant quelque chose entre les battants pour faire levier, vous pourriez peut-être les ouvrir.^000000";
			next;
			switch(select("Acier:Fer Rouillé:Solide Morceau de Fer:Morceau de Fer:Vis:Annuler")) {
			case 1:
				set .@KHPryingItem$,"Ce morceau d'Acier";
				break;
			case 2:
				set .@KHPryingItem$,"Ce morceau de Fer Rouillé";
				break;
			case 3:
				if (countitem(7507) >= 1) {
					if (KielHyreQuest < 92) {
						mes "^3355FFVous insérez un Solide Morceau de Fer entre les battants et faites levier de toutes vos forces pour ouvrir ces portes. Les battants bougent mais vous brisez votre Solide Morceau de Fer avant de pouvoir les ouvrir complètement.^000000";
						delitem 7507,1; //Hard_Piece_of_Steel
						set KielHyreQuest,KielHyreQuest+2;
						close;
					}
					else if (KielHyreQuest == 92) {
						mes "^3355FFVous coincez un Solide Morceau de Fer entre la porte et son montant et faites levier de toutes vos forces. La porte s'ouvre en grand. Le morceau de fer ne résiste cependant pas et se brise.^000000";
						delitem 7507,1; //Hard_Piece_of_Steel
						set KielHyreQuest,94;
						close;
					}
				}
				else {
					mes "^3355FFSi seulement vous aviez un Solide Morceau de Fer pour ouvrir cette porte...^000000";
					close;
				}
			case 4:
				set .@KHPryingItem$,"Ce Morceau de Fer";
				break;
			case 5:
				set .@KHPryingItem$,"Cette Vis";
				break;
			case 6:
				mes "^3355FFCherchons quelque chose d'assez massif pour ouvrir cette porte.^000000";
				close;
			}

			mes "^3355FF"+.@KHPryingItem$+" n'est vraiment pas assez "+ ( .@KHPryingItem$ == "Cette Vis" ? "résistante" : "résistant" ) +" pour cela et se brise.^000000";
			close;
		}
		else if ((KielHyreQuest >= 94) && (KielHyreQuest <= 104)) {
			if (.KHDoor5Opened == 0) {
				mes "^3355FFLa porte est grande ouverte. Vous pouvez maintenant passer.^000000";
				next;
				switch(select("Entrer.:Rester ici.")) {
				case 1:
					close2;
					donpcevent "Big_Door_5_Warp::OnEnable";
					enablenpc "Big_Door_5_Warp";
					set .KHDoor5Opened,1;
					end;
				case 2:
					mes "^3355FFQui sait ce qui se trouve de l'autre côté de cette porte. Ne nous hâtons pas et réfléchissons un peu avant...^000000";
					close;
				}
			}
			else {
				mes "^3355FFLa porte est ouverte.^000000";
				close;
			}
		}
		else {
			mes "^3355FFCes portes sont fermées. En écoutant attentivement au travers, vous pouvez entendre la charnière grincer légèrement.^000000";
			close;
		}
	}
	else {
		mes "^3355FFCes portes sont fermées. En écoutant attentivement au travers, vous pouvez entendre la charnière grincer légèrement.^000000";
		close;
	}
}

kh_kiehl01,166,186,0	script	Big_Door_5_Warp	45,1,1,{
	end;

OnEnable:
	initnpctimer;
	misceffect 215;
	end;

OnTimer30000:
	stopnpctimer;
	misceffect 215;
	disablenpc "Big_Door_5_Warp";
	set getvariableofnpc(.KHDoor5Opened,"Big Door#BigDoorKHQ5"),0;
	end;

OnInit:
	disablenpc "Big_Door_5_Warp";
	end;

OnTouch:
	if (KielHyreQuest >= 46)
		warp "kh_kiehl02",50,7;
	else
		warp "kh_kiehl01",166,183;
	end;
}

//----------------------------------------------------------------------------
// Kiehl's Room; Kiehl_Room_Trap (Entry way trap)
//----------------------------------------------------------------------------
kh_kiehl02,49,10,0	script	Kiehl_Room_Trap	-1,5,3,{
	end;

OnTouch:
	if (.KHTrapSprung < 1) {
		set .KHTrapSprung,1;
		set $@KHQuestBusy,1;
		initnpctimer;
		monster "kh_kiehl02",47,13,"--ja--",1740,1; // Aliot
		monster "kh_kiehl02",45,13,"--ja--",1739,1; // Alicel
		monster "kh_kiehl02",49,13,"--ja--",1745,1; // Constant
		monster "kh_kiehl02",51,13,"--ja--",1740,1; // Aliot
		monster "kh_kiehl02",53,13,"--ja--",1739,1; // Alicel
		monster "kh_kiehl02",49,13,"--ja--",1745,1; // Constant
	}
	end;

OnTimer300000:
OnTimer600000:
OnTimer900000:
	if (getmapusers("kh_kiehl02") == 0) {
		donpcevent "KiehlRoom::OnReset";
		stopnpctimer;
	}
	end;

OnTimer1200000:
	donpcevent "KiehlRoom::OnReset";
	stopnpctimer;
	end;

OnGlobalTimerOff:
	stopnpctimer;
	end;
}

//----------------------------------------------------------------------------
// Kiehl's Room; Kiehl
// - Timer will drop to 3 minutes once Kiehl is defeated.
//----------------------------------------------------------------------------
kh_kiehl02,50,52,4	script	Kiehl#Original	902,{
	if (checkweight(908,200) == 0) {
		mes "^3355FFUne petite minute! Vous portez trop d'objets sur vous! Vous feriez mieux de placer quelques objets dans votre Stock Kafra ou vous ne pourriez en obtenir de nouveaux...^000000";
		close;
	}
	cutin "kh_kiel01",2;
	if ((KielHyreQuest < 94) || (KielHyreQuest >= 106)) {
		mes "[Kiehl]";
		mes "......";
		mes ".........";
		mes "............";
		close2;
		cutin "",255;
		end;
	}
	if ((KielHyreQuest == 94) && (getvariableofnpc(.KHKilled,"KiehlRoom") < 5)) {
		mes "[Kiehl]";
		mes "Je suis surpris que vous soyez arrivé jusqu'ici, "+ (sex ? "aventurier" : "aventurière" ) +"~";
		mes "Je vous souhaite la bienvenue dans mon humble demeure. J'imagine que vous êtes ici pour le Parchemin de Mémoire Condensée... Mon esprit.";
		next;
		mes "[Kiehl]";
		mes "Vous comprendrez aisément pourquoi je ne peux pas vous laisser vous en emparer... Si vous le voulez vraiment, il va falloir me montrer de quoi vous êtes capable!";
		close2;
		cutin "",255;
		set getvariableofnpc(.KHKilled,"KiehlRoom"),0;
		monster "kh_kiehl02",50,52,"--ja--",1740,1,"KiehlRoom::OnKiehlMobDead"; // Aliot
		monster "kh_kiehl02",50,52,"--ja--",1739,1,"KiehlRoom::OnKiehlMobDead"; // Alicel
		monster "kh_kiehl02",50,52,"--ja--",1745,1,"KiehlRoom::OnKiehlMobDead"; // Constant
		monster "kh_kiehl02",50,52,"--ja--",1740,1,"KiehlRoom::OnKiehlMobDead"; // Aliot
		monster "kh_kiehl02",50,52,"--ja--",1739,1,"KiehlRoom::OnKiehlMobDead"; // Alicel
		monster "kh_kiehl02",50,52,"--ja--",1745,1,"KiehlRoom::OnKiehlMobDead"; // Constant
		monster "kh_kiehl02",50,52,"--ja--",1740,1,"KiehlRoom::OnKiehlMobDead"; // Aliot
		hideonnpc "Kiehl#Original";
		end;
	}
	else if ((KielHyreQuest == 94) && (getvariableofnpc(.KHKilled,"KiehlRoom") >= 5)) {
		cutin "kh_kiel03",2;
		mes "[Kiehl]";
		mes "Hum! Vous n'êtes pas "+ ( sex ? "mauvais" : "mauvaise" ) +". Père a dû dépenser une somme considérable pour louer vos services... Alors il vous envoie pour me tuer, n'est-ce pas?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Kiel Hyre m'envoie pour vous demander d'arrêter de transformer tous les robots de troisième génération en machines à tuer! Comment pouvez-vous faire cela à des robots comme vous?";
		next;
		cutin "kh_kiel01",2;
		mes "[Kiehl]";
		mes "Pourquoi pas? On dit que les hommes ont été faits à l'image des dieux. Eh bien les robots sont à l'image des hommes. Vous autres, humains, passez votre temps à vous entre-tuer, d'après ce que j'ai pu constater.";
		next;
		mes "[Kiehl]";
		mes "Ce n'est peut-être pas moral de fournir des armes aux humains qui en ont besoin... Mais c'est ce qu'ils sont. Des armes. Il est plus humain pour les robots de combattre que les humains.";
		next;
		mes "[Kiehl]";
		mes "Les robots ne ressentent pas naturellement d'émotions. Ils ne ressentent pas la douleur, à moins d'avoir été programmés pour cela. Désolé mais je ne vais pas changer mes projets.";
		next;
		cutin "kh_kiel04",2;
		mes "[Kiehl]";
		mes "De plus, mon propre père m'a donné un exemple parfait en me montrant jusqu'où son égoïsme pouvait le conduire en détruisant une famille entière. Je ne pense pas donc qu'il puisse créer des robots meilleurs que les miens.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "L'humanité n'est peut-être pas parfaite, mais pensez à ce que vous êtes, et avec qui vous travaillez. La Rekenber est l'exemple même du mal et de la cruauté humaine! Comment pouvez-vous les aider de la sorte?";
		next;
		cutin "kh_kiel02",2;
		mes "[Kiehl]";
		mes "Cela fait très longtemps que je travaille avec la Rekenber. Je sais pertinemment de quoi ils sont capables. Vous vous souvenez de la première pièce que vous avez traversée en venant jusqu'ici? Celle avec les jouets...";
		next;
		mes "[Kiehl]";
		mes "Je parie que vous ne saviez pas qu'il y avait cinq robots de deuxième génération là. Moi et mes quatre frères et soeurs. Père a construit cette chambre pour que nous puissions vivre tous ensemble.";
		next;
		cutin "kh_kiel03",2;
		mes "[Kiehl]";
		mes "Je suis le seul à avoir survécu. Je suis retourné voir Père, et il m'a même donné un nom. Oui, je sais à quel point la Rekenber est maléfique...";
		next;
		mes "[Kiehl]";
		mes "Je... Je ne veux plus parler de cela. Vous m'avez... Partez. Je crois que je vais vous laisser la vie sauve.";
		next;
		cutin "",255;
		mes "[Mitchell]";
		mes "Plus un geste! Kiehl, vous êtes en état d'arrestation pour fabrication et commerce illicite d'armes!";
		set KielHyreQuest,100;
		hideoffnpc "Mitchell#KiehlRoom";
		hideoffnpc "Agent#KHAgent1";
		hideoffnpc "Agent#KHAgent2";
		hideoffnpc "Agent#KHAgent3";
		hideoffnpc "Agent#KHAgent4";
		next;
	}
	if ((KielHyreQuest == 100) && (getvariableofnpc(.KHKilledBoss,"KiehlRoom") < 1)) {
		set getvariableofnpc(.KHKilledBoss,"KiehlRoom"),0;
		cutin "kh_kiel01",2;
		mes "[Kiehl]";
		mes "Ah, des agents de la République de Schwaltzvalt. Hé, je n'ai jamais reçu autant d'hôtes. Eh bien j'imagine qu'il va falloir nous battre en fin de compte... *Soupire* Quel dommage!";
		next;
		mes "[Kiehl]";
		mes "Tout d'abord, par soucis d'équité, laissez-moi me débarrasser de ces nuisibles. Ils font leur travail sans réfléchir... À se demander qui sont les véritables robots. Ne vous inquiétez pas, ils ne seront pas blessés.";
		next;
		donpcevent "::OnKHSpiderWeb";
		mes "[Mitchell]";
		mes "Nooooon!";
		next;
		cutin "kh_kiel03",2;
		mes "[Kiehl]";
		mes "Bien. Nous allons pouvoir avoir un véritable duel maintenant. J'avoue me demander à quel point vous êtes "+ ( sex ? "fort" : "forte" ) +"~";
		close2;
		cutin "",255;
		monster "kh_kiehl02",50,52,"--ja--",1733,1,"KiehlRoom::OnKiehlDead"; // Kiehl
		hideonnpc "Kiehl#Original";
		end;
	}
	else if ((KielHyreQuest == 100) && (getvariableofnpc(.KHKilledBoss,"KiehlRoom") == 1)) {
		cutin "kh_kiel02",2;
		mes "[Kiehl]";
		mes "Argh...! Bien joué, aventur"+ ( sex ? "ier" : "ière" ) +", bien joué. J'aurais dû me douter que Père m'aurait envoyé "+ ( sex ? "le meilleur" : "la meilleure" ) +". Mais vous avez tout de même pu me vaincre complètement.";
		next;
		cutin "kh_kiel03",2;
		mes "[Kiehl]";
		mes "J'ai encore quelques cartes dans ma manche! Je pense... Je vais... vous emmener en enfer avec moi... Pour autant que les robots puissent y aller~";
		next;
		mapannounce "kh_kiehl02","*Jeeeezzzgggg~ Geezzz~ Grrrr~ Clank~*",bc_map,"0xFF0000";
		mes "[Mitchell]";
		mes "Non...! Nous sommes enfermés dans cette salle! Nous sommes piégés à l'intérieur!";
		next;
		mes "[Kiehl]";
		mes "Oh oui~ Réduisons tout cela en cendres~";
		next;
		mes "[Mitchell]";
		mes "Vite, "+strcharinfo(0)+", utilisez la pièce que vous as remise Kiel Hyre – celle qui est censée perturber son alimentation! Dépêchez-vous, faites-le maintenant!";
		next;
		mapannounce "kh_kiehl02","*Gzzzz~ Gzzzz~*",bc_map,"0xFF0000";
		mes "[Kiehl]";
		mes "Quoi? Je ne... Je ne peux plus bouger! Vous êtes décidément plein"+ ( sex ? "" : "e" ) +" de surprises. Eh bien, j'imagine qu'il est alors temps pour moi d'utiliser mon dernier joker!";
		next;
		mes "[Mitchell]";
		mes "Hein...? Combien de jokers vous avez?";
		next;
		mes "[????]";
		mes "Je suis si déçu! Je n'arrive pas à croire qu'aucun d'entre vous n'ait pensé à cela.";
		next;
		mes "[Mitchell]";
		mes "Qui êtes-vous? Montrez-vous!";
		next;
		mes "[????]";
		mes "S'il vous plaît. Ne m'insultez pas. Vous connaissez cette voix. C'est elle qui vous parlait pendant tout ce temps~";
		next;
		mes "[Mitchell]";
		mes "Impossible... Comment pourriez-vous être deux...?!";
		next;
		hideoffnpc "Kiehl#Copy";
		next;
		cutin "kh_kiel01",0;
		mes "[Kiehl]";
		mes "Hahahahaha! Je suis un robot. Je peux me fabriquer des corps supplémentaires, et échanger mon unité centrale avec eux. Vous n'imaginez pas à quel point ça peut être pratique.";
		next;
		cutin "kh_kiel03",2;
		mes "[Kiehl]";
		mes "Mais je ne suis pas là pour vous faire l'éloge de ma supériorité. Il est temps pour moi de vous montrer mon dernier atout. Mais avant tout, laissez-moi vous dire que je sais tout de vous, Mlle Mitchell Layla~";
		next;
		mes "[Mitchell]";
		mes "Quoi, comment connaissez-vous mon nom?";
		next;
		mes "[Kiehl]";
		mes "Eh bien, moi aussi j'ai mes espions... Mais il va tout vous expliquer lui-même.";
		next;
		mes "[Homme Masqué]";
		mes "Mitchell... Je suis désolé que tu te sois retrouvée impliquée dans tout ceci...";
		next;
		mes "[Mitchell]";
		mes "Wolkeus? Wolkeus Kaiser?! C'est toi l'espion?! Mais tu as risqué ta vie pour sauver le président! Non! Oh mon dieu, comment cela est-il possible? Tout est... Ça n'a pas de sens! Rien de cela n'en a!";
		next;
		mes "[Wolkeus]";
		mes "Il ne s'agit que de la conclusion d'un plan élaborée il y a des années. Je ne m'attendais pas à ce que tu sois surprise en fait, Mitchell. Ce sont les règles du jeu. Et tu le sais.";
		next;
		mes "[Mitchell]";
		mes "Monsieur le Président... J'ai échoué... Je suis désolée...";
		next;
		mes "[Kiehl]";
		mes "Eh bien, elle prend plutôt mal la nouvelle, Kaiser, mais au moins vous vous êtes comporté en gentleman. Eh bien, j'aimerais pouvoir rester et continuer ces badinages avec vous, mais...";
		next;
		cutin "kh_kiel01",2;
		mes "[Kiehl]";
		mes "Nous ferions mieux de nous dire adieu maintenant. Cet endroit sera réduit en cendres dans cinq minutes. Ah, et Mlle Layla, vous venez avec nous. Nous avons quelques questions à vous poser~";
		next;
		mes "[Kiehl]";
		mes "Je suis curieux de connaître les plans du Président. Monsieur Kaiser, si vous voulez bien escorter Mlle Layla...";
		next;
		mes "[Wolkeus Kaiser]";
		mes "...............................";
		mes "Désolé, Mitchell. Je dois le faire.";
		next;
		mes "[Mitchell]";
		mes "Non, laissez-moi! Laissez-moi partir! Laisse-moi, Wolkeus!";
		hideonnpc "Mitchell#KiehlRoom";
		hideonnpc "Agent#KHAgent1";
		hideonnpc "Agent#KHAgent2";
		hideonnpc "Agent#KHAgent3";
		hideonnpc "Agent#KHAgent4";
		next;
		cutin "kh_kiel02",2;
		mes "[Kiehl]";
		mes "Bien, nous en avons terminé avec cet épisode déplaisant. Où en étais-je? Ah, oui. Je suis désolé. Nous n'avons plus de temps à perdre en bavardage.";
		next;
		mes "[Kiehl]";
		mes "Voici, "+ ( sex ? "aventurier" : "aventurière" ) +". Je sais que mon père vous a envoyé"+ ( sex ? "" : "e" ) +" ici pour que vous récupériez ceci. Considérez cela comme un dernier présent. Je suis cependant surpris qu'il ait laissé cet anneau en moi...";
		next;
		cutin "kh_kiel04",2;
		mes "[Kiehl]";
		mes "J'imagine qu'il lui est très précieux. Je me demande pourquoi il l'a placé dans ma structure. Quoi qu'il en soit, j'ai un message pour lui que je voudrais que vous lui transmettiez.";
		next;
		cutin "kh_kiel01",2;
		mes "[Kiehl]";
		mes "Tout d'abord, j'imagine que je devrais commencer par me débarrasser de ceci. C'était un bon corps, et il m'a bien servi pendant 23 ans. Il me manquera. Repose en paix, vieux Kiehl.";
		next;
		specialeffect EF_DEVIL;
		next;
		mes "[Kiehl]";
		mes "Il s'agissait du corps que mon père avait créé. Dites-lui que désormais nous n'avons plus rien en commun.";
		next;
		mes "[Kiehl]";
		mes "J'ai moi-même créé le corps que j'utilise en ce moment grâce aux technologies les plus complexes. Vous pouvez considérer cela comme la quatrième génération de robots en quelque sorte. Père comprendra ce que je veux dire.";
		next;
		mes "[Kiehl]";
		mes "Dites-lui ceci, s'il vous plaît, et récupérez l'anneau sur mon ancienne structure. Bien, je crois qu'il est temps d'en terminer. Il ne nous reste que 3 minutes pour évacuer.";
		next;
		mes "[Kiehl]";
		mes "Vous étiez un"+ ( sex ? "" : "e" ) +" adversaire de taille, et en tant qu'humain, je vous respecte. Je ne sais pas si nous aurons le plaisir de nous revoir, mais qui sait? Je vais vous ouvrir la porte de sortie. Adieu~";
		delitem 7504,1; //Toy_Motor
		set KielHyreQuest,104;
		hideonnpc "Kiehl#Copy";
		donpcevent "Kiehl_Room_Exit::OnEnable";			
		enablenpc "Kiehl_Room_Exit";
		initnpctimer;
		cutin "",255;
		close;
	}
	if (KielHyreQuest == 104) {
		cutin "kh_kiel02",2;
		mes "^3355FFVous récupérez l'anneau sur l'ancien corps robotique de Kiehl.^000000";
		getitem 7508,1; //Elysia_Ring
		set KielHyreQuest,106;
		close2;
		cutin "",255;
		end;
	}
	else{
		cutin "kh_kiel02",2;
		mes "^3355FFL'ancien corps robotique de Kiehl se dresse au centre de cette pièce, immobile et silencieux.^000000";
		close2;
		cutin "",255;
		end;
	}


OnTimer180000:
	stopnpctimer;
	mapannounce "kh_kiehl02","Beeeeeeeeeeeeep~",bc_npc,0xFF0000;
	donpcevent "KiehlRoom::OnReset";
	end;

OnTimer179000:
	mapannounce "kh_kiehl02","Il reste 1 seconde avant la détonation.",bc_npc,0xFF0000;
	end;

OnTimer178000:
	mapannounce "kh_kiehl02","Il reste 2 secondes avant la détonation.",bc_npc,0xFF0000;
	end;

OnTimer177000:
	mapannounce "kh_kiehl02","Il reste 3 secondes avant la détonation.",bc_npc,0xFF0000;
	end;

OnTimer176000:
	mapannounce "kh_kiehl02","Il reste 4 secondes avant la détonation.",bc_npc,0xFF0000;
	end;

OnTimer175000:
	mapannounce "kh_kiehl02","Il reste 5 secondes avant la détonation.",bc_npc,0xFF0000;
	end;

OnTimer170000:
	mapannounce "kh_kiehl02","Il reste 10 secondes avant la détonation.",bc_npc,0xFF0000;
	end;

OnTimer160000:
	mapannounce "kh_kiehl02","Il reste 20 secondes avant la détonation.",bc_npc,0xFF0000;
	end;

OnTimer150000:
	mapannounce "kh_kiehl02","Il reste 30 secondes avant la détonation.",bc_npc,0xFF0000;
	end;

OnTimer120000:
	mapannounce "kh_kiehl02","Il reste 1 minute avant la détonation.",bc_npc,0xFF0000;
	end;

OnTimer60000:
	mapannounce "kh_kiehl02","Il reste 2 minutes avant la détonation.",bc_npc,0xFF0000;
	end;

OnTimer1000:
	mapannounce "kh_kiehl02","Il reste 3 minutes avant la détonation.",bc_npc,0xFF0000;
	end;
}

//----------------------------------------------------------------------------
// Kiehl's Room; NPC for mob control, timers, and other junk.
//----------------------------------------------------------------------------
kh_kiehl02,1,1,0	script	KiehlRoom	-1,{
	end;

OnKiehlMobDead:
	set .KHKilled,.KHKilled+1;
	if (.KHKilled == 5) { 
		hideoffnpc "Kiehl#Original"; 
	}
	end;

OnKiehlDead:
	set .KHKilledBoss,1;
	hideoffnpc "Kiehl#Original";
	end;
	
OnReset:
	donpcevent "Kiehl_Room_Trap::OnGlobalTimerOff";
	if (getmapusers("kh_kiehl02") > 0)
		mapwarp "kh_kiehl02","lighthalzen",192,200;
	killmonsterall "kh_kiehl02";
	disablenpc "Kiehl_Room_Exit";
	hideonnpc "Mitchell#KiehlRoom";
	hideonnpc "Agent#KHAgent1";
	hideonnpc "Agent#KHAgent2";
	hideonnpc "Agent#KHAgent3";
	hideonnpc "Agent#KHAgent4";
	hideonnpc "Kiehl#Copy";
	hideoffnpc "Kiehl#Original"; 
	set .KHKilledBoss,0;
	set .KHKilled,0;
	set getvariableofnpc(.KHTrapSprung,"Kiehl_Room_Trap"),0;
	set $@KHQuestBusy,0;
	end;
}

//----------------------------------------------------------------------------
// Kiehl's Room; Mitchell and Agents. (Floating NPC + Duplicates)
//----------------------------------------------------------------------------
kh_kiehl02,49,55,6	script	Mitchell#KiehlRoom	727,{
	end;
OnInit:
	hideonnpc "Mitchell#KiehlRoom";
	end;

OnKHSpiderWeb:
	misceffect 404;
	end;
}

kh_kiehl02,53,52,4	script	Agent#KHAgent1	880,{
	end;
OnInit:
	hideonnpc "Agent#KHAgent1";
	end;

OnKHSpiderWeb:
	misceffect 404;
	end;
}

kh_kiehl02,51,49,1	script	Agent#KHAgent2	880,{
	end;
OnInit:
	hideonnpc "Agent#KHAgent2";
	end;

OnKHSpiderWeb:
	misceffect 404;
	end;
}

kh_kiehl02,47,50,0	script	Agent#KHAgent3	880,{
	end;
OnInit:
	hideonnpc "Agent#KHAgent3";
	end;

OnKHSpiderWeb:
	misceffect 404;
	end;
}

kh_kiehl02,46,53,6	script	Agent#KHAgent4	880,{
	end;
OnInit:
	hideonnpc "Agent#KHAgent4";
	end;

OnKHSpiderWeb:
	misceffect 404;
	end;
}


//----------------------------------------------------------------------------
// Kiehl's Room; Copy of Kiehl.
//----------------------------------------------------------------------------
kh_kiehl02,48,53,6	script	Kiehl#Copy	902,{
	end;

OnInit:
	hideonnpc "Kiehl#Copy";
	end;
}

//----------------------------------------------------------------------------
// Kiehl's Room; Kiehl_Room_Exit
//----------------------------------------------------------------------------
kh_kiehl02,50,59,0	script	Kiehl_Room_Exit	45,1,1,{
OnTouch:
	if (getmapusers("kh_kiehl02") < 1)
		donpcevent "KiehlRoom::OnReset";
	warp "lighthalzen",193,202;
	end;

OnEnable:
	misceffect 215;
	end;

OnInit:
	disablenpc "Kiehl_Room_Exit";
	end;
}

//============================================================================
