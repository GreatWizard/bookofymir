//Le Livre d'Ymir http://www.bookofymir.net/
//traduit par Myllena, corrigé par Icaros
//===== eAthena Script ======================================= 
//= Quest NPCs related to Payon
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= Pretend Murder, Ear Muffs, Oxygen, Derivuchi Cap, Helm of Angel Quests.
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Helm of Angel bug fixed [Lupus]
//= 1.2 Fixed skirt of virgin [Lupus]
//= 1.3 Fixed exploits [Lupus]
//= 1.4 Removed Duplicates [Silent]
//= 1.5 Updated Dialogs from "Pretend Murdered Quest" [Samuray22]
//= 1.5a replaced item "names" with item id. [Lupus]
//= 1.6 Updated Dialogs from "Oxygen Mask Quest" [Samuray22]
//= 1.7 Updated headgear quests. [L0ne_W0lf]
//============================================================ 



//==========================================================================================//
//				Pretend Murdered quest
//==========================================================================================//
payon_in01,99,72,4	script	Grand-Mère#2	103,{
	if (checkweight(908,1) == 0) {
		mes "^3355FFAttendez!";
		mes "Vous portez bien trop de choses sur vous, vous allez vous faire un lumbago!";
		mes "Vous devriez déposer des objets dans votre Stock Kafra...^000000";
		close;
	}
	if (countitem(1049) > 3) {
		mes "[Grand-Mère]";
		mes "Oh... Vous avez réussi à trouver des Robes de Vierge! Avec tout ça je pourrais confectionner des déguisements pour mes petits enfants pour le festival du mois prochain...";
		next;
		mes "[Grand-Mère]";
		mes "Est-ce que vous pourriez me donner vos ^3355FFRobes de Vierge^000000? Il m'en faudrait quatre... s'il vous plaît?";
		mes "Je vous en prie!";
		next;
		switch(select("D'accord.:Pas question!")) {
		case 1:
			delitem 1049,4;  //Skirt_Of_Virgin
			mes "[Grand-Mère]";
			mes "Uh-uh... Merci infiniment! Maintenant je vais pouvoir confectionner des déguisements pour mes petits-enfants.";
			mes "Et... Oh! Mais oui! Quand j'étais jeune, j'étais une actrice de théâtre...";
			mes "J'ai conservé un objet de ce temps-là, alors que je venais de quitter la troupe et me marier...";
			next;
			mes "[Grand-Mère]";
			mes "Tenez, prenez-le... S'il vous plaît!";
			mes "Je l'avais toujours gardé en souvenir... Je suis certaine que vous rigolerez beaucoup avec cet objet!";
			next;
			getitem 2293,1; // Pretend_Murdered
			mes "[Grand-Mère]";
			mes "Au revoir, aventurier~!";
			mes "Merci d'avoir rendu ce service à une ville dame comme moi...";
			close;
		case 2:
			mes "[Grand-Mère]";
			mes "Uh-uh... Comment pourrais-je un jour me procurer des ^3355FFRobes de Vierge^000000 toute seule... Je suis si faible et si fatiguée.";
			mes "Quel monde froid et sans pitié...";
			close;
		}
	}
	else {
		mes "[Grand-Mère]";
		mes "Je ramasse des champignons dans la montagne de Payon tous les jours afin de gagner ma vie. Je ne sais pas si j'aurais de quoi manger à ma faim le mois prochain...";
		mes "... Mais il y a un festival le mois prochain!";
		mes "Mes petits enfants... Je voulais leur coudre des déguisements pour qu'ils puissent s'amuser comme n'importe quels autres petits...";
		mes "L'idéal aurait été ce tissu, les ^3355FFRobes de Vierge^000000 ...";
		next;
		mes "[Grand-Mère]";
		mes "Jeune aventurier... J'ai une faveur à vous demander... Si jamais vous trouvez des ^3355FFRobes de Vierge^000000...";
		mes "Vous pourriez m'en apporter? Je vous en serais éternellement reconnaissante...";
		close;
	}
}

//========================================================================================//
//					Ear Muffs Quest
//========================================================================================//
payon_in01,18,10,4	script	Jeune Fille	76,{
	mes "[Jeune Fille Mystérieuse]";
	mes "Ma famille et moi avons récemment emménagé à Payon.";
	mes "Avant nous habitions dans une région au climat très froid, et nous avions développé un commerce de ^3355FFCache-Oreilles^000000 faits mains de grande qualité!";
	next;
	mes "[Jeune Fille Mystérieuse]";
	mes "J'essaye de faire perdurer l'entreprise familiale ici, à Payon, mais comme il fait chaud ce n'est pas très rentable.";
	Emotion e_dots;
	next;
	mes "[Jeune Fille Mystérieuse]";
	mes "Même si les Cache-Oreilles ne sont pas vraiment utiles ici, si vous voyagez régulièrement dans des pays froids, nos Cache-Oreilles pourraient beaucoup vous aider.";
	Emotion e_dots;
	next;
	mes "[Jeune Fille Mystérieuse]";
	mes "Si ça vous intéresse, tout ce dont j'ai besoin est: ^FF33551 Rubis Maudit^000000, ^3311AA1 Cache-Oreilles Renforcé,^000000 ^3355FF 200 Plumes^000000 et ^DDDD005000 Zenys^000000.";
	next;
	mes "[Jeune Fille Mystérieuse]";
	mes "Avec tous ces matériaux, je pourrais vous fabriquer une paire de Cache-Oreilles. Croyez-moi, ils sont de la meilleure qualité qui existe!";
	next;
	switch(select("Pourriez-vous me fabriquer des Cache-Oreilles?:Non, merci.")) {
	case 1:
		if ((countitem(724) > 0) && (countitem(949) > 199) && (countitem(5001) > 0) && (Zeny > 4999)) {
			mes "[Jeune Fille Mystérieuse]";
			delitem 724,1;  //Cardinal_Jewel_
			delitem 949,200;  //Feather
			delitem 5001,1;  //Headset
			set Zeny,Zeny-5000;
			mes "Oh, je vois que vous avez rapporté tous les matériaux... Tenez! Une paire de Cache-Oreilles.";
			getitem 2283,1; // Ear_Mufs
			next;
			mes "[Jeune Fille Mystérieuse]";
			mes "Merci! Vous ne le regretterez pas. Avec des Cache-Oreilles: Oreilles Protégées, Satisfaction Garantie~";
			Emotion e_dots;
			close;
		}
		else {
			mes "[Jeune Fille Mystérieuse]";
			mes "Je suis désolée, mais il vous manque des matériaux...";
			mes "Comprenez-moi, je ne peux pas vous faire de remise... Je dois bien vivre moi aussi...";
			Emotion e_dots;
			close;
		}
	case 2:
		mes "[Jeune Fille Mystérieuse]";
		mes "D'accord.";
		mes "Nous nous rencontrerons peut-être à nouveau.";
		mes "...un de ces jours.";
		Emotion e_dots;
		close;
	}
}

//========================================================================================//
//					Oxygen Mask Quest
//========================================================================================//
payon_in02,25,71,4	script	Jeune Homme#oxmq	86,{
	if (countitem(701) > 4) {
		mes "[Jeune Homme]";
		mes "ArrrGggghh!";
		mes "Ah, Non, c'est pas possible...";
		next;
		mes "[Jeune Homme]";
		mes "..... Je crois que je vais devenir fou...";
		next;
		switch(select("Lui montrer l'Ora Ora.:Lui donner l'Ora Ora.:Annuler.")) {
		case 1:
			mes "[Jeune Homme]";
			mes "Oooooohhhh..... c'est.. c'est... de... l'Ora Ora...??? Oh donnez-le moi, je vous en supplie!";
			close;
		case 2:
			if (countitem(701) > 4) {
				delitem 701,5;  //Ora_Ora
			}
			mes "[Jeune Homme]";
			mes "Muhahahahah!!! J'ai finalement réussi en avoir... de l'Ora Ora! Stupides et dégoûtants petits insectes... vous allez payer pour avoir envahi ma maison!!";
			next;
			mes "[Jeune Homme]";
			mes "..... Euh hem... désolé. Je suis un peu surmené.... Laissez-moi vous récompenser en échange de ce merveilleux trésor que vous m'avez rapporté......";
			next;
			getitem 5004,1; // Oxygen_Mask
			mes "[Jeune Homme]";
			mes "Si vous portez ce masque sur votre bouche, l'air que vous respirerez sera filtré et donc assez pur.";
			mes "Je l'ai trouvé quand je travaillais en tant que marin. Et comme je n'en ai plus besoin...";
			next;
			mes "[Jeune Homme]";
			mes "Hehehehe!! Je ne peux plus attendre, il faut que j'essaye ces Ora Ora!!!";
			close;
		case 3:
			mes "[Jeune Homme]";
			mes "Huhuhuhuhu...";
			mes "Comment faire pour se débarrasser de ces satanés insectes?... Et si jamais je n'y arrivais pas...?!";
			close;
		}
	}
	else {
		mes "[Jeune Homme]";
		mes "Uh...Ah! Je n'en peux plus!";
		mes "Satanés insectes!!";
		next;
		switch(select("L'écouter.:S'en aller.")) {
		case 1:
			mes "[Jeune Homme]";
			mes "~Soupir~ Je n'ai pas eu une vie facile. Vous savez, mes parents sont morts quand je n'étais encore qu'un petit garçon, alors j'ai dû me prendre en mains très tôt.";
			next;
			mes "[Jeune Homme]";
			mes "Ça n'a pas toujours été facile, mais après 10 ans de travail acharné, j'ai réussi à construire une maison de mes propres mains!";
			next;
			mes "[Jeune Homme]";
			mes "Malheureusement, comme je n'avais pas beaucoup d'argent, ce n'était pas tout à fait la maison de mes rêves...";
			mes "Et puis j'ai trouvé cette maison... Elle était grande et bon marché... alors je l'ai achetée sur un coup de tête, sans vraiment réfléchir....";
			next;
			mes "[Jeune Homme]";
			mes "Quelle erreur monumentale... Il s'est avéré que cette maison était remplie de MILLIERS d'INSECTES!!!";
			next;
			mes "[Jeune Homme]";
			mes "J'ai TOUT essayé pour m'en débarrasser! Je les ai écrasés, aspergés d'insecticide, j'ai posé des pièges à insectes...";
			mes "Rien n'y a fait! Alors finalement, j'ai même essayé de les brûler!!";
			next;
			mes "[Jeune Homme]";
			mes "Grrr! Mais que diable faut-il faire pour se débarrasser de ces saletés?!";
			next;
			mes "[Jeune Homme]";
			mes "J'ai entendu parler d'un objet appelé ^3355FFOra Ora^000000, que les gens utilisent dans certains pays pour combattre les insectes...";
			mes "Mais est-ce que ça suffira dans mon cas?";
			next;
			mes "[Jeune Homme]";
			mes "Je donnerai n'importe quoi pour ne plus voir ces sales bêtes... Comme je n'ai pas beaucoup d'argent, je ne peux pas me payer ^5555FF'5 Ora Oras'^000000.";
			mes "Si jamais vous trouvez des Ora Ora par hasard, faites le moi savoir... s'il vous plaît?";
			close;
		case 2:
			mes "[Jeune Homme]";
			mes "Je suis fatigué et j'en ai assez de me battre sans arrêt contre ces insectes... Ces sales bêtes!!";
			close;
		}
	}
}

//========================================================================================//
//			Derivuchi Cap(Hat of Petite Diablo), Helm of Angel
//========================================================================================//
payon_in01,56,12,7	script	Jeune Homme#12	89,{
	mes "[Jeune Homme]";
	mes "Que voulez-vous? ............";
	next;
	switch(select("Pourriez-vous me fabriquer un chapeau?:Euh... rien.")) {
	case 1:
		mes "[Jeune Homme]";
		mes "Je vois... Vous avez entendu parler de moi... D'accord, dites-moi ce que vous voulez.";
		next;
		switch(select("Heaume des Anges.:Chapeau de Deviruchi.:Je reviendrai plus tard.")) {
		case 1:
			mes "[Jeune Homme]";
			mes "Pour un Heaume des Anges j'aurais besoin de:";
			mes "- ^5555FF1 Heaume [1]^000000.";
			mes "- ^5555FF1 paire d'Ailes d'Ange^000000.";
			mes "- ^5555FF5 Crocs de Garm^000000.";
			next;
			mes "[Jeune Homme]";
			mes "Attendez un peu! Faites attention à ce que vous allez me donner.";
			next;
			mes "[Jeune Homme]";
			mes "S'il vous manque un objet, je ne ferai pas la distinction entre l'équipement que vous portez et les matériaux demandés.";
			mes "Si vous ne voulez pas risquer de perdre un objet qui vous est cher, laissez-le à l'abri dans votre stock.";
			next;
			mes "[Jeune Homme]";
			mes "Avez-vous tous les matériaux nécessaires?";
			next;
			switch(select("Lui donner.:S'en aller.")) {
			case 1:
				if ((countitem(2229) > 0) && (countitem(2254) > 0) && (countitem(7036) > 4)) {
					mes "[Jeune Homme]";
					mes ".....Hum. Voici votre Heaume des Anges.";
					delitem 2229,1;  //Helm_
					delitem 2254,1;  //Angelic_Chain
					delitem 7036,5;  //Fang_Of_Garm
					close2;
					getitem 5025,1; // Helm_Of_Angel
					end;
				}
				else {
					mes "[Jeune Homme]";
					mes "...Vous n'avez pas tous les matériaux nécessaires.";
					close;
				}
			case 2:
				mes "[Jeune Homme]";
				mes "Comme vous voulez...";
				close;
			}
		case 2:
			mes "[Jeune Homme]";
			mes "Pour un Chapeau de Deviruchi j'aurais besoin de:";
			mes "- ^5555FF600 Petites Cornes Maléfiques^000000.";
			mes "- ^5555FF40 Serres de Griffon^000000.";
			next;
			mes "[Jeune Homme]";
			mes "Attendez un peu! Faites attention à ce que vous allez me donner.";
			next;
			mes "[Jeune Homme]";
			mes "S'il vous manque un objet, je ne ferai pas la distinction entre l'équipement que vous portez et les matériaux demandés.";
			mes "Si vous ne voulez pas risquer de perdre un objet qui vous est cher, laissez-le à l'abri dans votre stock.";
			next;
			mes "[Jeune Homme]";
			mes "Avez-vous tous les matériaux nécessaires?";
			next;
			switch(select("Lui donner.:S'en aller.")) {
			case 1:
				if ((countitem(1038) > 599) && (countitem(7048) > 39)) {
					mes "[Jeune Homme]";
					mes ".....Hum. Voici votre Chapeau de Deviruchi.";
					delitem 1038,600;  //Petite_DiablOfs_Horn
					delitem 7048,40;  //Talon_Of_Griffin
					close2;
					getitem 5038,1; // Deviruchi_Cap
					end;
				}
				else {
					mes "[Jeune Homme]";
					mes "...Vous n'avez pas tous les matériaux nécessaires.";
					close;
				}
			case 2:
				mes "[Jeune Homme]";
				mes "Comme vous voulez...";
				close;
			}
		case 3:
			mes "[Jeune Homme]";
			mes "D'accord, je vois.....";
			close;
		}
	case 2:
		mes "[Jeune Homme]";
		mes "Hum.";
		mes "A plus.";
		close;
	}
}
