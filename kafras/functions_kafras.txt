//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Kafra Functions
//===== By: ================================================== 
//= eAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 6.1
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: ================================= 
//= v1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//= Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=	to block Kafra Password, read comments at 350 line
//=	Note: You can change '1337' value to another to raise password protection
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=	  Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress. 
//=	Need all coords for all guides.. and somehow pass their coords to kafra.
//=	Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=	Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=	Updated some functions to handle Guild Kafras the way they should. 
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=	Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=		Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//============================================================ 


// Main Function ===========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; //Clear outdated, unused variables

	//Display Kafra Welcome Message
	switch(getarg(0)){
		//Default message (obsolete)
		default:
		case 0:
			mes "Bienvenue à la Corporation Kafra. Nous vous accompagnons où que vous alliez!";
			break;

		//Niflheim specific message
		case 1:
			mes "[Employée Kafra]";
			mes "Bienvenue... La Corporation Kafra.... vous accompagnera par delà la mort.....";
			break;

		//Guild Castle Kafra message
		case 2:
			set @GID,getcharid(2);
			mes "[Employée Kafra]";
	 		mes "Bienvenue, très cher membre de la guilde ^5533FF" + GetGuildName(@GID) + "^000000. Nous vous accompagnons où que vous alliez!";
			break;

		//Amatsu specific message (obsolete)
		case 3:
			mes "[Employée Kafra]";
			mes "Oh, que venez-vous faire dans une contrée si éloignée? Etudier cette culture lointaine, ou bien simplement profiter du paysage?";
			mes "Pourquoi ne resteriez-vous pas encore quelques temps?";
			mes "L'air ici est toujours chargé de la senteur des fleurs sauvages...";
			mes "N'est-ce pas délicieux?";
			break;

		//Louyang and Ayothaya specific message (obsolete)
		case 4:
			mes "[Employée Kafra]";
			mes "Grâce à la Corporation Kafra, même en voyage, vous n'êtes jamais loin de la maison!";
			break;

		//NPC has it's own welcome message. (Display nothing)
		case 5:
			break;
	}
	next;

	M_Menu:
		cleararray @K_Menu0$[0],"",7;
	switch(getarg(1)){
		// Save and Storage only
		case 1:
			setarray @K_Menu0$[0],"Sauvegarder","Ouvrir mon Stock","Annuler";
			break;

		// Storage only
		case 2:
			setarray @K_Menu0$[0],"Ouvrir mon Stock","Annuler";
			break;

		// No Teleport (Common)
		case 3: 
			setarray @K_Menu0$[0],"Sauvegarder","Ouvrir mon Stock","Louer une Charrette","Guide","Demander d'Autres Informations","Annuler";
			break;

		// Case 4 is Einbroch no tele message.

		// No save, or teleport. (Common)
		case 5:
			setarray @K_Menu0$[0],"Ouvrir mon Stock","Louer une Charrette","Demander d'Autres Informations","Guide","Annuler";
			break;

		// Storage and Check Other Information only.
		case 6:
			setarray @K_Menu0$[0],"Ouvrir mon Stock","Guide","Demander d'Autres Informations","Annuler";
			break;

		// Save, Storage, and Pushcart only (Kafra Warehouse)
		case 7:
			setarray @K_Menu0$[0],"Sauvegarder","Ouvrir mon Stock","Louer une Charrette","Annuler";
			break;

		// Save, Storage, Other Check information. (Turbo track)
		case 8:
			setarray @K_Menu0$[0],"Sauvegarder","Ouvrir mon Stock","Demander d'Autres Informations","Annuler";
			break;

		// Common Employée Kafra
		default:
			setarray @K_Menu0$[0],"Sauvegarder","Ouvrir mon Stock","Utiliser le Service de Téléportation","Louer une Charrette","Guide","Demander d'Autres Informations","Annuler";
			break;
	}

	// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
	if(getarg(0)==2) {
		cleararray @K_Menu0$[0],"",7;
		setarray @K_Menu0$[0],"Ouvrir mon Stock","Ouvrir le Stock de Guilde","Louer une Charrette","Utiliser le Service de Téléportation","Annuler";
	}
	menu 	@K_Menu0$[0],K_Menu0,@K_Menu0$[1],K_Menu1,@K_Menu0$[2],K_Menu2,
		@K_Menu0$[3],K_Menu3,@K_Menu0$[4],K_Menu4,@K_Menu0$[5],K_Menu5,
		@K_Menu0$[6],K_Menu6,@K_Menu0$[7],K_Menu7;
	K_Menu0:
		set @num,0;
		goto K_Menuf;
	K_Menu1:
		set @num,1;
		goto K_Menuf;
	K_Menu2:
		set @num,2;
		goto K_Menuf;
	K_Menu3:
		set @num,3;
		goto K_Menuf;
	K_Menu4:
		set @num,4;
		goto K_Menuf;
	K_Menu5:
		set @num,5;
		goto K_Menuf;
	K_Menu6:
		set @num,6;
		goto K_Menuf;
	K_Menu7:
		set @num,7;
	K_Menuf:
		if (@K_Menu0$[@num] == "Sauvegarder") return;
		if (@K_Menu0$[@num] == "Ouvrir mon Stock"){
			// Do not charge for Guild Storage
			if(getarg(0) == 2) callfunc "F_KafStor",2,0,0;
			else callfunc "F_KafStor",0,getarg(3),getarg(0);
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Utiliser le Service de Téléportation"){
			// Display Einbroch "No Teleport Service" notice.
			if(getarg(1) == 4){
				mes "[Employée Kafra]";
				mes "A cause de la politique d'^FF0000Accord de Transport Limité ^000000, la Corporation Kafra ne peut fournir de Services de Téléportation au sein de la République de Schwarzwald.";
				next;
				mes "[Employée Kafra]";
				mes "Merci d'utiliser le Service de Zeppelin à votre disposition, ou de vous adresser à un représentant du Réseau de Téléporation.";
				mes "Merci de votre compréhension.";
				next;
				goto M_Menu;
			}
			callfunc "F_KafTele",getarg(0);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Louer une Charrette"){
			if(callfunc("F_KafCart",getarg(0),getarg(4)) == 1) next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Guide"){
			callfunc "F_KafGuide";
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Demander d'Autres Informations"){
			callfunc "F_KafInfo",getarg(2);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Annuler"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		}
		if (@K_Menu0$[@num] == "Ouvrir le Stock de Guilde"){
			callfunc "F_KafStor",1,0;
			next;
			goto M_Menu;
		}

}


// Storage Function =======================================================
function	script	F_KafStor	{
	// Unable to access Guild Storage (Busy)
	if(getarg(0) == 1){
		if(guildopenstorage(0) == 1){
			mes "[Employée Kafra]";
			mes "Je suis désolée, mais un autre membre de la guilde est déjà en train de consulter le stock.";
			mes "Veuillez patienter jusqu'à ce que ce membre ait terminé.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}

	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(basicskillcheck() && getskilllv(1) < 6){
		mes "[Employée Kafra]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666Désolée...";
			mes "Vous êtes un....";
			mes "Nooviiice... Vous avez besoiiin";
			mes "de six niveauuuux de compétences de baaase...^000000";
			return;
		}
		mes "Je suis désolée, mais vous devez être un Novice de Niveau de Job 6 au minimum pour pouvoir consulter votre stock.";
		return;
	}

	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(getarg(0) != 2){
		// Consume "Free Ticket for Kafra Storage" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<getarg(1)){
				mes "[Employée Kafra]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeenys...";
					mes "Plus de z-zenys...!";
					mes "Besoin de 150... zenys...";
					mes "Arrgh! Du saaaang~!^000000";
					return;
				}
				//Standard Message
				mes "Je suis désolée, mais vous";
				mes "n'avez pas assez d'argent pour";
				mes "utiliser le service de Stockage.";
				mes "Le prix d'accès au Stock est de "+getarg(1)+" Zenys.";
				return;
			}
			set Zeny, Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + (getarg(1)/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (getarg(0) != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Employée Kafra]";
			mes "^666666Merci.. d'avoir... utilisé...";
			mes "les.. services... Kafra...";
			mes "Merci.. d'avoir... utilisé...";
			mes "les.. services... Kafra...";
			mes "Merci.. d'avoir... utilisé...^000000";
		}
		// Normal message
		else {
			mes "[Employée Kafra]";
			mes "Merci d'avoir utilisé";
			mes "les Services Kafra!";
			mes "Nous espérons vous";
			mes "revoir très bientôt.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set

	close2;
	openstorage;
	cutin "", 255;
	end;
}


// Teleport Function ==================================================
function	script	F_KafTele	{
	mes "[Employée Kafra]";
	mes "Veuillez choisir";
	mes "votre destination.";
	next;

	menu 	@wrpC$[0],M_Wrp0, @wrpC$[1],M_Wrp1, @wrpC$[2],M_Wrp2, @wrpC$[3],M_Wrp3, 
		@wrpC$[4],M_Wrp4, @wrpC$[5],M_Wrp5, @wrpC$[6],M_Wrp6;

	M_Wrp0:
		set @num, 0;
		goto L_Warp;
	M_Wrp1:
		set @num, 1;
		goto L_Warp;
	M_Wrp2:
		set @num, 2;
		goto L_Warp;
	M_Wrp3:
		set @num, 3;
		goto L_Warp;
	M_Wrp4:
		set @num, 4;
		goto L_Warp;
	M_Wrp5:
		set @num, 5;
		goto L_Warp;
	M_Wrp6:
		set @num, 6;

	L_Warp:
		if (@wrpC$[@num] == "Annuler") return;
		// Consume "Free Ticket for Kafra Transportation" if available.
		// Do not consume if in Guild castle
		if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
		else {
			if (Zeny<@wrpP[@num]){
				mes "[Employée Kafra]";
				mes "Je suis désolée mais vous n'avez";
				mes "pas assez d'argent pour utiliser";
				mes "le Service de Téléportation. Aller";
				mes "à "+@wrpD$[@num]+" vous coûtera "+@wrpP[@num]+" Zenys.";
				close2;
				cutin "", 255;
				end;
			}
			set Zeny, Zeny-@wrpP[@num];
			set RESRVPTS, RESRVPTS + (@wrpP[@num]/16);
		}
		if (@wrpD$[@num] == "Alberta") warp "alberta", 117, 56;
		if (@wrpD$[@num] == "Al De Baran") warp "aldebaran",143,110;
		if (@wrpD$[@num] == "Aldebaran") warp "aldebaran",143,110;
		if (@wrpD$[@num] == "Comodo") warp "comodo", 207, 144;
		if (@wrpD$[@num] == "Izlude") warp "izlude", 91, 105;
		if (@wrpD$[@num] == "Geffen") warp "geffen", 120, 39;
		if (@wrpD$[@num] == "Morroc") warp "morocc", 156, 46;
		if (@wrpD$[@num] == "Payon") warp "payon", 168, 103;
		if (@wrpD$[@num] == "Prontera") warp "prontera", 116, 72;
		if (@wrpD$[@num] == "Mont de la Mort de Mjolnir") warp "mjolnir_02", 82, 347;
		if (@wrpD$[@num] == "Comodo Pharos Beacon") warp "cmd_fild07", 127, 134;
		if (@wrpD$[@num] == "Donjon des Orcs") warp "gef_fild10", 52, 326;
		if (@wrpD$[@num] == "Umbala") warp "umbala", 130, 130;
		if (@wrpD$[@num] == "Yuno") warp "yuno", 157, 123;
		cutin "", 255;
		end;
}


// Cart Function ========================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if(baseClass != Job_Merchant){
		mes "[Employée Kafra]";
		mes "Je suis désolée, mais la location de charrettes n'est proposée qu'aux Marchands, Forgerons et Alchimistes.";
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "[Employée Kafra]";
		mes "Désolée... Mais vous avez déjà une Charrette....";
		return 1;
	}

	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[Employée Kafra]";
		mes "La location de Charrette";
		mes "coûte "+getarg(1)+" Zenys.";
		mes "Acceptez-vous ce tarif?";
		next;
		menu "Oui, merci.",-, "Non.",M_End;
			if(Zeny<getarg(1)){
				mes "[Employée Kafra]";
				mes "Je suis désolée, mais";
				mes "vous n'avez pas assez";
				mes "d'argent pour payer les";
				mes ""+getarg(1)+" Zenys nécessaires à la location d'une Charrette.";
				return 1;
			}
			set Zeny,Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + 48;
	}
	setcart;
	return 1;

M_End:
	return 0;
}


// Pass Function ===============================================================
function	script	F_KafGuide	{
	mes "[Employée Kafra]";
	mes "Vous pouvez vous adresser à un Guide ou à un Garde de la Ville afin d'obtenir des informations touristiques.";
	return;
}

// Special Reserve Points Function ===========================================
function	script	F_KafInfo	{

	sM_Menu:
//Uncomment next line to block Kafra Storage Protection
//	if(getarg(0) == 0) menu "Consulter le nombre de Points Kafra.",sM_ResChk, "Localisation des Employées Kafras",sM_KafLoc, "Annuler",sM_End;
	if(getarg(0) == 0) menu "Consulter le nombre de Points Kafra.",sM_ResChk, "Mot de Passe du Stock",sM_KafCode, "Localisation des Employées Kafras",sM_KafLoc, "Annuler",sM_End;

	sM_ResChk:
		mes "[Employée Kafra]";
		mes "Voyons voir...";
		mes strcharinfo(0) + "...";
		mes "Ah, vous avez un total de";
		mes RESRVPTS+ " Points Kafra.";
		next;
		mes "[Employée Kafra]";
		mes "Vous pouvez échanger ces points contre de nombreux lots, au Bureau Central des Kafras à Aldebaran.";
		next;
		if(getarg(0) == 1) return;
		goto sM_Menu;

	sM_KafLoc:
		viewpoint 1,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 1,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 1,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 1,@viewpX[3],@viewpY[3],4,0xFF00FF;
		next;
		viewpoint 2,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 2,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 2,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 2,@viewpX[3],@viewpY[3],4,0xFF00FF;
		goto sM_Menu;

	sM_KafCode:
		callfunc("F_SetKafCode","[Employée Kafra]","Services Kafras");

	sM_End:
		return;
}


// End Function =====================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[Employée Kafra]";
	//Save
	if(getarg(1)==1) {
		mes "Votre Point de Sauvegarde";
		mes "a été enregistré ici,";
		mes getarg(2)+".";
		mes "Merci d'avoir utilisé";
		mes "les Services Kafras.";
	}

	// Generic End
	else if(getarg(0)==0 || getarg(0)==5) {
		mes "Nous sommes la Corporation Kafra,";
		mes "et nous faisons tout notre possible pour vous proposer les meilleurs services. Nous espérons avoir répondu à vos attentes.";
	}

	// Niflheim End
	else if(getarg(0)==1) {
		percentheal 0,- 25;
		mes "^666666La Corporation Kafra.... vous accompagnera par delà la mort.....~^000000";
	}

	close2;
	cutin "", 255;
	end;
}

// Check Storage Password Function ====================
function	script	F_CheckKafCode	{
	if(#kafra_code==0) return;
	mes "Entrez le Mot de Passe de votre Stock:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code-getcharid(3)-1337) {
		dispbottom "Mauvais Mot de Passe.";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// Set / Change / Clear Storage Password Function ====================
// getarg(0) = NPC Name, getarg(1) = Company Name
function	script	F_SetKafCode	{
	mes getarg(0);
	if(#kafra_code) {
		mes "Votre Stock est protégé par un Mot de Passe. Que voulez-vous faire?";
		next;
		menu "Changer le Mot de Passe -> 5000z",-,
		"Retirer le Mot de Passe -> 1000z",M_CLEAR,
		"Annuler",M_END;
	} else {
		mes "Les "+getarg(1)+" sont fiers de vous présenter leur toute nouvelle inovation:";
		mes "une protection supplémentaire grâce au Mot de Passe de votre Stock.";
		next;
		menu "Définir un Mot de Passe -> 5000z",M_SET,
		"Annuler",M_END;
	}

	mes getarg(0);
	mes "Commencez par entrer votre ^0000FFAncien Mot de Passe^000000.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0 || @code != #kafra_code-getcharid(3)-1337) {
		mes "Mauvais Mot de Passe.";
		emotion e_hmm;
		goto M_END;
	}
	next;

M_SET:
	mes getarg(0);
	mes "Entrez maintenant votre ^FF0000Nouveau Mot de Passe^000000 afin de protéger votre Stock des voleurs!";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "Le Mot de Passe n'a pas été modifié.";
		emotion e_hmm;
		goto M_END;
	}
	next;
	mes getarg(0);
	if(Zeny < 5000) goto L_ZENY;
	set Zeny,Zeny-5000;
	//set RESRVPTS, RESRVPTS + (5000/50);	//hardcoded password doesn't add pts

	set #kafra_code,@code+getcharid(3)+1337;
	mes "Votre Stock est maintenant protégé par un Mot de Passe.";
	mes "Merci d'avoir utilisé les "+getarg(1)+".";
	emotion e_thx;
	goto M_END;	

M_CLEAR:
	mes getarg(0);
	mes "Merci d'entrer votre Mot de Passe avant que celui-ci ne soit retiré.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "Le Mot de Passe a été retiré.";
		emotion e_hmm;
		goto M_END;
	}
	next;
	mes getarg(0);
	if(Zeny < 1000) goto L_ZENY;
	set Zeny,Zeny-1000;
	//set RESRVPTS, RESRVPTS + (1000/50);	//hardcoded password doesn't add pts
	if(@code == #kafra_code-getcharid(3)-1337) {
		set #kafra_code,0;
		mes "Vous avez effacé votre Mot de Passe.";
		mes "Merci d'avoir utilisé les "+getarg(1)+".";
		emotion e_thx;
	} else {
		mes "Mauvais Mot de Passe. Vos 1000z ne vous seront pas remboursés.";
		mes "La prochaine fois, veuillez entrer le Mot de Passe correct.";
		emotion e_sry;
	}
	goto M_END;	

L_ZENY:
	mes "Vous n'avez pas assez d'argent.";
	emotion e_cash;
M_END:
	close2;
	cutin "",255;
	end;
}

// Basic Password Validation Function ====================
function	script	F_EntKafCode	{
	mes "Entrez un nombre compris entre 1000 et 10000000:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "Hack: trop d'essais.";
	}
	input @code_;
	if(@code_<1000) {
		mes "Vous ne devriez pas utiliser un mot de passe aussi court.";
		return 0;
	}
	if(@code_>10000000) {
		mes "votre mot de passe est trop long.";
		return 0;
	}
	return @code_;
}
