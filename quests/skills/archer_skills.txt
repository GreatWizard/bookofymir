//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script =======================================
//= Archer Skill Quest
//===== By: ==================================================
//= eAthena dev team
//===== Current Version: =====================================
//= 1.4b
//===== Compatible With: =====================================
//= eAthena 7.15 +
//===== Description: =========================================
//= Arrow crafting, Arrow Repel skills
//===== Additional Comments: =================================
//= v1.0 Roberto message text is based off RO npc.  Jason message
//= text is custom from old version.
//= All items are from official quests though.[kobra_k88]
//= v1.0a Now using functions found in "Global_Functions.txt"
//= for class checks.[kobra_k88]
//= 1.2 Added Baby Class Support [Lupus]
//= 1.3 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 1.4 Fixed exploits [Lupus]
//= 1.4a Fixed some typos [IVBela]
//= 1.4b changed perm. variables to temp ones [Lupus]
//============================================================



//<======================== Roberto: Arrow crafting =========================>\\
morocc,121,109,5	script	Roberto	88,{
	mes "[Roberto]";
	if(baseClass == Job_Archer) goto L_Start;

L_Other:
	mes "Quoi? ... C'est la première fois que vous voyez un Archer? Pourquoi n'iriez-vous pas vous occuper de vos propres affaires et me laisser tranquille?";
	emotion e_what;
	close;
L_Start:
	if(BaseJob==Job_Archer && JobLevel<30) goto L_LowLvl;
	if(getskilllv(147)>0) goto L_GotSkill;
	if(@ARWCRFT) goto L_GetSkill;
	mes "Hum?.... Oh, vous semblez être un Archer expérimenté... Vous voulez discuter un peu? Chouette, je vais commencer par vous parler de mon enfance...";
	mes ".... . . . . . . . . . . .";
	next;
	mes "[Roberto]";
	mes "Quand j'ai eu 15 ans, j'ai décidé de travailler pour un forgeron........";
	next;
	mes "[Roberto]";
	mes "............... ~(bla bla bla)~...................";
	next;
	mes "[Roberto]";
	mes ".... alors je lui ai dit...........";
	next;
	mes "[Roberto]";
	mes "..~(bla bla)~........ ~(bla bla)~.........";
	next;
	mes "(de trèèès longues heures plus tard.....)";
	next;
	mes "[Roberto]";
	mes "Alors j'ai enfin trouvé la manière idéale de tailler mes flèches. J'étais plutôt doué!";
	next;
	mes "[Roberto]";
	mes "Est-ce que ça vous dirait que je vous enseigne comme faire? C'est tellement agréable de discuter avec vous, que j'ai une totale confiance en l'utilisation que vous ferez de ces connaissances........";
	next;
	menu "Zzz... Hein! Création de flèches? Euh... ouais.",M_0, "Zzzzz... Hein! Où suis-je??",M_1;

	M_0:
		mes "[Roberto]";
		mes "Très bien! Je peux vous apprendre l'art de la ^5533FFCréation de Flèches^000000, mais vous devrez d'abord me rapporter ces objets:";
		mes "^FF335520 Resin,";
		mes "7 Poison Spores,";
		mes "41 Pointed Scales,";
		mes "13 Trunks,";
		mes "1 Red potion^000000";
		set @ARWCRFT, 1;
		emotion e_ic;
		close;
	M_1:
		mes "[Roberto]";
		mes "................................................................";
		emotion e_ag;
		next;
		mes "[Roberto]";
		mes "Je vais vous apprendre une compétence très spéciale... Je l'appelle un coup de pied dans le C......";
		emotion e_pif;
		next;
		mes "!!!!BAM!!!!";
		percentheal -5,0;
		close;

L_GetSkill:
	if((countitem(907)<20) || (countitem(7033)<7) || (countitem(906)<41) || (countitem(1019)<13) || (countitem(501)<1)) goto L_NotEnuf;
	delitem 907, 20;
	delitem 7033, 7;
	delitem 906, 41;
	delitem 1019, 13;
	delitem 501, 1;
	mes "Vous avez tout apporté... Très bien.";
	next;
	mes "~ il vous confie une longue liste d'ingrédients ~";
	next;
	mes "[Roberto]";
	mes "Voilà pour vous. C'est tout ce dont vous aurez besoin.";
	next;
	mes ".......................";
	next;
	mes "[Roberto]";
	mes "Hum?...... Et bien oui, c'est tout. Qu'est-ce que vous attendiez de plus? Grâce à cette liste, vous pourrez fabriquer n'importe quel type de flèche!.......";
	next;
	mes "[Roberto]";
	mes "Vous n'espériez pas une grande cérémonie, non plus?.......";
	next;
	skill 147,1,0;
	set @ARWCRFT, 0;
	mes "[Roberto]";
	mes "...... Bonne chance! .......";
	close;

	L_NotEnuf:
		mes "Je suis désolé, mais il vous manque certains objets...";
		next;
		goto M_0;
L_GotSkill:
	mes "Hum?.... Désolé, mais je n'ai rien d'autre à vous apprendre.";
	close;
L_LowLvl:
	mes "Hum?... Vous vouliez me parler? Désolé mais je n'ai rien à vous dire...";
	emotion e_what;
	next;
	mes "[Roberto]";
	mes "Je ne parle pas aux Archers inexpérimentés tels que vous... Peut-être que, si vous vous entraîner un peu plus, je pourrais reconsidérer la chose...";
	close;
}


//<============================== Arrow Repel ===============================>\\
payon,103,63,5	script	Jason	88,{
	mes "[Jason]";
	if(baseClass == Job_Archer) goto L_Start;

L_Other:
	mes "Quelle valeur a la vie pour un misérable comme moi?";
	close;
L_Start:
	if(getskilllv(148)>0) goto L_GotSkill;
	if(BaseJob == Job_Archer && JobLevel < 35) goto L_LowLvl;
	if(@ARWREP) goto L_Check;
	mes "Bonjour, je suis le Maître Archer qui a inventé la compétence ^0033FFArrow Repel^000000. Je peux vous l'enseigner si vous me rapportez les ingrédients qu'utilisait ma grand-mère dans sa soupe si délicieuse!";
	next;
	mes "[Jason]";
	mes "La compétence Arrow Repel vous permet de garder vos ennemis à bonne distance.";
	next;

	L_List:
	mes "[Jason]";
	mes "Ah, la soupe?";
	mes "Oui, voici les ingrédients:";
	mes "- 2 ^FF0000Emeralds^000000";
	mes "- 3 ^FF0000Yoyo tails^000000";
	mes "- 10 ^FF0000Tentacles^000000";
	mes "- 10 ^FF0000Bill of birds^000000";
	mes "- 36 ^FF0000Banana Juices^000000";
	next;
	mes "[Jason]";
	mes "Dépêchez-vous!";
	set @ARWREP, 1;
	close;

L_LowLvl:
	mes "Oh, je pense que vous n'êtes pas assez expérimenté... Peut-être que, si vous vous entraîner un peu plus, je pourrais vous apprendre quelque chose d'utile...";
	close;

L_Check:
	if((countitem(721)<2) || (countitem(942)<3) || (countitem(962)<10) || (countitem(925)<10) || (countitem(532)<36)) goto L_NotEnuf;
	delitem 721,2;
	delitem 942,3;
	delitem 962,10;
	delitem 925,10;
	delitem 532,36;
	mes "OH! Merveilleux! Vous avez apporté tous les ingrédients!";
	mes "Je vais maintenant vous enseigner la compétence ^0033FFArrow Repel^000000.";
	next;
	skill 148,1,0;
	set @ARWREP, 0;
	mes "[Jason]";
	mes "Et voilà! Bonne chasse!";
	close;

	L_NotEnuf:
		mes "Je suis désolé, mais il vous manque certains objets...";
		next;
		goto L_List;
L_GotSkill:
	mes "Hé! Encore vous? J'ai bien peur de n'avoir plus rien à vous apprendre.";
	mes "Ah, la soupe? Elle était délicieuse, merci.";
	close;
}