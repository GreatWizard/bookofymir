//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script =======================================
//= Mage Skill Quest
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.4b
//===== Compatible With: =====================================
//= eAthena 7.15 +
//===== Description: =========================================
//= <Description> 
//===== Additional Comments: =================================
//= v1.0a Now using functions found in "Global_Functions.txt"
//= for class checks.[kobra_k88]
//= 1.2 Added Baby Class Support [Lupus]
//= 1.3 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 1.4 Fixed exploit [Lupus]
//= 1.4a Fixed some typos [IVBela]
//= 1.4b changed perm. variables to temp ones [Lupus]
//============================================================



geffen_in,151,119,4	script	BLIZZARDRISS	71,{
	mes "[BLIZZARDRISS]";
	if(baseClass == Job_Mage) goto L_Start;

L_Other:
	mes "Quel nouveau sortilège vais-je apprendre aujourd'hui......??";
	emotion e_hmm;
	close;

L_Start:
	if(@ENRGYCOAT) goto L_GetSkill;
	if(getskilllv(157)>0) goto L_GotSkill;
	mes "Hé! Mon ami! Je vois que vous êtes magicien. Si vous cherchez à l'intérieur de vous-même, peut-être découvrirez-vous des capacités intérieures cachées!!";
	next;
	menu "Capacités cachées.....?",M_0, "Annuler.",M_End;

	M_0:
		mes "[BLIZZARDRISS]";
		mes "Depuis des années j'ai étudié la magie ancienne de Geffen, et j'ai récemment découvert un sortilège inusité mais très pratique!";
		next;
		mes "[BLIZZARDRISS]";
		mes "Ce sortilège utilise votre énergie pyschocinétique afin de couvrir votre corps d'un bouclier magique.";
		next;
		mes "[BLIZZARDRISS]";
		mes "Seuls les utilisateurs confirmés en magie arriveront à maîtriser ce sortilège correctement!";
		next;
		mes "[BLIZZARDRISS]";
		mes "Ce sortilège se nomme ^5555FF'Energy Coat'^000000!. Voulez-vous que je vous l'enseigne?";
		next;
		menu "Wahou! Ça a l'air super!",sM_0, "Hum... pas intéressé.",sM_End;

		sM_0: 
			mes "[BLIZZARDRISS]";
			mes "Si vous voulez que je vous apprenne ce sortilège, apportez-moi:";
			mes "^5533FF 3 Glass Beads,";
			mes "1 One Carat Diamond,";
			mes "5 Shells,^000000";
			mes "et ^5533FF1 Solid shell^000000.";
			next;
			mes "[BLIZZARDRISS]";
			mes "Vous aurez également besoin d'avoir suffisamment d'expérience. Pour les Mages, le Niveau de Job 35 est requis au minimum.";
			mes "Pour les Sorciers ou les Sages, qui sont expérimentés en utilisation de la magie, il n'y a pas de Niveau minimum requis.";
			next;
			mes "[BLIZZARDRISS]";
			mes "Revenez quand vous serez bien entraîné et que vous aurez tous les objets requis!";
			set @ENRGYCOAT, 1;
			close;

		sM_End:
			mes "[BLIZZARDRISS]";
			mes "Humf! Faites comme bon vous semble. Mais ne venez pas vous plaindre après!!";
			emotion e_an;
			close;

	M_End:
		mes "[BLIZZARDRISS]";
		mes "L'homme sage connaît la patience! Entraînez-vous et revenez quand vous serez prêt.";
		close;

L_GetSkill:
	if(BaseJob==Job_Mage && JobLevel < 35) goto L_JobLvl;
	if(countitem(746)<3 || countitem(730)<1 || countitem(935)<5 || countitem(943)<1) goto L_Items;
	delitem 746,3;
	delitem 730,1;
	delitem 935,5;
	delitem 943,1;
	mes "Ohh mon dieu, mon dieu...... C'est l'heure de la leçon...";
 	next;
	mes "~ de nombreuses heures plus tard ~";
	next;
	skill 157,1,0;
	set @ENRGYCOAT,0;
 	mes "[BLIZZARDRISS]";
 	mes "Vous pouvez maintenant utiliser le Sortilège 'Energy Coat'!! Utilisez-le avec sagesse, mon ami!";
	emotion e_no1;
 	close;

	L_JobLvl:
		mes "Vous n'avez pas écouté mes explications? Vous devez avoir un Niveau de Job de 35 au minimum si vous voulez apprendre 'Energy Coat'.";
		emotion e_what;
		close;

	L_Items:
		mes "Vous n'avez pas écouté mes explications? Vous devez m'apporter les objets suivants si vous voulez apprendre 'Energy Coat':";
		mes "^5533FF 3 Glass Beads,";
		mes "1 One Carat Diamond,";
		mes "5 Shells,^000000";
		mes "et ^5533FF1 Solid shell^000000.";
		emotion e_what;
		close;

L_GotSkill:
	mes "Bien le bonjour! Alors, le sortilège 'Energy Coat' est super, n'est-ce pas? Bonne chance pour vos voyages.";
	close;
}