//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Quest NPCs related to Payon
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= Pretend Murder, Ear Muffs, Oxygen, Derivuchi Cap, Helm of Angel Quests.
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Helm of Angel bug fixed [Lupus]
//= 1.2 Fixed skirt of virgin [Lupus]
//= 1.3 Fixed exploits [Lupus]
//= 1.4 Removed Duplicates [Silent]
//= 1.5 Updated Dialogs from "Pretend Murdered Quest" [Samuray22]
//= 1.5a replaced item "names" with item id. [Lupus]
//= 1.6 Updated Dialogs from "Oxygen Mask Quest" [Samuray22]
//============================================================ 



//==========================================================================================//
//				Pretend Murdered quest
//==========================================================================================//
payon,154,170,4	script	Grand Mère	78,{
	if(countitem(1049) > 3 ) {
		mes "[Grand Mère]";
		mes "Oh... Vous avez réussi à trouver des Skirt of Virgin! Avec tout ça je pourrais confectionner des déguisements pour mes petits enfants pour le festival du mois prochain...";
		mes "Est-ce que vous pourriez me donner vos ^3355FFSkirt of Virgin^000000? Il m'en faudrait quatre... s'il vous plaît?";
		mes "Je vous en prie!";
		next;
		switch(select( "Lui donner.","Pas question!")) {
		case 1:
			delitem 1049,4;
			mes "[Grand Mère]";
			mes "Uh-uh... Merci infiniment! Maintenant je vais pouvoir confectionner des déguisements pour mes petits enfants.";
			mes "Et... Oh! Mais oui! Quand j'étais jeune, j'étais une actrice de théâtre...";
			mes "J'ai conservé un objet de ce temps là, alors que je venais de quitter la troupe et me marier...";
			next;
			mes "[Grand Mère]";
			mes "Tenez, prenez-le... S'il vous plaît!";
			mes "Je l'avais toujours gardé en souvenir... Je suis certaine que vous rigolerez beaucoup avec cet objet!";
			getitem 2293,1;
			close;
		case 2:
			mes "[Grand Mère]";
			mes "Uh-uh... Comment pourrais-je un jour me procurer des ^3355FFSkirt of Virgin^000000 toute seule... Je suis si faible et si fatiguée.";
			mes "Quel monde froid et sans pitié...";
			close;
		}
	}
		mes "[Grand Mère]";
		mes "Je ramasse des champignons dans montagne de Payon tous les jours afin de gagner ma vie. Je ne sais pas si j'aurais de quoi manger à ma faim le mois prochain...";
		mes "... Mais il y a un festival le mois prochain!";
		mes "Mes petits enfants... Je voulais leur coudre des déguisements pour qu'ils puissent s'amuser comme n'importe quels autres petits...";
		mes "L'idéal aurait été ce tissus, le ^3355FFSkirt of Virgin^000000 ...";
		next;
		mes "[Grand Mère]";
		mes "Jeune aventurier... J'ai une faveur à vous demander... Si jamais vous trouvez des ^3355FFSkirt of Virgin^000000...";
		mes "Vous pourriez m'en apporter? Je vous en serais éternellement reconnaissante...";
		close;
}


//========================================================================================//
//					Ear Muffs Quest
//========================================================================================//
payon_in01,18,10,4	script	Jeune Fille	75,{
	mes "[Jeune Fille Mystérieuse]";
	mes "Bonjour.";
	next;
	menu "Salut.",-,"Pourriez-vous me fabriquer des Ear Muffs?",M_Muffs, "Au revoir.",M_End;

		mes "[Jeune Fille Mystérieuse]";
		mes "Ma famille et moi avons récemment emménagé à Payon.";
		mes "Avant nous habitions dans une région au climat très froid, et nous avions développé un commerce d'^5555FFEar Muffs^000000 faits mains de grande qualité!";
		next;
		mes "[Jeune Fille Mystérieuse]";
		mes "J'essaye de faire perdurer l'entreprise familiale ici, à Payon, mais comme il fait chaud ce n'est pas très rentable.";
		next;
		mes "[Jeune Fille Mystérieuse]";
		mes "Même si les Ear Muffs ne sont pas vraiment utiles ici, si vous voyagez régulièrement dans des pays froids, nos Ear Muffs pourraient beaucoup vous aider.";
		next;
		mes "[Jeune Fille Mystérieuse]";
		mes "Si ça vous intéresse, tout ce dont j'ai besoin est:";
		mes "^5555FF1 Cursed Ruby,";
		mes "1 Headset,";
		mes "200 Feathers,";
		mes "et 5000 Zenys^000000.";
		next;
		mes "[Jeune Fille Mystérieuse]";
		mes "Avec tous ces matériaux, je pourrais vous fabriquer une paire d'Ear Muffs. Croyez-moi, ils sont de la meilleure qualité qui existe!";
		close;
	
	M_Muffs:
		if(countitem(724)<1 || countitem(5001)<1 || countitem(949)<200 || zeny<5000) goto sL_NotEnuf;
		delitem 724,1;
		delitem 5001,1;
		delitem 949,200;
		set Zeny, Zeny-5000;
		mes "[Jeune Fille Mystérieuse]";
		mes "Oh, je vois que vous avez rapporté tous les matériaux... Donnez-moi quelques instants...";
		next;
		mes "[Jeune Fille Mystérieuse]";
		mes "... ceci va comme ça ... ceci va là ...... un peu de glue ici ... quelques soudures par là..........";
		next;
		getitem 2283,1;
		mes "[Jeune Fille Mystérieuse]";
		mes "Tenez! Une paire d'Ear Muffs. Bonne journée!";
		close;

		sL_NotEnuf:
			mes "[Jeune Fille Mystérieuse]";
			mes "Je suis désolée, mais il vous manque des matériaux...";
			close;
	M_End:
		close;

}


//========================================================================================//
//					Oxygen Mask Quest
//========================================================================================//
payon_in02,25,71,3	script	Jeune Homme#11	86,{
	if(countitem(701) > 4) {
		mes "[Jeune Homme]";
		mes "ArrrGgghhh!! Non!... Ce n'est pas possible!..... Je crois que je vais devenir fou...";
		next;
		switch(select( "Lui montrer l'Ora Ora.","Lui donner l'Ora Ora.","Annuler.")) {
		case 1:
			mes "[Jeune Homme]";
			mes "Oooooohhhh..... c'est.. c'est... de... l'Ora Ora...??? Oh donnez le moi, je vous en supplie!";
			close;
		case 2:
			delitem 701, 5;
			mes "[Jeune Homme]";
			mes "Muhahahahah!!! J'ai finalement réussi en avoir... de l'Ora Ora! Stupides et dégoûtants petits insectes... vous allez payer pour avoir envahi ma maison!!";
			next;
			mes "[Jeune Homme]";
			mes "..... Euh hem... désolé. Je suis un peu surmené.... Laissez-moi vous récompenser en échange de ce merveilleux trésor que vous m'avez rapporté......";
			next;
			getitem 5004, 1;
			mes "[Jeune Homme]";
			mes "Si vous portez ce masque sur votre bouche, l'air que vous respirerez sera filtré et donc assez pur.";
			mes "Je l'ai trouvé quand je travaillais en tant que marin. Et comme je n'en ai plus besoin...";
			next;
			mes "[Jeune Homme]";
			mes "Hehehehe!! Je ne peux plus attendre, il faut que j'essaye ces Ora Ora!!!";
			close;
		case 3:
			mes "[Jeune Homme]";
			mes "Comment faire pour se débarrasser de ces satanés insectes?... Et si jamais je n'y arrivais pas...?!";
			close;
		}
	}
		mes "[Jeune Homme]";
		mes "Uh...Ah! Je n'en peux plus!";
		mes "Satanés insectes!!";
		next;
		if( select( "L'écouter.","S'en aller.") == 1) {
			mes "[Jeune Homme]";
			mes "~Soupir~ J'ai n'ai pas eu une vie facile. Vous savez, mes parents sont morts quand je n'étais encore qu'un petit garçon, alors j'ai du me prendre en mains très tôt.";
			next;
			mes "[Jeune Homme]";
			mes "Ça n'a pas toujours été facile, mais après 10 ans de travail acharné, j'ai réussi à construire une maison de mes propres mains!";
			next;
			mes "[Jeune Homme]";
			mes "Malheureusement, comme je n'avais pas beaucoup d'argent, ce n'était pas tout à fait la maison de mes rêves...";
			mes "Et puis j'ai trouvé cette maison... Elle était grande et bon marché... alors je l'ai achetée dans un coup de tête, sans vraiment réfléchir....";
			next;
			mes "[Jeune Homme]";
			mes "Quelle erreur monumentale... Il s'est avéré que cette maison était remplie de MILIERS d'INSECTES!!!";
			next;
			mes "[Jeune Homme]";
			mes "J'ai TOUT essayé pour m'en débarrasser! Je les ai écrasés, aspergés d'insecticide, j'ai posé des pièges à insectes...";
			mes "Rien n'y a fait! Alors finalement, j'ai même essayé de les brûler!!";
			next;
			mes "[Jeune Homme]";
			mes "Grrr! Mais que diable faut-il faire pour se débarrasser de ces saletés?!";
			next;
			mes "[Jeune Homme]";
			mes "J'ai entendu parler d'un objet appelé ^3355FFOra Ora^000000, que les gens utilisent dans certains pays pour combattre les insectes...";
			mes "Mais est-ce que ça suffira dans mon cas?";
			next;
			mes "[Jeune Homme]";
			mes "Je donnerai n'importe quoi pour ne plus voir ces sales bêtes... Comme je n'ai pas beaucoup d'argent, je ne peux pas me payer ^5555FF'5 Ora Oras'^000000.";
			mes "Si jamais vous trouvez des Ora Ora par hasard, faîtes le moi savoir... s'il vous plaît?";
			close;
		}
			mes "[Jeune Homme]";
			mes "Je suis fatigué et j'en ai assez de me battre sans arrêt contre ces insectes... Ces sales bêtes!!";
			close;
}


//========================================================================================//
//			Derivuchi Cap(Hat of Petite Diablo), Helm of Angel
//========================================================================================//
payon_in01,56,12,4	script	[Jeune Homme]#12	89,{
	mes "[Jeune Homme]";
	mes "Que voulez-vous?............";
	next;
	menu "Pourriez-vous me fabriquer un chapeau?",-, "Euh... rien.",M_End;

		mes "[Jeune Homme]";
		mes "Je vois... Vous avez entendu parlé de moi... D'accord, dîtes-moi ce que vous voulez.";
		next;
		menu "-Helm of Angel",-, "-Deviruchi Hat",sM_Devir, "-Je reviendrai plus tard",M_End;

			mes "[Jeune Homme]";
			mes "Pour un Helm of Angel j'aurais besoin de:";
			mes "- ^5555FF1 Helm (slotted)^000000.";
			mes "- ^5555FF1 Angel Wing^000000.";
			mes "- ^5555FF5 Fang of Garm^000000.";
			set @HAT, 1;
			next;
			goto sL_GetHat;
		sM_Devir:
			mes "[Jeune Homme]";
			mes "Pour un Deviruchi Hat j'aurais besoin de:";
			mes "- ^5555FF600 Little Evil Horn^000000.";
			mes "- ^5555FF40 Talon of Griffon^000000.";
			set @HAT, 2;

			sL_GetHat:
				mes "[Jeune Homme]";
				mes "Attendez un peu! Faîtes attention à ce que vous allez me donner.";
				mes "S'il vous manque un objet, je ne ferai pas la distinction entre l'équipement que vous portez et les matériaux demandés.";
				mes "Si vous ne voulez pas risquer de perdre un objet qui vous est cher, laissez-le à l'abri dans votre stock.";
				next;
				mes "[Jeune Homme]";
				mes "Avez-vous tous les matériaux nécessaires?";
				menu "-Lui donner.",-, "S'en aller.",M_End;

				if(@HAT == 2) goto ssL_Devir;

					if(countitem(2229)<1 || countitem(2254)<1 || countitem(7036)<5) goto ssL_NotEnuf;
					delitem 2229, 1;
					delitem 2254, 1;
					delitem 7036, 5;
					mes "[Jeune Homme]";
					mes "Voici votre Helm of Angel.";
					getitem 5025, 1;
					close;
				ssL_Devir:
					if(countitem(1038)<600 || countitem(7048)<40) goto ssL_NotEnuf;
					delitem 1038, 600;
					delitem 7048, 40;
					mes "[Jeune Homme]";
					mes "Voici votre Deviruchi Hat.";
					getitem 5038, 1;
					close;
				ssL_NotEnuf:
					mes "[Jeune Homme]";
					mes "Vous n'avez pas tous les matériaux nécessaires.";
					close;

	M_End:
		mes "[Jeune Homme]";
		mes "D'accord, je vois.....";
		close;

}
