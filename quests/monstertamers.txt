//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script =======================================
//= Monster Tamers
//===== By: ==================================================
//= kobra_k88, Darkchild, x[tsk]
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= eAthena 1.0
//===== Description: =========================================
//= Cute pet quest items
//===== Additional Comments: =================================
//= May not have all of the Monster Tamers, but the ones here are fully working.
//= 1.1 Fixed exploits [Lupus]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Added Bongun's taming item quest. Credits to Lone_Wolf. [SinSloth]
//============================================================


//<================================================= Alberta ===============================================>\\
alberta_in,173,77,3	script	Iwado#01	125,{
	mes "[Iwado]";
	mes "Apprivoiser un monstre est devenu très à la mode ces derniers temps! Pourquoi ne pas essayer vous aussi? ....";
M_Menu:
	next;
	menu 	"^5555FFApprivoiser un monstre^000000?",M_0,
		"^5555FF'Monster Juice'^000000?",M_1,
		"^5555FF'Singing Flower'^000000?",M_2,
		"^5555FF'Wild Flower'^000000?",M_3,
		"Pourriez-vous fabriquer un appât pour moi?",M_Make,
		"Annuler.",M_End;
	M_0:
		mes "[Iwado]";
		mes "Apprivoiser un monstre permet de pouvoir obtenir le monstre en question en tant qu'animal de compagnie. Bien évidement, tous les monstres ne se laissent pas apprivoiser facilement.";
		mes "Il faut utiliser un ^5555FF'appât'^000000 pour pouvoir sensibiliser les monstres et les faire devenir des animaux de compagnie.";
		next;
		mes "[Iwado]";
		mes "Ces appâts sont très durs à obtenir, mais il existe des membres de la Guilde d'Apprivoisement des Monstres, tel que moi, qui peuvent aider les amoureux des animaux à obtenir des appâts plus facilement.";
		next;
		mes "[Iwado]";
		mes "Voici les appâts que je vous propose:";
		mes "^5555FF'Monster Juice'^000000";
		mes "^5555FF'Singing Flower'^000000";
		mes "^5555FF'Wild Flower'^000000";
		next;
		mes "[Iwado]";
		mes "Ce sont des appâts vraiment efficaces! Vous devriez en essayer un!";
		goto M_Menu;
	M_1:
		mes "[Iwado]";
		mes "C'est une boisson faite à partir de sang d'animaux. Personne de sensé ne voudrait boire ce truc là, mais pour les '^FF5555'Hunter Fly'^000000, c'est une douceur de choix.";
		next;
		mes "[Iwado]";
		mes "Je peux vous fabriquer du ^5555FF'Monster Juice'^000000 à partir de:";
		mes "-^FF5555'1 Animal Gore'";
		mes "-'2 Apples'^000000";
		next;
		mes "[Iwado]";
		mes "Si vous me rapportez ces ingrédients, je vous fabriquerai un appât dans les règles de l'art!";
		goto M_Menu;
	M_2:
		mes "[Iwado]";
		mes "C'est la fleur d'une Singing Plant. Elle est unique et très rare, parfaite pour appâter les ^FF5555'Rockers'^000000.";
		next;
		mes "[Iwado]";
		mes "Je peux vous donner une ^5555FF'Singing Flower'^000000 si vous me rapportez:";
		mes "-^FF5555'1 Singing Plant'^000000";
		next;
		mes "[Iwado]";
		mes "Si vous me rapportez cet ingrédient, je vous fabriquerai un appât dans les règles de l'art!";
		goto M_Menu;
	M_3:
		mes "[Iwado]";
		mes "C'est une fleur sauvage qui pousse dans certaines régions de Rune-Midgard. On peut en utiliser une pour appâter un ^FF5555Orc Warrior^000000.";
		mes "Je peux vous donner une ^5555FF'Wild Flower'^000000 si vous me rapportez:";
		mes "-^FF5555'1 Fancy Flower'";
		mes "-'1 Clover'^000000";
		next;
		mes "[Iwado]";
		mes "Si vous me rapportez ces ingrédients, je vous fabriquerai un appât dans les règles de l'art!";
		goto M_Menu;
	M_Make:
		mes "[Iwado]";
		mes "Quel appât voudriez-vous?";
		next;
		menu "^5555FF'Monster Juice'^000000",sM_1, "^5555FF'Singing Flower'^000000",sM_2, "^5555FF'Wild Flower'^000000",sM_3, "Rien, merci",M_End;

		sM_1:
			if(countitem(702)<1 || countitem(512)<2) goto sL_NoItems;
			delitem 702,1;
			delitem 512,2;
			mes "[Iwado]";
			mes "Très bien, donnez-moi une minute, que je prépare l'appât...";
			next;
			mes "[Iwado]";
			mes "Et voilà! Du 'Monster Juice'! Utilisez-le à bon escient, et revenez quand vous voulez!";
			getitem 626,1;
			close;
		sM_2:
			if(countitem(707)<1) goto sL_NoItems;
			delitem 707,1;
			mes "[Iwado]";
			mes "Très bien, donnez-moi une minute, que je prépare l'appât...";
			next;
			mes "[Iwado]";
			mes "Et voilà! Une 'Singing Flower'! Utilisez-la à bon escient, et revenez quand vous voulez!";
			getitem 629,1;
			close;
		sM_3:
			if(countitem(2207)<1 || countitem(705)<1) goto sL_NoItems;
			delitem 2207,1;
			delitem 705,1;
			mes "[Iwado]";
			mes "Très bien, donnez-moi une minute, que je prépare l'appât...";
			next;
			mes "[Iwado]";
			mes "Et voilà! Une 'Wild Flower! Utilisez-la à bon escient, et revenez quand vous voulez!";
			getitem 10009,1;
			close;

			sL_NoItems:
				mes "[Iwado]";
				mes "Vous n'avez pas tous les ingrédients nécessaires pour cet appât. Revenez quand vous aurez tout.";
				close;
	M_End:
		mes "[Iwado]";
		mes "Oh... Je vois. Vous avez du mal à choisir quel monstre vous voulez élever pour l'instant. C'est une décision très importante, je vous comprends. Prenez votre temps.";
		close;

}


//<================================================= Al De Baran ===============================================>\\
aldeba_in,167,177,3	script	Yuu#02	125,{
	mes "[Yuu]";
	mes "Vous cherchez des appâts ou des accessoires? Quand il s'agit d'apprivoiser un monstre, personne dans Rune Midgard ne pourra mieux vous renseigner que moi.";
M_Menu:
	next;
	menu 	"^5555FF'Apprivoiser un monstre'^000000?",M_0,
		"^5555FF'Skull Helm'^000000?",M_1,
		"^5555FF'Monster Oxygen Mask'^000000?",M_2,
		"^5555FF'Silk Ribbon'^000000?",M_3,
		"^5555FF'Stellar Hairpin'^000000?",M_4,
		"^5555FF'Tiny Egg Shell'^000000?",M_5,
		"^5555FF'Rocker Glasses'^000000?",M_6,
		"Pourriez-vous fabriquer un accessoire pour moi?",M_Make,
		"Annuler",M_End;
	M_0:
		mes "[Yuu]";
		mes "Apprivoiser un monstre permet de pouvoir avoir le monstre en question en tant qu'animal de compagnie. Bien évidement, tous les monstres ne se laissent pas apprivoiser facilement.";
		mes "Il faut utiliser un ^5555FF'appât'^000000 pour pouvoir sensibiliser les monstres et les faire devenir des animaux de compagnie.";
		next;
		mes "[Yuu]";
		mes "Comme tous les animaux de compagnie, les monstres apprivoisés ont besoin qu'on leur témoigne de l'amour et qu'on s'occupe d'eux. C'est vraiment très important si on souhaite avoir un animal de compagnie.";
		next;
		mes "[Yuu]";
		mes "Le meilleur moyen de montrer votre affection à votre petit compagnon est d'obtenir un 'accessoire' qui puisse le différencier de ses autres congénères de Rune Midgard.";
		next;
		mes "[Yuu]";
		mes "Ces ^5555FF'Accessoires pour Animaux de Compagnie'^000000 sont très durs à obtenir. Mais il existe des membres de la Guilde d'Apprivoisement des Monstres, tel que moi, qui peuvent aider les amoureux des animaux à obtenir des accessoires plus facilement.";
		next;
		mes "[Yuu]";
		mes "Voici les accessoires que je vous propose:";
		mes "^5555FF'Skull Helm'";
		mes "'Monster Oxygen Mask'";
		mes "'Silk Ribbon'";
		mes "'Stellar Hairpin'";
		mes "'Tiny Egg Shell'";
		mes "'Rocker Glasses'^000000";
		next;
		mes "[Yuu]";
		mes "Ce sont des accessoires vraiment jolis! Vous devriez en essayer un!";
		goto M_Menu;
	M_1:
		mes "[Yuu]";
		mes "Cela pourra peut-être vous effrayer, mais ce casque fait en os a un mystérieux pouvoir de domptage sur les monstres, mais rend fou tout être humain qui le porte!";
		mes "Il irait particulièrement bien pour votre '^FF5555'Baphomet Jr'^000000.";
		next;
		mes "[Yuu]";
		mes "Je peux vous fabriquer un ^5555FF'Skull Helm'^000000 en échange de:";
		mes "-^FF5555'1 Bone Helm'^000000";
		next;
		mes "[Yuu]";
		mes "Si vous me rapportez cet ingrédient, je vous fabriquerai un accessoire dans les règles de l'art!";
		goto M_Menu;
	M_2:
		mes "[Yuu]";
		mes "Ce masque, une fois porté, rend les monstres difficiles à différencier d'un être humain, telle une illusion.";
		mes "Ce serait un superbe cadeau pour votre ^FF5555'Choncon', 'Steel Choncon', ou 'Hunter Fly'^000000.";
		next;
		mes "[Yuu]";
		mes "Je peux vous fabriquer un ^5555FF'Monster Oxygen Mask'^000000 en échange de:";
		mes "-^FF5555'1 Oxygen Mask'^000000";
		next;
		mes "[Yuu]";
		mes "Si vous me rapportez cet ingrédient, je vous fabriquerai un accessoire dans les règles de l'art!";
		goto M_Menu;
	M_3:
		mes "[Yuu]";
		mes "C'est un ruban tissé à partir d'une soie d'une extraordinaire douceur. Il rendra votre ^FF5555'Lunatic'^000000 encore plus mignon qu'il ne l'est déjà!.";
		next;
		mes "[Yuu]";
		mes "Je peux vous fabriquer un ^5555FF'Silk Ribbon'^000000 en échange de:";
		mes "-^FF5555'1 Ribbon (0 Slotts)'^000000";
		next;
		mes "[Yuu]";
		mes "Si vous me rapportez cet ingrédient, je vous fabriquerai un accessoire dans les règles de l'art!";
		goto M_Menu;
	M_4:
		mes "[Yuu]";
		mes "C'est un bijou très pointu, qui possède un pouvoir mystérieux si bien que les monstres veulent le porter à tout prix!";
		mes "Votre ^FF5555'Green Petite'^000000 l'adorera, c'est certain!";
		next;
		mes "[Yuu]";
		mes "Je peux vous fabriquer un ^5555FF'Stellar Hairpin'^000000 en échange de:";
		mes "-^FF5555'1 Stellar'^000000";
		next;
		mes "[Yuu]";
		mes "Si vous me rapportez cet ingrédient, je vous fabriquerai un accessoire dans les règles de l'art!";
		goto M_Menu;
	M_5:
		mes "[Yuu]";
		mes "Bien que minuscule, cette armure étincelante est particulièrement résistante. Elle protègera parfaitement votre ^FF5555'Picky'^000000.";
		mes "Je peux vous fabriquer un ^5555FF'Tiny Egg Shell'^000000 en échange de:";
		mes "-^FF5555'1 Egg Shell'^000000";
		next;
		mes "[Yuu]";
		mes "Si vous me rapportez ces ingrédients, je vous fabriquerai un accessoire dans les règles de l'art!";
		goto M_Menu;
	M_6:
		mes "[Yuu]";
		mes "Cette paire de lunettes est typique de celles que portent les geeks. Elle sera parfaite pour votre ^FF5555Rocker^000000.";
		next;
		mes "[Yuu]";
		mes "Je peux vous fabriquer une paire de ^5555FF'Rocker Glasses'^000000 en échange de:";
		mes "-^FF5555'1 Ribbon (0 Slotts)'";
		mes "-'2 Zargon'";
		mes "-'400 Zeny'^000000";
		next;
		mes "[Yuu]";
		mes "Si vous me rapportez ces ingrédients, je vous fabriquerai un accessoire dans les règles de l'art!";
		goto M_Menu;
	M_Make:
		mes "[Yuu]";
		mes "Quel accessoire voudriez-vous?";
		next;
		menu 	"^5555FF'Skull Helm'^000000",sM_1,
			"^5555FF'Monster Oxygen Mask'^000000",sM_2,
			"^5555FF'Silk Ribbon'^000000",sM_3,
			"^5555FF'Stellar Hairpin'^000000",sM_4,
			"^5555FF'Tiny Egg Shell'^000000",sM_5,
			"^5555FF'Rocker Glasses'^000000",sM_6,
			"Rien, merci",M_End;

		sM_1:
			if(countitem(5017)<1) goto sL_NoItems;
			delitem 5017,1;
			mes "[Yuu]";
			mes "Très bien, donnez-moi une minute, que je prépare l'accessoire...";
			next;
			mes "[Yuu]";
			mes "Et voilà! Un 'Skull Helm'! Utilisez-le à bon escient, et revenez quand vous voulez!";
			getitem 10001,1;
			close;
		sM_2:
			if(countitem(5004)<1) goto sL_NoItems;
			delitem 5004,1;
			mes "[Yuu]";
			mes "Très bien, donnez-moi une minute, que je prépare l'accessoire...";
			next;
			mes "[Yuu]";
			mes "Et voilà! Un 'Monster Oxygen Mask'! Utilisez-le à bon escient, et revenez quand vous voulez!";
			getitem 10002,1;
			close;
		sM_3:
			if(countitem(2208)<1) goto sL_NoItems;
			delitem 2208,1;
			mes "[Yuu]";
			mes "Très bien, donnez-moi une minute, que je prépare l'accessoire...";
			next;
			mes "[Yuu]";
			mes "Et voilà! Un 'Silk Ribbon'! Utilisez-le à bon escient, et revenez quand vous voulez!";
			getitem 10007,1;
			close;
		sM_4:
			if(countitem(2294)<1) goto sL_NoItems;
			delitem 2294,1;
			mes "[Yuu]";
			mes "Très bien, donnez-moi une minute, que je prépare l'accessoire...";
			next;
			mes "[Yuu]";
			mes "Et voilà! Un 'Stellar Hairpin'! Utilisez-le à bon escient, et revenez quand vous voulez!";
			getitem 10011,1;
			close;
		sM_5:
			if(countitem(5015)<1) goto sL_NoItems;
			delitem 5015,1;
			mes "[Yuu]";
			mes "Très bien, donnez-moi une minute, que je prépare l'accessoire...";
			next;
			mes "[Yuu]";
			mes "Et voilà! Un 'Tiny Egg Shell'! Utilisez-le à bon escient, et revenez quand vous voulez!";
			getitem 10012,1;
			close;
		sM_6:
			if(countitem(2208)<1 || countitem(912)<2 || Zeny<400) goto sL_NoItems;
			delitem 2208,1;
			delitem 912,2;
			set Zeny, Zeny-400;
			mes "[Yuu]";
			mes "Très bien, donnez-moi une minute, que je prépare l'accessoire...";
			next;
			mes "[Yuu]";
			mes "Et voilà! Une paire de 'Rocker Glasses'! Utilisez-les à bon escient, et revenez quand vous voulez!";
			getitem 10014,1;
			close;

			sL_NoItems:
				mes "[Yuu]";
				mes "Vous n'avez pas tous les ingrédients nécessaires pour cet accessoire. Revenez quand vous aurez tout.";
				close;
	M_End:
		mes "[Yuu]";
		mes "Oh... Je vois. Vous avez du mal à choisir quel monstre vous voulez élever pour l'instant. C'est une décision très importante, je vous comprends. Prenez votre temps.";
		close;

}


//<================================================= Izlude ===============================================>\\
izlude_in,128,64,3	script	Shogo#03	125,{
	mes "[Shogo]";
	mes "Vous voulez apprivoiser un monstre très particulier pour en faire votre animal de compagnie? Alors je suis la personne qu'il vous faut...";
M_Menu:
	next;
	menu 	"^5555FFApprivoiser un monstre^000000?",M_0,
		"^5555FF'Book of Devil'^000000?",M_1,
		"^5555FF'No Recipient'^000000?",M_2,
		"^5555FF'Horror of Tribe'^000000?",M_3,
		"Pourriez-vous fabriquer un appât pour moi?",M_Make,
		"Annuler",M_End;
	M_0:
		mes "[Shogo]";
		mes "Apprivoiser un monstre permet de pouvoir avoir le monstre en question en tant qu'animal de compagnie. Bien évidement, tous les monstres ne se laissent pas apprivoiser facilement.";
		mes "Il faut utiliser un ^5555FF'appât'^000000 pour pouvoir sensibiliser les monstres et les faire devenir des animaux de compagnie.";
		next;
		mes "[Shogo]";
		mes "Ces appâts sont très durs à obtenir, mais il existe des membres de la Guilde d'Apprivoisement des Monstres, tel que moi, qui peuvent aider les amoureux des animaux à obtenir des appâts plus facilement.";
		next;
		mes "[Shogo]";
		mes "Voici les appâts que je vous propose:";
		mes "^5555FF'Book of Devil'^000000,";
		mes "^5555FF'No Recipient'^000000,";
		mes "^5555FF'Horror of Tribe'^000000.";
		next;
		mes "[Shogo]";
		mes "Ce sont des appâts vraiment efficaces! Vous devriez en essayer un!";
		goto M_Menu;
	M_1:
		mes "[Shogo]";
		mes "Ce livre est bien trop puissant pour tout mortel qui voudrait en lire le contenu et percer le secret de l'apprivoisement du diable!";
		mes "Mais si vous le lisez, vous pourrez peut-être appâter un ^FF5555'Baphomet Jr.'^000000...";
		next;
		mes "[Shogo]";
		mes "Je peux vous fabriquer un ^5555FF'Book of Devil'^000000 en échange de:";
		mes "-^FF5555'1 Old Magic Book'";
		mes "-'2 Horrendous Mouth'^000000";
		next;
		mes "[Shogo]";
		mes "Si vous me rapportez ces ingrédients, je vous fabriquerai un appât dans les règles de l'art!";
		goto M_Menu;
	M_2:
		mes "[Shogo]";
		mes "C'est une vieille lettre qui n'a jamais pû être remise à son destinataire...";
		mes "Pour un être humain, c'est sans importance. Mais pour les ^FF5555'Munaks'^000000, cela présente un certain intérêt...";
		next;
		mes "[Shogo]";
		mes "Je peux vous fabriquer un ^5555FF'No Recipient'^000000 en échange de:";
		mes "-^FF5555'1 Old Portrait'^000000";
		next;
		mes "[Shogo]";
		mes "Si vous me rapportez cet ingrédient, je vous fabriquerai un appât dans les règles de l'art!";
		goto M_Menu;
	M_3:
		mes "[Shogo]";
		mes "Ce trophé est l'objet favori des ^FF5555'Orc Warriors'^000000.";
		mes "Je peux vous fabriquer un ^5555FF'Horror of Tribe'^000000 en échange de:";
		mes "-^FF5555'1 Chivalry Emblem'";
		mes "-'1 Scorpion Tail'^000000";
		next;
		mes "[Shogo]";
		mes "Si vous me rapportez ces ingrédients, je vous fabriquerai un appât dans les règles de l'art!";
		goto M_Menu;
	M_Make:
		mes "[Shogo]";
		mes "Quel appât voudriez-vous?";
		next;
		menu "^5555FF'Book of Devil'^000000",sM_1, "^5555FF'No Recipient'^000000",sM_2, "^5555FF'Horror of Tribe'^000000",sM_3, "Rien, merci",M_End;

		sM_1:
			if(countitem(1006)<1 || countitem(958)<2) goto sL_NoItems;
			delitem 1006,1;
			delitem 958,2;
			mes "[Shogo]";
			mes "Très bien, donnez-moi une minute, que je prépare l'appât...";
			next;
			mes "[Shogo]";
			mes "Et voilà! Un 'Book of Devil'! Utilisez-le à bon escient, et revenez quand vous voulez!";
			getitem 642,1;
			close;
		sM_2:
			if(countitem(7014)<1) goto sL_NoItems;
			delitem 7014,1;
			mes "[Shogo]";
			mes "Très bien, donnez-moi une minute, que je prépare l'appât...";
			next;
			mes "[Shogo]";
			mes "Et voilà! Un 'No Recipient'! Utilisez-le à bon escient, et revenez quand vous voulez!";
			getitem 636,1;
			close;
		sM_3:
			if(countitem(1004)<1 || countitem(904)<1) goto sL_NoItems;
			delitem 1004,1;
			delitem 904,1;
			mes "[Shogo]";
			mes "Très bien, donnez-moi une minute, que je prépare l'appât...";
			next;
			mes "[Shogo]";
			mes "Et voilà! Un 'Horror of Tribe'! Utilisez-le à bon escient, et revenez quand vous voulez!";
			getitem 635,1;
			close;

			sL_NoItems:
				mes "[Shogo]";
				mes "Vous n'avez pas tous les ingrédients nécessaires pour cet appât. Revenez quand vous aurez tout.";
				close;
	M_End:
		mes "[Shogo]";
		mes "Oh... Je vois. Vous avez du mal à choisir quel monstre vous voulez élever pour l'instant. C'est une décision très importante, je vous comprends. Prenez votre temps.";
		close;

}

//'Her heart' item to catch Bongun

comodo,112,182,0	script	Grand Mère	103,{
	if (countitem(1558) > 0) {
		mes "[Grand Mère]";
		mes "Aidez moi s'il vous plaît...";
		mes "Avez-vous vu ma petite fille,";
		if (Sex == 1) {
			mes "monsieur? Ma pauvre petite fille";
		}
		else {
			mes "mademoiselle? Ma pauvre petite fille";
		}
		mes "a disparu...";
		next;
		mes "[Grand Mère]";
		mes "Je ne me souviens plus précisément de la date, mais c'était lorsque nous vivions à Payon. Elle était tellement mignonne... Elle adorait quand je lui faisais une ^000077tresse^000000...";
		next;
		mes "[Grand Mère]";
		mes "Elle travaillait souvent avec le chef du village... Elle était si gentille, et s'entendait très bien avec le fils du chef...";
		next;
		mes "[Grand Mère]";
		mes "Mais un jour, notre déité s'est mis en colère et a lancé un sort sur le fils du chef du village, et il tomba très malade!";
		mes "Les habitants décidèrent que le meilleur moyen était de donner ma petite fille en mariage au fils du chef afin de lever la malédiction...";
		next;
		mes "[Grand Mère]";
		mes "Le fils du chef fut guéri, mais j'ai perdu ma petite fille!!";
		mes "Je ne peux pas le regarder en face sans penser à ma petite fille, alors j'ai fui cette situation et je suis venue ici, à Comodo...";
		next;

		switch(select("Allons, haut les coeurs!:Peut-être que ce journal...?")) {

		case 1:
			mes "[Grand Mère]";
			mes "Ma petite fille hante mes nuits.";
			mes "J'essaye de penser à autre chose depuis des années, mais c'est impossible...";
			close;

		case 2:
			if (countitem(901) > 0) {
				mes "[Grand Mère]";
				mes "Oh mon dieu!";
				mes "C'est le journal intime de ma petite fille!";
				mes "C'est son écriture! Oh mon dieu...!";
				next;
				mes "[Grand Mère]";
				if (Sex == 1) {
					mes "Jeune homme... Je peux vous le lire si vous voulez, et si";
				}
				else {
					mes "Jeune fille... Je peux vous le lire si vous voulez, et si";
				}
				mes "vous me donnez une Danggie. S'il vous plaît... Je n'ai plus aucun objet lui appartenant...";
				next;

				switch(select("Pas question:D'accord")) {

				case 1:
					mes "[Grand Mère]";
					mes "Oh...?";
					mes "J'en suis très attristée...";
					mes "Vraiment...";
					next;
					mes "[Grand Mère]";
					mes "Je peux simplement prier pour que notre déité prenne bien soin d'elle.";
					close;

				case 2:
					delitem 1558,1;
					delitem 901,1;
					mes "[Grand Mère]";
					if (Sex == 1) {
						mes "Oh!";
						mes "Merci du fond du coeur,";
						mes "jeune homme~!";
					}
					else {
						mes "Oh!";
						mes "Merci du fond du coeur,";
						mes "jeune fille~!";
					}
					next;
					mes "[Grand Mère]";
					mes "Je vais...";
					mes "Je vais vous en lire un passage.";
					mes "Voyons voir...";
					next;
					mes "[Grand Mère]";
					mes "Apparemment le chef du village a traité ma petite fille comme sa propre enfant!";
					mes "Il y a tant de souvenirs heureux dans ce livre!";
					next;
					getitem 659,1;
					mes "[Grand Mère]";
					mes "Oh, merci. Je sais maintenant que ma petite fille vit heureuse, et j'ai même un souvenir d'elle!";
					mes "Soyez béni!";
					close;

				}

			}

			else {
				mes "[Grand Mère]";
				mes "Comment? Quel journal? Tout de même pas CE journal...?";
				mes "Je... Je ne me souviens plus...";
				next;
				mes "[Grand Mère]";
				mes "Si seulement je pouvais ne serait-ce que lui refaire une ^000077tresse^000000, pouvoir toucher ses cheveux à nouveau...";
				mes "Oh, ma chère petite fille...";
				close;
			}


		}

	}

	else {
		mes "[Grand Mère]";
		mes "Ma chère petite fille...";
		mes "Pourquoi me l'a-t-on enlevée...?";
		close;
	}

}
