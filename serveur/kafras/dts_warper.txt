//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Dungeon Teleport Service
//===== By: ================================================== 
//= Evera
//===== Current Version: ===================================== 
//= 2.4
//===== Compatible With: ===================================== 
//= eAthena
//===== Description: =========================================
//= Cool Event Corp NPCs vs. Kafra Dungeon Teleport Service and voting system.
//= This is a special event on official servers where there's a
//= big competition between Kafra Corp. and Cool Event Corp.
//= To warp to selected levels of certain dungeons.
//= Also includes Cool Event Corp HQ NPCs.
//===== Variables: ===========================================
//=Server - 	$dtsvote		= Variable for # of votes
//=					= Positive = Cool Corp winning
//=					= Negative = Kafra Corp winning
//=		$dts			= Variable for DTS status
//=					= (1<<0) Election
//=					= (1<<1) Cool Corp Enabled
//=					= (1<<2) Kafra Enabled
//=					= (1<<3) No winner last election
//=					= (1<<4) Cool Corp won last election
//=					= (1<<5) Kafra won last election
//=		$dtsday			= Week count for dts vote count, used for resetting players' votes
//=Character -	dtseligible 		= eligibility status, 0 not eligible yet, 1 eligible.
//=					= $dtsday used when MISC_QUEST|128, and signifies that person already voted.
//=		MISC_QUEST		= |128 = eligible and voted.
//=NPC Func. -	arg(0) 			= 0, Cool Event Corp Voting Staff; 1, Kafra Corp Voting Staff
//=		arg(1)			= Kafra only, Cool Corp script does not use. Changes illus for different sprites.
//=					= 0 = 4_f_kafra6 (yellow ponytail, classic outfit, spr 112)
//=					= 1 = 4_f_kafra5 (pink hair, classic outfit, spr 113)
//=					= 2 = 4_f_kafra4 (orange short hair, classic outfit, spr 114)
//=					= 3 = 4_f_kafra3 (brown bangs, classic outfit, spr 115)
//=					= 4 = 4_f_kafra2 (brown ponytale, classic outfit, spr 116)
//=					= 5 = 4_f_kafra1 (blue hair, classic outfit, spr 117)
//=					= 6 = 4_f_agentkafra (purp hair, black outfit, spr 859)
//=					= 7 = 4_f_kafra8 (blue hair, brown outfit, spr 860)
//=					= 8 = 4_f_kafra9 (red hair, brown outfit, spr 861)
//=Temporary - 	@dtstemp		= Temporary buffer, used in GM vote-fixing
//=		@dtstemps$		= Temporary buffer, like above, but a string.
//=		$@dtstempg		= Temporary buffer, but global
//===== Additional Comments: ================================= 
//= Some unofficial translations, some unofficial dungeon warps
//= Also includes unofficial GM-enabled tweaking for script
//= Includes NPCs in Cool Event Corp headquarters
//= If new locations are found of this NPC, please report on forums
//= http://www.eathena.ws/board/index.php?showforum=106
//= Base level 60 required to vote
//===== Version History: ====================================
//= 1.0	Initial release [Evera]
//= 1.1	SVN release, removed from major town (found true info after research),
//=	put more arg(2) parameters, fixed a few bugs [Evera]
//= 1.2 Mushed some variables together, optimized a bit [Evera]
//= 1.3 Removed selfconfig [Evera]
//= 1.4 Changed global variables to read from 3 global variables, updated names,
//= 	fixed array bug [Evera]
//= 1.5 Mushed voting varialbes into 1 variable. [Evera]
//= 1.6 Removed Duplicates [Silent]
//= 1.7	Fixed bug of DTS_Admin not running by itself [Evera]
//= 1.8	Changed requirement to lvl 60 [Evera]
//= 1.9 Removed global eligibility option [Evera]
//= 2.0 Fixed zeny bug [Evera]
//= 2.1 Fixed headers with and updated with newer information [Evera]
//= 2.2 Changed dtseligible 2 to MISC_QUEST|128, to clean up variable usage. [Evera]
//=	Also added F_ClearGarbage function to the scripts. [Evera]
//= 2.3	Changed dtseligible to be $dtsday when MISC_QUEST|128
//=	Added $dtsday for vote session counter
//=	Fixed bug that involved having both warpers enabled at the same time. [Evera]
//=	New $dtsday system and bug fix[Evera]
//= 2.3a fixed 2 bugs with comparision [Lupus]
//= 2.3b corrected Glast warp coords [Lupus] 2.3c Bailand -> Bayalan
//= 2.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//============================================================ 

//Yuno
yuno,153,191,4	script	Votes Cool Event Corp.::CoolEventVotingStaff01	874,{
	callfunc "F_DTS_Warp",0,0;
}
yuno,162,191,6	script	Votes Kafra Corp.#01	861,{
	callfunc "F_DTS_Warp",1,8;
}
//Lighthalzen
lighthalzen,154,60,6	script	Votes Cool Event Corp.::CoolEventVotingStaff02	874,{
	callfunc "F_DTS_Warp",0,0;
}
//Prontera
prontera,147,125,6	script	Votes Cool Event Corp.::CoolEventVotingStaff03	874,{
	callfunc "F_DTS_Warp",0,0;
}
prontera,164,125,4	script	Votes Kafra Corp.#02	115,{
	callfunc "F_DTS_Warp",1,3;
}

//Cool Event Staff Headquarters NPCs
//Kudiuu (Maintenance Guy)
lhz_in02,20,274,6	script	Kudiuu#dts	851,{
	mes "[Kudiuu]";
	mes "Par tous les diables...!";
	mes "Est-ce que cette pièce sera nettoyée un jour?!";
	mes "*Tousse tousse* Il y a tellement de poussière ici, ce n'est pas bon pour la santé!";
	close;
}
//Cesuna (Zondaman at desk)
lhz_in02,36,284,1	script	Cesuna#01	874,{
	mes "[Cesuna]";
	mes "Argh! Je suis complètement enterré sous le boulot! Mais j'ai une flemme monstrueuse! J'ai besoin d'une pause...";
	next;
	mes "[Cesuna]";
	mes "*Soupir...*";
	mes "Je me demande si Saera serait d'accord pour sortir avec moi? Ce serait cool~";
	close;
}
//Jellarin (Event planner)
lhz_in02,40,279,3	script	Jellarin	833,{
	mes "[Jellarin]";
	mes "Je n'aime pas ça. Mais cette idée ne me plaît pas non plus. Qu'est-ce que je pourrais faire pour le prochain évènement?";
	next;
	mes "[Jellarin]";
	mes "J'ai besoin de quelque chose d'unique, de grand, quelque chose qui impressionnerait vraiment tout le monde... mais quoi?";
	mes "Hé, vous n'auriez pas quelques idées?";
	close;
}
//Baoto (Cool Event Manager)
lhz_in02,110,283,5	script	Baoto	853,{
	mes "[Baoto]";
	mes "Humm...";
	mes "Les employés ont l'air de trainasser un peu trop ces temps-ci. Ce n'est pas sain...";
	next;
	mes "[Baoto]";
	mes "On dirait que je vais devoir user de mon fouet une fois de plus. Ha ha ha ha!";
	close;
}
//Saera (Secretary), contains GM menu
lhz_in02,36,274,3	script	Saera#02	831,{
	callfunc "F_ClearGarbage";				//Clear outdated, unused variables
	if($dts == 0){
		set $dts,$dts|(1<<0);				//Sets NPCs to election mode if first time running
		set $dts,$dts|(1<<3);
	}
	if($dts&(1<<1) && $dts&(1<<2) || $dts&(1<<0) && $dts&(1<<1) || $dts&(1<<0) && $dts&(1<<2)){
		if($dts&(1<<0)) set $dts,$dts&~(1<<0);
		if($dts&(1<<1)) set $dts,$dts&~(1<<1);
		if($dts&(1<<2)) set $dts,$dts&~(1<<2);
		set $dts,$dts|(1<<0);
	}
	if($dts&(1<<4) && $dts&(1<<5) || $dts&(1<<3) && $dts&(1<<4) || $dts&(1<<3) && $dts&(1<<5)){
		if($dts&(1<<3)) set $dts,$dts&~(1<<3);
		if($dts&(1<<4)) set $dts,$dts&~(1<<4);
		if($dts&(1<<5)) set $dts,$dts&~(1<<5);
		set $dts,$dts|(1<<3);
	}
	mes "[Saera]";
	mes "Bienvenue au Quartier Général Temporaire de la Corporation Cool Event.";
	mes "En quoi puis-je vous aider?";
	next;
	if(getgmlevel()>=40){ 						//Unofficial Text, GM-configurable settings for Event (GM level above 40 required)
		mes "[Saera]";
		mes "Oh, c'est vous,";
		mes strcharinfo(0)+". Que souhaitez-vous faire aujourd'hui?";
		next;
		menu "Attribuer des votes.",Lfixvote,
		"Définir la société de téléportation",Lsettele,
		"Définir le gagnant de la précédente élection",Lsetlast,
		"Re-vérifier les votes",Lmanuvotecheck,
		"Accéder au menu normal.",-;
		mes "[Saera]";
		mes "Ok";
		next;
	}
	menu "Quartier Général Temporaire?",Ltemphead,
	"Elections?",Lvoting,"Rien, merci.",Lnothx;

	Ltemphead:
		mes "[Saera]";
		mes "Les bâtiments de notre Quartier Général sont encore en construction, c'est pourquoi nous nous sommes basés ici pour l'instant.";
		close;

	Lvoting:
		mes "[Saera]";
		mes "La Corporation Kafra et la Corporation Cool Event travaillent conjointement sur un projet de téléportation directe vers les donjons.";
		next;
		mes "[Saera]";
		mes "Pour des raisons techniques, les deux compagnies ne peuvent pas proposer ce service de téléportation vers les mêmes donjons. De ce fait, les utilisateurs doivent voter pour choisir la compagnie qu'ils souhaitent utiliser.";
		next;
		if((dtseligible == 0 || dtseligible == 3) && baselevel >= 60 && MISC_QUEST&128 == 0) set dtseligible,1;
		//Clear previous var of dtseligible,3 being tried for eligibility already
		if(dtseligible != $dtsday && baselevel >= 60 && MISC_QUEST&128){
			set MISC_QUEST,MISC_QUEST&~128;
			set dtseligible,1;
		}
		switch(dtseligible){
			default:
				mes "[Saera]";
				mes "Seules certaines personnes sont habilitées à voter. Vous pouvez consulter votre statut d'électeur après des quartiers généraux des deux compagnies.";
				mes "Merci de votre compréhension~";
				close;
				break;
			case 1:
				mes "[Saera]";
				mes "Je vois que votre statut d'électeur est à jour. Vous pouvez dès à présent enregistrer votre vote auprès d'un représentant de l'élection. Merci~";
				close;
				break;
			case 2:
				mes "[Saera]";
				mes "Je vois que votre statut d'électeur est à jour, et que vous avez déjà voté! Merci de votre participation.";
				close;
				break;
		}

	Lnothx:
		mes "[Saera]";
		mes "Merci.";
		mes "Passez une bonne journée.";
		close;
									//GM options start
	Lfixvote:							//Fix Vote
		mes "[Saera]";
		mes "Hum, d'accord.";
		if($dtsv > 0) mes "La Cool Event Corp. est actuellement en tête avec "+$dtsv+" votes.";
		if($dtsv < 0) mes "La Kafra Corp. est actuellement en tête avec "+$dtsv*-1+" votes.";
		if($dtsv == 0) mes "Les votes sont actuellement à égalité.";
		mes "A qui souhaitez-vous attribuer vos votes?";
		next;
		menu "Kafra Corp.",-,"Cool Event Corp.",Lfixcool;

	Lfixkafra:							//Fixing vote for Kafra
		mes "[Saera]";
		if($dtsv > 0) mes "La Cool Event Corp. est actuellement en tête avec "+$dtsv+" votes.";
		if($dtsv < 0) mes "La Kafra Corp. est actuellement en tête avec "+$dtsv*-1+" votes.";	//multiplied by -1 because var is negative
		if($dtsv == 0) mes "Les votes sont actuellement à égalité.";
		mes "Veuillez entrer le nombre de votes que vous souhaitez accorder à la Kafra Corp.";
		input @dtstemp;					//Set buffer for Kafra vote
		next;
		mes "[Saera]";
		mes "Vous avez ajouté "+@dtstemp+" votes pour la Kafra Corp.";
		if($dtsv > 0) mes "La Cool Event Corp. est actuellement en tête avec "+$dtsv+" votes.";
		if($dtsv < 0) mes "La Kafra Corp. est actuellement en tête avec "+$dtsv*-1+" votes.";
		if($dtsv == 0) mes "Les votes sont actuellement à égalité.";
		mes "Êtes-vous sûr de vouloir valider ces changements?";
		next;
		menu "Oui",-,"Non",Lnothx;
		set $dtsv,$dtsv-@dtstemp;				//Set buffer to Kafra vote
		mes "[Saera]";
		mes "Très bien, la modification a été enregistrée.";
		if($dtsv > 0) mes "La Cool Event Corp. est actuellement en tête avec "+$dtsv+" votes.";
		if($dtsv < 0) mes "La Kafra Corp. est actuellement en tête avec "+$dtsv*-1+" votes.";
		if($dtsv == 0) mes "Les votes sont actuellement à égalité.";
		close;

	Lfixcool:						//Fixing vote for cool corp
		mes "[Saera]";
		mes "Voyons voir...";
		if($dtsv > 0) mes "La Cool Event Corp. est actuellement en tête avec "+$dtsv+" votes.";
		if($dtsv < 0) mes "La Kafra Corp. est actuellement en tête avec "+$dtsv*-1+" votes.";
		if($dtsv == 0) mes "Les votes sont actuellement à égalité.";
		mes "Veuillez entrer le nombre de votes que vous souhaitez accorder à la Cool Event Corp.";
		input @dtstemp;					//Set buffer for Cool vote
		next;
		mes "[Saera]";
		mes "You inputted "+@dtstemp+" for Cool Event Corp";
		if($dtsv > 0) mes "La Cool Event Corp. est actuellement en tête avec "+$dtsv+" votes.";
		if($dtsv < 0) mes "La Kafra Corp. est actuellement en tête avec "+$dtsv*-1+" votes.";
		if($dtsv == 0) mes "Les votes sont actuellement à égalité.";
		mes "Êtes-vous sûr de vouloir valider ces changements?";
		next;
		menu "Oui",-,"Non",Lnothx;			//Confirmaiton
		set $dtsv,@dtsv+@dtstemp;			//Set buffer to Cool vote
		mes "[Saera]";
		mes "Très bien, la modification a été enregistrée.";
		if($dtsv > 0) mes "La Cool Event Corp. est actuellement en tête avec "+$dtsv+" votes.";
		if($dtsv < 0) mes "La Kafra Corp. est actuellement en tête avec "+$dtsv*-1+" votes.";
		if($dtsv == 0) mes "Les votes sont actuellement à égalité.";
		close;

	Lsettele:							//Set current DTS tele
		mes "[Saera]";
		mes "Pour définir la société en charge des téléportations, tapez 'Kafra' pour désigner la Corporation Kafra, ou 'Cool' pour désigner la Corporation Cool Event. Tapez 'Election' pour activer les élections et la possibilité de voter, et 'Annuler' si vous souhaitez annuler.";
		if($dts&(1<<0)) mes "Les élections sont actuellement ouvertes.";
		if($dts&(1<<1)) mes "La Cool Event Corp. est actuellement désignée comme la compagnie de téléportation vers les donjons.";
		if($dts&(1<<2)) mes "La Kafra Corp. est actuellement désignée comme la compagnie de téléportation vers les donjons.";
		next;
		input @dtstemps$;					//Typed in choices to prevent mistakes
		mes "Voyons voir...";
		next;
		if(@dtstemps$ == "Election" || @dtstemps$ == "election"){
			if($dts&(1<<1)){
				set $dts,$dts&~(1<<1);			//Removes previous winner
				set $dts,$dts|(1<<0);			//Changes status to election
			}
			if($dts&(1<<2)){
				set $dts,$dts&~(1<<2);
				set $dts,$dts|(1<<0);
			}
			mes "[Saera]";
			mes "Les élections sont maintenant ouvertes.";
			close;
		}
		if(@dtstemps$ == "Cool" || @dtstemps$ == "cool"){
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<1);
			}
			if($dts&(1<<2)){
				set $dts,$dts&~(1<<2);
				set $dts,$dts|(1<<1);
			}
			mes "[Saera]";
			mes "La Cool Event Corp. est actuellement désignée comme la compagnie de téléportation vers les donjons.";
			close;
		}
		if(@dtstemps$ == "Kafra" || @dtstemps$ == "kafra"){
			if($dts&(1<<1)){
				set $dts,$dts&~(1<<1);
				set $dts,$dts|(1<<2);
			}
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<2);
			}
			mes "[Saera]";
			mes "La Kafra Corp. est actuellement désignée comme la compagnie de téléportation vers les donjons.";
			close;
		}
		if(@dtstemps$ == "Annuler" || @dtstemps$ == "annuler") goto Lnothx;
		mes "[Saera]";
		mes "Veuillez entrer un mot correct.";
		next;
		goto Lsettele;

	Lsetlast:							//Set last winner
		mes "[Saera]";
		mes "Pour définir le vainqueur des dernières élections, tapez 'Kafra' pour désigner la Corporation Kafra comme vainqueur dans les archives des résultats des élections, ou 'Cool' pour désigner la Corporation Cool Event. Tapez 'Aucun' pour supprimer l'archive du dernier résultat, et 'Annuler' si vous souhaitez annuler.";
		if($dts&(1<<3)) mes "Il n'y a pas de précédent vainqueur.";
		if($dts&(1<<4)) mes "La Cool Event Corp. était le dernier vainqueur.";
		if($dts&(1<<5)) mes "La Kafra Corp. était le dernier vainqueur.";
		next;
		input @dtstemps$;
		mes "[Saera]";
		mes "Voyons voir...";
		next;
		if(@dtstemps$ == "Aucun" || @dtstemps$ == "aucun"){
			if($dts&(1<<4)){
				set $dts,$dts&~(1<<4);			//removes last winner
				set $dts,$dts|(1<<3);			//sets current last winner
			}
			if($dts&(1<<5)){
				set $dts,$dts&~(1<<5);
				set $dts,$dts|(1<<3);
			}
			mes "[Saera]";
			mes "Les archives des résultats des élections ne montreront aucun gagnant pour les précédentes élections.";
			close;
		}
		if(@dtstemps$ == "Cool" || @dtstemps$ == "cool"){
			if($dts&(1<<3)){
				set $dts,$dts&~(1<<3);
				set $dts,$dts|(1<<4);
			}
			if($dts&(1<<5)){
				set $dts,$dts&~(1<<5);
				set $dts,$dts|(1<<4);
			}
			mes "[Saera]";
			mes "La Cool Event Corp. est maintenant enregistrée comme vainqueur des dernières élections.";
			close;
		}
		if(@dtstemps$ == "Kafra" || @dtstemps$ == "kafra"){
			if($dts&(1<<3)){
				set $dts,$dts&~(1<<3);
				set $dts,$dts|(1<<5);
			}
			if($dts&(1<<4)){
				set $dts,$dts&~(1<<4);
				set $dts,$dts|(1<<5);
			}
			mes "[Saera]";
			mes "La Kafra Corp. est maintenant enregistrée comme vainqueur des dernières élections.";
			close;
		}
		if(@dtstemps$ == "Annuler" || @dtstemps$ == "annuler") goto Lnothx;
		mes "[Saera]";
		mes "Veuillez entrer un mot correct.";
		next;
		goto Lsetlast;

	Lmanuvotecheck:
		mes "[Saera]";
		mes "Êtes-vous certain de vouloir re-vérifier les votes?";
		next;
		menu "Oui",Lmanuvoteyes,"Non",Lnothx;

	Lmanuvoteyes:
		if($dtsday<4 || $dtsday>=100) set $dtsday,4;
		else set $dtsday,$dtsday+1;
		if($dts&(1<<1) && $dts&(1<<2) || $dts&(1<<0) && $dts&(1<<1) || $dts&(1<<0) && $dts&(1<<2)){
			if($dts&(1<<0)) set $dts,$dts&~(1<<0);
			if($dts&(1<<1)) set $dts,$dts&~(1<<1);
			if($dts&(1<<2)) set $dts,$dts&~(1<<2);
			set $dts,$dts|(1<<0);
		}
		if($dts&(1<<4) && $dts&(1<<5) || $dts&(1<<3) && $dts&(1<<4) || $dts&(1<<3) && $dts&(1<<5)){
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			set $dts,$dts|(1<<3);
		}
		if($dtsv == 0){
			set $@dtstemp,rand(1,2);
			if($@dtstemp == 1) set $dtsv,$dtsv+100;
			else set $dtsv,$dtsv-100;
		}
		if($dtsv > 0){
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);			//removes last winner varialbes
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);				//removes winner variable
				set $dts,$dts|(1<<3);				//sets last winner varialbe
				set $dts,$dts|(1<<1);				//sets current winner variable
			}
			if($dts&(1<<1)) set $dts,$dts|(1<<4);
			if($dts&(1<<2)){
				set $dts,$dts&~(1<<2);
				set $dts,$dts|(1<<1);
				set $dts,$dts|(1<<5);
			}
			set $dtsv,0;
		}
		else{
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<2);
				set $dts,$dts|(1<<3);
			}
			if($dts&(1<<1)){
				set $dts,$dts&~(1<<1);
				set $dts,$dts|(1<<2);
				set $dts,$dts|(1<<4);
			}
			if($dts&(1<<2)) set $dts,$dts|(1<<5);
			set $dtsv,0;
		}
		mes "[Saera]";
		mes "Les votes ont été recalculés.";
		if($dts&(1<<0)) mes "Les élections sont actuellement ouvertes.";
		if($dts&(1<<1)) mes "La Cool Event Corp. est actuellement désignée comme la compagnie de téléportation vers les donjons.";
		if($dts&(1<<2)) mes "La Kafra Corp. est actuellement désignée comme la compagnie de téléportation vers les donjons.";
		close;
}

//Function for Voting Staff NPC
function	script	F_DTS_Warp	{
	callfunc "F_ClearGarbage"; //Clear outdated, unused variables
	if($dts == 0){ 						//Sets NPCs to election mode if first time running
		set $dts,$dts|(1<<0);
		set $dts,$dts|(1<<3);
	}
	if((dtseligible == 0 || dtseligible == 3) && baselevel >= 60 && MISC_QUEST&128 == 0) set dtseligible,1;	//Clear previous var of dtseligible,3 being tried for eligibility already
	if(dtseligible != $dtsday && baselevel >= 60 && MISC_QUEST&128){
		set MISC_QUEST,MISC_QUEST&~128;
		set dtseligible,1;
	}
	switch(getarg(0)){
	case 0:
		cutin "zonda_01",2;
		mes "[Votes Cool Event Corp.]";
		mes "Bonjour! N'oubliez pas de faire entendre votre voix en votant à l'élection de la future compagnie de téléportation vers les donjons!";
		break;
	case 1:
		switch(getarg(1)){
		case 0: cutin "kafra_06",2; break;			//Finding arguments to find which illust to use
		case 1: cutin "kafra_05",2; break;			//Used Red hair "kafra_09" for black outfit kafra
		case 2: cutin "kafra_04",2; break;			//because I couldn't find a suitable illust
		case 3: cutin "kafra_03",2; break;
		case 4: cutin "kafra_02",2; break;
		case 5: cutin "kafra_01",2; break;
		case 6: cutin "kafra_09",2; break;
		case 7: cutin "kafra_08",2; break;
		case 8: cutin "kafra_09",2; break;
		default: cutin "kafra_09",2; break;
		}
		mes "[Votes Kafra Corp.]";
		mes "Bonjour, aventurier.";
		mes "Comme vous le savez peut-être déjà, des élections sont en cours pour désigner la future compagnie de téléportation vers les donjons.";
		mes "En quoi puis-je vous aider?";
		break;
	}
	next;
	menu "Des élections?",Lelection,"Voter.",Lvote,
	"Utiliser le service de téléportation.",Lteleport,"Annuler.",Lcancel;

	Lelection: 							//Reason for election explanation
		switch(getarg(0)){
		case 0:
			mes "[Votes Cool Event Corp.]";
			mes "La Corporation Cool Event a pour projet de proposer des nouveaux services à ses clients: un système de téléportation directe vers les donjons. Cependant...";
			next;
			mes "[Votes Cool Event Corp.]";
			mes "La Corporation Kafra, qui a déjà le monopole sur de nombreux services, a élaboré un projet très similaire au notre.";
			next;
			mes "[Votes Cool Event Corp.]";
			mes "Pour des raisons de limites technologiques, une seule de nos deux compagnies peut être choisie pour assurer ce service de téléportation vers les donjons.";
			mes "De ce fait, nous souhaitons laisser nos clients choisir la compagnie de leur choix, à travers des élections.";
			next;
			mes "[Votes Cool Event Corp.]";
			mes "Ces élections auront lieu régulièrement, afin que nos clients puissent tester et comparer la qualité de nos services.";
			next;
			mes "[Votes Cool Event Corp.]";
			mes "Tout le monde ne peut pas voter: il faut remplir des conditions afin de devenir électeur. Pour obtenir plus d'informations, veuillez vous rendre dans notre quartier général à Lighthalzen, dans la République de Schwarzwalt.";
			mes "Merci, bonne journée.";
			break;
		case 1:
			mes "[Votes Kafra Corp.]";
			mes "La Corporation Cool Event et la Corporation Kafra ont toutes deux élaboré un projet de service de téléportation directe vers les donjons.";
			next;
			mes "[Votes Kafra Corp.]";
			mes "Pour des raisons de limites technologiques, une seule de nos deux compagnies peut être choisie pour assurer ce service de téléportation vers les donjons.";
			mes "De ce fait, nous avons décidé d'un commun accord de laisser nos clients choisir la compagnie de leur choix, à travers des élections.";
			next;
			mes "[Votes Kafra Corp.]";
			mes "Chacune de nos deux compagnies possèdent des politiques de garantie pour ce futur service de téléportation vers les donjons. Par ces élections, nos clients pourront choisir d'eux-mêmes ce qu'il y a de mieux pour eux.";
			next;
			mes "[Votes Kafra Corp.]";
			mes "Pour l'instant, ces nouveaux services de téléportation vers les donjons sont proposés sur de courtes périodes. Ainsi, les élections ont lieu régulièrement, et nos clients peuvent comparer la qualité des services des deux compagnies avant d'émettre leur décision finale.";
			next;
			mes "[Votes Kafra Corp.]";
			mes "Si vous remplissez les conditions pour devenir électeur, n'oubliez pas de voter afin de décider laquelle de nos deux compagnie proposera les services de téléportation vers les donjons.";
			mes "Merci de votre soutien~";
			break;
		}
		goto Lend;

	Lvote: //If you clicked you wanted to vote
		if(dtseligible == 1) goto Leligible; 			//Var check if eligible

	Lnoteligible: 							//Text displayed if wanted to vote, but not eligible/voted
		switch(getarg(0)){
		case 0:
			mes "[Votes Cool Event Corp.]";
			mes "Je suis désolé, mais vous n'êtes pas habilité à voter pour ces élections. Pour obtenir plus d'informations, veuillez vous rendre dans notre quartier général à Lighthalzen.";
			mes "Merci de votre compréhension.";
			break;
		case 1:
			mes "[Votes Kafra Corp.]";
			mes "Je suis désolée, mais vous n'êtes pas habilité à voter pour ces élections. Pour obtenir plus d'informations, veuillez vous rendre dans notre quartier général à Al De Baran.";
			mes "Merci, passez une bonne journée.";
			break;
		}
		goto Lend;

	Leligible: 							//Text displayed if eligible to vote
		if(MISC_QUEST&128 && $dtsday == dtseligible) goto Lnoteligible; //Var check if voted
		switch(getarg(0)){
		case 0:
			mes "[Votes Cool Event Corp.]"; 		//Unofficial text
			mes "Pour qui souhaitez-vous voter?";
			break;
		case 1:
			mes "[Votes Kafra Corp.]";
			mes "A qui souhaitez-vous accorder votre vote?";		//End Unofficial text
			break;
		}
		next;
		menu "Corporation Cool Event",Lvotecool,"Corporation Kafra",Lvotekafra;
	
	Lvotecool:
		set $dtsv,$dtsv+1;				//Adds 1 to cool count
		set MISC_QUEST,MISC_QUEST|128;				//Sets var so that you can't vote over and over
		set dtseligible,$dtsday;
		switch(getarg(0)){
		case 0:
			mes "[Votes Cool Event Corp.]";
			mes "Votre vote a bien été enregistré. Nous vous remercions pour votre soutien, et nous espérons pouvoir vous proposer bientôt des services d'excellence.";
			break;
		case 1:
			mes "[Votes Kafra Corp.]";
			mes "Votre vote pour la Corporation Cool Event a bien été enregistré.";
			mes "Merci, passez une bonne journée.";
			break;
		}
		goto Lend;

	Lvotekafra:
		setd $dtsv,$dtsv-1; 				//Adds 1 to kafra count
		set MISC_QUEST,MISC_QUEST|128; 			//Sets var to prevent cheating
		set dtseligible,$dtsday;
		switch(getarg(0)){
		case 0:
			mes "[Votes Cool Event Corp.]";		//Unofficial Text
			mes "Votre vote pour la Corporation Kafra a bien été enregistré.";
			mes "Merci, passez une bonne journée.";
			break;
		case 1:
			mes "[Votes Kafra Corp.]";
			mes "Votre vote a bien été enregistré. Nous vous remercions pour votre soutien, et nous espérons pouvoir vous proposer bientôt des services d'excellence.";
			break;
		}
		goto Lend;						//End Unofficial text

	Lteleport: 							//Selected Teleport
		switch(getarg(0)){
		case 0:
			if($dts&(1<<0)){
				mes "[Votes Cool Event Corp.]";
				mes "Désolé, mais les élections pour désigner la compagnie en charge du service de téléportation vers les donjons sont actuellement en cours. Nous ne pouvons proposer nos services avant l'annonce du résultat.";
				goto Lend;
			}
			if($dts&(1<<1)) goto Lteleenabled;
			mes "[Votes Cool Event Corp.]";		//Text if Kafra won DTS elect.
			mes "Je suis désolé, mais la Corporation Cool Event n'est actuellement pas la compagnie en charge du service de téléportation vers les donjons. Votez pour nous aux prochaines élections~";
			break;
		case 1:
			if($dts&(1<<0)){
				mes "[Votes Kafra Corp.]";
				mes "Désolée, mais les élections pour désigner la compagnie en charge du service de téléportation vers les donjons sont actuellement en cours. Nous ne pouvons proposer nos services avant l'annonce du résultat.";
				goto Lend;
			}
			if($dts&(1<<2)) goto Lteleenabled;
			mes "[Votes Kafra Corp.]";
			mes "Je suis désolée, mais la Corporation Kafra n'est actuellement pas la compagnie en charge du service de téléportation vers les donjons. Votez pour nous aux prochaines élections~";
			break;
		}
		goto Lend;

	Lteleenabled: 							//Shows DTS tele selections
	cleararray @dtswarpmap$[0],"",getarraysize(@dtswarpmap$);
	cleararray @dtswarp$[0],"",getarraysize(@dtswarp$);
	switch(getarg(0)){
	case 0:
		mes "[Votes Cool Event Corp.]";
		mes "Veuillez choisir votre destination.";
		mes "Veuillez noter que nous n'acceptons aucun ticket de réduction ou de bons de quelque autre sorte.";
		setarray @dtswarpmap$[0],"Île de Byalan, Niveau 4","Tour de l'Horloge, 3ième sous-sol";
		setarray @dtswarp$[0],@dtswarpmap$[0]+" -> 4000 Zenys",
		@dtswarpmap$[1]+" -> 4000 Zenys","Annuler";
		if($dts&(1<<4)){ 
			set @dtswarpmap$[2],"Glast Heim, Entrée";
			setarray @dtswarp$[2],@dtswarpmap$[2]+" -> 4000 Zenys","Annuler";
		}
		break;
	case 1:
		mes "[Votes Kafra Corp.]";
		mes "Merci d'avoir choisir la Corporation Kafra pour vos services de téléportation vers les donjons.";
		mes "Veuillez noter que nous n'acceptons aucun ticket de réduction ou de bons de quelque autre sorte.";
		mes "Choisissez votre destination.";
		setarray @dtswarpmap$[0],"Fabrique de Jouets, Niveau 2","Tour de l'Horloge, Niveau 3";
		setarray @dtswarp$[0],@dtswarpmap$[0]+" -> 4000 Zenys",
		@dtswarpmap$[1]+" -> 4000 Zenys","Annuler";
		if($dts&(1<<5)){ 
			set @dtswarpmap$[2],"Donjon de Lave, Niveau 2";
			setarray @dtswarp$[2],@dtswarpmap$[2]+" -> 4000 Zenys","Annuler";
		}
		break;
	}
	next;
	switch(select(@dtswarp$[0],@dtswarp$[1],@dtswarp$[2],@dtswarp$[3])){
		case 1:
			set @num, 0;
			break;
		case 2:
			set @num, 1;
			break;
		case 3:
			set @num, 2;
			break;
		case 4:
			set @num, 3;
			break;
	}
	Lwarp:
		if (@dtswarp$[@num] == "Annuler") goto Lcancel;
		if (Zeny<4000) goto Lnomoney;
		set Zeny, Zeny-4000;
		if (@dtswarpmap$[@num] == "Fabrique de Jouets, Niveau 2") warp "xmas_dun02",130,123;
 //Maps to warp to
		if (@dtswarpmap$[@num] == "Tour de l'Horloge, Niveau 3") warp "alde_dun03",265,22;

		if (@dtswarpmap$[@num] == "Donjon de Lave, Niveau 2") warp "mag_dun02",47,40;

		if (@dtswarpmap$[@num] == "Île de Byalan, Niveau 4") warp "iz_dun03",32,63;

		if (@dtswarpmap$[@num] == "Tour de l'Horloge, 3ième sous-sol") warp "alde_dun03",277,178;

		if (@dtswarpmap$[@num] == "Glast Heim, Entrée") warp "glast_01",370,304;

		cutin "", 255;
		end;
	Lnomoney:
		switch(getarg(0)){
		case 0:
			mes "[Votes Cool Event Corp.]"; 		//Unofficial Text
			mes "Humm, apparemment, vous n'avez pas assez d'argent pour accéder à ce service.";
			break;
		case 1:
			mes "[Votes Kafra Corp.]";
			mes "Humm, apparemment, vous n'avez pas assez d'argent pour accéder à ce service.";
			break;
		}
		goto Lend;						//End unofficial text

	Lcancel: 							//Selected Cancel on first menu
		switch(getarg(0)){
		case 0:
			mes "[Votes Cool Event Corp.]";	
			mes "La Corporation Cool Event se donne toujours au maximum pour vous offrir les meilleurs services.";
			mes "Nous espérons que nos efforts et nos services de qualités satisfont nos clients.";
			mes "Merci, passez une bonne journée.";
			break;
		case 1:
			mes "[Votes Kafra Corp.]";
			mes "La Corporation Kafra se donne toujours au maximum pour vous proposer les meilleurs services. Nous espérons avoir répondu à vos exigences d'aventurier dans les standards de l'excellence.";
			break;
		}
		goto Lend;
	Lend:
		close2;
		cutin "", 255;
		end;

}

//Elections administration NPC (hidden)
-	script	DTS_Admin	-1,{
	OnSun0100:					//Works only at 1am on sunday
		if($dtsday<4 || $dtsday>=100) set $dtsday,4;
		else set $dtsday,$dtsday+1;
		if($dts == 0){
			set $dts,$dts|(1<<0);
			set $dts,$dts|(1<<3);
		}
		if($dts&(1<<1) && $dts&(1<<2) || $dts&(1<<0) && $dts&(1<<1) || $dts&(1<<0) && $dts&(1<<2)){
			if($dts&(1<<0)) set $dts,$dts&~(1<<0);
			if($dts&(1<<1)) set $dts,$dts&~(1<<1);
			if($dts&(1<<2)) set $dts,$dts&~(1<<2);
			set $dts,$dts|(1<<0);
		}
		if($dts&(1<<4) && $dts&(1<<5) || $dts&(1<<3) && $dts&(1<<4) || $dts&(1<<3) && $dts&(1<<5)){
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			set $dts,$dts|(1<<3);
		}
		if($dtsv == 0){				//If tied, gives random side 100 votes
			set $@dtstemp,rand(1,2);
			if($@dtstemp == 1) set $dtsv,$dtsv+100;
			else set $dtsv,$dtsv-100;
		}
		if($dtsv > 0){
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<1);
				set $dts,$dts|(1<<3);
			}
			if($dts&(1<<1)) set $dts,$dts|(1<<4);
			if($dts&(1<<2)){
				set $dts,$dts&~(1<<2);
				set $dts,$dts|(1<<1);
				set $dts,$dts|(1<<5);
			}
			set $dtsv,0;
		}
		else{
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<2);
				set $dts,$dts|(1<<3);
			}
			if($dts&(1<<1)){
				set $dts,$dts&~(1<<1);
				set $dts,$dts|(1<<2);
				set $dts,$dts|(1<<4);
			}
			if($dts&(1<<2)) set $dts,$dts|(1<<5);
			set $dtsv,0;
		}
		end;
}
