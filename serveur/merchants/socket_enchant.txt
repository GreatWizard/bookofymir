//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script =======================================
//= Socket Enchant
//===== By: ==================================================
//= eAthena dev team
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= item loses all element refinement and upgrades but
//= cards too?
//===== Additional Comments: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2	Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1	Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed Ontouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//============================================================

payon,140,151,5	script	Seiyablem	84,{

	mes "[Seiyablem]";
	mes "Bonjour.";
	mes "Je suis un ingénieur spécialisé dans l'amélioration des Armes et Armures classiques.";
	mes "Le tarif, les matériaux nécessaires, et la chance de réussite, dépendent des spécificités de l'objet sur lequel je dois travailler.";
	next;
	mes "[Seiyablem]";
	mes "Améliorer une arme ou une armure peut paraître simple, mais c'est en fait bien plus compliqué qu'on ne le croit.";
	mes "Êtes-vous intéressé par mes services?";
	next;
	switch( select( "Amélioration d'une Arme.","Amélioration d'une Armure.","Amélioration?","Annuler." ) )
	{
		case 1:
			mes "[Seiyablem]";
			mes "Vous voulez améliorer une Arme?";
			mes "Les Armes sont classées en différentes catégories suivant la difficulté à travailler sur celles-ci. Les Armes de Classe C sont relativement faciles à travailler, et les Armes de Classe S sont les plus coriaces.";
			mes "Quelle est la Classe de l'Arme sur laquelle vous voulez essayer une amélioration?";
			set @sockettype,1;
			next;
			switch( select( "Classe C","Classe B","Classe A","Classe S" ) )
			{
				case 1:
					mes "[Seiyablem]";
					mes "Une arme de Classe C?";
					mes "Cela paraît raisonnable.";
     					mes "Veuillez choisir votre arme:";
					next;
					switch( select( "Trident","Rope","Violin","Book of Billows","Book of Mother Earth","Book of the Blazing Sun","Book of Gust of Wind" ) )
					{
						case 1: callfunc "Func_Socket",1,1460,1461,40,66,200,1010,10; break;
						case 2: callfunc "Func_Socket",1,1950,1951,40,66,200,1010,10; break;
						case 3: callfunc "Func_Socket",1,1901,1902,40,66,200,1010,10; break;
						case 4: callfunc "Func_Socket",1,1553,1568,40,66,200,1010,10; break;
						case 5: callfunc "Func_Socket",1,1554,1569,40,66,200,1010,10; break;
						case 6: callfunc "Func_Socket",1,1555,1570,40,66,200,1010,10; break;
						case 7: callfunc "Func_Socket",1,1556,1571,40,66,200,1010,10; break;
					}
				break;
				
				case 2:
					mes "[Seiyablem]";
					mes "Une arme de Classe B?";
					mes "Très bien...";
     					mes "Veuillez choisir votre arme:";
					next;
					switch( select( "Chain","Gladius","Gakkung Bow","Pike","Haedonggum","Lute","Wire","Waghnakh","Arbalest Bow","Orcish Axe","Scimitar","Spike" ) )
					{
						case 1: callfunc "Func_Socket",1,1519,1520,40,61,300,1010,10; break;
						case 2: callfunc "Func_Socket",2,1219,1220,40,61,300,984,1,999,5; break;
						case 3: callfunc "Func_Socket",2,1714,1716,40,61,300,984,2,999,5; break;
						case 4: callfunc "Func_Socket",1,1407,1408,40,61,200,1010,10; break;
						case 5: callfunc "Func_Socket",2,1123,1128,40,61,300,984,2,999,5; break;
						case 6: callfunc "Func_Socket",1,1905,1906,40,61,300,1011,10; break;
						case 7: callfunc "Func_Socket",1,1954,1955,40,61,300,1011,10; break;
						case 8: callfunc "Func_Socket",1,1801,1802,40,61,300,1010,10; break;
						case 9: callfunc "Func_Socket",2,1713,1715,40,61,300,984,2,999,5; break;
						case 10: callfunc "Func_Socket",1,1304,1309,40,61,200,984,1; break;
						case 11: callfunc "Func_Socket",1,1113,1114,40,61,200,984,1; break;
						case 12: callfunc "Func_Socket",1,1523,1538,40,61,300,984,1; break;
					}
				break;
				
				case 3:
					mes "[Seiyablem]";
					mes "Ooh une arme de Classe A?";
					mes "Nous entrons en territoire à haut risque!";
      					mes "Veuillez choisir votre arme:";
					next;
					switch( select( "Hunter Bow","Survivor's Rod(INT)","Zweihander","Flamberge","Infiltrator","Ballista","Stunner","Berserk","Claymore","Dragon Killer","Katar of Frozen Icicle","Katar of Quaking","Katar of Raging Blaze","Katar of Piercing Wind","Golden Mace","Oriental Lute","Queen's Whip","Phantom Spear","Schweizersabel" ) )
					{
						case 1: callfunc "Func_Socket",2,1718,1726,40,61,500,984,2,999,10; break;
						case 2: callfunc "Func_Socket",2,1619,1620,40,61,500,984,5,999,10; break;
						case 3: callfunc "Func_Socket",2,1168,1171,40,61,800,984,5,999,10; break;
						case 4: callfunc "Func_Socket",2,1129,1149,40,61,500,984,2,999,10; break;
						case 5: callfunc "Func_Socket",2,1261,1266,40,61,700,984,5,999,10; break;
						case 6: callfunc "Func_Socket",2,1722,1727,40,61,500,984,5,999,10; break;
						case 7: callfunc "Func_Socket",2,1522,1532,40,61,500,984,2,999,10; break;
						case 8: callfunc "Func_Socket",2,1814,1816,40,61,500,984,5,999,10; break;
						case 9: callfunc "Func_Socket",2,1163,1172,40,61,500,984,5,999,10; break;
						case 10: callfunc "Func_Socket",1,13001,13030,40,61,500,984,2; break;
						case 11: callfunc "Func_Socket",1,1256,1275,40,61,500,984,2; break;
						case 12: callfunc "Func_Socket",1,1257,1276,40,61,500,984,2; break;
						case 13: callfunc "Func_Socket",1,1258,1277,40,61,500,984,2; break;
						case 14: callfunc "Func_Socket",1,1259,1278,40,61,500,984,2; break;
						case 15: callfunc "Func_Socket",1,1524,1539,40,61,500,984,2; break;
						case 16: callfunc "Func_Socket",1,1918,1922,40,61,500,984,2; break;
						case 17: callfunc "Func_Socket",1,1970,1976,40,61,500,984,2; break;
						case 18: callfunc "Func_Socket",1,1477,1479,40,61,500,984,2; break;
						case 19: callfunc "Func_Socket",1,1167,1178,40,61,500,984,2; break;
					}
				break;
				
				case 4:
					mes "[Seiyablem]";
					mes "Whoa, whoa, whoa...";
					mes "Une arme de Classe S? Très bien...";
					mes "Ce serait un miracle si j'y arrivais, mais bon... On ne sait jamais!";
					mes "Veuillez choisir votre arme:";
					next;
					switch( select( "Gungnir","Poison Knife","Ice Pick","Sucsamad","Ginnungagap","Cutlas","Crescent Scythe","Survivor's Rod(DEX)","Zephyrus","Mailbreaker","Swordbreaker","Assassin Dagger","Grand Cross","Executioner" ) )
					{
						case 1: callfunc "Func_Socket",2,1413,1418,40,51,1000,984,5,999,10; break;
						case 2: callfunc "Func_Socket",2,1239,13016,40,51,1000,984,5,999,10; break;
						case 3: callfunc "Func_Socket",2,1230,13017,40,51,2000,984,5,999,10; break;
						case 4: callfunc "Func_Socket",2,1236,13018,40,51,1000,984,5,999,10; break;
						case 5: callfunc "Func_Socket",2,13002,13019,40,51,1000,984,5,999,10; break;
						case 6: callfunc "Func_Socket",2,1135,13400,40,51,1000,984,5,999,10; break;
						case 7: callfunc "Func_Socket",2,1466,1476,40,51,1000,984,5,999,10; break;
						case 8: callfunc "Func_Socket",2,1617,1618,40,51,2000,984,5,999,10; break;
						case 9: callfunc "Func_Socket",2,1468,1481,40,51,1000,984,5,999,10; break;
						case 10: callfunc "Func_Socket",2,1225,13032,40,51,1000,984,5,999,10; break;
						case 11: callfunc "Func_Socket",2,1224,13031,40,51,1000,984,5,999,10; break;
						case 12: callfunc "Func_Socket",2,1232,13033,40,51,1000,984,5,999,10; break;
						case 13: callfunc "Func_Socket",2,1528,1540,40,51,1000,984,5,999,10; break;
						case 14: callfunc "Func_Socket",2,1169,1179,40,51,1000,984,5,999,10; break;
					}
				break;
			}
		break;
		
		case 2:
			mes "[Seiyablem]";
			mes "Une armure, vous dîtes?";
			mes "D'accord, pas de problème.";
			mes "Les armures sont divisées en quatre Classes différentes, qui dépendent de la difficulté de réussite.";
			mes "La Classe C est celle sur laquelle il est le plus facile de travailler, et la Classe S la plus difficile.";
			mes "De quelle Classe est l'Armure sur laquelle vous voulez essayer  une amélioration?";
			set @sockettype,2;
			next;
			switch( select( "C Class","B Class","A Class","S Class" ) )
			{
				case 1:
					mes "[Seiyablem]";
					mes "Une armure de Classe C?";
					mes "Cela paraît raisonnable.";
      					mes "Veuillez choisir votre armure:";
					next;
					switch( select( "Mantle","Coat","Circlet","Biretta","Sunflower","Ph.D Hat","Big Ribbon","Boys Cap" ) )
					{
						case 1: callfunc "Func_Socket",1,2307,2308,40,66,200,999,3; break;
						case 2: callfunc "Func_Socket",1,2309,2310,40,66,200,999,3; break;
						case 3: callfunc "Func_Socket",1,2232,2233,40,66,200,999,3; break;
						case 4: callfunc "Func_Socket",1,2216,2217,40,66,200,999,3; break;
						case 5: callfunc "Func_Socket",1,2253,5351,40,66,100,999,3; break;
						case 6: callfunc "Func_Socket",1,5012,5347,40,66,200,999,3; break;
						case 7: callfunc "Func_Socket",1,2244,5348,40,66,200,999,5; break;
						case 8: callfunc "Func_Socket",1,5016,5349,40,66,200,999,5; break;
					}
				break;
				
				case 2:
					mes "[Seiyablem]";
					mes "Une armure de Classe B? Très bien...";
      					mes "Veuillez choisir votre armure:";
					next;
					switch( select( "Mirror Shield","Chain Mail","Saint Robe","Silk Robe","Boots","Shoes","Muffler","Guard","Buckler","Shield","Bongun Hat","Skull Ring","High Heels" ) )
					{
						case 1:  callfunc "Func_Socket",1,2107,2108,40,61,250,999,5; break;
						case 2:  callfunc "Func_Socket",1,2314,2315,40,61,250,999,5; break;
						case 3:  callfunc "Func_Socket",1,2325,2326,40,61,300,999,5; break;
						case 4:  callfunc "Func_Socket",1,2321,2322,40,61,300,999,5; break;
						case 5:  callfunc "Func_Socket",1,2405,2406,40,61,300,999,5; break;
						case 6:  callfunc "Func_Socket",1,2403,2404,40,61,300,999,5; break;
						case 7:  callfunc "Func_Socket",1,2503,2504,40,61,300,999,5; break;
						case 8:  callfunc "Func_Socket",1,2101,2102,40,61,300,999,5; break;
						case 9:  callfunc "Func_Socket",1,2103,2104,40,61,300,999,5; break;
						case 10: callfunc "Func_Socket",1,2105,2106,40,61,250,999,5; break;
						case 11: callfunc "Func_Socket",1,5046,5168,40,61,250,999,5; break;
						case 12: callfunc "Func_Socket",1,2609,2715,40,61,300,999,5; break;
						case 13: callfunc "Func_Socket",1,2409,2432,40,61,300,999,5; break;
					}
				break;
				
				case 3:
					mes "[Seiyablem]";
					mes "Ooh, une armure de Classe A?";
					mes "Nous entrons en territoire à haut risque!";
      					mes "Veuillez choisir votre armure:";
					next;
					switch( select( "Gemmed Sallet","Bucket Hat","Memory Book","Tights","Legion Plate Armor","Full Plate","Thief Clothes","Greaves","Coif","Manteau","Helm","Ninja Suit","Orc Helm","Ancient Cape","Monk Hat","Golden Gear","Brooch","Munak Hat","Pirate Bandana","Black Leather Boots" ) )
					{
						case 1:  callfunc "Func_Socket",1,2230,2231,40,61,400,985,1; break;
						case 2:  callfunc "Func_Socket",1,5114,5120,40,61,400,985,1; break;
						case 3:  callfunc "Func_Socket",1,2109,2121,40,61,400,985,1; break;
						case 4:  callfunc "Func_Socket",1,2330,2331,40,61,400,985,1; break;
						case 5:  callfunc "Func_Socket",1,2341,2342,40,61,400,985,1; break;
						case 6:  callfunc "Func_Socket",1,2316,2317,40,61,400,985,1; break;
						case 7:  callfunc "Func_Socket",1,2335,2336,40,61,400,985,1; break;
						case 8:  callfunc "Func_Socket",1,2411,2412,40,61,400,985,1; break;
						case 9:  callfunc "Func_Socket",1,5092,5093,40,61,400,985,1; break;
						case 10: callfunc "Func_Socket",1,2505,2506,40,61,400,985,1; break;
						case 11: callfunc "Func_Socket",1,2228,2229,40,61,400,985,1; break;
						case 12: callfunc "Func_Socket",1,2337,2359,40,61,400,985,1; break;
						case 13: callfunc "Func_Socket",1,2299,5157,40,61,400,985,1; break;
						case 14: callfunc "Func_Socket",1,2507,2525,40,61,400,985,1; break;
						case 15: callfunc "Func_Socket",1,2251,5158,40,61,400,985,1; break;
						case 16: callfunc "Func_Socket",1,2246,5159,40,61,400,985,1; break;
						case 17: callfunc "Func_Socket",1,2605,2625,40,61,400,985,1; break;
						case 18: callfunc "Func_Socket",1,2264,5167,40,61,300,985,1; break;
						case 19: callfunc "Func_Socket",1,2287,5350,40,61,500,985,1; break;
						case 20: callfunc "Func_Socket",1,2425,2434,40,61,500,985,1; break;
					}
				break;
				
				case 4:
					mes "[Seiyablem]";
					mes "Hum... Une armure de Classe S?";
					mes "Vous êtes sûr de vous?";
					mes "Très bien... Veuillez choisir votre armure... Mais je ne vous garantie rien!";
					next;
					switch( select( "Majestic Goat","Spiky Band","Bone Helm","Corsair","Crown","Tiara","Sphinx Hat","Robe of Cast","Earring","Ring","Bow Thimble","Mage Coat","Holy Robe","Sacred Mission","Undershirt","Pantie" ) )
					{
						case 1:  callfunc "Func_Socket",1,2256,5160,40,51,2000,985,2; break;
						case 2:  callfunc "Func_Socket",1,2258,5161,40,51,2000,985,2; break;
						case 3:  callfunc "Func_Socket",1,5017,5162,40,51,2000,985,2; break;
						case 4:  callfunc "Func_Socket",1,5019,5163,40,51,2000,985,2; break;
						case 5:  callfunc "Func_Socket",1,2235,5165,40,51,2000,985,2; break;
						case 6:  callfunc "Func_Socket",1,2234,5164,40,51,2000,985,2; break;
						case 7:  callfunc "Func_Socket",1,5053,5166,40,51,2000,985,2; break;
						case 8:  callfunc "Func_Socket",1,2343,2360,40,51,2000,985,2; break;
						case 9:  callfunc "Func_Socket",1,2602,2622,40,51,2000,985,2; break;
						case 10: callfunc "Func_Socket",1,2601,2621,40,51,2000,985,2; break;
						case 11: callfunc "Func_Socket",1,2619,2671,40,51,2000,985,2; break;
						case 12: callfunc "Func_Socket",1,2334,2372,40,51,1000,985,1; break;
						case 13: callfunc "Func_Socket",1,2327,2373,40,51,1000,985,1; break;
						case 14: callfunc "Func_Socket",1,2111,2128,40,51,1000,985,1; break;
						case 15: callfunc "Func_Socket",1,2522,2523,40,51,1000,985,1; break;
						case 16: callfunc "Func_Socket",1,2339,2371,40,51,1000,985,1; break;
					}
				break;
			}
		break;
		
		case 3:
			mes "[Seiyablem]";
			mes "Je peux essayer d'améliorer une arme ou une armure pour vous, à savoir, tenter d'y ajouter un Slot.";
			mes "C'est très compliqué, et je vous mentirais si je vous disais que je maîtrise parfaitement ce processus.";
			mes "Ceci dit, je pense avoir identifié certains facteurs de réussite...";
			next;
			mes "[Seiyablem]";
			mes "Dans certaines villes, les tentatives d'addition de Slots ont plus de chances de réussir s'il s'agit d'un équipement peu raffiné.";
			mes "Dans d'autres villes, c'est l'inverse.";
			mes "Bizarre, non?";
			next;
			mes "[Seiyablem]";
			mes "Oh, il y a également quelque chose de très important à savoir avant de se lancer dans une tentative d'amélioration d'équipement.";
			mes "Si vous me demandez d'ajouter un Slot à un objet, faîtes bien attention de ne pas en avoir plusieurs du même type dans votre inventaire.";
			next;
			mes "[Seiyablem]";
			mes "Par exemple, si vous possédez un Manteau +7 et un Manteau normal dans votre inventaire, il se peut que je ne choisisse pas le bon pendant le processus d'amélioration.";
			mes "Faîtes donc bien attention.";
			next;
			mes "[Seiyablem]";
			mes "^FF0000Ne portez sur vous que l'équipement sur lequel vous souhaitez que je travaille^000000.";
			mes "Par mesure de sécurité, il vaut mieux déposer tous vos autres équipements dans votre Stock Kafra.";
			close;
			
		case 4:
			mes "[Seiyablem]";
			mes "A bientôt, aventurier.";
			close;
	}
}
				
function	script	Func_Socket	{

	mes "[Seiyablem]";
	mes "Vous souhaitez tenter une amélioration sur l'objet " +getitemname(getarg(1))+ "?";
	if (getarg(0) == 1)
	{
		if(getarg(5) > 999) mes "Très bien, alors apportez-moi ^FF0000" +getarg(7)+ " " +getitemname(getarg(6))+ "s^000000, ainsi que^FF0000 " +getarg(5) / 1000+",000 000 zenys^000000.";
		else mes "Très bien, alors apportez-moi ^FF0000" +getarg(7)+ " " +getitemname(getarg(6))+ "s^000000, ainsi que^FF0000 " +getarg(5)+" 000 zenys^000000.";
	}
	else if (getarg(0) == 2)
	{
		if(getarg(5) > 999) mes "Très bien, alors apportez-moi ^FF0000" +getarg(7)+ " " +getitemname(getarg(6))+ "^000000, ^FF0000" +getarg(9)+ " " +getitemname(getarg(8))+ "^000000 ainsi que^FF0000 " +getarg(5) / 1000+",000 000 zenys^000000.";
		else mes "Très bien, alors apportez-moi ^FF0000" +getarg(7)+ " " +getitemname(getarg(6))+ "^000000, ^FF0000" +getarg(9)+ " " +getitemname(getarg(8))+ "^000000 ainsi que^FF0000 " +getarg(5)+"000 zenys^000000.";
	}
	mes "Ah, et n'oubliez pas d'apporter votre " +getitemname(getarg(1))+ "!";
	next;

	if(@sockettype == 1)
	{
		mes "[Seiyablem]";
		mes "Je peux tenter l'amélioration de cet équipement si vous avez les matériaux et l'argent nécessaires sur vous.";
		mes "Cependant, sachez qu'il y a une chance que le processus échoue.";
		mes "Je dois donc vous avertir...";
		next;
		mes "[Seiyablem]";
		mes "Si le processus d'amélioration échoue, l'^FF0000Arme^000000, ^FF0000ainsi que toutes les cartes présentes le cas échant, seront détruites^000000.";
		mes "Souhaitez-vous poursuivre le processus d'amélioration de votre Arme?";
		next;
	}
	else if(@sockettype == 2)
	{
		mes "[Seiyablem]";
		mes "Je peux tenter l'amélioration de cet équipement si vous aves les matériaux et l'argent nécessaires sur vous.";
		mes "Cependant, sachez qu'il y a une chance que le processus échoue.";
		mes "Je dois donc vous avertir...";
		next;
		mes "[Seiyablem]";
		mes "Si le processus d'amélioration échoue, l'^FF0000Armure^000000, ^FF0000son raffinement^000000 ^FF0000ainsi que toutes les cartes présentes le cas échant, seront détruites^000000.";
		mes "Souhaitez-vous poursuivre le processus d'amélioration de votre Armure?";
		next;
	}

	if ( select("Bien sûr!","J'ai changé d'avis.") == 2 )
	{
		mes "[Seiyablem]";
		mes "Besoin de temps pour réfléchir, hein?";
		mes "Je comprends.";
		mes "Souvenez-vous simplement du fameux adage qui dit que la chance sourit aux audacieux~";
		close;
	}

	mes "[Seiyablem]";
	mes "Très bien! Le vrai travail va pouvoir commencer!";
	mes "Vous feriez mieux de m'encourager ou de prier... enfin comme vous voulez, mais je ne garantie pas le résultat.";
	next;
	switch(getarg(0))
	{
		case 1:
			if((Zeny < getarg(5)*1000) || (countitem(getarg(6)) < getarg(7)) || (!countitem(getarg(1))))
			{
				mes "[Seiyablem]";
				mes "J'aimerais beaucoup travailler sur votre équipement, mais il vous manque certaines choses...";
				mes "Êtes-vous certain d'avoir l'équipement en question, les matériaux, et l'argent nécessaires?";
				close;
			}
			break;
	
		case 2:
			if((Zeny < getarg(5)*1000) || (countitem(getarg(6)) < getarg(7)) || (countitem(getarg(8)) < getarg(9)) || (!countitem(getarg(1))))
			{
				mes "[Seiyablem]";
				mes "J'aimerais beaucoup travailler sur votre équipement, mais il vous manque certaines choses...";
				mes "Êtes-vous certain d'avoir l'équipement en question, les matériaux, et l'argent nécessaires?";
				close;
			}
			break;
	}

	set .@socketrand,rand(1,100);
	if((.@socketrand > getarg(3)) && (.@socketrand < getarg(4)))
	{
		if(getarg(4) == 51) specialeffect EF_LORD;
		else specialeffect EF_SANCTUARY;
		mes "[Seiyablem]";
		mes "Et bien on dirait que c'est une réussite!";
		mes "Et voilà pour vous!";
		delitem getarg(1),1;
		delitem getarg(6),getarg(7);
		if(getarg(0) == 2) delitem getarg(8),getarg(9);
		set Zeny,Zeny - getarg(5)*1000;
		getitem getarg(2),1;
		next;
		mes "[Seiyablem]";
		mes "A bientôt, mon pote!";
		close;
	}
	else
	{
		specialeffect EF_FREEZING;
		mes "[Seiyablem]";
		mes "Wah! ...Je suis désolé, mais j'ai échoué.";
		mes "Ne soyez pas déçu, vous retenterez votre chance une autre fois, d'accord?";
		delitem getarg(1),1;
		delitem getarg(6),getarg(7);
		if(getarg(0) == 2) delitem getarg(8),getarg(9);
		set Zeny,Zeny - getarg(5)*1000;
		next;
		mes "[Seiyablem]";
		mes "A bientôt!";
		close;
	}
}

lhz_in02,281,35,5	duplicate(Seiyablem)	Seiyablem#lhz	84
prt_in,33,70,5	duplicate(Seiyablem)	Seiyablem#prt	84
morocc,51,41,5	duplicate(Seiyablem)	Seiyablem#moc	84

payon,143,143,7	script	Jeune Homme#socket	97,{

	mes "[Jeune Homme]";
	mes "J'étudie afin de devenir Ingénieur d'Amélioration d'Armes et d'Armures.";
	mes "C'est un nouveau domaine d'études, qui permet d'ajouter des Slots aux équipements... Une activité très lucrative qui plus est.";
	next;
	mes "[Jeune Homme]";
	mes "Bien sûr, il faut beaucoup de chance et d'argent pour ce faire...";
	mes "Mais si vous êtes vraiment chanceux, vous pouvez obtenir des Armes ou des Armures uniques, avec plus de Slots qu'à l'origine!";
	mes "Ça semble dingue, non?";
	next;
	mes "[Jeune Homme]";
	mes "Mais ne vous réjouissez pas trop vite.";
	mes "Ce travail d'Amélioration ne peut être pratiqué que sur certains équipements, et l'ajout de Slots ne peut pas dépasser deux supplémentaires.";
	mes "J'ai encore tellement à apprendre...";
	close;
}

lhz_in02,269,33,5	duplicate(Jeune Homme#socket)	Jeune Homme#lhz	97
prt_in,31,57,1	duplicate(Jeune Homme#socket)	Jeune Homme#prt	97
morocc,60,42,3	duplicate(Jeune Homme#socket)	Jeune Homme#moc	97
