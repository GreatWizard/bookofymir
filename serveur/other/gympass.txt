//= Le Livre d'Ymir http://bookofymir.free.fr/
//= traduit par No Healing
//===== eAthena Script ======================================= 
//= Gym Pass Cash Item NPC
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Exchange Gym Passes to learn "Enlarge Weight Limit"
//= iRO NPC situated in Payon beside Kafra Shop.
//===== Additional Comments: ================================= 
//= 1.0 First version [L0ne_W0lf]
//= 1.1 Fixed the NPC saying you "00" [L0ne_W0lf]
//= 1.2 Replaced effect numerics with constants. [Samuray22]
//============================================================ 

payon,173,141,4	script	Ripped Cabus#GymPass	899,{
	mes "[Ripped]";
	mes "Salut à vous!";
	mes "Les gens ne font plus autant d'efforts de nos jours. Même si vous gagnez votre vie en combattant, certaines parties de votre corps peuvent s'être ramollies par endroit. Vous voyez ce que je veux dire?";
	next;
	mes "[Ripped]";
	mes "Mais si vous vous entraînez avec moi je peux vous garantir que vous serez capable de soulever et de transporter bien plus de choses.";
	mes "Il faudra juste que vous me présentiez un ^FF0000Gym Pass^000000 chaque fois que vous voudrez vous entraîner, et nous pourrons commencer.";
	next;
	mes "[Ripped]";
	mes "Mais ne vous excitez pas trop: quelque soit le temps que je passe à vous entraîner, vous pourriez toujours vouloir en faire plus.";
	mes "Vous avez déjà entendu parler de quelqu'un qui en avait trop fait? Non? C'est parce qu'ils sont morts! Vous voyez?";
	next;
	mes "[Ripped]";
	mes "Voyons... pour vous je dirai que je pourrai vous entraîner ^FF000010 fois^000000 pour augmenter votre capacité à porter des objets sans trop de risques.";
	mes "Alors... Êtes-vous prêt à transpirer?";
	next;
	switch(select("Oui.:Non.:Hum, je me suis ramolli.")) {
	case 1:
		if (countitem(7776) == 0) {
			mes "[Ripped]";
			mes "Qu'est-ce que je vous ai dit, l'ami?";
			mes "Vous devez me présenter votre ^FF0000Gym Pass^000000 si vous voulez faire une séance de remise en forme et vous entraîner à porter plus de choses.";
			close;
		}
		else if (getskilllv("ALL_INCCARRY") >= 10) {
			mes "[Ripped]";
			mes "Je ne pense pas que je puisse vous entraîner plus que cela, l'ami!";
			mes "Regardez votre musculature: ce serait dangereux pour votre corps de vouloir en faire plus. Et puis je vous ai dit ce qui se passait quand on essayait de dépasser ses limites, pas vrai?";
			close;
		}
		else {
			mes "[Ripped]";
			mes "Oh, génial, je vois que vous avez apporté votre Gym Pass.";
			mes "Bon, faîtes la même chose que moi jusqu'à ce que vous sentiez vraiment vos muscles travailler.";
			mes "Prêt? On commence!";
			next;
			specialeffect2 EF_EARTHSPIKE;
			next;
			specialeffect2 EF_DEVIL;
			next;
			specialeffect2 EF_COIN;
			next;
			specialeffect2 EF_SIGHTRASHER;
			next;
			skill "ALL_INCCARRY",getskilllv("ALL_INCCARRY")+1,0;
			delitem 7776,1;
			set gympassmemory,gympassmemory+1;
			mes "[Ripped]";
			mes "Voilà, maintenant vous devriez être capable de porter plus d'objets.";
			mes "Voyons, à ce rythme nous pourrions avoir ^FF00000" + (10-getskilllv(681)) + "^000000 séances supplémentaires.";
			close;
		}
	case 2:
		mes "[Ripped]";
		mes "Bah, c'est dommage. Eh bien, revenez si vous changez d'avis. Et parlez de moi à vos amis: si ils se sont ramollis, je les aiderais à les remettre en forme!";
		close;
	case 3:
		if (getskilllv("ALL_INCCARRY") == 0) {
			mes "[Ripped]";
			mes "Euh... On ne s'est jamais entraînés ensemble, ça j'en suis sûr!";
			close;
		}
		else {
			mes "[Ripped]";
			mes "Qu'est-ce qui s'est passé? Vous avez laissé vos dorsaux s'atrophier? Heureusement pour eux, il y a ce qu'on appelle la mémoire musculaire. Ça ne devrait pas prendre trop de temps de reconstruire votre musculature...";
			next;
			specialeffect2 EF_EARTHSPIKE;
			next;
			specialeffect2 EF_DEVIL;
			next;
			specialeffect2 EF_COIN;
			next;
			specialeffect2 EF_SIGHTRASHER;
			next;
			skill 681,gympassmemory,0;
			mes "[Ripped]";
			mes "Qu'est-ce que vous en dites? Vous avez retrouvé votre musculature aussi facilement que ça! Évitez de vous laisser aller à l'avenir, d'accord?";
			close;
		}
	}
}
