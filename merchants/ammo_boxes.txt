//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script =======================================
//= Magazine Dealer Kenny
//===== By: ==================================================
//= eAthena dev team
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= eAthena 1.0+
//===== Description: =========================================
//= Turns bullets/spheres into packs/casings.
//===== Additional Comments: =================================
//= 1.0 First version. [SinSloth]
//= 1.1 Optimized version - Reduced to only one function [SinSloth]
//= 1.2 Optimized^2, corrected npc's name [ultramage]
//= 1.2a Optimized. Please, ommit extra NPC names [Lupus]
//============================================================

que_ng,187,149,3	script	Kenny	83,{

	mes "[Kenny]";	
	mes "Bonjour, mon nom est Kenny.";
	if(BaseJob != Job_Gunslinger) {
		mes "Je peux fabriquer des ^0000FFBullet Casings^000000";
		mes "et des ^0000FFSphere Packs^000000!";
		next;
		mes "[Kenny]";
		mes "Mais vous n'êtes pas Gunslinger alors ça ne vous intéressera sûrement pas...";
		close;
	}
	mes "Je peux fabriquer des ^0000FFBullet Casings^000000 ";
	mes "et des ^0000FFSphere Packs^000000 pour vous!";
	next;
	switch(select("Lightning Sphere Pack","Blind Sphere Pack","Poison Sphere Pack","Freezing Sphere Pack","Flare Sphere Pack","Bullet Casing","Shell of Blood Casing","Silver Bullet Casing","Annuler")) {
	case 1: callfunc "Func_Casing",13204,12144; break;
	case 2: callfunc "Func_Casing",13206,12145; break;
	case 3: callfunc "Func_Casing",13205,12146; break;
	case 4: callfunc "Func_Casing",13207,12147; break;
	case 5: callfunc "Func_Casing",13203,12148; break;
	case 6: callfunc "Func_Casing",13200,12149; break;
	case 7: callfunc "Func_Casing",13202,12150; break;
	case 8: callfunc "Func_Casing",13201,12151; break;
	default:
		mes "[Kenny]";
		mes "D'accord, pas de problème.";
		mes "Revenez quand vous voulez!";
		close;
	}
	close;
}

function	script		Func_Casing	{

	mes "[Kenny]";
	mes "Combien en voulez-vous?";
	next;
	mes "[Kenny]";
	mes "" +getitemname(getarg(1))+ " nécessite";
	if(getarg(0) == 13202) 
		mes "500 Shells of Blood";
	else
		mes "500 " +getitemname(getarg(0))+ "s";
	mes "et 500 Zenys pièce.";
	next;
	mes "[Kenny]";
	mes "Vous pouvez en échanger 50 au maximum.";
	mes "Entrez 0 si vous voulez annuler.";
	next;
	input .@amount;
	mes "[Kenny]";
	if(.@amount < 1) {
		mes "D'accord, pas de problème.";
		mes "Revenez quand vous voulez!";
		close;
	}
	if(.@amount > 50)	{
		mes "Choisissez un nombre compris entre 1 et 50.";
		close;
	}
	//Weight checking
	if(checkweight(getarg(1), .@amount) != 1)	{
		mes "Vous portez déjà trop de choses...";
		close;
	}
	
	//Materials checking
	if(countitem(getarg(0)) < .@amount * 500) {
		mes "Désolé, mais vous devez fournir au moins 500 Bullets ou 500 Spheres ainsi que 500 zenys pour que je puisse créer 1 Bullet Casing ou 1 Sphere Pack.";
		close;
	}
	
	//Zeny checking
	if(Zeny < .@amount * 500) {
		mes "Vous n'avez pas assez d'argent pour tout ça...";
		close;
	}

	mes "Et voilà~!";
	if(getarg(1) < 12149)
		mes "Voici vos Packs~";
	else
		mes "Voici vos Casings~";
	set Zeny, Zeny - .@amount * 500;
	delitem getarg(0), .@amount * 500;
	getitem getarg(1), .@amount;
	close;
}