//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Refining NPCs
//===== By: ==================================================
//= Syrus22 (1.1) dafide18 (1.4) Skotlex (1.5)
//===== Current Version: =====================================
//= 2.3
//===== Compatible With: =====================================
//= Any Athena Version
//===== Description: =========================================
//= Refining NPCs and Metal Salesmen.
//===== Additional Comments: =================================
//= 1.0 - by A bunch of people!
//=       Syrus22 - Completely redid the script using functions... also
//=       added the option for auto safe refining and multiple refining.
//= 1.1 - Negative input bug fixed [Lupus]
//= 1.2 - Added additional reparimen in morroc and payon. Added
//=       Christopher the blacksmith in Geffen. Edited some dialogue [kobra_k88]
//= 1.3 - New Payon Locations [Darkchild]
//=       Corrected zeny subtraction thx to jpnmania77.[kobra_k88]
//= 1.3a - Temporary corrected an exploit. Need to check sources
//=        to fully fix bug [Shinigami]
//=        Fixed repairman prices [shadowlady]
//=        Fixed bug that skips requirements thanks to sir_loon [massdriller]
//=        Fixed itemid error thanks to -Vitamin- [massdriller]
//= 1.4 - check again item in refining procedure to avoid
//=        hacker that can change item [dafide18]
//= 1.5 - Fixed crashing due to badly used callfunc's [Skotlex]
//        Lupus, don't rollback this important fix again! >.<
//= 1.5a - Corrected an unneeded callfunc, fixed the anti-bot 
//=       exploit ruining the safe refine loop. [Skotlex]
//= 1.5b - Fixed Spelling mistakes. [Nexon] 
//= 1.6	- Replaced all breaks for ends as per the new script engine [Skotlex]
//= 1.7 - Added Einbroch Refiners (Custom names ^^;) and a duplicated BS Shop. [Poki#3]
//= 1.8 - Added Lighthalzen Refiners (Custom names again ^^;) [Poki#3]
//= 1.8a - Fixed wrong indication thanks to NeoSaro [Lupus]
//= 1.9 - Rewrote repairman, removed the Steel from repair cost [DracoRPG]
//= 2.0 - Fixed missed equppment presence check. Thx2 Coltaro [Lupus]
//= 2.0a - Added weight checks thanks to Neouni [Playtester]
//= 2.0b - Fixed the names of Lighthalzen and Einbroch refiners thanks to Maud_Dib [Kargha]
//= 2.1 - Removed Duplicates [Silent]
//= 2.2 - Changed name from "Emvertacon" to "Emveretarcon". [Samuray22]
//	- Thanks to Barron-Monster.
//= 2.2b - Changed name from "Pharacon" to "Phracon". [Samuray22]
//	- Thanks to Barron-Monster.
//= 2.3 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//============================================================


//=========================================================
// Christopher: Geffen Blacksmith
//=========================================================
geffen_in,110,172,2	script	Christopher#1::Chrisbs	63,{
	mes "[Christopher Guillenrow]";
	mes "Bienvenue à l'Atelier de Christopher. Ici vous trouverez tout c'qu'il faut comme équipements pour forger. En quoi puis-je vous aider?";
	M_Menu:
	next;
	menu "Acheter une Enclume",M_0, "Acheter un Objet pour la Forge",M_1, "Acheter de Métaux",M_2, "Raffiner des Minerais",M_3, "Annuler",M_End;

	M_0:
		mes "[Christopher Guillenrow]";
		mes "Plus l'Enclume est de bonne qualité, meilleure s'ra la chance de réussite de forger une arme. Mais 'faut pouvoir y mettre le prix.";
		next;
		menu "Enclume - 30 000z.",sM_Anvil, "Enclume en Oridecon - 120 000z.",sM_OriAnvil, "Enclume en Or - 300 000z.",sM_GolAnvil,
			"De meilleures enclumes s'il vous plaît.",sM_BetAnvil, "Annuler.",M_Menu;

		sM_Anvil:
			if(Zeny < 30000) goto L_NoZeny;
			if(checkweight(986,1) == 0 ) goto L_OverWeight;
			set Zeny, Zeny - 30000;
			getitem 986,1;
			mes "[Christopher Guillenrow]";
			mes "C'est la moins cher, mais elle est quand même très efficace.";
			next;
			goto L_Thanks;
		sM_OriAnvil:
			if(Zeny < 120000) goto L_NoZeny;
			if(checkweight(987,1) == 0 ) goto L_OverWeight;
			set Zeny, Zeny - 120000;
			getitem 987,1;
			mes "[Christopher Guillenrow]";
			mes "Ah! V'z'avez l'oeil pour choisir les enclumes. Celle-ci vous conviendra parfaitement.";
			next;
			goto L_Thanks;
		sM_GolAnvil:
			if(Zeny < 300000) goto L_NoZeny;
			if(checkweight(988,1) == 0 ) goto L_OverWeight;
			set Zeny, Zeny - 300000;
			mes "[Christopher Guillenrow]";
			mes "C'est la meilleure enclume que je possède dans c't'atelier! Avec ça, vous f'rez les meilleures forges de tous les temps!";
			getitem 988,1;
			next;
			goto L_Thanks;
		sM_BetAnvil:
			mes "[Christopher Guillenrow]";
			mes "J'suis désolé, mais je n'ai rien de mieux que l'Enclume en Or.";
			mes "Peut-être que 'Ringgel', le fabricant légendaire d'Enclumes, pourrait vous renseigner... Mais il est dur à dénicher c'gars-là.";
			close;

	M_1:
		mes "[Christopher Guillenrow]";
		mes "Tout Forgeron qui se respecte utilise d'bons objets pour forger. Vous constaterez rapidement que y'a pas mieux que mes outils sur le marché dans l'coin!";
		mes "Choisissez ce que vous voulez.";
		sM_Menu1:
		next;
		menu "Mini-Furnace - 150z.",sM_Furn, "Marteau en Acier - 1000z.",sM_IrHam, "Marteau en Or - 3000z.",sM_GldHam,
		     "Marteau en Oridecon - 5000z.",sM_OriHam, "Annuler",M_Menu;

		sM_Furn:
			mes "[Christopher Guillenrow]";
			mes "C'est indispensable si vous voulez raffiner des métaux!!";
			mes "Bon, combien en voulez-vous? Si vous n'en voulez pas, vous n'avez qu'à dire '0'. Sinon, le maximum que vous pouvez acheter d'un coup est 1000";
			set @input, 0;
			input @input;
			next;
			if(@input < 1 ) goto sM_Menu1;
			if(@input > 1000 ) goto sM_Max;
			if(Zeny < 150 * @input) goto L_NoZeny;
			if(checkweight(612,@input) == 0 ) goto L_OverWeight;
			set Zeny, Zeny - (150 * @input);
			getitem 612, @input;
			goto L_Thanks;
		sM_IrHam:
			if(Zeny < 1000) goto L_NoZeny;
			if(checkweight(613,1) == 0 ) goto L_OverWeight;
			set Zeny, Zeny - 1000;
			getitem 613,1;
			goto L_Thanks;
		sM_GldHam:
			if(Zeny < 3000) goto L_NoZeny;
			if(checkweight(614,1) == 0 ) goto L_OverWeight;
			set Zeny, Zeny - 3000;
			getitem 614,1;
			goto L_Thanks;
		sM_OriHam:
			if(Zeny < 5000) goto L_NoZeny;
			if(checkweight(615,1) == 0 ) goto L_OverWeight;
			set Zeny, Zeny - 5000;
			getitem 615,1;
			goto L_Thanks;
		sM_Max:
			mes "[Christopher Guillenrow]";
			mes "Désolé, mais vous ne pouvez pas en acheter plus de 1000 à la fois.";
			close;

	M_2:
		mes "[Christopher Guillenrow]";
		mes "Je peux vous proposer deux types de métaux. Lequel voulez-vous?";
		set @chris, 1;
		set @name$, "Christopher Guillenrow";
		callfunc "phramain";

	M_3:
		mes "[Christopher Guillenrow]";
		mes "Je peux raffiner les minerais suivants : 'Rough Oridecon' et 'Rough Elunium'. J'aurais besoin de cinq 'Rough' pour fabriquer un minerais pur.";
		mes "Ça vous intéresse?";
		set @chris, 1;
		set @name$, "Christopher Guillenrow";
		callfunc "orimain";

	M_End:
		close;

	L_NoZeny:
		mes "[Christopher Guillenrow]";
		mes "Je ne suis pas sûr d'pouvoir vous accorder un prix au rabais. Je n'peux pas me permettre de perdre des bénéfices à cause de vous.";
		emotion 4;
		close;

	L_OverWeight:
		mes "[Christopher Guillenrow]";
		mes "Vous avez l'air un peu frêle... Je ne suis pas sûr que vous puissiez porter tout ça.";
		close;

	L_Thanks:
		mes "[Christopher Guillenrow]";
		mes "Merci d'avoir fait vos achats dans mon atelier. Revenez quand vous voulez!";
		emotion 15;
		close;
}

ein_in01,38,29,6	duplicate(Chris)	Christopher#2	63

//=====================================================================================
// Weapon/Armor Refiners
//=====================================================================================
prt_in,63,60,4	script	Hollengrhen	85,{
	set @name$,"Hollengrhen";
	callfunc "refinemain";
	end;
}
morocc_in,73,38,4	script	Aragham	99,{
	set @name$,"Aragham";
	callfunc "refinemain";
	end;
}
payon,144,173,4	script	Antonio	88,{
	set @name$,"Antonio";
	callfunc "refinemain";
	end;
}
alberta_in,28,58,4	script	Fredrik	85,{
	set @name$,"Fredrik";
	callfunc "refinemain";
	end;
}
yuno_in01,164,26,6	script	Disturb	88,{
	set @name$,"Disturb";
	callfunc "refinemain";
	end;
}
ein_in01,24,87,6	script	Manthasman	826,{
	set @name$,"Manthasman Pruhag";
	callfunc "refinemain";
	end;
}
lhz_in02,282,20,7	script	Fulerr	869,{
	set @name$,"Fulerr";
	callfunc "refinemain";
	end;
}

//============================================================
//= Main Refiner Function
//============================================================
//= To allow auto safe refining/multiple refining set the
//= @features variable to 1
//============================================================
function	script	refinemain	{
	set @features,0;

	mes "[" + @name$ + "]";
	mes "Je suis Forgeron... Je peux raffiner n'importe quelle arme ou armure de votre choix!";
	mes "Quel équipement souhaiteriez-vous que je raffine?";
	M_Menu:
	next;
	menu getequipname(1),PART1,getequipname(2),PART2,getequipname(3),PART3,getequipname(4),PART4,getequipname(5),PART5,
	     getequipname(6),PART6,getequipname(7),PART7,getequipname(8),PART8,getequipname(9),PART9,getequipname(10),PART10;

	//Head Gear
	PART1:
		set @part,1;
		if (getequipisequiped(1)) goto CHECK1;
		mes "[" + @name$ + "]";
		mes "Vous voulez peut-être que je raffine le petit pois qui vous sert de cerveau?";
		emotion 6;
		goto M_Menu;
	//Armor
	PART2:
		set @part,2;
		if (getequipisequiped(2)) goto CHECK1;
		mes "[" + @name$ + "]";
		mes "Vous voulez que j'applique mon châlumeau sur votre peau...?";
		emotion 6;
		goto M_Menu;
	//Left Hand
	PART3:
		set @part,3;
		if (getequipisequiped(3)) goto CHECK1;
		mes "[" + @name$ + "]";
		mes "J'aurais beau essayer, je ne peux pas transformer votre main gauche en arme de guerre...";
		emotion 4;
		goto M_Menu;
	//Right Hand
	PART4:
		set @part,4;
		if (getequipisequiped(4)) goto CHECK1;
		mes "[" + @name$ + "]";
		mes "J'aurais beau essayer, je ne peux pas transformer votre main droite en arme de guerre...";
		emotion 4;
		goto M_Menu;
	//Garment
	PART5:
		set @part,5;
		if (getequipisequiped(5)) goto CHECK1;
		mes "[" + @name$ + "]";
		mes "Mais regardez-vous... Vous n'avez même pas de Garment sur vous...";
		goto M_Menu;
	//Foot Gear
	PART6:
		set @part,6;
		if (getequipisequiped(6)) goto CHECK1;
		mes "[" + @name$ + "]";
		mes "Argh!! Ça pue des pieds ici! Je ne peux pas raffiner dans ces conditions... erk!!";
		emotion 16;
		goto M_Menu;
	//Accessory1
	PART7:
		set @part,7;
		if (getequipisequiped(7)) goto CHECK1;
		mes "[" + @name$ + "]";
		mes "Qu'entendez-vous par 'Accessoire'? Où vous en voyez un, vous?";
		emotion 20;
		goto M_Menu;
	//Accessory2
	PART8:
		set @part,8;
		if (getequipisequiped(8)) goto CHECK1;
		mes "[" + @name$ + "]";
		mes "Qu'entendez-vous par 'Accessoire'? Où vous en voyez un, vous?";
		emotion 20;
		goto M_Menu;
	PART9:
		set @part,9;
		if (getequipisequiped(9)) goto CHECK1;
		mes "[" + @name$ + "]";
		mes "Qu'est-ce que vous voulez que je fasse? Vous n'avez aucun équipement...";
		emotion 20;
		goto M_Menu;
	PART10:
		set @part,10;
		if (getequipisequiped(10)) goto CHECK1;
		mes "[" + @name$ + "]";
		mes "Qu'est-ce que vous voulez que je fasse? Vous n'avez aucun équipement...";
		emotion 20;
		goto M_Menu;

//Check if the item is refinable...
CHECK1:
	if(getequipisenableref(@part)) goto CHECK2;
	mes "[" + @name$ + "]";
	mes "Je ne peux pas travailler sur cet équipement...";
	close;

//Check if the item is identified... (Don't know why this is in here... but kept it anyway)
CHECK2:
	if(getequipisidentify(@part)) goto CHECK3;
	mes "[" + @name$ + "]";
	mes "Vous devez d'abord identifier cet équipement.";
	close;

//Check to see if the items is already +10
CHECK3:
	if(getequiprefinerycnt(@part) < 10) goto REFINE0;
	mes "[" + @name$ + "]";
	mes "Cet équipement est déjà à son maximum, je ne peux pas le raffiner plus.";
	close;

//Refine Armor
REFINE0:
	set @refineitemid, getequipid(@part); // save id of the item
	set @refinerycnt, getequiprefinerycnt(@part); //save refinery count
	if(getequipweaponlv(@part) > 0) goto REFINE1;
	set @matname$,"Elunium";
	set @material,985;
	set @price,2000;
	set @safe,4;
	if(@features == 1) goto L_refinefeatures;
	goto L_refinenormal;
//Refine Level 1 Weapon
REFINE1:
	if(getequipweaponlv(@part) > 1) goto REFINE2;
	set @matname$,"Phracon";
	set @material,1010;
	set @price,50;
	set @safe,7;
	if(@features == 1) goto L_refinefeatures;
	goto L_refinenormal;
//Refine Level 2 Weapon
REFINE2:
	if(getequipweaponlv(@part) > 2) goto REFINE3;
	set @matname$,"Emveretarcon";
	set @material,1011;
	set @price,200;
	set @safe,6;
	if(@features == 1) goto L_refinefeatures;
	goto L_refinenormal;
//Refine Level 3 Weapon
REFINE3:
	if(getequipweaponlv(@part) > 3) goto REFINE4;
	set @matname$,"Oridecon";
	set @material,984;
	set @price,5000;
	set @safe,5;
	if(@features == 1) goto L_refinefeatures;
	goto L_refinenormal;
//Refine Level 4 Weapon
REFINE4:
	set @matname$,"Oridecon";
	set @material,984;
	set @price,20000;
	set @safe,4;
	if(@features == 1) goto L_refinefeatures;
	goto L_refinenormal;

L_refinenormal:
	mes "[" + @name$ + "]";
	mes "Afin de raffiner cet équipement, j'aurais besoin de minerais ^ff9999" + @matname$ + "^000000 et de " + @price + " Zenys.";
	mes "Voulez-vous continuer?";
	next;
	menu "Oui.",-,"Non.",Lcancel;

		if (getequippercentrefinery(@part) == 100) goto L_Sub;
		mes "[" + @name$ + "]";
		mes "Hum... Attendez une minute! Cet équipement a déjà été raffiné un certain nombre de fois!";
		mes "Je dois vous prévenir, que si vous le raffinez ENCORE, il pourrait SE DETRUIRE durant la forge et devenir INUTILISABLE!!";
		next;
		mes "["+@name$+"]";
		mes "Voulez vous tout de même tenter de le raffiner? Si oui, je ne vous garantie pas le résultat...";
		next;
		menu "Oui.",-,"Non.",Lcancel1;

		L_Sub:
			if ((countitem(@material) < 1) || (Zeny < @price)) goto Lcancel2;
			set Zeny,Zeny-@price;
			delitem @material,1;

Lrefine:
	if (getequipisequiped(@part) == 0) goto LNoItem; // hacker has removed the item (not changed, why?)
	if (getequipid(@part) != @refineitemid) goto LNoFake; // hacker has changed the item
	if (getequiprefinerycnt(@part) != @refinerycnt) goto LNoFake; // hacker has changed the item
	if (getequippercentrefinery(@part) <= rand(100)) goto Lfail;
	mes "["+@name$+"]";
	mes "Clang! Clang! Clang!";
	successrefitem @part;
	next;
	mes "["+@name$+"]";
	mes "HAHA, quel talent! On dirait que mes vieux os ne sont pas encore rouillés! Splendide... tout simplement splendide...";
	emotion 21;
	close;

Lfail:
	mes "[" + @name$ + "]";
	mes "Clang! Clang! Clang!";
	failedrefitem @part;
	next;
	mes "["+@name$+"]";
	mes "Aaahhh!! Oh non...!!";
	emotion 16;
	next;
	mes "["+@name$+"]";
	mes "He... Hem... Je suis désolé mais le raffinement a ^ff0000échoué ^000000.";
	next;
	mes "["+@name$+"]";
	mes "Jai vraiment honte de ce que j'ai fait... MAIS je vous avais prévenu qu'il y avait des risques!";
	close;

LNoItem:
	mes "[" + @name$ + "]";
	mes "Faites plus attention, vous n'avez pas d'équipement sur vous...";
	close;

LNoFake:
	mes "[" + @name$ + "]";
	mes "Clan... Non mais vous vous imaginiez que je serais stupide au point de ne rien voir!?!";
	mes "Vous avez changé d'équipement...";
	mes "Allez-vous en, avant que je vous assome avec mon marteau!!!";
	close;

Lcancel:
	mes "[" + @name$ + "]";
	mes "Hum... Il faudrait savoir ce que vous voulez...";
	close;

Lcancel1:
	mes "[" + @name$ + "]";
	mes "Bon choix.";
	mes "Ah ça oui... un choix judicieux. Je serais terriblement embarassé si je détruisais votre équipement par mégarde.";
	close;

Lcancel2:
	mes "[" + @name$ + "]";
	mes "C'est tout ce que vous avez? Malheureusement je ne peux pas travailler si vous ne me donnez pas ce qui est nécessaire. Essayez de vous mettre à ma place.";
	close;

// New Refining Functions ========================
L_refinefeatures:
	if(getequiprefinerycnt(@part) >= @safe) goto Lnosafe;
	mes "[" + @name$ + "]";
	mes "Je peux raffiner cet équipement jusqu'à sa limite de sécurité, ou bien le nombre de fois que vous le souhaitez... C'est vous qui choisissez...";
	next;
	menu "Jusqu'à la limite de sécurité.",Lsafe,"Je vous dirai combien de fois.",Lnosafe,"J'ai changé d'avis...",Lcancel;

Lsafe:
	set @refinecnt,@safe - getequiprefinerycnt(@part);
	set @fullprice,@price * @refinecnt;
	mes "[" + @name$ + "]";
	mes "Cela vous coûtera " + @refinecnt + " " + @matname$ + " et " + @fullprice + " Zenys. Ça vous va?";
	next;
	menu "Oui.",-,"Non.",Lcancel;
	if((countitem(@material) < @refinecnt) || (Zeny < @fullprice)) goto Lcancel2;
	set Zeny,Zeny - @fullprice;
	delitem @material,@refinecnt;
	goto L_refinesafe;

Lnosafe:
	mes "[" + @name$ + "]";
	mes "Donc, combien de fois souhaitez-vous que je raffine cet équipement?";
	next;
	input @refinecnt;
	if (@refinecnt<1) goto Lcancel3; //fixed by Lupus
	set @refinecheck,@refinecnt + getequiprefinerycnt(@part);
	if(@refinecheck > 10) goto Lcancel3;
	set @fullprice,@price * @refinecnt;
	mes "[" + @name$ + "]";
	mes "Cela vous coûtera " + @refinecnt + " " + @matname$ + " et " + @fullprice + " Zenys. Ça vous va?";
	next;
	menu "Oui.",-,"Non.",Lcancel;
		if(@refinecheck > @safe) goto Lwarn;
		if((countitem(@material) < @refinecnt) || (Zeny < @fullprice)) goto Lcancel2;
		set Zeny,Zeny - @fullprice;
		delitem @material,@refinecnt;
		goto L_refinenumber;
		end;

		Lwarn:
			set @refinecheck,@refinecheck - @safe;
			mes "[" + @name$ + "]";
			mes "Cela signifique que j'essayerai de raffiner cet équipement " + @refinecheck + " fois au delà de sa limite de sécurité. Votre équipement sera peut-être détruit... Est-ce que vous voulez quand même le faire?";
			next;
			menu "Oui.",-,"Non.",Lcancel1;
				if((countitem(@material) < @refinecnt) || (Zeny < @fullprice)) goto Lcancel2;
				set Zeny,Zeny - @fullprice;
				delitem @material,@refinecnt;
				goto L_refinenumber;

Lcancel3:
	mes "[" + @name$ + "]";
	mes "Je ne peux pas raffiner cet objet autant de fois.";
	close;

// SubFunction: Safe Refine ---------------------
L_refinesafe:
	if (getequipisequiped(@part) == 0) goto LNoItem; // hacker has removed the item (no changed, why?)
	if (getequipid(@part) != @refineitemid) goto LNoFake; // hacker has changed the item
	if (getequippercentrefinery(@part) < 100) goto LNoFake; // hacker has changed the item (it is not safe anymore)
	mes "Clang, clang!!!";
	successrefitem @part;
	emotion 21;
	set @refinecnt,@refinecnt - 1;
	next;
	if(@refinecnt == 0) goto Lend;
	goto L_refinesafe;

	Lend:
		mes "[" + @name$ + "]";
		mes "Et voilà, terminé! Revenez quand vous voulez.";
		close;
		
// SubFunction: Refine 
L_refinenumber:
	if (getequipisequiped(@part) == 0) goto LNoItem; // hacker has removed the item (no changed, why?)
	if (getequipid(@part) != @refineitemid) goto LNoFake; // hacker has changed the item
	mes "Clang, clang!!!";
	if (getequippercentrefinery(@part)<=rand(100)) goto Lfail_number;
	successrefitem @part;
	emotion 21;
	set @refinecnt,@refinecnt - 1;
	next;
	if(@refinecnt == 0) goto Lend;
	goto L_refinenumber;

	Lfail_number:
		failedrefitem @part;
		emotion 23;
		mes "[" + @name$ + "]";
		mes "WAHHHH!!! Je suis vraiment désolé... Je vous avais prévenu que ça pouvait arriver...";
		set @refinecnt,@refinecnt - 1;
		if(@refinecnt == 0) goto Lend2;
		mes "Je vous rends vos Zenys et vos Minerais...";
		getitem @material,@refinecnt;
		set @fullprice,@refinecnt * @price;
		set Zeny,Zeny + @fullprice;

	Lend2:
		close;
}



//==============================================================================
// Material Salesmen
//==============================================================================
prt_in,56,69,4	script	Vurewell	86,{
	set @name$,"Vurewell";
	callfunc "phramain";
	end;
}
payon,145,178,4	script	Begnahd	88,{
	set @name$,"Begnahd";
	callfunc "phramain";
	end;
}
morocc_in,65,37,4	script	Sade	99,{
	set @name$,"Sade";
	callfunc "phramain";
	end;
}
alberta_in,18,59,5	script	Kahlamanlith	86,{
	set @name$,"Kahlamanlith";
	callfunc "phramain";
	end;
}
yuno_in01,171,26,6	script	Dillemat	88,{
	set @name$,"Dillemat";
	callfunc "phramain";
	end;
}
ein_in01,15,87,7	script	Tirehaus	86,{
	set @name$,"Tirehaus";
	callfunc "phramain";
	end;
}
lhz_in02,278,24,3	script	Krugg	86,{
	set @name$,"Krugg";
	callfunc "phramain";
	end;
}

//============================================================
//= Material Salesmen Functions
//============================================================
function	script	phramain	{
	if(@chris == 1) goto M_Menu;
	mes "[" + @name$ + "]";
	mes "Salut. Je vends quelques métaux que je viens juste d'extraire.";
	mes "Je vous propose du Phracon et de l'Emveretarcon.";
	mes "Voulez-vous en acheter?";
	M_Menu:
	set @chris, 0;
	next;
	menu"Phracon - 200 Zenys",PHARA,"Emveretarcon - 1000 Zenys",EMVER;

	PHARA:
		set @itemid,1010;
		set @value,200;
		goto CONTINUE;

	EMVER:
		set @itemid,1011;
		set @value,1000;

CONTINUE:
	mes "[" + @name$ + "]";
	mes "Combien en voulez-vous?";
	next;
	input @ammount;
	if (@ammount <= 0) goto L_BELOW;
	if (@ammount > 999) goto L_ABOVE;
	if (zeny < @value * @ammount) goto L_NOZENY;
	if (checkweight(@itemid,@ammount) == 0) goto L_WEIGHT;
	set Zeny,Zeny-@value*@ammount;
	getitem @itemid,@ammount;
	mes "[" + @name$ + "]";
	mes "Et voilà!";
	close;

L_NOZENY:
	mes "Vous n'avez pas assez de Zenys!";
	close;
L_WEIGHT:
	mes "Vous ne pourrez jamais porter tout ça!";
	close;
L_BELOW:
	mes "Vous devez entrer un nombre POSITIF!";
	close;
L_ABOVE:
	mes "Vous ne pouvez pas en acheter autant à la fois!";
	close;
}



//==============================================================================
// Ori/Elu Refiners
//==============================================================================
prt_in,63,69,4	script	Dietrich	84,{
	set @name$,"Dietrich";
	callfunc "orimain";
	end;
}
payon,137,178,4	script	Hakhim	88,{
	set @name$,"Hakhim";
	callfunc "orimain";
	end;
}
morocc_in,72,32,4	script	Abdul	99,{
	set @name$,"Abdul";
	callfunc "orimain";
	end;
}
alberta_in,21,63,5	script	Xenophon	84,{
	set @name$,"Xenophon";
	callfunc "orimain";
	end;
}
yuno_in01,171,22,6	script	Delayt	88,{
	set @name$,"Delayt";
	callfunc "orimain";
	end;
}
ein_in01,18,82,6	script	Matestein	84,{
	set @name$,"Matestein";
	callfunc "orimain";
	end;
}
lhz_in02,281,24,5	script	Fruel	84,{
	set @name$,"Fruel";
	callfunc "orimain";
	end;
}

//============================================================
//= Ori/Elu Functions
//============================================================
function	script	orimain	{
	if(@chris == 1) goto M_Menu;
	mes "[" + @name$ + "]";
	mes "Si vous m'apportez des 'Rough Oridecon'";
	mes "ou bien des 'Rough Elunium',";
	mes "je peux les purifier pour vous.";
	mes "Vous devez m'en apporter 5 de chaque.";
	M_Menu:
	set @chris, 0;
	next;
	menu "Fabriquer de l'Oridecon",M_Ori, "Fabriquer de l'Elunium",M_Elu, "Informations sur les pierres enchantées",M_Stones;

	M_Ori:
		if(countitem(756) < 5) goto L_NoOri;
		delitem 756,5;
		getitem 984,1;
		mes "[" + @name$ + "]";
		mes "Comme promis, voici votre Oridecon.";
		mes "Revenez quand vous voulez.";
		close;

		L_NoOri:
			mes "[" + @name$ + "]";
			mes "Hein? C'est une blague j'espère? Ne vous ai-je pas dit que j'avais besoin de 5 'Rough Oridecon' pour fabriquer un Oridecon?";
			close;
	M_Elu:
		if(countitem(757) < 5) goto L_NoElu;
		delitem 757,5;
		getitem 985,1;
		mes "[" + @name$ + "]";
		mes "Comme promis, voici votre Elunium.";
		mes "Revenez quand vous voulez.";
		close;

		L_NoElu:
			mes "[" + @name$ + "]";
			mes "Hein? C'est une blague j'espère? Ne vous ai-je pas dit que j'avais besoin de 5 'Rough Elunium' pour fabriquer un Elunium?";
			close;
	M_Stones:
		mes "[" + @name$ + "]";
		mes "Les pierres enchantées, hein...";
		next;
		mes "[" + @name$ + "]";
		mes "Et bien, depuis 20 ans que j'exerce le métier de mineur, j'en ai entendu parlé de nombreuses fois... mais je n'en ai jamais vu de mes propres yeux.";
		next;
		mes "[" + @name$ + "]";
		mes "On m'a dit que les pierres enchantées posserdaient des propriété élémentales, telles que ^5533FFl'Eau, la Terre, le Feu, le Vent^000000.";
		next;
		mes "[" + @name$ + "]";
		mes "Si quelqu'un arrive à intégrer une Pierre Enchantée dans une arme, pendant la forge, cette arme possèdera la même propriété que la pierre.";
		next;
		mes "[" + @name$ + "]";
		mes "Bien sûr, il faut que le forgeron en question soit suffisament compétent.";
		goto M_Menu;
}



//=====================================================================================
// Equipment Repairmen
//=====================================================================================
prt_in,62,54,2	script	Grendal	84,{
	set @name$,"Grendal";
	callfunc "repairmain";
	end;
}
//Temp Spot, Not Sure Where To place
payon,149,182,2	script	Atelier de Réparations#1	88,{
	set @name$,"Atelier de Réparations";
	callfunc "repairmain";
	end;
}
morocc_in,71,40,2	script	Atelier de Réparations#2	99,{
	set @name$,"Atelier de Réparations";
	callfunc "repairmain";
	end;
}
lhz_in02,284,14,3	script	Atelier de Réparations#3	86,{
	set @name$,"Atelier de Réparations";
	callfunc "repairmain";
	end;
}

//============================================================
//= Equipment Repair Function
//============================================================
function	script	repairmain	{
	set @repairprice,5000;
	mes "[" + @name$ + "]";
	mes "Je suis forgeron, je peux réparer n'importe lequel de vos équipements cassés.";
	mes "Dites-moi quel équipement vous souhaitez que je répare.";
	next;
	if(getbrokenid(1)==NULL) {
		mes "[" + @name$ + "]";
		mes "On dirait que vous n'avez rien à réparer...";
		close;
	}
	set @choice,select(getitemname(getbrokenid(1)),getitemname(getbrokenid(2)),
		getitemname(getbrokenid(3)),getitemname(getbrokenid(4)),getitemname(getbrokenid(5)),
		getitemname(getbrokenid(6)),getitemname(getbrokenid(7)),getitemname(getbrokenid(8)),
		getitemname(getbrokenid(9)),getitemname(getbrokenid(10)));
	mes "[" + @name$ + "]";
	mes "Je vais réparer votre " + getitemname(getbrokenid(@choice)) + ".";
	mes "Afin d'effectuer la réparation, j'aurais besoin de " + @repairprice + " Zenys.";
	mes "Voulez-vous continuer?";
	next;
	if(select("Oui","Non") == 2) {
		mes "[" + @name$ + "]";
		mes "Ok...";
		close;
	}
	if (Zeny < @repairprice) {
		mes "[" + @name$ + "]";
		mes "C'est tout ce que vous avez?";
		mes "Désolé, mais j'ai des enfants à nourrir...";
		close;
	}
	set Zeny,Zeny-@repairprice;
	repair(@choice);
	close;

}
