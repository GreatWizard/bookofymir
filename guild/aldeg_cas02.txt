//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script =======================================
//= War of Emperium (Al De baran)
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= NPCs that relate to Al De Baran Guild Castle 2
//===== Additional Comments: =================================
//= 1.0 First Version. No longer uses functions. [L0ne_W0lf]
//= 1.1 Corrected flag-in spawn point. [L0ne_W0lf]
//============================================================

// Guild Flag (Town)
//============================================================
aldebaran,149,97,4	script	Hohenschwangau#a2-1::Hohenschwangau	722,{
	end;

OnRecvCastleA02:
	FlagEmblem GetCastleData("aldeg_cas02",1);
	end;
}

// Guild Flag (Outside Castle)
//============================================================
alde_gld,99,251,4	script	Hohenschwangau#a2-2::Hohenschwangau2	722,{
	set .@GID, GetCastleData("aldeg_cas02",1);
	if (.@GID == 0) {
		mes " [ Edit du Divin Royaume de Rune Midgard ]";
		mes " ";
		mes "1. Sous la Divine Ordonnance du Roi de Rune Midgard, ce château n'appartient pour l'instant à aucune guilde.";
		mes " ";
		mes "2. Celui qui éliminera tous les gardiens qui protègent ce château et qui détruira l'Emperium, sera reconnu par le Roi de Rune Midgard en tant qu'humble possesseur du château.";
	}
	else {
		if (getcharid(2) == .@GID) {
			mes "Brave guerrier... voulez vous retourner dans votre humble base?";
			next;
			if (select("Retourner au château.:Annuler.") == 1) {
				if (getcharid(2) == GetCastleData("aldeg_cas02",1)) {
					warp "aldeg_cas02",84,74;
					end;
				}
			}
			close;
		}
		mes " [ Edit du Divin Royaume de Rune Midgard ]";
		mes " ";
		mes "1. Sous la Divine Ordonnance du Roi de Rune Midgard, ce château appartient à la Guilde ^ff0000" + GetGuildName(.@GID) + "^000000.";
		mes " ";
		mes "2. Le Chef de la Guilde ^ff0000"+ GetGuildName(.@GID) + "^000000 est";
		mes "^FF0000" + GetGuildMaster(.@GID) + "^000000";
		mes "A quiconque souhaitant remettre en cause ce fait, qu'il lève son épée et se batte pour l'Honneur.";
	}
	close;

OnRecvCastleA02:
	FlagEmblem GetCastleData("aldeg_cas02",1);
	end;
}

alde_gld,99,244,4	duplicate(Hohenschwangau2)	Hohenschwangau#a2-3	722

// Guild Flag (Inside Castle)
//============================================================
aldeg_cas02,82,71,2	script	Hohenschwangau#a2-4::Hohenschwangau3	722,{
	set .@GID, GetCastleData("aldeg_cas02",1);
	if (.@GID == 0) {
		mes " [ Edit du Divin Royaume de Rune Midgard ]";
		mes " ";
		mes "1. Sous la Divine Ordonnance du Roi de Rune Midgard, ce château n'appartient pour l'instant à aucune guilde.";
		mes " ";
		mes "2. Celui qui éliminera tous les gardiens qui protègent ce château et qui détruira l'Emperium, sera reconnu par le Roi de Rune Midgard en tant qu'humble possesseur du château.";
	}
	else {
		mes " [ Edit du Divin Royaume de Rune Midgard ]";
		mes " ";
		mes "1. Sous la Divine Ordonnance du Roi de Rune Midgard, ce château appartient à la Guilde ^ff0000" + GetGuildName(.@GID) + "^000000.";
		mes " ";
		mes "2. Le Chef de la Guilde ^ff0000"+ GetGuildName(.@GID) + "^000000 est";
		mes "^FF0000" + GetGuildMaster(.@GID) + "^000000";
		mes "A quiconque souhaitant remettre en cause ce fait, qu'il lève son épée et se batte pour l'Honneur.";
	}
	close;

OnRecvCastleA02:
	FlagEmblem GetCastleData("aldeg_cas02",1);
	end;
}

aldeg_cas02,67,30,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-5	722
aldeg_cas02,183,140,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-6	722
aldeg_cas02,212,152,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-7	722
aldeg_cas02,108,39,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-8	722
aldeg_cas02,57,213,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-9	722
aldeg_cas02,91,181,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-10	722
aldeg_cas02,103,53,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-11	722
aldeg_cas02,73,53,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-12	722
aldeg_cas02,63,41,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-13	722
aldeg_cas02,229,6,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-14	722
aldeg_cas02,230,40,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-15	722
aldeg_cas02,197,40,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-16	722
aldeg_cas02,32,213,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-17	722
aldeg_cas02,121,29,2	duplicate(Hohenschwangau3)	Hohenschwangau#a2-18	722

// AGIT Manager
//============================================================
aldeg_cas02,214,24,0	duplicate(Gld_Agit_Manager)	Agit#aldeg_cas02	-1

// Steward
//============================================================
aldeg_cas02,78,74,0	duplicate(Gld_Mngr_Template)	Chenchenlie#aldeg_cas02	55

// Guardians
//============================================================
aldeg_cas02,78,74,0	duplicate(Gld_Guard_Template)	Gardien#aldeg_cas02	-1

// Kafra Staff
//============================================================
aldeg_cas02,84,74,0	duplicate(Gld_Kafra_Template)	Kafra#aldeg_cas02	117

// Master's Room Exit
//============================================================
aldeg_cas02,139,235,0	script	#switch_A02	111,{
	mes " ";
	mes "Vous remarquez un petit levier...";
	mes "Voulez-vous l'activer?";
	next;
	switch(select("Oui.:Non.")) {
	case 1:
		warp "aldeg_cas02",78,75;
		break;
	case 2:
		break;
	}
	end;
}

// Guild Treasure Protection
//============================================================
aldeg_cas02,135,230,0	duplicate(Gld_Trea_Protect)	ban_warp#aldeg_cas02	-1,8,8

// Guild Treasure Spawns
//============================================================
aldeg_cas02,135,230,0	duplicate(Gld_Trea_Spawn)	Coffre#aldeg_cas02	-1

// Guild Dungeon Switch
//============================================================
aldeg_cas02,194,136,0	duplicate(Gld_Dun_Template)	#aldeg_cas02	111
