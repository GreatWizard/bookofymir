//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script =======================================
//= War of Emperium Guild Manager Function
//===== By: ==================================================
//= jAthena - kalen (1.0) & eAthena Team
//===== Current Version: =====================================
//= 1.9c
//===== Compatible With: =====================================
//= eAthena SVN; RO Episode 4+
//===== Description: =========================================
//= [ Aegis Conversion]
//= The Guild Manager allows the Guildmaster to invest in comerce
//= and defense, hire guardians and kafras, go to the treasure room,
//= and surrender the guild castle.
//==============================================
//= Break down of arguments used in the function:
//=   arg(0): name of Castle Manager
//=   arg(1): name of guild castle.
//=   arg(2): x1 coordinate for warp to treasure room
//=   arg(3): y1 coordinate for warp to treasure room
//=   arg(4): guild script suffix for kafra, Guardian scripts etc.
//===== Additional Comments: =================================
//= 1.31: Added support for Emsolute Develop [celest]
//= 1.2: All Guild manager scripts use this function. Optimized Comerce and Defense investment. [kobra_k88]
//= 1.2a Function now returns to script that called it. Added disablenpc line to surrender castle option to remove kafra upon surrender.[kobra_k88]
//= 1.2b U can't surrender the base during WOE [Lupus]
//= 1.2c Fixed issue of guardians hp not increasing upon defense investment.[kobra_k88]
//= 1.3 Now you can't install Guardians during WOE [Lupus]
//= 1.4 Remove surrender abbility (Was 100% custom as far as I can tell) [Kayla]
//= 1.41 Fixed possible Economy investment overflow with Emsolute Develop learnt [Lupus]
//= 1.5 Official Novice Castles Menu (u can't invest / hire guardians) [Lupus]
//= 1.6 According to recent info u can re-install Guardians during WoE [Lupus]
//= 1.6a Fix for guild manager recognizing [KarLaeda]
//= 1.6b Fixed the chance for double invest, now 50% instead of 49% [Lupus]
//= 1.7 Changed the names of the Kafra from "Service" to "Staff" [L0ne_W0lf]
//= 1.8 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=	This update changes quite a bit of the internal workings of the guild manager.
//=	No longer uses defined numerical values for guardian HP calculation
//=	Guardian summon display list is now DYNAMIC. Updated with guardian corrections.
//=	Added dialog for castle Abandoning. Commented out be default.
//= 1.9 Fixed guild members being able to access Guild Master services. [L0ne_W0lf]
//= 1.9a Fixed Kafra cutin not closing if you choose not to dismiss her. [L0ne_W0lf]
//= 1.9b Corrected impropper variable being used for defense investment. [L0ne_W0lf]
//= 1.9c Fixed a typo with zeny checkm when installing guardian. [L0ne_W0lf]
//============================================================

function	script	F_GldManager	{
	set .@GID, GetCastleData(getarg(1),1);

	// Define the types of guardians on a per castle basis
	// 1 - Soldier Guardian; 2 - Archer Guardian; 3 - Knight Guardian
	// Aldebaran (Luina)
	if (getarg(1) == "aldeg_cas01") setarray .@guardiantype[0],1,2,2,2,2,3,3,3;
	if (getarg(1) == "aldeg_cas02") setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
	if (getarg(1) == "aldeg_cas03") setarray .@guardiantype[0],3,3,1,1,1,2,2,2;
	if (getarg(1) == "aldeg_cas04") setarray .@guardiantype[0],2,2,2,1,1,1,3,3;
	if (getarg(1) == "aldeg_cas05") setarray .@guardiantype[0],2,2,1,1,3,3,3,3;
	// Geffen (Britoniah)
	if (getarg(1) == "gefg_cas01") setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
	if (getarg(1) == "gefg_cas02") setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
	if (getarg(1) == "gefg_cas03") setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
	if (getarg(1) == "gefg_cas04") setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
	if (getarg(1) == "gefg_cas05") setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
	// Payon (Baulder)
	if (getarg(1) == "payg_cas01") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	if (getarg(1) == "payg_cas02") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	if (getarg(1) == "payg_cas03") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	if (getarg(1) == "payg_cas04") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	if (getarg(1) == "payg_cas05") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	// Prontera (Valkyrie Realms)
	if (getarg(1) == "prtg_cas01") setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
	if (getarg(1) == "prtg_cas02") setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
	if (getarg(1) == "prtg_cas03") setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
	if (getarg(1) == "prtg_cas04") setarray .@guardiantype[0],3,3,3,1,1,1,2,2;
	if (getarg(1) == "prtg_cas05") setarray .@guardiantype[0],3,3,3,1,1,1,2,2;

	if (.@GID == 0){
		mes "[ "+getarg(0)+" ]";
		mes "J'attends les ordres de mon Maître. Brave aventurier, suis ta destinée!";
		return 0;
	}
	if (getcharid(2) != .@GID || strcharinfo(0) != getguildmaster(.@GID)){
		mes "[ "+getarg(0)+" ]";
		mes "Je suis employé ici afin de suivre les ordres de Maître ^ff0000" + getguildmaster(.@GID) + "^000000! Hé, vous ne faîtes même pas partie de la Guilde!";
		mes "Gardiens! Saisissez cet intrus!";
		return 0;
	}
	mes "[ "+getarg(0)+" ]";
	mes "Bienvenue, Maître ^ff0000" + getguildmaster(.@GID) + "^000000 ! Je vais vous assister du mieux possible!";
	next;

	// To allow abandoning of castles, uncomment the following switch,
	// comment out the switch below it, and uncomment case 7.
	//switch(select("Castle briefing:Invest in commercial growth:Invest in Castle Defenses:Summon Guardian:Employ / discharge a storehouse staff member:Go into Master's room:Abandon Castle")) {
	switch(select("Résumé de l'Etat du Château:Investissements Commerciaux:Investissements Défensifs:Mise en Place de Gardiens:Emploi ou Renvoi d'une Kafra:Entrer dans la Salle des Coffres")) {
	case 1:
		mes "[ "+getarg(0)+" ]";
		mes "Résumé des Investissements du Château.";
		mes " ";
		mes " ^0000ffInvestissements Commerciaux Actuels: " + GetCastleData(getarg(1),2) + ".";
		if (GetCastleData(getarg(1),4) > 0)
			mes "^0000ff - Vous avez investi " + GetCastleData(getarg(1),4) + " fois aujourd'hui.";
		mes "Investissement Défensif Actuel : " + GetCastleData(getarg(1),3)  + "^000000 points.";
		if (GetCastleData(getarg(1),5) > 0)
			mes "^0000ff- Vous avez investi " +  GetCastleData(getarg(1),5)  + " fois aujourd'hui.^000000";
		return 0;

	case 2:
		set .@Economy,GetCastleData(getarg(1),2);
		if(.@Economy < 8) set .@eco_invest,10000;
		if(.@Economy >= 8) set .@eco_invest,20000;
		if(.@Economy >= 16) set .@eco_invest,40000;
		if(.@Economy >= 25) set .@eco_invest,80000;
		if(.@Economy >= 34) set .@eco_invest,160000;
		if(.@Economy >= 44) set .@eco_invest,320000;
		if(.@Economy >= 54) set .@eco_invest,640000;
		if(.@Economy >= 65) set .@eco_invest,1280000;
		if(.@Economy >= 76) set .@eco_invest,2560000;
		if(.@Economy >= 88) set .@eco_invest,5120000;
		mes "[ "+getarg(0)+" ]";
		mes "Grâce aux Investissements Commerciaux, la production de la Guilde augmentera.";
		mes "Si vous vous projetez dans l'avenir, un investissement est nécessaire.";
		mes " ";
		mes "Vous pouvez investir jusqu'à deux fois par jour. La seconde fois vous coûtera plus cher.";
		if (.@Economy == 100) {
			mes " ";
			mes "^ff0000L'Investissement Commercial de notre Château est à son maximum. Vous êtes un Maître très dévoué, comme je m'en doutais.^000000";
			return 0;
		}
		mes " ";
		if (GetCastleData(getarg(1),4) == 2) {
			mes "^ff0000Vous avez déjà investi deux fois aujourd'hui, c'est le maximum possible.^000000 J'espère que nos richesses ne cesseront de s'accroître!";
			return 0;
		}
		else if (GetCastleData(getarg(1),4) == 0)
			mes "L'argent nécessaire pour cet investissement est de ^ff0000" + .@eco_invest + "^000000 Zenys. Souhaitez-vous investir?";
		else
			mes "Vous avez déjà investi une fois aujourd'hui, vous pouvez investir à nouveau pour ^ff0000" + @eco_invest + "^000000 Zenys.";
		next;
		if (select("Investir en Commerce.:Annuler") == 1) {
			mes "[ "+getarg(0)+" ]";
			if (Zeny < .@eco_invest) {
				mes "Maître, vous n'avez pas assez d'argent pour investir. L'investissement est donc annulé.";
				return 0;
			}
			set zeny,zeny-.@eco_invest;
			SetCastleData getarg(1),4,GetCastleData(getarg(1),4)+1;
			SetCastleData getarg(1),2,.@Economy + 1 + (.@Economy<99 && rand(2) && getgdskilllv(.@GID,10014));
			mes "L'investissement a été pris en compte.";
			return 0;
		}
		mes "[ "+getarg(0)+" ]";
		mes "Je ferai selon vos instructions, Maître... Rien n'est impossible à accomplir pour vous et votre guilde.";
		return 0;

	case 3:
		set .@Defence,GetCastleData(getarg(1),3);
		if(.@Defence < 8) set .@def_invest,20000;
		if(.@Defence >= 8) set .@def_invest,40000;
		if(.@Defence >= 16) set .@def_invest,80000;
		if(.@Defence >= 25) set .@def_invest,160000;
		if(.@Defence >= 34) set .@def_invest,320000;
		if(.@Defence >= 44) set .@def_invest,640000;
		if(.@Defence >= 54) set .@def_invest,1280000;
		if(.@Defence >= 65) set .@def_invest,2560000;
		if(.@Defence >= 76) set .@def_invest,5120000;
		if(.@Defence >= 88) set .@def_invest,10240000;
		mes "[ "+getarg(0)+" ]";
		mes "Grâce aux Investissements Défensifs, les Gardiens et l'Emperium deviendront plus résistants.";
		mes "Si vous vous projetez dans l'avenir, un investissement est nécessaire.";
		mes " ";
		mes "Vous pouvez investir jusqu'à deux fois par jour. La seconde fois vous coûtera plus cher.";
		if (.@Defence == 100) {
			mes " ";
			mes "^ff0000L'investissement défensif de ce château a déjà atteint le maximum de 100 points. Il n'est plus nécessaire d'investir à nouveau.^000000";
			return 0;
		}
		mes " ";
		if (GetCastleData(getarg(1),5) == 2) {
			mes "^ff0000Vous avez déjà investi deux fois aujourd'hui, c'est le maximum possible.^000000 J'espère que nos richesses ne cesseront de s'accroître!";
			return 0;
		}
		else if (GetCastleData(getarg(1),5) == 1)
			mes "L'argent nécessaire pour cet investissement est de ^ff0000" + .@def_invest + "^000000 Zenys. Souhaitez-vous investir?";
		else
			mes "Vous avez déjà investi une fois aujourd'hui, vous pouvez investir à nouveau pour ^ff0000" + @def_invest + "^000000 Zenys.";
		next;

		if (select("Investir en Défense.:Annuler") == 1) {
			mes "[ "+getarg(0)+" ]";
			if (Zeny < .@def_invest) {
				mes "Maître, vous n'avez pas assez d'argent pour investir. L'investissement est donc annulé.";
				return 0;
			}
			set Zeny,Zeny-.@def_invest;
			SetCastleData getarg(1),5,GetCastleData(getarg(1),5)+1;
			SetCastleData getarg(1),3,.@Defence+1;
			mes "L'investissement a été pris en compte.";
			return 0;
		}
		mes "[ "+getarg(0)+" ]";
		mes "Je ferai selon vos instructions, Maître... Rien n'est impossible à accomplir pour vous et votre guilde.";
		return 0;

	case 4:
		mes "[ "+getarg(0)+" ]";
		mes "Voulez vous invoquer un Gardien? Ce sera un protecteur dévoué.";
		mes "Veuillez choisir un Gardien.";
		next;

		for( set .@i, 0; .@i <= 7 ; set .@i, .@i+1 )
		{
			if      (.@guardiantype[.@i] == 1) set .@type$,"Gardien Soldat";
			else if (.@guardiantype[.@i] == 2) set .@type$,"Gardien Archer";
			else                               set .@type$,"Gardien Chevalier";

			if( guardianinfo(getarg(1),.@i,0) )
				setarray .@name$[.@i], .@type$ + " - En Place (" + guardianinfo(getarg(1),.@i,2) + "/" + guardianinfo(getarg(1),.@i,1) + ")";
			else
				setarray .@name$[.@i], .@type$ + " - Non Déployé";
		}
	
		set .@menu$,.@name$[0]+":"+.@name$[1]+":"+.@name$[2]+":"+.@name$[3]+":"+.@name$[4]+":"+.@name$[5]+":"+.@name$[6]+":"+.@name$[7];

		// this thing will propagate outside of this function
		switch(select(.@menu$)) {
		case 1: set @GDnum,10; break;
		case 2: set @GDnum,11; break;
		case 3: set @GDnum,12; break;
		case 4: set @GDnum,13; break;
		case 5: set @GDnum,14; break;
		case 6: set @GDnum,15; break;
		case 7: set @GDnum,16; break;
		case 8: set @GDnum,17; break;
		}

		mes "[ "+getarg(0)+" ]";
		mes "Voulez-vous réellement mettre en place un Gardien? Cela vous coûtera ^ff000010 000 Zenys^000000.";
		next;

		if (select("Mise en Place:Annuler") == 1) {
			mes "[ "+getarg(0)+" ]";
			if (getgdskilllv(.@GID,10002) == 0) {
				mes "Maître, je suis désolé, mais vous ne pouvez pas mettre en place de Gardiens pour le moment. Votre Guilde doit d'abord apprendre la compétence ^ff0000Guardian Research^000000.";
				mes "La mise en place de Gardiens a été annulée.";
				return 0;
			}
			if (GetCastleData(getarg(1),@GDnum) == 1) {
				mes "Maître, ce Gardien a déjà été mis en place...";
				return 0;
			}
			if (Zeny < 10000) {
				mes "Je suis désolé, Maître, mais vous n'avez pas assez d'argent.";
				return 0;
			}
			set zeny,zeny-10000;
			SetCastleData getarg(1),@GDnum,1; // mark as 'installed'
			// guardian will be spawned outside of this function
			mes "Un Gardien a été mis en place. Il protègera le château des attaques ennemies.";
			return 1;

		}
		mes "[ "+getarg(0)+" ]";
		mes "J'ai fait selon vos ordres, Maître, mais je vous recommande tout de même la mise en place de Gardiens pour notre défense.";
		return 0;

	case 5:
		if (GetCastleData(getarg(1),9) == 1) {
			mes "[ "+getarg(0)+" ]";
			mes "Nous employons actuellement une Employée Kafra... Voulez vous renvoyer l'actuelle Employée Kafra?";
			next;
			switch(select("Renvoyer:Annuler")) {
				cutin "kafra_01",2;
				mes "[ Employée Kafra ]";
				mes "Si je ne vous ai pas servi au mieux, je vous présente toutes mes excuses.";
				next;
				switch(select("Renvoyer:Annuler")) {
				case 1:
					mes "[ Employée Kafra ]";
					mes "Je suis confuse de ne pas avoir réussi à servir votre Guilde comme j'aurais dû....";
					next;
					cutin "kafra_01",255;
					break;
				case 2:
					mes "[ Employée Kafra ]";
					mes "Je travaillerai dur pour vous... Merci!";
					close2;					
					cutin "kafra_01",255;
					return 0;
				}
				break;
			case 2:
				mes "[ "+getarg(0)+" ]";
				mes "Elle est très travailleuse, vous avez raison de le garder à votre service.";
				return 0;
			}	
			disablenpc "Employée Kafra#"+getarg(4);
			SetCastleData getarg(1),9,0;
			mes "[ "+getarg(0)+" ]";
			mes "....";
			mes "J'ai renvoyé l'Employée Kafra... Mais... Êtes-vous staisfait de votre décision?";
			return 0;
		}
		else {
			mes "[ "+getarg(0)+" ]";
			mes "Voulez-vous contacter la Corporation Kafra pour engager une Employée dans votre Château?";
			mes "^ff0000 10 000 Zenys sont nécessaires pour leurs services. ";
			next;
			if (select("Engager.:Annuler") == 1) {
				mes "[ "+getarg(0)+" ]";
				if (getgdskilllv(.@GID,10001) == 0) {
					mes "Maître, vous n'avez pas de contrat avec la Corporation Kafra.";
					mes "Afin de pouvoir employer une Kafra, votre guilde doit dabord apprendre la compétence ^ff0000Contract With Kafra^000000.";
					return 0;
				}
				if (Zeny < 10000) {
					mes "[ "+getarg(0)+" ]";
					mes "Maître, vous n'avez pas assez d'argent. L'emploi d'une Kafra a été annulé.";
					return 0;
				}
				set Zeny,Zeny-10000;
				enablenpc "Employée Kafra#"+getarg(4);
				SetCastleData getarg(1),9,1;
				mes "Vous avez passé un contrat avec la Corporation Kafra.";
				next;
				cutin "kafra_01",2;
				mes "[ Employée Kafra ]";
				mes "C'est un honneur de pouvoir vous proposer mes services. Je vous aiderai au mieux.";
				next;
				cutin "kafra_01",255;
				mes "[ "+getarg(0)+" ]";
				mes "Je pense que l'Employée Kafra sera très bénéfique pour les membres de la Guilde.";
				return 0;
			}
			mes "[ "+getarg(0)+" ]";
			mes "Très bien, Maître. Mais je vous suggère d'employer une Kafra très bientôt!";
			return 0;
		}

	case 6:
		mes "[ "+getarg(0)+" ]";
		mes "Voulez-vous vous rendre dans la Salle des Coffres? Seul vous, le Chef de Guilde, êtes autorisé à entrer dans cette salle.";
		next;
		if (select("Entrer.:Annuler") == 1) {
			mes "[ "+getarg(0)+" ]";
			mes "Suivez-moi, je vais vous montrer le passage secret pour y accéder.";
			mes "Vous devrez activer le petit levier dans la salle afin d'en resortir.";
			warp getarg(1),getarg(2),getarg(3);
			end;
		}
		mes "[ "+getarg(0)+" ]";
		mes "Nous recevons des coffres chaque jour.";
		mes "Vous devriez en prendre possession chaque fois que vous le pouvez.";
		return 0;

	//case 7:
	//	mes "[ "+getarg(0)+" ]";
	//	mes "Master!!";
	//	mes "Will you give up this Castle after all that we have all gone through?!";
	//	mes "If.. If this is your final decision, what does all the blood that has been shed mean to you!?";
	//	mes "Please reconsider! Master!!";
	//	next;
	//	if (select("Leave Agit.:Cancel") == 1) {
	//		mes "[ "+getarg(0)+" ]";
	//		mes "Master!! Please think it over, Master!!";
	//		mes "The blood we shed so far...don't you care for all the sacrifices that were made?!";
	//		next;
	//		if (select("Cancel:Leave Castle.") == 1) {
	//			mes "[ "+getarg(0)+" ]";
	//			mes "I knew you were kidding, Master!!";
	//			mes "Please, Master.. Although you were kidding, don't do that again. You really scared me for a moment.";
	//			return 0;
	//		}
	//		mes "[ "+getarg(0)+" ]";
	//		mes "Master!!.....";
	//		mes "...... Ma..s...ter.....";
	//		callfunc "F_GuildBreak",getarg(0),getarg(4);
	//		return 0;
	//	}
	//	mes "[ "+getarg(0)+" ]";
	//	mes "I knew you were kidding, Master!!";
	//	mes "Please, Master.. Although you were kidding, don't do that again. You really scared me for a moment.";
	//	return 0;
	}
}
