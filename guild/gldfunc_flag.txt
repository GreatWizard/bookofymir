//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script =======================================
//= War of Emperium Guild Flags Function
//===== By: ==================================================
//= jAthena - kalen (1.0)
//= 1.1 by Akaru, ho|yAnge|X, and Valaris
//===== Current Version: =====================================
//= 1.6
//===== Compatible With: =====================================
//= eAthena SVN; RO Episode 4+
//===== Description: =========================================
//= Flags display messages stating whether or not a guild castle has been captured.
//= Allows the guild members to return to a captured castle.
//======================================
//= Break down of arguments used in the function:
//=   arg(0): name of City.
//=   arg(1): name of guild castle.
//=   arg(2): x1 coordinate for warp
//=   arg(3): y1 coordinate for warp
//=   arg(4): Teleportable (0/1) 
//=   arg(5): Display Flag Dialog (0/1)
//===== Additional Comments: =================================
//= v1.2: All Guild flags scripts use this function. [kobra_k88]
//= v1.3: Can now only flag in from outside the castle [Kayla]
//= v1.3a: Implemented k3dt's exploit fix [Vicious]
//= v1.4: By default town flags are set to no longer show text. [L0ne_W0lf]
//= v1.5: Updated remaining dialog to iRO standards. [L0ne_W0lf]
//= v1.6 Restructred script, and removed use of gotos. [L0ne_W0lf]
//= 6.17 Whoops. fices a @GID -> .@GID. [L0ne_W0lf]
//============================================================

function	script	F_Flags	{
	set .@GID, GetCastleData(getarg(1),1);
	if (getarg(5) == 0) return;
	if (.@GID == 0) {
		mes "[ Edit du Divin Royaume de Rune Midgard ]";
		mes " ";
		mes "1. Sous la Divine Ordonance du Roi de Royaume de Rune Midgard, ";
		mes "Nous déclarons que";
		mes "ce château n'appartient à aucune guilde.";
		mes " ";
		mes "2. A celui qui pourra ";
		mes "surpasser toutes les épreuves";
		mes "et détruite l'Emperium,";
		mes "le Roi lui acordera la";
		mes "possession de ce château.";
		return;
	}
	else {
		if (getcharid(2) == .@GID && getarg(4) == 1) {
			mes "Brave guerrier...";
			mes "Voulez-vous retourner dans votre humble base?";
			next;
			if (select("Retourner au château de Guilde.:S'en aller.") == 1) {
				if (getcharid(2) == GetCastleData(getarg(1),1))
					warp getarg(1),getarg(2),getarg(3);
			}
			return;
		}
		mes "[ Edit du Divin Royaume de Rune Midgard ]";
		mes " ";
		mes "1. Sous la Divine Ordonance du";
		mes "Roi de Royaume de Rune Midgard,";
		mes "nous approuvons la possession de";
		mes "ce château par la Guilde ^ff0000" + GetGuildName(.@GID) + "^000000.";
		mes " ";
		mes "2. Le Chef de la Guilde ^ff0000"+ GetGuildName(.@GID) + "^000000 est";
		mes "^FF0000" + GetGuildMaster(.@GID) + "^000000";
		mes "A quiconque souhaitant remettre en cause ce fait,";
		mes "qu'il lève son épée et se batte pour l'Honneur.";
		return;
	}
}
