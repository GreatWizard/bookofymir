//Le Livre d'Ymir http://bookofymir.free.fr/
//traduit par Myllena
//===== eAthena Script ======================================= 
//= Rachel Guide
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.2a
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Rachel City guide
//===== Additional Comments: ================================= 
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 Change the while loops a bit. [L0ne_W0lf]
//= 1.2 Deleted an Unnecessary "next;" [Samuray22]
//	-Thanks to $ephiroth.
//= 1.2a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//============================================================ 

rachel,138,146,5	script	Guide de Rachel	934,{
	mes "[Guide de Rachel]";
	mes "Bienvenue à Rachel, capitale d'Arunafeltz. Ici, souffle la douce chaleur de la déesse Freya.";
	mes "En quoi puis-je vous aider?";
	set .@loop1,1;
	while(.@loop1) {
		next;
		switch(select("Guide de la Ville:Effacer les Informations de la Mini-Map:Astuce:Annuler")) {
		case 1:
			mes "[Guide de Rachel]";
			mes "Je peux vous renseigner sur n'importe quel bâtiment de la ville.";
			mes "Où souhaitez-vous aller?";
			if (.@compass_check == 0) {
				mes "Voulez-vous que je laisse des indications sur votre Mini-Map?";
				next;
				if (select("Oui:Non") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("Cheshrumnir:Auberge:Armurerie:Boutique:Aéroport:Annuler")) {
				case 1:
					mes "[Guide de Rachel]";
					mes "Cheshrumnir est un sol sacré où loge le pape, incarnation de la déesse Freya.";
					mes "Prenez la route vers le Nord et vous tomberez dessus.";
					if (.@compass_check == 1) viewpoint 1,150,249,1,0xFF0000;
					break;
				case 2:
					mes "[Guide de Rachel]";
					mes "Vous pourrez vous reposer à l'Auberge si vous êtes fatigué d'un long voyage.";
					mes "Le bâtiment situé à ma gauche est l'Auberge de Rachel.";
					if (.@compass_check == 1) viewpoint 1,115,149,2,0xFF00FF;
					break;
				case 3:
					mes "[Guide de Rachel]";
					mes "L'Armurerie est située près de la Porte Ouest.";
					if (.@compass_check == 1) viewpoint 1,42,87,3,0x99FFFF;
					break;
				case 4:
					mes "[Guide de Rachel]";
					mes "L'Epicerie de Rachel offre des potions de la meilleure qualité!";
					mes "Elle est située près de la Porte Ouest.";
					if (.@compass_check == 1) viewpoint 1,83,78,4,0x0000FF;
					break;
				case 5:
					mes "[Guide de Rachel]";
					mes "L'Aéroport est situé en dehors de la ville, après la Porte Est.";
					if (.@compass_check == 1) viewpoint 1,273,125,5,0x00FF00;
					break;
				case 6:
					mes "[Guide de Rachel]";
					mes "N'hésitez pas à me demander si vous voulez que j'efface les informations de votre Mini-Map.";
					set .@loop2,0;
					break;
				}
			}
			break;

		case 2:
			viewpoint 2,150,249,1,0xFF0000;
			viewpoint 2,115,149,2,0xFF00FF;
			viewpoint 2,42,87,3,0x99FFFF;
			viewpoint 2,83,78,4,0x0000FF;
			viewpoint 2,273,125,5,0x00FF00;
			mes "[Guide de Rachel]";
			mes "Toutes les indications ont été effacées.";
			break;
		case 3:
			mes "[Guide de Rachel]";
  			mes "Votre Mini-Map est située dans le coin haut droit de votre écran.";
			mes "Si vous ne la voyez pas, pressez Ctrl + Tab ou cliquez sur le bouton 'Map' dans votre fenêtre d'informations.";
			next;
			mes "[Guide de Rachel]";
 			mes "Sur votre Mini-Map, cliquez sur '+' ou '-' pour modifier le zoom.";
			mes "J'espère que vous apprécierez votre séjour à Rachel.";
			break;
		case 4:
			mes "[Guide de Rachel]";
			mes "J'espère que vous apprécierez votre séjour à Arunafeltz.";
			close2;
			set .@loop1,0;
			break;
		}
	}
	close;
	end;
}